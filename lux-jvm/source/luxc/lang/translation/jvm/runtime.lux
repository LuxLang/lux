(.module:
  [lux (#- Type)
   [abstract
    [monad (#+ do)]
    ["." enum]]
   [data
    [binary (#+ Binary)]
    ["." product]
    [text
     ["%" format (#+ format)]]
    [collection
     ["." list ("#@." functor)]
     ["." row]]]
   ["." math
    [number
     ["n" nat]]]
   [target
    [jvm
     ["." type (#+ Type)
      ["." category (#+ Void Value' Value Return' Return Primitive Object Class Array Var Parameter Method)]
      ["." reflection]]]]
   [tool
    [compiler
     [arity (#+ Arity)]
     ["." phase]
     [language
      [lux
       ["." synthesis]
       ["." generation]]]
     [meta
      [archive (#+ Output)
       ["." artifact (#+ Registry)]]]]]]
  [luxc
   [lang
    [host
     ["$" jvm (#+ Label Inst Def Operation)
      ["$d" def]
      ["_" inst]]]]]
  ["." // (#+ ByteCode)])

(def: $Text (type.class "java.lang.String" (list)))
(def: #export $Tag type.int)
(def: #export $Flag (type.class "java.lang.Object" (list)))
(def: #export $Value (type.class "java.lang.Object" (list)))
(def: #export $Index type.int)
(def: #export $Stack (type.array $Value))
(def: $Throwable (type.class "java.lang.Throwable" (list)))

(def: nullary_init_methodT
  (type.method [(list) type.void (list)]))

(def: throw_methodT
  (type.method [(list) type.void (list)]))

(def: #export logI
  Inst
  (let [PrintStream (type.class "java.io.PrintStream" (list))
        outI (_.GETSTATIC (type.class "java.lang.System" (list)) "out" PrintStream)
        printI (function (_ method)
                 (_.INVOKEVIRTUAL PrintStream method (type.method [(list $Value) type.void (list)])))]
    (|>> outI (_.string "LOG: ") (printI "print")
         outI _.SWAP (printI "println"))))

(def: variant_method
  (type.method [(list $Tag $Flag $Value) //.$Variant (list)]))

(def: #export variantI
  Inst
  (_.INVOKESTATIC //.$Runtime "variant_make" variant_method))

(def: #export leftI
  Inst
  (|>> _.ICONST_0
       _.NULL
       _.DUP2_X1
       _.POP2
       variantI))

(def: #export rightI
  Inst
  (|>> _.ICONST_1
       (_.string "")
       _.DUP2_X1
       _.POP2
       variantI))

(def: #export someI Inst rightI)

(def: #export noneI
  Inst
  (|>> _.ICONST_0
       _.NULL
       (_.string synthesis.unit)
       variantI))

(def: (tryI unsafeI)
  (-> Inst Inst)
  (<| _.with_label (function (_ @from))
      _.with_label (function (_ @to))
      _.with_label (function (_ @handler))
      (|>> (_.try @from @to @handler (type.class "java.lang.Exception" (list)))
           (_.label @from)
           unsafeI
           someI
           _.ARETURN
           (_.label @to)
           (_.label @handler)
           noneI
           _.ARETURN)))

(def: #export partials_field Text "partials")
(def: #export apply_method Text "apply")
(def: #export num_apply_variants Nat 8)

(def: #export (apply_signature arity)
  (-> Arity (Type Method))
  (type.method [(list.repeat arity $Value) $Value (list)]))

(def: adt_methods
  Def
  (let [store_tagI (|>> _.DUP _.ICONST_0 (_.ILOAD 0) (_.wrap type.int) _.AASTORE)
        store_flagI (|>> _.DUP _.ICONST_1 (_.ALOAD 1) _.AASTORE)
        store_valueI (|>> _.DUP _.ICONST_2 (_.ALOAD 2) _.AASTORE)]
    (|>> ($d.method #$.Public $.staticM "variant_make"
                    (type.method [(list $Tag $Flag $Value) //.$Variant (list)])
                    (|>> _.ICONST_3
                         (_.ANEWARRAY $Value)
                         store_tagI
                         store_flagI
                         store_valueI
                         _.ARETURN)))))

(def: frac_methods
  Def
  (|>> ($d.method #$.Public $.staticM "decode_frac" (type.method [(list $Text) //.$Variant (list)])
                  (tryI
                   (|>> (_.ALOAD 0)
                        (_.INVOKESTATIC (type.class "java.lang.Double" (list)) "parseDouble" (type.method [(list $Text) type.double (list)]))
                        (_.wrap type.double))))
       ))

(def: (illegal_state_exception message)
  (-> Text Inst)
  (let [IllegalStateException (type.class "java.lang.IllegalStateException" (list))]
    (|>> (_.NEW IllegalStateException)
         _.DUP
         (_.string message)
         (_.INVOKESPECIAL IllegalStateException "<init>" (type.method [(list $Text) type.void (list)])))))

(def: pm_methods
  Def
  (let [tuple_sizeI (|>> (_.ALOAD 0)
                         _.ARRAYLENGTH)
        last_rightI (|>> tuple_sizeI
                         _.ICONST_1
                         _.ISUB)
        leftsI (_.ILOAD 1)
        left_indexI leftsI
        sub_leftsI (|>> leftsI
                        last_rightI
                        _.ISUB)
        sub_tupleI (|>> (_.ALOAD 0)
                        last_rightI
                        _.AALOAD
                        (_.CHECKCAST //.$Tuple))
        recurI (: (-> Label Inst)
                  (function (_ @loop)
                    (|>> sub_leftsI (_.ISTORE 1)
                         sub_tupleI (_.ASTORE 0)
                         (_.GOTO @loop))))]
    (|>> ($d.method #$.Public $.staticM "pm_fail" throw_methodT
                    (|>> (illegal_state_exception "Invalid expression for pattern-matching.")
                         _.ATHROW))
         ($d.method #$.Public $.staticM "apply_fail" throw_methodT
                    (|>> (illegal_state_exception "Error while applying function.")
                         _.ATHROW))
         ($d.method #$.Public $.staticM "pm_push" (type.method [(list $Stack $Value) $Stack (list)])
                    (|>> _.ICONST_2
                         (_.ANEWARRAY $Value)
                         _.DUP
                         _.ICONST_1
                         (_.ALOAD 0)
                         _.AASTORE
                         _.DUP
                         _.ICONST_0
                         (_.ALOAD 1)
                         _.AASTORE
                         _.ARETURN))
         ($d.method #$.Public $.staticM "pm_variant" (type.method [(list //.$Variant $Tag $Flag) $Value (list)])
                    (<| _.with_label (function (_ @loop))
                        _.with_label (function (_ @perfect_match!))
                        _.with_label (function (_ @tags_match!))
                        _.with_label (function (_ @maybe_nested))
                        _.with_label (function (_ @mismatch!))
                        (let [$variant (_.ALOAD 0)
                              $tag (_.ILOAD 1)
                              $last? (_.ALOAD 2)
                              
                              variant_partI (: (-> Nat Inst)
                                               (function (_ idx)
                                                 (|>> (_.int (.int idx)) _.AALOAD)))
                              ::tag (: Inst
                                       (|>> (variant_partI 0) (_.unwrap type.int)))
                              ::last? (variant_partI 1)
                              ::value (variant_partI 2)

                              super_nested_tag (|>> _.SWAP ## variant::tag, tag
                                                    _.ISUB)
                              super_nested (|>> super_nested_tag ## super_tag
                                                $variant ::last? ## super_tag, super_last
                                                $variant ::value ## super_tag, super_last, super_value
                                                ..variantI)
                              
                              update_$tag _.ISUB
                              update_$variant (|>> $variant ::value
                                                   (_.CHECKCAST //.$Variant)
                                                   (_.ASTORE 0))
                              iterate! (: (-> Label Inst)
                                          (function (_ @loop)
                                            (|>> update_$variant
                                                 update_$tag
                                                 (_.GOTO @loop))))
                              
                              not_found _.NULL])
                        (|>> $tag ## tag
                             (_.label @loop)
                             $variant ::tag ## tag, variant::tag
                             _.DUP2 (_.IF_ICMPEQ @tags_match!) ## tag, variant::tag
                             _.DUP2 (_.IF_ICMPGT @maybe_nested) ## tag, variant::tag
                             $last? (_.IFNULL @mismatch!) ## tag, variant::tag
                             super_nested ## super_variant
                             _.ARETURN
                             (_.label @tags_match!) ## tag, variant::tag
                             $last? ## tag, variant::tag, last?
                             $variant ::last? ## tag, variant::tag, last?, variant::last?
                             (_.IF_ACMPEQ @perfect_match!) ## tag, variant::tag
                             (_.label @maybe_nested) ## tag, variant::tag
                             $variant ::last? ## tag, variant::tag, variant::last?
                             (_.IFNULL @mismatch!) ## tag, variant::tag
                             (iterate! @loop)
                             (_.label @perfect_match!) ## tag, variant::tag
                             ## _.POP2
                             $variant ::value
                             _.ARETURN
                             (_.label @mismatch!) ## tag, variant::tag
                             ## _.POP2
                             not_found
                             _.ARETURN)))
         ($d.method #$.Public $.staticM "tuple_left" (type.method [(list //.$Tuple $Index) $Value (list)])
                    (<| _.with_label (function (_ @loop))
                        _.with_label (function (_ @recursive))
                        (let [left_accessI (|>> (_.ALOAD 0) left_indexI _.AALOAD)])
                        (|>> (_.label @loop)
                             leftsI last_rightI (_.IF_ICMPGE @recursive)
                             left_accessI
                             _.ARETURN
                             (_.label @recursive)
                             ## Recursive
                             (recurI @loop))))
         ($d.method #$.Public $.staticM "tuple_right" (type.method [(list //.$Tuple $Index) $Value (list)])
                    (<| _.with_label (function (_ @loop))
                        _.with_label (function (_ @not_tail))
                        _.with_label (function (_ @slice))
                        (let [right_indexI (|>> leftsI
                                                _.ICONST_1
                                                _.IADD)
                              right_accessI (|>> (_.ALOAD 0)
                                                 _.SWAP
                                                 _.AALOAD)
                              sub_rightI (|>> (_.ALOAD 0)
                                              right_indexI
                                              tuple_sizeI
                                              (_.INVOKESTATIC (type.class "java.util.Arrays" (list)) "copyOfRange"
                                                              (type.method [(list //.$Tuple $Index $Index)
                                                                            //.$Tuple
                                                                            (list)])))])
                        (|>> (_.label @loop)
                             last_rightI right_indexI
                             _.DUP2 (_.IF_ICMPNE @not_tail)
                             ## _.POP
                             right_accessI
                             _.ARETURN
                             (_.label @not_tail)
                             (_.IF_ICMPGT @slice)
                             ## Must recurse
                             (recurI @loop)
                             (_.label @slice)
                             sub_rightI
                             _.ARETURN
                             )))
         )))

(def: #export try (type.method [(list //.$Function) //.$Variant (list)]))

(def: io_methods
  Def
  (let [StringWriter (type.class "java.io.StringWriter" (list))
        PrintWriter (type.class "java.io.PrintWriter" (list))
        string_writerI (|>> (_.NEW StringWriter)
                            _.DUP
                            (_.INVOKESPECIAL StringWriter "<init>" nullary_init_methodT))
        print_writerI (|>> (_.NEW PrintWriter)
                           _.SWAP
                           _.DUP2
                           _.POP
                           _.SWAP
                           (_.boolean true)
                           (_.INVOKESPECIAL PrintWriter "<init>" (type.method [(list (type.class "java.io.Writer" (list)) type.boolean) type.void (list)]))
                           )]
    (|>> ($d.method #$.Public $.staticM "try" ..try
                    (<| _.with_label (function (_ @from))
                        _.with_label (function (_ @to))
                        _.with_label (function (_ @handler))
                        (|>> (_.try @from @to @handler $Throwable)
                             (_.label @from)
                             (_.ALOAD 0)
                             _.NULL
                             (_.INVOKEVIRTUAL //.$Function apply_method (apply_signature 1))
                             rightI
                             _.ARETURN
                             (_.label @to)
                             (_.label @handler)
                             string_writerI ## TW
                             _.DUP2 ## TWTW
                             print_writerI ## TWTP
                             (_.INVOKEVIRTUAL $Throwable "printStackTrace" (type.method [(list (type.class "java.io.PrintWriter" (list))) type.void (list)])) ## TW
                             (_.INVOKEVIRTUAL StringWriter "toString" (type.method [(list) $Text (list)])) ## TS
                             _.SWAP _.POP leftI
                             _.ARETURN)))
         )))

(def: reflection
  (All [category]
    (-> (Type (<| Return' Value' category)) Text))
  (|>> type.reflection reflection.reflection))

(def: runtime_id
  0)

(def: translate_runtime
  (Operation [artifact.ID Binary])
  (let [runtime_class (..reflection //.$Runtime)
        bytecode ($d.class #$.V1_6 #$.Public $.finalC runtime_class (list) (type.class "java.lang.Object" (list)) (list)
                           (|>> adt_methods
                                frac_methods
                                pm_methods
                                io_methods))
        directive [runtime_class bytecode]]
    (do phase.monad
      [_ (generation.execute! directive)
       _ (generation.save! ..runtime_id directive)]
      (wrap [..runtime_id bytecode]))))

(def: function_id
  1)

(def: translate_function
  (Operation [artifact.ID Binary])
  (let [applyI (|> (enum.range n.enum 2 num_apply_variants)
                   (list@map (function (_ arity)
                               ($d.method #$.Public $.noneM apply_method (apply_signature arity)
                                          (let [preI (|> (enum.range n.enum 0 (dec arity))
                                                         (list@map _.ALOAD)
                                                         _.fuse)]
                                            (|>> preI
                                                 (_.INVOKEVIRTUAL //.$Function apply_method (apply_signature (dec arity)))
                                                 (_.CHECKCAST //.$Function)
                                                 (_.ALOAD arity)
                                                 (_.INVOKEVIRTUAL //.$Function apply_method (apply_signature 1))
                                                 _.ARETURN)))))
                   (list& ($d.abstract_method #$.Public $.noneM apply_method (apply_signature 1)))
                   $d.fuse)
        $Object (type.class "java.lang.Object" (list))
        function_class (..reflection //.$Function)
        bytecode ($d.abstract #$.V1_6 #$.Public $.noneC function_class (list) $Object (list)
                              (|>> ($d.field #$.Public $.finalF partials_field type.int)
                                   ($d.method #$.Public $.noneM "<init>" (type.method [(list type.int) type.void (list)])
                                              (|>> (_.ALOAD 0)
                                                   (_.INVOKESPECIAL $Object "<init>" nullary_init_methodT)
                                                   (_.ALOAD 0)
                                                   (_.ILOAD 1)
                                                   (_.PUTFIELD //.$Function partials_field type.int)
                                                   _.RETURN))
                                   applyI))
        directive [function_class bytecode]]
    (do phase.monad
      [_ (generation.execute! directive)
       _ (generation.save! ..function_id directive)]
      (wrap [..function_id bytecode]))))

(def: #export translate
  (Operation [Registry Output])
  (do phase.monad
    [runtime_payload ..translate_runtime
     function_payload ..translate_function]
    (wrap [(|> artifact.empty
               artifact.resource
               product.right
               artifact.resource
               product.right)
           (row.row runtime_payload
                    function_payload)])))
