(.module:
  [library
   [lux "*"
    [tool
     [compiler
      [language
       [lux
        ["." synthesis]
        [phase
         ["." extension]]]]]]]]
  [luxc
   [lang
    [host
     [jvm {"+" [Phase]}]]]]
  [//
   ["." common]
   ["." primitive]
   ["." structure]
   ["." reference]
   ["." case]
   ["." loop]
   ["." function]])

(def: .public (translate archive synthesis)
  Phase
  (case synthesis
    (^ (synthesis.bit value))
    (primitive.bit value)
    
    (^ (synthesis.i64 value))
    (primitive.i64 value)
    
    (^ (synthesis.f64 value))
    (primitive.f64 value)
    
    (^ (synthesis.text value))
    (primitive.text value)

    (^ (synthesis.variant data))
    (structure.variant translate archive data)

    (^ (synthesis.tuple members))
    (structure.tuple translate archive members)

    (^ (synthesis.variable variable))
    (reference.variable archive variable)

    (^ (synthesis.constant constant))
    (reference.constant archive constant)

    (^ (synthesis.branch/let data))
    (case.let translate archive data)

    (^ (synthesis.branch/if data))
    (case.if translate archive data)

    (^ (synthesis.branch/get data))
    (case.get translate archive data)

    (^ (synthesis.branch/case data))
    (case.case translate archive data)

    (^ (synthesis.loop/recur data))
    (loop.recur translate archive data)

    (^ (synthesis.loop/scope data))
    (loop.scope translate archive data)

    (^ (synthesis.function/apply data))
    (function.call translate archive data)

    (^ (synthesis.function/abstraction data))
    (function.function translate archive data)

    (#synthesis.Extension extension)
    (extension.apply archive translate extension)))
