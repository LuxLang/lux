... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except or and)
   [data
    ["[0]" text
     ["%" \\injection (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]]])

(the .public (quote text)
  (-> Text Text)
  (format text.double_quote text text.double_quote))

(the .public (block content)
  (-> Text Text)
  (format content text.new_line text.new_line))

(the .public (plural singular)
  (-> Text Text)
  (format singular "(s)"))

(the .public (sentence content)
  (-> Text Text)
  (format content "."))

(the .public paragraph
  (-> (List Text) Text)
  (|>> (list#each ..sentence)
       (text.interposed text.new_line)))

(with_template [<name> <word>]
  [(the .public <name>
     (-> (List Text) Text)
     ... https://en.wikipedia.org/wiki/Serial_comma
     (text.interposed (format ", " <word> " ")))]

  [or     "or"]
  [and    "and"]
  [and/or "and/or"]
  )

(every .public Section
  (Record
   [#title Text
    #content Text]))

(the .public (section value)
  (-> Section Text)
  (format (..block (its #title value))
          (its #content value)))
