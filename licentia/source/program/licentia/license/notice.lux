(.module:
  [library
   [lux #*
    [data
     ["." text
      ["%" format (#+ format)]]
     [collection
      ["." list ("#\." functor)]]]
    [math
     [number
      ["n" nat]]]]]
  ["." // #_
   ["#." time]
   ["#." copyright]
   ["_" term]
   [//
    ["$" document]]])

(def: .public end_of_license
  ($.sentence "END OF TERMS AND CONDITIONS"))

(def: .public (copyright_holder holder)
  (-> //copyright.Holder Text)
  (let [(^slots [#//time.start #//time.end]) (value@ #//copyright.period holder)
        single_year? (n.= start end)
        period_section (if single_year?
                         (%.nat start)
                         (format (%.nat start) "-" (%.nat end)))]
    (format "Copyright (C) " period_section " " (value@ #//copyright.name holder))))

(def: .public copyright
  (-> (List //copyright.Holder) Text)
  (|>> (list\each ..copyright_holder)
       (text.interposed text.new_line)))
