(.using
 [library
  [lux (.full)
   [data
    ["[0]" text
     ["%" format (.only format)]]]]]
 ["[0]" // (.only Commercial)
  ["_" term]
  [//
   ["$" document]]])

(def: .public cannot_sell
  (let [preamble (format "Without limiting other conditions in " _.license)
        direct_condition (format "the grant of rights under " _.license
                                 " will not include, and does not grant to " _.recipient
                                 ", the right to " _.sell " " _.licensed_work)
        derivative_condition (format "or any " _.derivative_work)]
    ($.sentence (format preamble
                        ", " direct_condition
                        ", " derivative_condition))))

(def: .public require_contributor_attribution
  ($.sentence (format "All advertising materials mentioning features or use of " _.licensed_work
                      " must include an acknowledgement of the authorship of every " _.contributor)))

(def: .public disallow_contributor_endorsement
  (let [subject (format "The name of no " _.contributor)
        capability "endorse or promote products"
        source (format "any " _.extension)
        condition "specific prior written permission"]
    ($.sentence (format subject " may be used to " capability " derived from " source " without " condition))))
