... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Definition)
   [control
    ["[0]" maybe (.use "[1]#[0]" functor)]]
   [data
    ["[0]" bit]
    ["[0]" text
     ["%" \\injection (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor monoid)]]]]]
 [//
  ["[0]" license (.only Identification
                        Termination
                        Liability
                        Distribution
                        Commercial
                        Extension
                        Entity Black_List
                        URL Attribution
                        License)
   ["[0]" copyright]
   ["[0]" definition (.only Definition)]
   ["[0]" grant]
   ["[0]" limitation]
   ["[0]" assurance]
   ["[0]" liability]
   ["[0]" distribution]
   ["[0]" commercial]
   ["[0]" extension]
   ["[0]" submission]
   ["[0]" miscellaneous]
   ["[0]" black_list]
   ["[0]" notice]
   ["_" term]
   ["[0]" addendum]]
  ["$" document]])

(the .public (definition value)
  (-> Definition Text)
  (format ($.quote (its definition.#term value)) ": " (its definition.#meaning value)))

(the .public (identification value)
  (-> Identification Text)
  (format (its license.#name value) text.new_line
          (its license.#version value)))

(the .public (grant termination)
  (-> Termination Text)
  (`` (format (,, (with_template [<title> <content>]
                    [($.block ($.section [#$.title <title>
                                          #$.content <content>]))]
                    
                    ["Grant of Copyright License"
                     grant.copyright]
                    
                    ["Grant of Patent License"
                     (grant.patent (its license.#patent_retaliation? termination))]
                    
                    ["Effective Date for the Grants"
                     grant.date]

                    ["Grant Termination"
                     (grant.termination (its license.#termination_period termination)
                                        (its license.#grace_period termination))]

                    ["No Retroactive Effect of Termination"
                     grant.no_retroactive_termination])))))

(the .public limitation
  Text
  (`` (format (,, (with_template [<title> <content>]
                    [($.block ($.section [#$.title <title>
                                          #$.content <content>]))]
                    
                    ["Limitations on Grant Scope"
                     limitation.grant]
                    
                    ["Limitations on Trademarks"
                     limitation.trademark]

                    [(format "Limitations on " ($.plural _.secondary_license))
                     limitation.secondary_licenses])))))

(the .public assurance
  Text
  (`` (format (,, (with_template [<title> <content>]
                    [($.block ($.section [#$.title <title>
                                          #$.content <content>]))]
                    
                    ["Representation"
                     assurance.representation]
                    
                    ["Fair Use"
                     assurance.fair_use])))))

(the .public (liability value)
  (-> Liability Text)
  (`` (format (,, (with_template [<title> <condition> <content>]
                    [(if <condition>
                       ($.block ($.section [#$.title <title>
                                            #$.content <content>]))
                       "")]

                    ["Disclaimer of Warranty"
                     bit.on
                     liability.warranty]
                    
                    ["Limitation of Liability"
                     bit.on
                     liability.limitation]
                    
                    ["Litigation"
                     bit.on
                     liability.litigation]
                    
                    ["Accepting Warranty or Additional Liability"
                     (its license.#can_accept? value)
                     liability.can_accept]

                    ["High Risk Activities"
                     (its license.#disclaim_high_risk? value)
                     liability.disclaim_high_risk])))))

(the .public (distribution distribution)
  (-> Distribution Text)
  (`` (format (,, (with_template [<title> <condition> <content>]
                    [(if <condition>
                       ($.block ($.section [#$.title <title>
                                            #$.content <content>]))
                       "")]
                    
                    [(format "Distribution of a " _.source_code_form)
                     bit.on
                     distribution.source_code_form]
                    
                    [(format "Distribution of an " _.object_form)
                     bit.on
                     distribution.object_form]
                    
                    [(format "Distribution of an " _.extension)
                     (or (its license.#can_re_license? distribution)
                         (its license.#can_multi_license? distribution))
                     (distribution.extension distribution)])))))

(the .public (commercial value)
  (-> Commercial Text)
  (`` (format (,, (with_template [<title> <condition> <content>]
                    [(if <condition>
                       ($.block ($.section [#$.title <title>
                                            #$.content <content>]))
                       "")]

                    ["Non-Commerciality"
                     (not (its license.#can_sell? value))
                     commercial.cannot_sell]

                    [(format _.contributor " Attribution")
                     (its license.#require_contributor_credit? value)
                     commercial.require_contributor_attribution]

                    [(format _.contributor " Endorsement")
                     (not (its license.#allow_contributor_endorsement? value))
                     commercial.disallow_contributor_endorsement]
                    )))))

(the .public (extension value)
  (-> Extension Text)
  (let [[show? document] (when (its license.#notification_period value)
                           {.#Some period}
                           [true (extension.notification_requirement period)]

                           {.#None}
                           [false ""])]
    (`` (format (,, (with_template [<condition> <title> <content>]
                      [(if <condition>
                         ($.block ($.section [#$.title <title>
                                              #$.content <content>]))
                         "")]

                      [(its license.#same_license? value) "License Retention"
                       ($.paragraph (list#composite extension.sharing_requirement
                                                    extension.license_conflict_resolution))]
                      
                      [(its license.#must_be_distinguishable? value) (format _.extension " Distinctness")
                       extension.distinctness_requirement]
                      
                      [show? (format _.source_code_form " Availability")
                       document]
                      
                      [(its license.#must_describe_modifications? value) (format "Description of " ($.plural _.modification))
                       extension.description_requirement]))))))

(the .public (attribution value)
  (-> Attribution Text)
  (let [copyright_notice (format "Attribution Copyright Notice: " (its license.#copyright_notice value))
        phrase (when (its license.#phrase value)
                 {.#Some phrase}
                 (format text.new_line "Attribution Phrase: " phrase text.new_line)

                 {.#None}
                 "")
        url (format text.new_line "Attribution URL: " (its license.#url value))
        image (when (its license.#image value)
                {.#Some image}
                (format text.new_line "Attribution Image: " image)

                {.#None}
                "")]
    (format copyright_notice
            phrase
            url
            image)))

(the .public (miscellaneous identified?)
  (-> Bit Text)
  (`` (format (,, (with_template [<title> <condition> <content>]
                    [(if <condition>
                       ($.block ($.section [#$.title <title>
                                            #$.content <content>]))
                       "")]

                    ["Entire Agreement"
                     bit.on
                     miscellaneous.entire_agreement]

                    ["Relationship of Parties"
                     bit.on
                     miscellaneous.relationship_of_parties]

                    ["Independent Development"
                     bit.on
                     miscellaneous.independent_development]

                    ["Consent To Breach Not Waiver"
                     bit.on
                     miscellaneous.not_waiver]
                    
                    ["Severability"
                     bit.on
                     miscellaneous.severability]

                    ["Export Restrictions"
                     bit.on
                     miscellaneous.export_restrictions]

                    [(format "Versions of " _.license)
                     identified?
                     miscellaneous.new_versions]
                    )))))

(the black_list_spacing (format text.new_line text.new_line))

(the .public (license value)
  (-> License Text)
  (let [identification (|> value
                           (its license.#identification)
                           (maybe#each ..identification)
                           (maybe.default ""))
        identified? (when (its license.#identification value)
                      {.#Some _}
                      true

                      {.#None}
                      false)]
    (`` (format ($.block identification)
                ($.block (notice.copyright (its license.#copyright_holders value)))
                
                (when (its license.#black_lists value)
                  {.#End}
                  ""

                  black_lists
                  ($.block ($.section [#$.title (format "Denial of " _.license)
                                       #$.content (|> black_lists
                                                      (list#each black_list.black_list)
                                                      (text.interposed ..black_list_spacing))])))
                
                ($.section [#$.title "Definitions"
                            #$.content (|> definition.all
                                           (list#each (|>> ..definition $.block))
                                           (text.interposed ""))])

                ($.block ($.section [#$.title (format "Acceptance of " _.license)
                                     #$.content limitation.acceptance]))

                (..grant (its license.#termination value))
                ..limitation
                ..assurance

                ($.block ($.section [#$.title (format _.submission " of " ($.plural _.contribution))
                                     #$.content submission.contribution]))

                (..liability (its license.#liability value))
                (..distribution (its license.#distribution value))
                (..commercial (its license.#commercial value))
                (..extension (its license.#extension value))

                (|> value
                    (its license.#attribution)
                    (maybe#each (|>> ..attribution
                                     ["Attribution Information"]
                                     $.section
                                     $.block))
                    (maybe.default ""))

                (..miscellaneous identified?)

                (addendum.output (its license.#addendum value))
                
                notice.end_of_license
                ))))
