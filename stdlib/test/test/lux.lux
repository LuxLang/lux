(;module:
  lux
  lux/test
  (lux (control ["M" monad #+ do Monad])
       [io]
       [math]
       ["r" math/random]
       (data [maybe]
             [text "text/" Eq<Text>]
             text/format)
       [macro]
       (macro ["s" syntax #+ syntax:])))

(context: "Value identity."
  (<| (times +100)
      (do @
        [size (|> r;nat (:: @ map (|>. (n/% +100) (n/max +10))))
         x (r;text size)
         y (r;text size)]
        ($_ seq
            (test "Every value is identical to itself, and the 'id' function doesn't change values in any way."
                  (and (is x x)
                       (is x (id x))))

            (test "Values created separately can't be identical."
                  (not (is x y)))
            ))))

(do-template [category rand-gen inc dec even? odd? = < >]
  [(context: (format "[" category "] " "Moving up-down or down-up should result in same value.")
     (<| (times +100)
         (do @
           [value rand-gen]
           (test "" (and (|> value inc dec (= value))
                         (|> value dec inc (= value)))))))

   (context: (format "[" category "] " "(x+1) > x && (x-1) < x")
     (<| (times +100)
         (do @
           [value rand-gen]
           (test "" (and (|> value inc (> value))
                         (|> value dec (< value)))))))

   (context: (format "[" category "] " "Every odd/even number is surrounded by two of the other kind.")
     (<| (times +100)
         (do @
           [value rand-gen]
           (test ""
                 (if (even? value)
                   (and (|> value inc odd?)
                        (|> value dec odd?))
                   (and (|> value inc even?)
                        (|> value dec even?)))))))]

  ["Nat" r;nat n/inc n/dec n/even? n/odd? n/= n/< n/>]
  ["Int" r;int i/inc i/dec i/even? i/odd? i/= i/< i/>]
  )

(do-template [category rand-gen = < > <= >= min max]
  [(context: (format "[" category "] " "The symmetry of numerical comparisons.")
     (<| (times +100)
         (do @
           [x rand-gen
            y rand-gen]
           (test ""
                 (or (= x y)
                     (if (< y x)
                       (> x y)
                       (< x y)))))))

   (context: (format "[" category "] " "Minimums and maximums.")
     (<| (times +100)
         (do @
           [x rand-gen
            y rand-gen]
           (test ""
                 (and (and (<= x (min x y))
                           (<= y (min x y)))
                      (and (>= x (max x y))
                           (>= y (max x y)))
                      )))))]

  ["Int"  r;int  i/= i/< i/> i/<= i/>= i/min i/max]
  ["Nat"  r;nat  n/= n/< n/> n/<= n/>= n/min n/max]
  ["Frac" r;frac f/= f/< f/> f/<= f/>= f/min f/max]
  ["Deg"  r;deg  d/= d/< d/> d/<= d/>= d/min d/max]
  )

(do-template [category rand-gen = + - * / <%> > <0> <1> <factor> %x <cap> <prep>]
  [(context: (format "[" category "] " "Additive identity")
     (<| (times +100)
         (do @
           [x rand-gen]
           (test ""
                 (and (|> x (+ <0>) (= x))
                      (|> x (- <0>) (= x)))))))

   (context: (format "[" category "] " "Addition & Substraction")
     (<| (times +100)
         (do @
           [x (:: @ map <prep> rand-gen)
            y (:: @ map <prep> rand-gen)
            #let [x (* <factor> x)
                  y (* <factor> y)]]
           (test ""
                 (and (|> x (- y) (+ y) (= x))
                      (|> x (+ y) (- y) (= x)))))))

   (context: (format "[" category "] " "Multiplicative identity")
     (<| (times +100)
         (do @
           [x rand-gen]
           (test ""
                 ## Skip this test for Deg
                 ## because Deg division loses the last
                 ## 32 bits of precision.
                 (or (text/= "Deg" category)
                     (and (|> x (* <1>) (= x))
                          (|> x (/ <1>) (= x))))))))

   (context: (format "[" category "] " "Multiplication & Division")
     (<| (times +100)
         (do @
           [x (:: @ map <cap> rand-gen)
            y (|> rand-gen
                  (:: @ map <cap>)
                  (r;filter (|>. (= <0>) not)))
            #let [r (<%> y x)
                  x' (- r x)]]
           (test ""
                 ## Skip this test for Deg
                 ## because Deg division loses the last
                 ## 32 bits of precision.
                 (or (text/= "Deg" category)
                     (or (> x' y)
                         (|> x' (/ y) (* y) (= x'))))
                 ))))]

  ["Nat"  r;nat  n/= n/+ n/- n/* n// n/% n/> +0  +1              +1000000         %n (n/% +1000) id]
  ["Int"  r;int  i/= i/+ i/- i/* i// i/% i/>  0   1               1000000         %i (i/%  1000) id]
  ["Frac" r;frac f/= f/+ f/- f/* f// f/% f/>  0.0 1.0             1000000.0       %r id          math;floor]
  ["Deg"  r;deg  d/= d/+ d/- d/* d// d/% d/>   .0 ("lux deg max") ("lux deg max") %f id          id]
  )

(do-template [category rand-gen -> <- = <cap> %a %z]
  [(context: (format "[" category "] " "Numeric conversions")
     (<| (times +100)
         (do @
           [value rand-gen
            #let [value (<cap> value)]]
           (test ""
                 (|> value -> <- (= value))))))]

  ["Int->Nat"  r;int  int-to-nat  nat-to-int  i/= (i/%  1000000) %i %n]
  ["Nat->Int"  r;nat  nat-to-int  int-to-nat  n/= (n/% +1000000) %n %i]
  ["Int->Frac" r;int  int-to-frac frac-to-int i/= (i/%  1000000) %i %r]
  ["Frac->Int" r;frac frac-to-int int-to-frac f/= math;floor     %r %i]
  ## [r;frac frac-to-deg deg-to-frac f/= (f/% 1.0) %r %f]
  )

(context: "Simple macros and constructs"
  ($_ seq
      (test "Can write easy loops for iterative programming."
            (i/= 1000
                 (loop [counter 0
                        value 1]
                   (if (i/< 3 counter)
                     (recur (i/inc counter) (i/* 10 value))
                     value))))

      (test "Can create lists easily through macros."
            (and (case (list 1 2 3)
                   (#;Cons 1 (#;Cons 2 (#;Cons 3 #;Nil)))
                   true

                   _
                   false)
                 
                 (case (list& 1 2 3 (list 4 5 6))
                   (#;Cons 1 (#;Cons 2 (#;Cons 3 (#;Cons 4 (#;Cons 5 (#;Cons 6 #;Nil))))))
                   true

                   _
                   false)))

      (test "Can have defaults for Maybe values."
            (and (is "yolo" (maybe;default "yolo"
                                           #;None))
                 
                 (is "lol" (maybe;default "yolo"
                                          (#;Some "lol")))))
      ))

(template: (hypotenuse x y)
  (i/+ (i/* x x) (i/* y y)))

(context: "Templates."
  (<| (times +100)
      (do @
        [x r;int
         y r;int]
        (test "Template application is a stand-in for the templated code."
              (i/= (i/+ (i/* x x) (i/* y y))
                   (hypotenuse x y))))))

(context: "Cross-platform support."
  ($_ seq
      (test "Can provide default in case there is no particular platform support."
            (for {"" false}
                 true))
      (test "Can pick code depending on the platform being targeted."
            (for {"JVM" true
                  "JS" true}
                 false))))
