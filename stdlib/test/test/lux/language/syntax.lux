(.module:
  [lux #*
   [control
    [monad (#+ do)]]
   [data
    [number]
    ["e" error]
    ["." text
     format
     ["l" lexer]]
    [collection
     ["." list]
     ["dict" dictionary (#+ Dictionary)]]]
   [math
    ["r" random ("r/" Monad<Random>)]]
   [macro
    ["." code]]
   [language
    ["&" syntax]]
   test])

(def: default-cursor
  Cursor
  {#.module ""
   #.line   +0
   #.column +0})

(def: ident-part^
  (r.Random Text)
  (do r.Monad<Random>
    [#let [digits "0123456789"
           delimiters "()[]{}#.\""
           space "\t\v \n\r\f"
           invalid-range (format digits delimiters space)
           char-gen (|> r.nat
                        (:: @ map (|>> (n/% +256) (n/max +1)))
                        (r.filter (function (_ sample)
                                    (not (text.contains? (text.from-code sample)
                                                         invalid-range)))))]
     size (|> r.nat (:: @ map (|>> (n/% +20) (n/max +1))))]
    (r.text char-gen size)))

(def: ident^
  (r.Random Ident)
  (r.seq ident-part^ ident-part^))

(def: code^
  (r.Random Code)
  (let [numeric^ (: (r.Random Code)
                    ($_ r.either
                        (|> r.bit (r/map code.bit))
                        (|> r.nat (r/map code.nat))
                        (|> r.int (r/map code.int))
                        (|> r.rev (r/map code.rev))
                        (|> r.frac (r/map code.frac))))
        textual^ (: (r.Random Code)
                    ($_ r.either
                        (do r.Monad<Random>
                          [size (|> r.nat (r/map (n/% +20)))]
                          (|> (r.unicode size) (r/map code.text)))
                        (|> ident^ (r/map code.symbol))
                        (|> ident^ (r/map code.tag))))
        simple^ (: (r.Random Code)
                   ($_ r.either
                       numeric^
                       textual^))]
    (r.rec
     (function (_ code^)
       (let [multi^ (do r.Monad<Random>
                      [size (|> r.nat (r/map (n/% +3)))]
                      (r.list size code^))
             composite^ (: (r.Random Code)
                           ($_ r.either
                               (|> multi^ (r/map code.form))
                               (|> multi^ (r/map code.tuple))
                               (do r.Monad<Random>
                                 [size (|> r.nat (r/map (n/% +3)))]
                                 (|> (r.list size (r.seq code^ code^))
                                     (r/map code.record)))))]
         (r.either simple^
                   composite^))))))

(context: "Lux code syntax."
  (<| (times +100)
      (do @
        [sample code^
         other code^]
        ($_ seq
            (test "Can parse Lux code."
                  (case (&.read "" (dict.new text.Hash<Text>)
                                [default-cursor +0 (code.to-text sample)])
                    (#e.Error error)
                    #0

                    (#e.Success [_ parsed])
                    (:: code.Equivalence<Code> = parsed sample)))
            (test "Can parse Lux multiple code nodes."
                  (case (&.read "" (dict.new text.Hash<Text>)
                                [default-cursor +0 (format (code.to-text sample) " "
                                                           (code.to-text other))])
                    (#e.Error error)
                    #0

                    (#e.Success [remaining =sample])
                    (case (&.read "" (dict.new text.Hash<Text>)
                                  remaining)
                      (#e.Error error)
                      #0

                      (#e.Success [_ =other])
                      (and (:: code.Equivalence<Code> = sample =sample)
                           (:: code.Equivalence<Code> = other =other)))))
            ))))

(context: "Frac special syntax."
  (<| (times +100)
      (do @
        [numerator (|> r.nat (:: @ map (|>> (n/% +100) .int int-to-frac)))
         denominator (|> r.nat (:: @ map (|>> (n/% +100) (n/max +1) .int int-to-frac)))
         signed? r.bit
         #let [expected (|> numerator (f// denominator) (f/* (if signed? -1.0 1.0)))]]
        (test "Can parse frac ratio syntax."
              (case (&.read "" (dict.new text.Hash<Text>)
                            [default-cursor +0
                             (format (if signed? "-" "")
                                     (%i (frac-to-int numerator))
                                     "/"
                                     (%i (frac-to-int denominator)))])
                (#e.Success [_ [_ (#.Frac actual)]])
                (f/= expected actual)

                _
                #0)
              ))))

(context: "Nat special syntax."
  (<| (times +100)
      (do @
        [expected (|> r.nat (:: @ map (n/% +1_000)))]
        (test "Can parse nat char syntax."
              (case (&.read "" (dict.new text.Hash<Text>)
                            [default-cursor +0
                             (format "#" (%t (text.from-code expected)) "")])
                (#e.Success [_ [_ (#.Nat actual)]])
                (n/= expected actual)

                _
                #0)
              ))))

(def: comment-text^
  (r.Random Text)
  (let [char-gen (|> r.nat (r.filter (function (_ value)
                                       (not (or (text.space? value)
                                                (n/= (char "#") value)
                                                (n/= (char "(") value)
                                                (n/= (char ")") value))))))]
    (do r.Monad<Random>
      [size (|> r.nat (r/map (n/% +20)))]
      (r.text char-gen size))))

(def: comment^
  (r.Random Text)
  (r.either (do r.Monad<Random>
              [comment comment-text^]
              (wrap (format "## " comment "\n")))
            (r.rec (function (_ nested^)
                     (do r.Monad<Random>
                       [comment (r.either comment-text^
                                          nested^)]
                       (wrap (format "#( " comment " )#")))))))

(context: "Multi-line text & comments."
  (<| (seed +12137892244981970631)
      ## (times +100)
      (do @
        [#let [char-gen (|> r.nat (r.filter (function (_ value)
                                              (not (or (text.space? value)
                                                       (n/= (char "\"") value))))))]
         x char-gen
         y char-gen
         z char-gen
         offset-size (|> r.nat (r/map (|>> (n/% +10) (n/max +1))))
         #let [offset (text.join-with "" (list.repeat offset-size " "))]
         sample code^
         comment comment^
         unbalanced-comment comment-text^]
        ($_ seq
            (test "Will reject invalid multi-line text."
                  (let [bad-match (format (text.from-code x) "\n"
                                          (text.from-code y) "\n"
                                          (text.from-code z))]
                    (case (&.read "" (dict.new text.Hash<Text>)
                                  [default-cursor +0
                                   (format "\"" bad-match "\"")])
                      (#e.Error error)
                      #1

                      (#e.Success [_ parsed])
                      #0)))
            (test "Will accept valid multi-line text"
                  (let [good-input (format (text.from-code x) "\n"
                                           offset (text.from-code y) "\n"
                                           offset (text.from-code z))
                        good-output (format (text.from-code x) "\n"
                                            (text.from-code y) "\n"
                                            (text.from-code z))]
                    (case (&.read "" (dict.new text.Hash<Text>)
                                  [(|> default-cursor (update@ #.column (n/+ (dec offset-size))))
                                   +0
                                   (format "\"" good-input "\"")])
                      (#e.Error error)
                      #0

                      (#e.Success [_ parsed])
                      (:: code.Equivalence<Code> =
                          parsed
                          (code.text good-output)))))
            (test "Can handle comments."
                  (case (&.read "" (dict.new text.Hash<Text>)
                                [default-cursor +0
                                 (format comment (code.to-text sample))])
                    (#e.Error error)
                    #0

                    (#e.Success [_ parsed])
                    (:: code.Equivalence<Code> = parsed sample)))
            (test "Will reject unbalanced multi-line comments."
                  (and (case (&.read "" (dict.new text.Hash<Text>)
                                     [default-cursor +0
                                      (format "#(" "#(" unbalanced-comment ")#"
                                              (code.to-text sample))])
                         (#e.Error error)
                         #1

                         (#e.Success [_ parsed])
                         #0)
                       (case (&.read "" (dict.new text.Hash<Text>)
                                     [default-cursor +0
                                      (format "#(" unbalanced-comment ")#" ")#"
                                              (code.to-text sample))])
                         (#e.Error error)
                         #1

                         (#e.Success [_ parsed])
                         #0)))
            ))))
