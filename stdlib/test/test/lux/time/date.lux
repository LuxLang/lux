(.module:
  [lux #*
   [io]
   [control
    [monad (#+ do Monad)]
    [pipe]]
   [data
    [error]]
   [math
    ["r" random ("random/" Monad<Random>)]]
   [time
    ["@." instant]
    ["@" date]]]
  lux/test
  [test [lux [time ["_." instant]]]])

(def: month
  (r.Random @.Month)
  (r.either (r.either (r.either (random/wrap #@.January)
                                (r.either (random/wrap #@.February)
                                          (random/wrap #@.March)))
                      (r.either (random/wrap #@.April)
                                (r.either (random/wrap #@.May)
                                          (random/wrap #@.June))))
            (r.either (r.either (random/wrap #@.July)
                                (r.either (random/wrap #@.August)
                                          (random/wrap #@.September)))
                      (r.either (random/wrap #@.October)
                                (r.either (random/wrap #@.November)
                                          (random/wrap #@.December))))))

(context: "(Month) Equivalence."
  (<| (times +100)
      (do @
        [sample month
         #let [(^open "@/") @.Equivalence<Month>]]
        (test "Every value equals itself."
              (@/= sample sample)))))

(context: "(Month) Order."
  (<| (times +100)
      (do @
        [reference month
         sample month
         #let [(^open "@/") @.Order<Month>]]
        (test "Valid Order."
              (and (or (@/< reference sample)
                       (@/>= reference sample))
                   (or (@/> reference sample)
                       (@/<= reference sample)))))))

(context: "(Month) Enum."
  (<| (times +100)
      (do @
        [sample month
         #let [(^open "@/") @.Enum<Month>]]
        (test "Valid Enum."
              (and (not (@/= (@/succ sample)
                             sample))
                   (not (@/= (@/pred sample)
                             sample))
                   (|> sample @/succ @/pred (@/= sample))
                   (|> sample @/pred @/succ (@/= sample)))))))

(def: day
  (r.Random @.Day)
  (r.either (r.either (r.either (random/wrap #@.Sunday)
                                (random/wrap #@.Monday))
                      (r.either (random/wrap #@.Tuesday)
                                (random/wrap #@.Wednesday)))
            (r.either (r.either (random/wrap #@.Thursday)
                                (random/wrap #@.Friday))
                      (random/wrap #@.Saturday))))

(context: "(Day) Equivalence."
  (<| (times +100)
      (do @
        [sample day
         #let [(^open "@/") @.Equivalence<Day>]]
        (test "Every value equals itself."
              (@/= sample sample)))))

(context: "(Day) Order."
  (<| (times +100)
      (do @
        [reference day
         sample day
         #let [(^open "@/") @.Order<Day>]]
        (test "Valid Order."
              (and (or (@/< reference sample)
                       (@/>= reference sample))
                   (or (@/> reference sample)
                       (@/<= reference sample)))))))

(context: "(Day) Enum."
  (<| (times +100)
      (do @
        [sample day
         #let [(^open "@/") @.Enum<Day>]]
        (test "Valid Enum."
              (and (not (@/= (@/succ sample)
                             sample))
                   (not (@/= (@/pred sample)
                             sample))
                   (|> sample @/succ @/pred (@/= sample))
                   (|> sample @/pred @/succ (@/= sample)))))))

(def: #export date
  (r.Random @.Date)
  (|> _instant.instant (:: r.Monad<Random> map @instant.date)))

(context: "(Date) Equivalence."
  (<| (times +100)
      (do @
        [sample date
         #let [(^open "@/") @.Equivalence<Date>]]
        (test "Every value equals itself."
              (@/= sample sample)))))

(context: "(Date) Order."
  (<| (times +100)
      (do @
        [reference date
         sample date
         #let [(^open "@/") @.Order<Date>]]
        (test "Valid Order."
              (and (or (@/< reference sample)
                       (@/>= reference sample))
                   (or (@/> reference sample)
                       (@/<= reference sample)))))))

(context: "(Date) Codec"
  (<| (seed +6623983470548808292)
      ## (times +100)
      (do @
        [sample date
         #let [(^open "@/") @.Equivalence<Date>
               (^open "@/") @.Codec<Text,Date>]]
        (test "Can encode/decode dates."
              (|> sample
                  @/encode
                  @/decode
                  (pipe.case> (#error.Success decoded)
                              (@/= sample decoded)

                              (#error.Error error)
                              #0))))))
