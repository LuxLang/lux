(.module:
  lux
  (lux [io]
       (control [monad #+ do]
                pipe)
       (data [product]
             [maybe]
             [error]
             [number]
             text/format
             (coll [list "list/" Functor<List> Fold<List>]
                   (dictionary ["dict" unordered #+ Dict])
                   (set ["set" unordered])))
       (lang [".L" analysis #+ Variable Analysis "variable/" Eq<Variable>]
             ["//" synthesis #+ Arity Synthesis]
             (synthesis [".S" expression])
             [".L" extension])
       ["r" math/random]
       test)
  [//primitive])

(def: constant-function
  (r.Random [Arity Analysis Analysis])
  (r.rec
   (function (_ constant-function)
     (do r.Monad<Random>
       [function? r.bool]
       (if function?
         (do @
           [[arity bodyA predictionA] constant-function]
           (wrap [(inc arity)
                  (#analysisL.Function (list) bodyA)
                  predictionA]))
         (do @
           [predictionA //primitive.primitive]
           (wrap [+0 predictionA predictionA])))))))

(def: (pick scope-size)
  (-> Nat (r.Random Nat))
  (|> r.nat (:: r.Monad<Random> map (n/% scope-size))))

(def: function-with-environment
  (r.Random [Arity Analysis Variable])
  (do r.Monad<Random>
    [num-locals (|> r.nat (:: @ map (|>> (n/% +100) (n/max +10))))
     #let [indices (list.n/range +0 (dec num-locals))
           absolute-env (list/map (|>> #analysisL.Local) indices)
           relative-env (list/map (|>> #analysisL.Foreign) indices)]
     [arity bodyA predictionA] (: (r.Random [Arity Analysis Variable])
                                  (loop [arity +1
                                         global-env relative-env]
                                    (let [env-size (list.size global-env)
                                          resolver (list/fold (function (_ [idx var] resolver)
                                                                (dict.put idx var resolver))
                                                              (: (Dict Nat Variable)
                                                                 (dict.new number.Hash<Nat>))
                                                              (list.zip2 (list.n/range +0 (dec env-size))
                                                                         global-env))]
                                      (do @
                                        [nest? r.bool]
                                        (if nest?
                                          (do @
                                            [num-picks (:: @ map (n/max +1) (pick (inc env-size)))
                                             picks (|> (r.set number.Hash<Nat> num-picks (pick env-size))
                                                       (:: @ map set.to-list))
                                             [arity bodyA predictionA] (recur (inc arity)
                                                                              (list/map (function (_ pick)
                                                                                          (maybe.assume (list.nth pick global-env)))
                                                                                        picks))]
                                            (wrap [arity
                                                   (#analysisL.Function (list/map (|>> #analysisL.Foreign) picks)
                                                                        bodyA)
                                                   predictionA]))
                                          (do @
                                            [chosen (pick (list.size global-env))]
                                            (wrap [arity
                                                   (#analysisL.Variable (#analysisL.Foreign chosen))
                                                   (maybe.assume (dict.get chosen resolver))])))))))]
    (wrap [arity
           (#analysisL.Function absolute-env bodyA)
           predictionA])))

(def: local-function
  (r.Random [Arity Analysis Variable])
  (loop [arity +0
         nest? true]
    (if nest?
      (do r.Monad<Random>
        [nest?' r.bool
         [arity' bodyA predictionA] (recur (inc arity) nest?')]
        (wrap [arity'
               (#analysisL.Function (list) bodyA)
               predictionA]))
      (do r.Monad<Random>
        [chosen (|> r.nat (:: @ map (|>> (n/% +100) (n/max +2))))]
        (wrap [arity
               (#analysisL.Variable (#analysisL.Local chosen))
               (|> chosen (n/+ (dec arity)) #analysisL.Local)])))))

(context: "Function definition."
  (<| (times +100)
      (do @
        [[arity//constant function//constant prediction//constant] constant-function
         [arity//environment function//environment prediction//environment] function-with-environment
         [arity//local function//local prediction//local] local-function]
        ($_ seq
            (test "Nested functions will get folded together."
                  (|> function//constant
                      (//.run (expressionS.synthesizer extensionL.empty))
                      (case> (^ (#error.Success (//.function/abstraction [environment arity output])))
                             (and (n/= arity//constant arity)
                                  (//primitive.corresponds? prediction//constant output))
                             
                             _
                             (n/= +0 arity//constant))))
            (test "Folded functions provide direct access to environment variables."
                  (|> function//environment
                      (//.run (expressionS.synthesizer extensionL.empty))
                      (case> (^ (#error.Success (//.function/abstraction [environment arity (#//.Variable output)])))
                             (and (n/= arity//environment arity)
                                  (variable/= prediction//environment output))
                             
                             _
                             false)))
            (test "Folded functions properly offset local variables."
                  (|> function//local
                      (//.run (expressionS.synthesizer extensionL.empty))
                      (case> (^ (#error.Success (//.function/abstraction [environment arity (#//.Variable output)])))
                             (and (n/= arity//local arity)
                                  (variable/= prediction//local output))
                             
                             _
                             false)))
            ))))

(context: "Function application."
  (<| (times +100)
      (do @
        [arity (|> r.nat (:: @ map (|>> (n/% +10) (n/max +1))))
         funcA //primitive.primitive
         argsA (r.list arity //primitive.primitive)]
        ($_ seq
            (test "Can synthesize function application."
                  (|> (analysisL.apply [funcA argsA])
                      (//.run (expressionS.synthesizer extensionL.empty))
                      (case> (^ (#error.Success (//.function/apply [funcS argsS])))
                             (and (//primitive.corresponds? funcA funcS)
                                  (list.every? (product.uncurry //primitive.corresponds?)
                                               (list.zip2 argsA argsS)))
                             
                             _
                             false)))
            (test "Function application on no arguments just synthesizes to the function itself."
                  (|> (analysisL.apply [funcA (list)])
                      (//.run (expressionS.synthesizer extensionL.empty))
                      (case> (#error.Success funcS)
                             (//primitive.corresponds? funcA funcS)

                             _
                             false)))
            ))))
