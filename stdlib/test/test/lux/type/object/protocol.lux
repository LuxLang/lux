(.module:
  lux
  (lux (data text/format)
       (type (object protocol))))

(type: Counter (Object (Method Any Nat)))

(def: (count [tick return] state)
  (Class Nat (Method Any Nat))
  (let [state' (inc state)]
    [(return state') state']))

(def: counter
  (-> Nat Counter)
  (object count))

(def: _test0
  [Nat Counter]
  ((counter +0) (message [])))

(protocol: Protocol0
  (method0 [Bool Nat Text] Bool)
  (method1 [Nat Text Bool] Nat)
  (method2 [Text Bool Nat] Text))

(type: Object0 (Object Protocol0))

(def: object0
  Object0
  (loop [num-calls +0]
    (function (_ message)
      [(case message
         (#method0 [arg0 arg1 arg2] output)
         (output (n/= +0 (n/% +2 num-calls)))
         
         (#method1 [arg0 arg1 arg2] output)
         (output num-calls)
         
         (#method2 [arg0 arg1 arg2] output)
         (output (%n num-calls)))
       (recur (inc num-calls))])))

(def: _test1
  [Nat Object0]
  (object0 (method1 [+0 "0" false])))

(protocol: (Read a)
  (read [] a))

(def: (readM [tick return] state)
  (All [s] (Class s (Method Any s)))
  [(return state) state])

(protocol: (Add n)
  (+ n Any)
  (- n Any))

(protocol: (Mul n)
  (* n Any)
  (/ n Any))

(do-template [<name> <op>]
  [(def: (<name> [diff return] state)
     (Class Nat (Method Nat Any))
     [(return []) (<op> diff state)])]

  [+M n/+]
  [-M n/-]
  [*M n/*]
  [/M n//]
  )

(def: addM
  (Class Nat (Add Nat))
  (alt +M -M))

(def: mulM
  (Class Nat (Mul Nat))
  (alt *M /M))

(type: (Number n)
  ($_ Alt
      (Read n)
      (Add n)
      (Mul n)))

## TODO: Fix when new-luxc is the official compiler.
## (protocol: (Number n)
##   (^read (Read n))
##   (^add (Add n))
##   (^mul (Mul n)))

(def: numberM
  (Class Nat (Number Nat))
  ($_ alt
      readM
      addM
      mulM))

(type: NatO (Object (Number Nat)))

(def: numberO
  NatO
  (object numberM +1))

(def: _test2
  [Nat NatO]
  (numberO (+0 (read []))))

(def: _test3
  [Any NatO]
  (numberO (+1 (+0 (+ +123)))))

(def: _test4
  [Any NatO]
  (numberO (+1 (+1 (* +123)))))

## TODO: Fix when new-luxc is the official compiler.
## (def: _test2
##   [Nat NatO]
##   (numberO (^read (read []))))

## (def: _test3
##   [Any NatO]
##   (numberO (^add (+ +123))))

## (def: _test4
##   [Any NatO]
##   (numberO (^mul (* +123))))
