(.module:
  [lux #*
   [control
    [monad
     [indexed (#+ do)]]]
   [type
    ["." resource (#+ Res)]]
   ["." io]]
  lux/test)

(context: "Sub-structural typing."
  ($_ seq
      (test "Can produce and consume keys in an ordered manner."
            (<| (n/= (n/+ +123 +456))
                io.run
                resource.run-sync
                (do resource.IxMonad<Sync>
                  [res|left (resource.ordered-sync +123)
                   res|right (resource.ordered-sync +456)
                   right (resource.read-sync res|right)
                   left (resource.read-sync res|left)]
                  (wrap (n/+ left right)))))
      
      (test "Can exchange commutative keys."
            (<| (n/= (n/+ +123 +456))
                io.run
                resource.run-sync
                (do resource.IxMonad<Sync>
                  [res|left (resource.commutative-sync +123)
                   res|right (resource.commutative-sync +456)
                   _ (resource.exchange-sync [+1 +0])
                   left (resource.read-sync res|left)
                   right (resource.read-sync res|right)]
                  (wrap (n/+ left right)))))

      (test "Can group and un-group keys."
            (<| (n/= (n/+ +123 +456))
                io.run
                resource.run-sync
                (do resource.IxMonad<Sync>
                  [res|left (resource.commutative-sync +123)
                   res|right (resource.commutative-sync +456)
                   _ (resource.group-sync +2)
                   _ (resource.un-group-sync +2)
                   right (resource.read-sync res|right)
                   left (resource.read-sync res|left)]
                  (wrap (n/+ left right)))))
      ))
