##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;module:
  lux
  (lux (codata [io])
       (control monad)
       (data text/format
             [text "Text/" Eq<Text>]
             [number]
             [product]
             [sum]
             (struct [list]))
       (codata function)
       ["&" cli]
       (math ["R" random])
       pipe)
  lux/test)

(test: "CLI"
  [num-args (:: @ map (%+ +10) R;nat)
   #let [(^open "Nat/") number;Codec<Text,Nat>
         gen-arg (:: @ map Nat/encode R;nat)]
   option-name (R;text +5)
   args (R;list num-args gen-arg)]
  ($_ seq
      (assert "Can read any argument."
              (|> (&;run &;any args)
                  (case> (#;Left _)
                         (=+ +0 num-args)
                         
                         (#;Right arg)
                         (and (not (=+ +0 num-args))
                              (Text/= arg (default (undefined)
                                            (list;head args)))))))

      (assert "Can safely fail parsing an argument."
              (|> (&;run (&;opt &;any) args)
                  (case> (#;Right (#;Some arg))
                         (and (not (=+ +0 num-args))
                              (Text/= arg (default (undefined)
                                            (list;head args))))

                         (#;Right #;None)
                         (=+ +0 num-args)

                         _
                         false)))
      
      (assert "Can read multiple arguments."
              (and (|> (&;run (&;some &;any) args)
                       (case> (#;Left _)
                              false
                              
                              (#;Right args')
                              (=+ num-args (list;size args'))))
                   (|> (&;run (&;many &;any) args)
                       (case> (#;Left _)
                              (=+ +0 num-args)
                              
                              (#;Right args')
                              (=+ num-args (list;size args'))))))

      (assert "Can use custom token parsers."
              (|> (&;run (&;parse Nat/decode) args)
                  (case> (#;Left _)
                         (=+ +0 num-args)
                         
                         (#;Right parsed)
                         (Text/= (Nat/encode parsed)
                                 (default (undefined)
                                   (list;head args))))))

      (assert "Can obtain option values."
              (and (|> (&;run (&;option (list option-name)) (list& option-name args))
                       (case> (#;Left _)
                              (=+ +0 num-args)
                              
                              (#;Right value)
                              (Text/= value (default (undefined)
                                              (list;head args)))))
                   (|> (&;run (&;option (list option-name)) args)
                       (case> (#;Left _) true (#;Right _) false))))

      (assert "Can check flags."
              (and (|> (&;run (&;flag (list option-name)) (list& option-name args))
                       (case> (#;Right true) true _ false))
                   (|> (&;run (&;flag (list option-name)) args)
                       (case> (#;Right false) true _ false))))

      (assert "Can query if there are any more inputs."
              (and (|> (&;run &;end args)
                       (case> (#;Right []) (=+ +0 num-args) _ false))
                   (|> (&;run (&;not &;end) args)
                       (case> (#;Right []) (not (=+ +0 num-args)) _ false))))
      ))
