(;module:
  lux
  (lux [io]
       (control monad)
       (data [text "Text/" Monoid<Text>]
             text/format
             [bool "b/" Eq<Bool>]
             [number "r/" Number<Real>]
             (coll [list "List/" Fold<List> Functor<List>])
             [product])
       ["R" math/random]
       ["&" math/simple])
  lux/test)

(do-template [<category> <generator> <=> <+> <-> <*> </> <%> <0>]
  [(context: (format <category> " arihtmetic")
     [x <generator>
      y (|> <generator> (R;filter (. not (<=> <0>))))]
     ($_ seq
         (test "Can add."
               (<=> (<+> y x) (&;+ y x)))
         (test "Can subtract."
               (<=> (<-> y x) (&;- y x)))
         (test "Can multiply."
               (<=> (<*> y x) (&;* y x)))
         (test "Can divide."
               (<=> (</> y x) (&;/ y x)))
         (test "Can get remainder."
               (<=> (<%> y x) (&;% y x)))
         ))]

  ["Nat"  R;nat  n.= n.+ n.- n.* n./ n.% +0]
  ["Int"  R;int  i.= i.+ i.- i.* i./ i.%  0]
  ["Real" R;real r.= r.+ r.- r.* r./ r.%  0.0]
  ["Deg"  R;deg  d.= d.+ d.- d.* d./ d.% .0]
  )

(do-template [<category> <generator> <lt> <lte> <gt> <gte>]
  [(context: (format <category> " comparisons")
     [x <generator>
      y <generator>]
     ($_ seq
         (test "<"
               (b/= (<lt> y x) (&;< y x)))
         (test "<="
               (b/= (<lte> y x) (&;<= y x)))
         (test ">"
               (b/= (<gt> y x) (&;> y x)))
         (test ">="
               (b/= (<gte> y x) (&;>= y x)))
         ))]

  ["Nat"  R;nat  n.< n.<= n.> n.>=]
  ["Int"  R;int  i.< i.<= i.> i.>=]
  ["Real" R;real r.< r.<= r.> r.>=]
  ["Deg" R;deg d.< d.<= d.> d.>=]
  )

(do-template [<category> <generator> <=> <min> <max>]
  [(context: (format <category> " min & max")
     [x <generator>
      y <generator>]
     ($_ seq
         (test "Min."
               (<=> (<min> y x) (&;min y x)))
         (test "Max."
               (<=> (<max> y x) (&;max y x)))
         ))]

  ["Nat" R;nat n.= n.min n.max]
  ["Int" R;int i.= i.min i.max]
  )

(do-template [<category> <generator> <=> <inc> <dec>]
  [(context: (format <category> " inc & dec")
     [x <generator>]
     ($_ seq
         (test "Inc."
               (<=> (<inc> x) (&;inc x)))
         (test "Dec."
               (<=> (<dec> x) (&;dec x)))
         ))]

  ["Nat" R;nat n.= n.inc n.dec]
  ["Int" R;int i.= i.inc i.dec]
  )

(do-template [<category> <generator> <even?> <odd?>]
  [(context: (format <category> " even & odd")
     [x <generator>]
     ($_ seq
         (test "Even."
               (b/= (<even?> x) (&;even? x)))
         (test "Odd."
               (b/= (<odd?> x) (&;odd? x)))
         ))]

  ["Nat"  R;nat n.even? n.odd?]
  ["Int"  R;int i.even? i.odd?]
  )
