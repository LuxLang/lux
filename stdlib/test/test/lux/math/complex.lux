##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;module:
  lux
  (lux (codata [io])
       (control monad)
       (data [text "Text/" Monoid<Text>]
             text/format
             [bool "b/" Eq<Bool>]
             [number "r/" Number<Real>]
             (struct [list "List/" Fold<List> Functor<List>])
             [product])
       (codata function)
       (math ["R" random])
       pipe
       ["&" math/complex])
  lux/test)

## Based on org.apache.commons.math4.complex.Complex
## https://github.com/apache/commons-math/blob/master/src/test/java/org/apache/commons/math4/complex/ComplexTest.java

(def: gen-dim
  (R;Random Real)
  (do R;Monad<Random>
    [factor R;nat
     measure R;real]
    (wrap (r.* (|> factor nat-to-int int-to-real)
               measure))))

(def: gen-complex
  (R;Random &;Complex)
  (do R;Monad<Random>
    [real gen-dim
     imaginary gen-dim]
    (wrap (&;complex real imaginary))))

(test: "Construction"
  [real gen-dim
   imaginary gen-dim]
  ($_ seq
      (assert "Can build and tear apart complex numbers"
              (let [r+i (&;complex real imaginary)]
                (and (r.= real (get@ #&;real r+i))
                     (r.= imaginary (get@ #&;imaginary r+i)))))

      (assert "If either the real part or the imaginary part is NaN, the composite is NaN."
              (and (&;nan? (&;complex number;nan imaginary))
                   (&;nan? (&;complex real number;nan))))
      ))

(test: "Absolute value"
  [real gen-dim
   imaginary gen-dim]
  ($_ seq
      (assert "Absolute value of complex >= absolute value of any of the parts."
              (let [r+i (&;complex real imaginary)
                    abs (&;c.abs r+i)]
                (and (r.>= (r/abs real) abs)
                     (r.>= (r/abs imaginary) abs))))

      (assert "The absolute value of a complex number involving a NaN on either dimension, results in a NaN value."
              (and (number;nan? (&;c.abs (&;complex number;nan imaginary)))
                   (number;nan? (&;c.abs (&;complex real number;nan)))))

      (assert "The absolute value of a complex number involving an infinity on either dimension, results in an infinite value."
              (and (r.= number;+inf (&;c.abs (&;complex number;+inf imaginary)))
                   (r.= number;+inf (&;c.abs (&;complex real number;+inf)))
                   (r.= number;+inf (&;c.abs (&;complex number;-inf imaginary)))
                   (r.= number;+inf (&;c.abs (&;complex real number;-inf)))))
      ))

(test: "Addidion, substraction, multiplication and division"
  [x gen-complex
   y gen-complex
   factor gen-dim]
  ($_ seq
      (assert "Adding 2 complex numbers is the same as adding their parts."
              (let [z (&;c.+ y x)]
                (and (&;c.= z
                            (&;complex (r.+ (get@ #&;real y)
                                            (get@ #&;real x))
                                       (r.+ (get@ #&;imaginary y)
                                            (get@ #&;imaginary x)))))))

      (assert "Subtracting 2 complex numbers is the same as adding their parts."
              (let [z (&;c.- y x)]
                (and (&;c.= z
                            (&;complex (r.- (get@ #&;real y)
                                            (get@ #&;real x))
                                       (r.- (get@ #&;imaginary y)
                                            (get@ #&;imaginary x)))))))

      (assert "Subtraction is the inverse of addition."
              (and (|> x (&;c.+ y) (&;c.- y) (&;c.= x))
                   (|> x (&;c.- y) (&;c.+ y) (&;c.= x))))

      (assert "Division is the inverse of multiplication."
              (|> x (&;c.* y) (&;c./ y) (&;c.= x)))

      (assert "Scalar division is the inverse of scalar multiplication."
              (|> x (&;c.*' factor) (&;c./' factor) (&;c.= x)))
      ))

(test: "Conjugate, reciprocal, signum, negation"
  [x gen-complex]
  ($_ seq
      (assert "Conjugate has same real part as original, and opposite of imaginary part."
              (let [cx (&;conjugate x)]
                (and (r.= (get@ #&;real x)
                          (get@ #&;real cx))
                     (r.= (r/negate (get@ #&;imaginary x))
                          (get@ #&;imaginary cx)))))

      (assert "The reciprocal functions is its own inverse."
              (|> x &;reciprocal &;reciprocal (&;c.= x)))

      (assert "x*(x^-1) = 1"
              (|> x (&;c.* (&;reciprocal x)) (&;c.= &;one)))

      (assert "Absolute value of signum is always 1."
              (|> x &;c.signum &;c.abs (r.= 1.0)))

      (assert "Negation is its own inverse."
              (let [there (&;c.negate x)
                    back-again (&;c.negate there)]
                (and (not (&;c.= there x))
                     (&;c.= back-again x))))

      (assert "Negation doesn't change the absolute value."
              (r.= (&;c.abs x)
                   (&;c.abs (&;c.negate x))))
      ))

(test: "Trigonometry"
  [x gen-complex]
  ($_ seq
      (assert "Arc-sine is the inverse of sine."
              (|> x &;sin &;asin (&;c.= x)))

      (assert "Arc-cosine is the inverse of cosine."
              (|> x &;cos &;acos (&;c.= x)))

      (assert "Arc-tangent is the inverse of tangent."
              (|> x &;tan &;atan (&;c.= x))))
  )

(test: "Power 2 and exponential/logarithm"
  [x gen-complex]
  ($_ seq
      (assert "Square root is inverse of power 2.0"
              (|> x (&;pow' 2.0) &;sqrt (&;c.= x)))

      (assert "Logarithm is inverse of exponentiation."
              (and (|> x &;exp &;log (&;c.= x))
                   (|> x &;log &;exp (&;c.= x))))
      ))

(test: "Complex roots"
  [sample gen-complex
   degree (|> R;nat (:: @ map (|>. (n.max +1) (n.% +5))))
   #let [(^open "L/") (list;Eq<List> &;Eq<Complex>)]]
  (assert "Can calculate the N roots for any complex number."
          (L/= (list;repeat degree sample)
               (List/map (&;pow' (|> degree nat-to-int int-to-real))
                         (&;nth-root degree sample)))))

(test: "Codec"
  [sample gen-complex
   #let [(^open "c/") &;Codec<Text,Complex>]]
  (assert "Can encode/decode complex numbers."
          (|> sample c/encode c/decode
              (case> (#;Right output)
                     (&;c.= sample output)

                     _
                     false))))
