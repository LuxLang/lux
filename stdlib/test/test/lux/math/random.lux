(;module:
  lux
  (lux [io]
       (control [monad #+ do Monad])
       (data [number]
             text/format
             (coll [list]
                   [sequence]
                   [array]
                   [queue]
                   [stack]
                   [set]
                   [dict]))
       (math ["r" random]))
  lux/test)

(context: "Random."
  [size (|> r;nat (:: @ map (|>. (n.% +100) (n.max +10))))
   _list (r;list size r;nat)
   _sequence (r;sequence size r;nat)
   _array (r;array size r;nat)
   _queue (r;queue size r;nat)
   _stack (r;stack size r;nat)
   _set (r;set number;Hash<Nat> size r;nat)
   _dict (r;dict number;Hash<Nat> size r;nat r;nat)
   top r;nat
   filtered (|> r;nat (r;filter (n.<= top)))
   shuffle-seed r;nat
   #let [sorted (|> _sequence sequence;to-list (list;sort n.<))
         shuffled (|> sorted sequence;from-list (r;shuffle shuffle-seed))
         re-sorted (|> shuffled sequence;to-list (list;sort n.<))]]
  ($_ seq
      (test "Can produce lists."
            (n.= size (list;size _list)))
      (test "Can produce sequences."
            (n.= size (sequence;size _sequence)))
      (test "Can produce arrays."
            (n.= size (array;size _array)))
      (test "Can produce queues."
            (n.= size (queue;size _queue)))
      (test "Can produce stacks."
            (n.= size (stack;size _stack)))
      (test "Can produce sets."
            (n.= size (set;size _set)))
      (test "Can produce dicts."
            (n.= size (dict;size _dict)))
      (test "Can filter values."
            (n.<= top filtered))
      (test "Can shuffle sequences."
            (let [(^open "v/") (sequence;Eq<Sequence> number;Eq<Nat>)
                  sorted (sequence;from-list sorted)]
              (and (not (v/= sorted shuffled))
                   (v/= sorted (sequence;from-list re-sorted)))))
      ))
