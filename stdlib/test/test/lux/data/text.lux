(;module:
  lux
  (lux [io]
       (control monad
                pipe)
       (data ["&" text]
             [char]
             text/format
             [number]
             (coll [list]))
       ["R" math/random])
  lux/test)

(context: "Size"
  [size (:: @ map (n.% +100) R;nat)
   sample (R;text size)]
  (test "" (or (and (n.= +0 size)
                    (&;empty? sample))
               (n.= size (&;size sample)))))

(def: bounded-size
  (R;Random Nat)
  (|> R;nat
      (:: R;Monad<Random> map (|>. (n.% +20) (n.+ +1)))))

(context: "Locations"
  [size bounded-size
   idx (:: @ map (n.% size) R;nat)
   sample (R;text size)]
  (test "" (|> sample
               (&;nth idx)
               (case> (^multi (#;Some char)
                              [(char;as-text char) char']
                              [[(&;index-of' char' sample)
                                (&;last-index-of' char' sample)
                                (&;index-of char' idx sample)
                                (&;last-index-of char' idx sample)]
                               [(#;Some io) (#;Some lio)
                                (#;Some io') (#;Some lio')]])
                      (and (n.<= idx io)
                           (n.>= idx lio)

                           (n.= idx io')
                           (n.>= idx lio')

                           (&;contains? char' sample))

                      _
                      false
                      ))
        ))

(context: "Text functions"
  [sizeL bounded-size
   sizeR bounded-size
   sampleL (R;text sizeL)
   sampleR (R;text sizeR)
   #let [sample (&;concat (list sampleL sampleR))
         fake-sample (&;join-with " " (list sampleL sampleR))
         dup-sample (&;join-with "" (list sampleL sampleR))
         enclosed-sample (&;enclose [sampleR sampleR] sampleL)
         (^open) &;Eq<Text>]]
  (test "" (and (not (= sample fake-sample))
                (= sample dup-sample)
                (&;starts-with? sampleL sample)
                (&;ends-with? sampleR sample)
                (= enclosed-sample
                   (&;enclose' sampleR sampleL))
                
                (|> (&;split sizeL sample)
                    (case> (#;Right [_l _r])
                           (and (= sampleL _l)
                                (= sampleR _r)
                                (= sample (&;concat (list _l _r))))

                           _
                           false))
                
                (|> [(&;clip +0 sizeL sample)
                     (&;clip sizeL (&;size sample) sample)
                     (&;clip' sizeL sample)
                     (&;clip' +0 sample)]
                    (case> [(#;Right _l) (#;Right _r) (#;Right _r') (#;Right _f)]
                           (and (= sampleL _l)
                                (= sampleR _r)
                                (= _r _r')
                                (= sample _f))

                           _
                           false))
                )
        ))

(context: "More text functions"
  [sizeP bounded-size
   sizeL bounded-size
   #let [## The wider unicode charset includes control characters that
         ## can make text replacement work improperly.
         ## Because of that, I restrict the charset.
         normal-char-gen (|> R;char (:: @ map (|>. char;code (n.% +128) (n.max +1) char;char)))]
   sep1 (R;text' normal-char-gen +1)
   sep2 (R;text' normal-char-gen +1)
   #let [part-gen (|> (R;text' normal-char-gen sizeP)
                      (R;filter (. not (&;contains? sep1))))]
   parts (R;list sizeL part-gen)
   #let [sample1 (&;concat (list;interpose sep1 parts))
         sample2 (&;concat (list;interpose sep2 parts))
         (^open "&/") &;Eq<Text>]]
  ($_ seq
      (test "Can split text through a separator."
            (n.= (list;size parts)
                 (list;size (&;split-all-with sep1 sample1))))

      (test "Can replace occurrences of a piece of text inside a larger text."
            (&/= sample2
                 (&;replace-all sep1 sep2 sample1)))
      ))

(context: "Other text functions"
  (let [(^open "&/") &;Eq<Text>]
    ($_ seq
        (test "Can transform texts in certain ways."
              (and (&/= "abc" (&;lower-case "ABC"))
                   (&/= "ABC" (&;upper-case "abc"))
                   (&/= "ABC" (&;trim " \tABC\n\r"))))
        )))

(context: "Structures"
  (let [(^open "&/") &;Order<Text>]
    ($_ seq
        (test "" (&/< "bcd" "abc"))
        (test "" (not (&/< "abc" "abc")))
        (test "" (not (&/< "abc" "bcd")))
        (test "" (&/<= "bcd" "abc"))
        (test "" (&/<= "abc" "abc"))
        (test "" (not (&/<= "abc" "bcd")))
        (test "" (&/> "abc" "bcd"))
        (test "" (not (&/> "abc" "abc")))
        (test "" (not (&/> "bcd" "abc")))
        (test "" (&/>= "abc" "bcd"))
        (test "" (&/>= "abc" "abc"))
        (test "" (not (&/>= "bcd" "abc")))
        )))

(context: "Codec"
  [size bounded-size
   sample (R;text size)
   #let [(^open) &;Eq<Text>]]
  (test "" (|> sample
               (:: &;Codec<Text,Text> encode)
               (:: &;Codec<Text,Text> decode)
               (case> (#;Right decoded)
                      (= sample decoded)

                      _
                      false))))
