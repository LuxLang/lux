##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;module:
  lux
  (lux (codata [io])
       (control monad)
       (data (struct ["&" queue])
             [number])
       (math ["R" random])
       pipe)
  lux/test)

(test: "Queues"
  [size (:: @ map (n.% +100) R;nat)
   sample (R;queue size R;nat)
   non-member (|> R;nat
                  (R;filter (. not (&;enqueued? number;Eq<Nat> sample))))]
  ($_ seq
      (assert "I can query the size of a queue (and empty queues have size 0)."
              (if (n.= +0 size)
                (&;empty? sample)
                (n.= size (&;size sample))))

      (assert "Enqueueing and dequeing affects the size of queues."
              (and (n.= (n.inc size) (&;size (&;enqueue non-member sample)))
                   (or (&;empty? sample)
                       (n.= (n.dec size) (&;size (&;dequeue sample))))
                   (n.= size (&;size (&;dequeue (&;enqueue non-member sample))))))

      (assert "Transforming to/from list can't change the queue."
              (let [(^open "&/") (&;Eq<Queue> number;Eq<Nat>)]
                (|> sample
                    &;to-list &;from-list
                    (&/= sample))))

      (assert "I can always peek at a non-empty queue."
              (case (&;peek sample)
                #;None     (&;empty? sample)
                (#;Some _) true))

      (assert "I can query whether an element belongs to a queue."
              (and (not (&;enqueued? number;Eq<Nat> sample non-member))
                   (&;enqueued? number;Eq<Nat> (&;enqueue non-member sample)
                                non-member)
                   (case (&;peek sample)
                     #;None
                     (&;empty? sample)
                     
                     (#;Some first)
                     (and (&;enqueued? number;Eq<Nat> sample first)
                          (not (&;enqueued? number;Eq<Nat> (&;dequeue sample) first))))))
      ))
