(;module:
  lux
  (lux [io]
       (control monad
                codec
                eq)
       (data [text "Text/" Monoid<Text>]
             text/format
             [error #- fail]
             [bool]
             [char]
             [maybe]
             [number "i/" Number<Int>]
             (format ["&" json])
             (coll [vector #+ vector]
                   [dict]
                   [list]))
       [compiler #+ with-gensyms]
       [macro]
       (macro [ast]
              [syntax #+ syntax:]
              [poly #+ derived:])
       ["R" random]
       pipe
       test)
  )

(def: gen-json
  (R;Random &;JSON)
  (R;rec (lambda [gen-json]
           (do R;Monad<Random>
             [size (:: @ map (n.% +2) R;nat)]
             ($_ R;alt
                 (:: @ wrap [])
                 R;bool
                 (|> R;real (:: @ map (r.* 1_000_000.0)))
                 (R;text size)
                 (R;vector size gen-json)
                 (R;dict text;Hash<Text> size (R;text size) gen-json)
                 )))))

(test: "JSON"
  [sample gen-json
   #let [(^open "&/") &;Eq<JSON>
         (^open "&/") &;Codec<Text,JSON>]]
  ($_ seq
      (assert "Every JSON is equal to itself."
              (&/= sample sample))

      (assert "Can encode/decode JSON."
              (|> sample &/encode &/decode
                  (case> (#;Right result)
                         (&/= sample result)

                         (#;Left _)
                         false)))
      ))

(type: Variant
  (#Case0 Bool)
  (#Case1 Int)
  (#Case2 Real))

(type: Record
  {#unit Unit
   #bool Bool
   #int Int
   #real Real
   #char Char
   #text Text
   #maybe (Maybe Int)
   #list (List Int)
   #variant Variant
   #tuple [Int Real Char]})

(def: gen-record
  (R;Random Record)
  (do R;Monad<Random>
    [size (:: @ map (n.% +2) R;nat)
     #let [gen-int (|> R;int (:: @ map (|>. i/abs (i.% 1_000_000))))]]
    ($_ R;seq
        (:: @ wrap [])
        R;bool
        gen-int
        R;real
        R;char
        (R;text size)
        (R;maybe gen-int)
        (R;list size gen-int)
        ($_ R;alt R;bool gen-int R;real)
        ($_ R;seq gen-int R;real R;char)
        )))

(derived: (&;Codec<JSON,?> Record))

(struct: _ (Eq Record)
  (def: (= recL recR)
    (let [variant/= (lambda [left right]
                      (case [left right]
                        [(#Case0 left') (#Case0 right')]
                        (:: bool;Eq<Bool> = left' right')
                        
                        [(#Case1 left') (#Case1 right')]
                        (i.= left' right')
                        
                        [(#Case2 left') (#Case2 right')]
                        (r.= left' right')

                        _
                        false))]
      (and (:: bool;Eq<Bool> = (get@ #bool recL) (get@ #bool recR))
           (i.= (get@ #int recL) (get@ #int recR))
           (r.= (get@ #real recL) (get@ #real recR))
           (:: char;Eq<Char> = (get@ #char recL) (get@ #char recR))
           (:: text;Eq<Text> = (get@ #text recL) (get@ #text recR))
           (:: (maybe;Eq<Maybe> number;Eq<Int>) = (get@ #maybe recL) (get@ #maybe recR))
           (:: (list;Eq<List> number;Eq<Int>) = (get@ #list recL) (get@ #list recR))
           (variant/= (get@ #variant recL) (get@ #variant recR))
           (let [[tL0 tL1 tL2] (get@ #tuple recL)
                 [tR0 tR1 tR2] (get@ #tuple recR)]
             (and (i.= tL0 tR0)
                  (r.= tL1 tR1)
                  (:: char;Eq<Char> = tL2 tR2)))
           ))))

(test: "Polytypism"
  [sample gen-record
   #let [(^open "&/") Eq<Record>
         (^open "&/") Codec<JSON,Record>]]
  (assert "Can encode/decode arbitrary types."
          (|> sample &/encode &/decode
              (case> (#;Right result)
                     (&/= sample result)

                     (#;Left _)
                     false))))
