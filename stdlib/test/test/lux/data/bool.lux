##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;module:
  lux
  (lux (control [monad])
       [io]
       (data bool)
       ["R" random])
  lux/test)

(test: "Boolean operations."
  [value R;bool]
  (assert "" (and (not (and value (not value)))
                  (or value (not value))

                  (not (:: Or@Monoid<Bool> unit))
                  (:: Or@Monoid<Bool> append value (not value))
                  (:: And@Monoid<Bool> unit)
                  (not (:: And@Monoid<Bool> append value (not value)))
                  
                  (:: Eq<Bool> = value (not (not value)))
                  (not (:: Eq<Bool> = value (not value)))

                  (not (:: Eq<Bool> = value ((complement id) value)))
                  (:: Eq<Bool> = value ((complement not) value))

                  (case (|> value
                            (:: Codec<Text,Bool> encode)
                            (:: Codec<Text,Bool> decode))
                    (#;Right dec-value)
                    (:: Eq<Bool> = value dec-value)

                    (#;Left _)
                    false)
                  )))
