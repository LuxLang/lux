(;module:
  lux
  (lux [io]
       (control monad
                comonad)
       (data ["&" identity]
             [text "Text/" Monoid<Text> Eq<Text>]))
  lux/test)

(context: "Identity"
  (let [(^open "&/") &;Monad<Identity>
        (^open "&/") &;CoMonad<Identity>]
    ($_ seq
        (test "Functor does not affect values."
              (Text/= "yololol" (&/map (Text/append "yolo") "lol")))
        
        (test "Applicative does not affect values."
              (and (Text/= "yolo" (&/wrap "yolo"))
                   (Text/= "yololol" (&/apply (&/wrap (Text/append "yolo")) (&/wrap "lol")))))
        
        (test "Monad does not affect values."
              (Text/= "yololol" (do &;Monad<Identity>
                                  [f (wrap Text/append)
                                   a (wrap "yolo")
                                   b (wrap "lol")]
                                  (wrap (f a b)))))
        
        (test "CoMonad does not affect values."
              (and (Text/= "yololol" (&/unwrap "yololol"))
                   (Text/= "yololol" (be &;CoMonad<Identity>
                                       [f Text/append
                                        a "yolo"
                                        b "lol"]
                                       (f a b)))))
        )))
