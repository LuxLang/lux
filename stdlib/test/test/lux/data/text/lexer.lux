(.module:
  [lux #*
   [control
    [monad (#+ do Monad)]
    pipe
    ["p" parser]]
   [io]
   [data
    ["E" error]
    [text ("text/" Equivalence<Text>)
     format
     ["&" lexer]]
    [collection
     [list]]]
   [math
    ["r" random]]]
  lux/test)

## [Utils]
(def: (should-fail input)
  (All [a] (-> (E.Error a) Bool))
  (case input
    (#.Left _) true
    _          false))

(def: (should-passT test input)
  (-> Text (E.Error Text) Bool)
  (case input
    (#.Right output)
    (text/= test output)
    
    _
    false))

(def: (should-passL test input)
  (-> (List Text) (E.Error (List Text)) Bool)
  (let [(^open "list/") (list.Equivalence<List> text.Equivalence<Text>)]
    (case input
      (#.Right output)
      (list/= test output)
      
      _
      false)))

(def: (should-passE test input)
  (-> (Either Text Text) (E.Error (Either Text Text)) Bool)
  (case input
    (#.Right output)
    (case [test output]
      [(#.Left test) (#.Left output)]
      (text/= test output)

      [(#.Right test) (#.Right output)]
      (text/= test output)

      _
      false)
    
    _
    false))

## [Tests]
(context: "End"
  ($_ seq
      (test "Can detect the end of the input."
            (|> (&.run ""
                       &.end)
                (case> (#.Right _) true _ false)))
      
      (test "Won't mistake non-empty text for no more input."
            (|> (&.run "YOLO"
                       &.end)
                (case> (#.Left _) true _ false)))
      ))

(context: "Literals"
  (<| (times +100)
      (do @
        [size (|> r.nat (:: @ map (|>> (n/% +100) (n/max +10))))
         sample (r.unicode size)
         non-sample (|> (r.unicode size)
                        (r.filter (|>> (text/= sample) not)))]
        ($_ seq
            (test "Can find literal text fragments."
                  (and (|> (&.run sample
                                  (&.this sample))
                           (case> (#.Right []) true _ false))
                       (|> (&.run non-sample
                                  (&.this sample))
                           (case> (#.Left _) true _ false))))
            ))))

(context: "Custom lexers"
  ($_ seq
      (test "Can lex anything"
            (and (should-passT "A" (&.run "A"
                                          &.any))
                 (should-fail (&.run ""
                                     &.any))))

      (test "Can lex characters ranges."
            (and (should-passT "Y" (&.run "Y"
                                          (&.range (char "X") (char "Z"))))
                 (should-fail (&.run "M"
                                     (&.range (char "X") (char "Z"))))))
      
      (test "Can lex upper-case and &.lower-case letters."
            (and (should-passT "Y" (&.run "Y"
                                          &.upper))
                 (should-fail (&.run "m"
                                     &.upper))
                 
                 (should-passT "y" (&.run "y"
                                          &.lower))
                 (should-fail (&.run "M"
                                     &.lower))))

      (test "Can lex numbers."
            (and (should-passT "1" (&.run "1"
                                          &.decimal))
                 (should-fail (&.run " "
                                     &.decimal))

                 (should-passT "7" (&.run "7"
                                          &.octal))
                 (should-fail (&.run "8"
                                     &.octal))

                 (should-passT "1" (&.run "1"
                                          &.hexadecimal))
                 (should-passT "a" (&.run "a"
                                          &.hexadecimal))
                 (should-passT "A" (&.run "A"
                                          &.hexadecimal))
                 (should-fail (&.run " "
                                     &.hexadecimal))
                 ))

      (test "Can lex alphabetic characters."
            (and (should-passT "A" (&.run "A"
                                          &.alpha))
                 (should-passT "a" (&.run "a"
                                          &.alpha))
                 (should-fail (&.run "1"
                                     &.alpha))))

      (test "Can lex alphanumeric characters."
            (and (should-passT "A" (&.run "A"
                                          &.alpha-num))
                 (should-passT "a" (&.run "a"
                                          &.alpha-num))
                 (should-passT "1" (&.run "1"
                                          &.alpha-num))
                 (should-fail (&.run " "
                                     &.alpha-num))))

      (test "Can lex white-space."
            (and (should-passT " " (&.run " "
                                          &.space))
                 (should-fail (&.run "8"
                                     &.space))))
      ))

(context: "Combinators"
  ($_ seq
      (test "Can combine lexers sequentially."
            (and (|> (&.run "YO"
                            (p.seq &.any &.any))
                     (case> (#.Right ["Y" "O"]) true
                            _ false))
                 (should-fail (&.run "Y"
                                     (p.seq &.any &.any)))))
      
      (test "Can create the opposite of a lexer."
            (and (should-passT "a" (&.run "a"
                                          (&.not (p.alt &.decimal &.upper))))
                 (should-fail (&.run "A"
                                     (&.not (p.alt &.decimal &.upper))))))
      
      (test "Can select from among a set of characters."
            (and (should-passT "C" (&.run "C"
                                          (&.one-of "ABC")))
                 (should-fail (&.run "D"
                                     (&.one-of "ABC")))))

      (test "Can avoid a set of characters."
            (and (should-passT "D" (&.run "D"
                                          (&.none-of "ABC")))
                 (should-fail (&.run "C"
                                     (&.none-of "ABC")))))
      
      (test "Can lex using arbitrary predicates."
            (and (should-passT "D" (&.run "D"
                                          (&.satisfies (function (_ c) true))))
                 (should-fail (&.run "C"
                                     (&.satisfies (function (_ c) false))))))
      
      (test "Can apply a lexer multiple times."
            (and (should-passT "0123456789ABCDEF" (&.run "0123456789ABCDEF"
                                                         (&.many &.hexadecimal)))
                 (should-fail (&.run "yolo"
                                     (&.many &.hexadecimal)))

                 (should-passT "" (&.run ""
                                         (&.some &.hexadecimal)))))
      ))
