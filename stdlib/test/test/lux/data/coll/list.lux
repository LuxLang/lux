(;module:
  lux
  (lux [io]
       (control monad)
       (data (coll ["&" list])
             [text "Text/" Monoid<Text>]
             [number]
             [bool]
             [product])
       ["R" math/random]
       pipe)
  lux/test)

(def: bounded-size
  (R;Random Nat)
  (|> R;nat
      (:: R;Monad<Random> map (|>. (n.% +100) (n.+ +10)))))

(test: "Lists: Part 1"
  [size bounded-size
   idx (:: @ map (n.% size) R;nat)
   sample (R;list size R;nat)
   other-size bounded-size
   other-sample (R;list other-size R;nat)
   separator R;nat
   #let [(^open) (&;Eq<List> number;Eq<Nat>)
         (^open "&/") &;Functor<List>]]
  ($_ seq
      (assert "The size function should correctly portray the size of the list."
              (n.= size (&;size sample)))

      (assert "The repeat function should produce as many elements as asked of it."
              (n.= size (&;size (&;repeat size []))))

      (assert "Reversing a list does not change it's size."
              (n.= (&;size sample)
                   (&;size (&;reverse sample))))
      
      (assert "Reversing a list twice results in the original list."
              (= sample
                 (&;reverse (&;reverse sample))))

      (assert "Filtering by a predicate and its complement should result in a number of elements equal to the original list."
              (and (n.= (&;size sample)
                        (n.+ (&;size (&;filter n.even? sample))
                             (&;size (&;filter (bool;complement n.even?) sample))))
                   (let [[plus minus] (&;partition n.even? sample)]
                     (n.= (&;size sample)
                          (n.+ (&;size plus)
                               (&;size minus))))))

      (assert "If every element in a list satisfies a predicate, there can't be any that satisfy its complement."
              (if (&;every? n.even? sample)
                (and (not (&;any? (bool;complement n.even?) sample))
                     (&;empty? (&;filter (bool;complement n.even?) sample)))
                (&;any? (bool;complement n.even?) sample)))

      (assert "Any element of the list can be considered its member."
              (let [elem (default (undefined)
                           (&;nth idx sample))]
                (&;member? number;Eq<Nat> sample elem)))
      ))

(test: "Lists: Part 2"
  [size bounded-size
   idx (:: @ map (n.% size) R;nat)
   sample (R;list size R;nat)
   other-size bounded-size
   other-sample (R;list other-size R;nat)
   separator R;nat
   #let [(^open) (&;Eq<List> number;Eq<Nat>)
         (^open "&/") &;Functor<List>]]
  ($_ seq
      (assert "Appending the head and the tail should yield the original list."
              (let [head (default (undefined)
                           (&;head sample))
                    tail (default (undefined)
                           (&;tail sample))]
                (= sample
                   (#;Cons head tail))))

      (assert "Appending the inits and the last should yield the original list."
              (let [(^open) &;Monoid<List>
                    inits (default (undefined)
                            (&;inits sample))
                    last (default (undefined)
                           (&;last sample))]
                (= sample
                   (append inits (list last)))))

      (assert "Functor should go over every element of the list."
              (let [(^open) &;Functor<List>
                    there (map n.inc sample)
                    back-again (map n.dec there)]
                (and (not (= sample there))
                     (= sample back-again))))

      (assert "Splitting a list into chunks and re-appending them should yield the original list."
              (let [(^open) &;Monoid<List>
                    [left right] (&;split idx sample)
                    [left' right'] (&;split-with n.even? sample)]
                (and (= sample
                        (append left right))
                     (= sample
                        (append left' right'))
                     (= sample
                        (append (&;take idx sample)
                                (&;drop idx sample)))
                     (= sample
                        (append (&;take-while n.even? sample)
                                (&;drop-while n.even? sample)))
                     )))

      (assert "Segmenting the list in pairs should yield as many elements as N/2."
              (n.= (n./ +2 size)
                   (&;size (&;as-pairs sample))))

      (assert "Sorting a list shouldn't change it's size."
              (n.= (&;size sample)
                   (&;size (&;sort n.< sample))))

      (assert "Sorting a list with one order should yield the reverse of sorting it with the opposite order."
              (= (&;sort n.< sample)
                 (&;reverse (&;sort n.> sample))))
      ))

(test: "Lists: Part 3"
  [size bounded-size
   idx (:: @ map (n.% size) R;nat)
   sample (R;list size R;nat)
   other-size bounded-size
   other-sample (R;list other-size R;nat)
   separator R;nat
   #let [(^open) (&;Eq<List> number;Eq<Nat>)
         (^open "&/") &;Functor<List>]]
  ($_ seq
      (assert "If you zip 2 lists, the result's size will be that of the smaller list."
              (n.= (&;size (&;zip2 sample other-sample))
                   (n.min (&;size sample) (&;size other-sample))))
      
      (assert "I can pair-up elements of a list in order."
              (let [(^open) &;Functor<List>
                    zipped (&;zip2 sample other-sample)
                    num-zipper (&;size zipped)]
                (and (|> zipped (map product;left) (= (&;take num-zipper sample)))
                     (|> zipped (map product;right) (= (&;take num-zipper other-sample))))))
      
      (assert "You can generate indices for any size, and they will be in ascending order."
              (let [(^open) &;Functor<List>
                    indices (&;indices size)]
                (and (n.= size (&;size indices))
                     (= indices
                        (&;sort n.< indices))
                     (&;every? (n.= (n.dec size))
                               (&;zip2-with n.+
                                            indices
                                            (&;sort n.> indices)))
                     )))

      (assert "The 'interpose' function places a value between every member of a list."
              (let [(^open) &;Functor<List>
                    sample+ (&;interpose separator sample)]
                (and (n.= (|> size (n.* +2) n.dec)
                          (&;size sample+))
                     (|> sample+ &;as-pairs (map product;right) (&;every? (n.= separator))))))

      (assert "List append is a monoid."
              (let [(^open) &;Monoid<List>]
                (and (= sample (append unit sample))
                     (= sample (append sample unit))
                     (let [[left right] (&;split size (append sample other-sample))]
                       (and (= sample left)
                            (= other-sample right))))))

      (assert "Applicative allows you to create singleton lists, and apply lists of functions to lists of values."
              (let [(^open) &;Applicative<List>]
                (and (= (list separator) (wrap separator))
                     (= (map n.inc sample)
                        (apply (wrap n.inc) sample)))))

      (assert "List concatenation is a monad."
              (let [(^open) &;Monad<List>
                    (^open) &;Monoid<List>]
                (= (append sample other-sample)
                   (join (list sample other-sample)))))

      (assert "You can find any value that satisfies some criterium, if such values exist in the list."
              (case (&;find n.even? sample)
                (#;Some found)
                (and (n.even? found)
                     (&;any? n.even? sample)
                     (not (&;every? (bool;complement n.even?) sample)))

                #;None
                (and (not (&;any? n.even? sample))
                     (&;every? (bool;complement n.even?) sample))))

      (assert "You can iteratively construct a list, generating values until you're done."
              (= (&;n.range +0 (n.dec size))
                 (&;iterate (lambda [n] (if (n.< size n) (#;Some (n.inc n)) #;None))
                            +0)))

      (assert "Can enumerate all elements in a list."
              (let [enum-sample (&;enumerate sample)]
                (and (= (&;indices (&;size enum-sample))
                        (&/map product;left enum-sample))
                     (= sample
                        (&/map product;right enum-sample)))))
      ))

(test: "Monad transformer"
  (let [lift (&;lift-list io;Monad<IO>)
        (^open "io/") io;Monad<IO>]
    (assert "Can add list functionality to any monad."
            (|> (io;run (do (&;ListT io;Monad<IO>)
                          [a (lift (io/wrap 123))
                           b (wrap 456)]
                          (wrap (i.+ a b))))
                (case> (^ (list 579)) true
                       _              false)))
    ))
