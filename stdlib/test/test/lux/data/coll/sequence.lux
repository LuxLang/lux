(.module:
  lux
  (lux [io]
       (control [monad #+ do Monad])
       (data (coll ["&" sequence]
                   [list "list/" Fold<List>])
             [number]
             [maybe])
       ["r" math/random])
  lux/test)

(context: "Sequences"
  (<| (times +100)
      (do @
        [size (|> r.nat (:: @ map (|>> (n/% +100) (n/max +1))))
         idx (|> r.nat (:: @ map (n/% size)))
         sample (r.sequence size r.nat)
         other-sample (r.sequence size r.nat)
         non-member (|> r.nat (r.filter (|>> (&.member? number.Eq<Nat> sample) not)))
         #let [(^open "&/") (&.Eq<Sequence> number.Eq<Nat>)
               (^open "&/") &.Apply<Sequence>
               (^open "&/") &.Monad<Sequence>
               (^open "&/") &.Fold<Sequence>
               (^open "&/") &.Monoid<Sequence>]]
        ($_ seq
            (test "Can query size of sequence."
                  (if (&.empty? sample)
                    (and (n/= +0 size)
                         (n/= +0 (&.size sample)))
                    (n/= size (&.size sample))))

            (test "Can add and remove elements to sequences."
                  (and (n/= (inc size) (&.size (&.add non-member sample)))
                       (n/= (dec size) (&.size (&.pop sample)))))

            (test "Can put and get elements into sequences."
                  (|> sample
                      (&.put idx non-member)
                      (&.nth idx)
                      maybe.assume
                      (is? non-member)))

            (test "Can update elements of sequences."
                  (|> sample
                      (&.put idx non-member) (&.update idx inc)
                      (&.nth idx) maybe.assume
                      (n/= (inc non-member))))

            (test "Can safely transform to/from lists."
                  (|> sample &.to-list &.from-list (&/= sample)))

            (test "Can identify members of a sequence."
                  (and (not (&.member? number.Eq<Nat> sample non-member))
                       (&.member? number.Eq<Nat> (&.add non-member sample) non-member)))

            (test "Can fold over elements of sequence."
                  (n/= (list/fold n/+ +0 (&.to-list sample))
                       (&/fold n/+ +0 sample)))
            
            (test "Functor goes over every element."
                  (let [there (&/map inc sample)
                        back-again (&/map dec there)]
                    (and (not (&/= sample there))
                         (&/= sample back-again))))

            (test "Apply allows you to create singleton sequences, and apply sequences of functions to sequences of values."
                  (and (&/= (&.sequence non-member) (&/wrap non-member))
                       (&/= (&/map inc sample)  (&/apply (&/wrap inc) sample))))

            (test "Sequence concatenation is a monad."
                  (&/= (&/compose sample other-sample)
                       (&/join (&.sequence sample other-sample))))
            ))))
