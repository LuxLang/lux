(.module:
  lux
  (lux [io]
       (control [monad #+ do Monad]
                comonad)
       (data [maybe]
             [text "Text/" Monoid<Text>]
             text/format
             (coll [list]
                   ["&" sequence])
             [number "Nat/" Codec<Text,Nat>])
       ["r" math/random])
  lux/test)

(context: "Sequences"
  (<| (times +100)
      (do @
        [size (|> r.nat (:: @ map (|>> (n/% +100) (n/max +2))))
         offset (|> r.nat (:: @ map (n/% +100)))
         factor (|> r.nat (:: @ map (|>> (n/% +100) (n/max +2))))
         elem r.nat
         cycle-seed (r.list size r.nat)
         cycle-sample-idx (|> r.nat (:: @ map (n/% +1000)))
         #let [(^open "List/") (list.Equivalence<List> number.Equivalence<Nat>)
               sample0 (&.iterate inc +0)
               sample1 (&.iterate inc offset)]]
        ($_ seq
            (test "Can move along a sequence and take slices off it."
                  (and (and (List/= (list.n/range +0 (dec size))
                                    (&.take size sample0))
                            (List/= (list.n/range offset (dec (n/+ offset size)))
                                    (&.take size (&.drop offset sample0)))
                            (let [[drops takes] (&.split size sample0)]
                              (and (List/= (list.n/range +0 (dec size))
                                           drops)
                                   (List/= (list.n/range size (dec (n/* +2 size)))
                                           (&.take size takes)))))
                       (and (List/= (list.n/range +0 (dec size))
                                    (&.take-while (n/< size) sample0))
                            (List/= (list.n/range offset (dec (n/+ offset size)))
                                    (&.take-while (n/< (n/+ offset size))
                                                  (&.drop-while (n/< offset) sample0)))
                            (let [[drops takes] (&.split-while (n/< size) sample0)]
                              (and (List/= (list.n/range +0 (dec size))
                                           drops)
                                   (List/= (list.n/range size (dec (n/* +2 size)))
                                           (&.take-while (n/< (n/* +2 size)) takes)))))
                       ))

            (test "Can repeat any element and infinite number of times."
                  (n/= elem (&.nth offset (&.repeat elem))))

            (test "Can obtain the head & tail of a sequence."
                  (and (n/= offset (&.head sample1))
                       (List/= (list.n/range (inc offset) (n/+ offset size))
                               (&.take size (&.tail sample1)))))

            (test "Can filter sequences."
                  (and (n/= (n/* +2 offset)
                            (&.nth offset
                                   (&.filter n/even? sample0)))
                       (let [[evens odds] (&.partition n/even? (&.iterate inc +0))]
                         (and (n/= (n/* +2 offset)
                                   (&.nth offset evens))
                              (n/= (inc (n/* +2 offset))
                                   (&.nth offset odds))))))

            (test "Functor goes over 'all' elements in a sequence."
                  (let [(^open "&/") &.Functor<Sequence>
                        there (&/map (n/* factor) sample0)
                        back-again (&/map (n// factor) there)]
                    (and (not (List/= (&.take size sample0)
                                      (&.take size there)))
                         (List/= (&.take size sample0)
                                 (&.take size back-again)))))

            (test "CoMonad produces a value for every element in a sequence."
                  (let [(^open "&/") &.Functor<Sequence>]
                    (List/= (&.take size (&/map (n/* factor) sample1))
                            (&.take size
                                    (be &.CoMonad<Sequence>
                                      [inputs sample1]
                                      (n/* factor (&.head inputs)))))))

            (test "'unfold' generalizes 'iterate'."
                  (let [(^open "&/") &.Functor<Sequence>
                        (^open "List/") (list.Equivalence<List> text.Equivalence<Text>)]
                    (List/= (&.take size
                                    (&/map Nat/encode (&.iterate inc offset)))
                            (&.take size
                                    (&.unfold (function (_ n) [(inc n) (Nat/encode n)])
                                              offset)))))

            (test "Can cycle over the same elements as an infinite sequence."
                  (|> (&.cycle cycle-seed)
                      maybe.assume
                      (&.nth cycle-sample-idx)
                      (n/= (|> cycle-seed
                               (list.nth (n/% size cycle-sample-idx))
                               maybe.assume))))
            ))))
