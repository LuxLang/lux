(.module:
  lux
  (lux [io]
       (control ["M" monad #+ do Monad]
                pipe)
       (data number
             [text "Text/" Monoid<Text> Eq<Text>]
             text/format)
       ["r" math/random])
  lux/test)

(do-template [category rand-gen <Eq> <Order>]
  [(context: (format "[" category "] " "Eq & Order")
     (<| (times +100)
         (do @
           [x rand-gen
            y rand-gen]
           (test "" (and (:: <Eq> = x x)
                         (or (:: <Eq> = x y)
                             (:: <Order> < y x)
                             (:: <Order> > y x)))))))]

  ["Nat"  r.nat  Eq<Nat>  Order<Nat>]
  ["Int"  r.int  Eq<Int>  Order<Int>]
  ["Frac" r.frac Eq<Frac> Order<Frac>]
  ["Deg"  r.deg  Eq<Deg>  Order<Deg>]
  )

(do-template [category rand-gen <Number> <Order>]
  [(context: (format "[" category "] " "Number")
     (<| (times +100)
         (do @
           [x rand-gen
            #let [(^open) <Number>
                  (^open) <Order>]]
           (test "" (and (>= x (abs x))
                         ## abs(0.0) == 0.0 && negate(abs(0.0)) == -0.0
                         (or (Text/= "Frac" category)
                             (not (= x (negate x))))
                         (= x (negate (negate x)))
                         ## There is loss of precision when multiplying
                         (or (Text/= "Deg" category)
                             (= x (* (signum x)
                                     (abs x)))))))))]

  ## ["Nat"  r.nat  Number<Nat>]
  ["Int"  r.int  Number<Int>  Order<Int>]
  ["Frac" r.frac Number<Frac> Order<Frac>]
  ["Deg"  r.deg  Number<Deg>  Order<Deg>]
  )

(do-template [category rand-gen <Enum> <Number> <Order>]
  [(context: (format "[" category "] " "Enum")
     (<| (times +100)
         (do @
           [x rand-gen]
           (test "" (let [(^open) <Number>
                          (^open) <Order>]
                      (and (> x
                              (:: <Enum> succ x))
                           (< x
                              (:: <Enum> pred x))
                           
                           (= x
                              (|> x (:: <Enum> pred) (:: <Enum> succ)))
                           (= x
                              (|> x (:: <Enum> succ) (:: <Enum> pred)))
                           ))))))]

  ["Nat"  r.nat Enum<Nat> Number<Nat> Order<Nat>]
  ["Int"  r.int Enum<Int> Number<Int> Order<Int>]
  )

(do-template [category rand-gen <Number> <Order> <Interval> <test>]
  [(context: (format "[" category "] " "Interval")
     (<| (times +100)
         (do @
           [x (|> rand-gen (r.filter <test>))
            #let [(^open) <Number>
                  (^open) <Order>]]
           (test "" (and (<= x (:: <Interval> bottom))
                         (>= x (:: <Interval> top)))))))]

  ["Nat"  r.nat  Number<Nat>  Order<Nat>  Interval<Nat>  (function (_ _) true)]
  ["Int"  r.int  Number<Int>  Order<Int>  Interval<Int>  (function (_ _) true)]
  ## Both min and max values will be positive (thus, greater than zero)
  ["Frac" r.frac Number<Frac> Order<Frac> Interval<Frac> (f/> 0.0)]
  ["Deg"  r.deg  Number<Deg>  Order<Deg>  Interval<Deg>  (function (_ _) true)]
  )

(do-template [category rand-gen <Number> <Order> <Monoid> <cap> <test>]
  [(context: (format "[" category "] " "Monoid")
     (<| (times +100)
         (do @
           [x (|> rand-gen (:: @ map (|>> (:: <Number> abs) <cap>)) (r.filter <test>))
            #let [(^open) <Number>
                  (^open) <Order>
                  (^open) <Monoid>]]
           (test "Composing with identity doesn't change the value."
                 (and (= x (compose identity x))
                      (= x (compose x identity))
                      (= identity (compose identity identity)))))))]

  ["Nat/Add"  r.nat  Number<Nat>  Order<Nat>  Add@Monoid<Nat>  (n/% +1000)  (function (_ _) true)]
  ["Nat/Mul"  r.nat  Number<Nat>  Order<Nat>  Mul@Monoid<Nat>  (n/% +1000)  (function (_ _) true)]
  ["Nat/Min"  r.nat  Number<Nat>  Order<Nat>  Min@Monoid<Nat>  (n/% +1000)  (function (_ _) true)]
  ["Nat/Max"  r.nat  Number<Nat>  Order<Nat>  Max@Monoid<Nat>  (n/% +1000)  (function (_ _) true)]
  ["Int/Add"  r.int  Number<Int>  Order<Int>  Add@Monoid<Int>  (i/% 1000)   (function (_ _) true)]
  ["Int/Mul"  r.int  Number<Int>  Order<Int>  Mul@Monoid<Int>  (i/% 1000)   (function (_ _) true)]
  ["Int/Min"  r.int  Number<Int>  Order<Int>  Min@Monoid<Int>  (i/% 1000)   (function (_ _) true)]
  ["Int/Max"  r.int  Number<Int>  Order<Int>  Max@Monoid<Int>  (i/% 1000)   (function (_ _) true)]
  ["Frac/Add" r.frac Number<Frac> Order<Frac> Add@Monoid<Frac> (f/% 1000.0) (f/> 0.0)]
  ["Frac/Mul" r.frac Number<Frac> Order<Frac> Mul@Monoid<Frac> (f/% 1000.0) (f/> 0.0)]
  ["Frac/Min" r.frac Number<Frac> Order<Frac> Min@Monoid<Frac> (f/% 1000.0) (f/> 0.0)]
  ["Frac/Max" r.frac Number<Frac> Order<Frac> Max@Monoid<Frac> (f/% 1000.0) (f/> 0.0)]
  ["Deg/Add"  r.deg  Number<Deg>  Order<Deg>  Add@Monoid<Deg>  (d/% .125)   (function (_ _) true)]
  ## ["Deg/Mul"  r.deg  Number<Deg>  Order<Deg>  Mul@Monoid<Deg>  (d/% .125)   (function (_ _) true)]
  ["Deg/Min" r.deg Number<Deg> Order<Deg> Min@Monoid<Deg> (d/% .125)   (function (_ _) true)]
  ["Deg/Max" r.deg Number<Deg> Order<Deg> Max@Monoid<Deg> (d/% .125)   (function (_ _) true)]
  )

(do-template [<category> <rand-gen> <Eq> <Codec>]
  [(context: (format "[" <category> "] " "Alternative formats")
     (<| (times +100)
         (do @
           [x <rand-gen>]
           (test "Can encode/decode values."
                 (|> x
                     (:: <Codec> encode)
                     (:: <Codec> decode)
                     (case> (#.Right x')
                            (:: <Eq> = x x')

                            (#.Left _)
                            false))))))]

  ["Nat/Binary"   r.nat  Eq<Nat>  Binary@Codec<Text,Nat>]
  ["Nat/Octal"    r.nat  Eq<Nat>  Octal@Codec<Text,Nat>]
  ["Nat/Decimal"  r.nat  Eq<Nat>  Codec<Text,Nat>]
  ["Nat/Hex"      r.nat  Eq<Nat>  Hex@Codec<Text,Nat>]

  ["Int/Binary"   r.int  Eq<Int>  Binary@Codec<Text,Int>]
  ["Int/Octal"    r.int  Eq<Int>  Octal@Codec<Text,Int>]
  ["Int/Decimal"  r.int  Eq<Int>  Codec<Text,Int>]
  ["Int/Hex"      r.int  Eq<Int>  Hex@Codec<Text,Int>]

  ["Deg/Binary"   r.deg  Eq<Deg>  Binary@Codec<Text,Deg>]
  ["Deg/Octal"    r.deg  Eq<Deg>  Octal@Codec<Text,Deg>]
  ["Deg/Decimal"  r.deg  Eq<Deg>  Codec<Text,Deg>]
  ["Deg/Hex"      r.deg  Eq<Deg>  Hex@Codec<Text,Deg>]

  ["Frac/Binary"  r.frac Eq<Frac> Binary@Codec<Text,Frac>]
  ["Frac/Octal"   r.frac Eq<Frac> Octal@Codec<Text,Frac>]
  ["Frac/Decimal" r.frac Eq<Frac> Codec<Text,Frac>]
  ["Frac/Hex"     r.frac Eq<Frac> Hex@Codec<Text,Frac>]
  )

(context: "Can convert frac values to/from their bit patterns."
  (<| (times +100)
      (do @
        [raw r.frac
         factor (|> r.nat (:: @ map (|>> (n/% +1000) (n/max +1))))
         #let [sample (|> factor nat-to-int int-to-frac (f/* raw))]]
        (test "Can convert frac values to/from their bit patterns."
              (|> sample frac-to-bits bits-to-frac (f/= sample))))))

(context: "Macros for alternative numeric encodings."
  ($_ seq
      (test "Binary."
            (and (n/= (bin "+11001001") (bin "+11_00_10_01"))
                 (i/= (bin "11001001") (bin "11_00_10_01"))
                 (d/= (bin ".11001001") (bin ".11_00_10_01"))
                 (f/= (bin "1100.1001") (bin "11_00.10_01"))))
      (test "Octal."
            (and (n/= (oct "+615243") (oct "+615_243"))
                 (i/= (oct "615243") (oct "615_243"))
                 (d/= (oct ".615243") (oct ".615_243"))
                 (f/= (oct "6152.43") (oct "615_2.43"))))
      (test "Hexadecimal."
            (and (n/= (hex "+deadBEEF") (hex "+dead_BEEF"))
                 (i/= (hex "deadBEEF") (hex "dead_BEEF"))
                 (d/= (hex ".deadBEEF") (hex ".dead_BEEF"))
                 (f/= (hex "deadBE.EF") (hex "dead_BE.EF"))))))
