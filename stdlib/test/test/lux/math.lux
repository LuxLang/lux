(;module:
  lux
  (lux [io]
       (control ["M" monad #+ do Monad])
       (data [text "Text/" Monoid<Text>]
             text/format
             [bool "b/" Eq<Bool>]
             [number "f/" Number<Frac>]
             (coll [list "List/" Fold<List> Functor<List>])
             [product])
       ["R" math/random]
       ["&" math])
  lux/test)

(def: (within? margin-of-error standard value)
  (-> Frac Frac Frac Bool)
  (f.< margin-of-error
       (f/abs (f.- standard value))))

## (def: margin Frac 0.0000001)

## ## The JVM trigonometry functions sometimes give me funky results.
## ## I won't be testing this, until I can figure out what's going on, or
## ## come up with my own implementation
## (context: "Trigonometry"
##   [angle (|> R;frac (:: @ map (f.* &;tau)))]
##   ($_ seq
##       (test "Sine and arc-sine are inverse functions."
##             (|> angle &;sin &;asin (within? margin angle)))

##       (test "Cosine and arc-cosine are inverse functions."
##             (|> angle &;cos &;acos (within? margin angle)))

##       (test "Tangent and arc-tangent are inverse functions."
##             (|> angle &;tan &;atan (within? margin angle)))
##       ))

(context: "Roots"
  [factor (|> R;nat (:: @ map (|>. (n.% +1000)
                                   (n.max +1)
                                   nat-to-int
                                   int-to-frac)))
   base (|> R;frac (:: @ map (f.* factor)))]
  ($_ seq
      (test "Square-root is inverse of square."
            (|> base (&;pow 2.0) &;root2 (f.= base)))
      
      (test "Cubic-root is inverse of cube."
            (|> base (&;pow 3.0) &;root3 (f.= base)))
      ))

(context: "Rounding"
  [sample (|> R;frac (:: @ map (f.* 1000.0)))]
  ($_ seq
      (test "The ceiling will be an integer value, and will be >= the original."
            (let [ceil'd (&;ceil sample)]
              (and (|> ceil'd frac-to-int int-to-frac (f.= ceil'd))
                   (f.>= sample ceil'd)
                   (f.<= 1.0 (f.- sample ceil'd)))))

      (test "The floor will be an integer value, and will be <= the original."
            (let [floor'd (&;floor sample)]
              (and (|> floor'd frac-to-int int-to-frac (f.= floor'd))
                   (f.<= sample floor'd)
                   (f.<= 1.0 (f.- floor'd sample)))))

      (test "The round will be an integer value, and will be < or > or = the original."
            (let [round'd (&;round sample)]
              (and (|> round'd frac-to-int int-to-frac (f.= round'd))
                   (f.<= 1.0 (f/abs (f.- sample round'd))))))
      ))

(context: "Exponentials and logarithms"
  [sample (|> R;frac (:: @ map (f.* 10.0)))]
  (test "Logarithm is the inverse of exponential."
        (|> sample &;exp &;log (within? 1.0e-15 sample))))

(context: "Greatest-Common-Divisor and Least-Common-Multiple"
  [#let [gen-nat (|> R;nat (:: @ map (|>. (n.% +1000) (n.max +1))))]
   x gen-nat
   y gen-nat]
  ($_ (test "GCD"
            (let [gcd (&;gcd x y)]
              (and (n.= +0 (n.% gcd x))
                   (n.= +0 (n.% gcd y))
                   (n.>= +1 gcd))))

      (test "LCM"
            (let [lcm (&;lcm x y)]
              (and (n.= +0 (n.% x lcm))
                   (n.= +0 (n.% y lcm))
                   (n.<= (n.* x y) lcm))))
      ))

(context: "Infix syntax"
  [x R;nat
   y R;nat
   z R;nat
   #let [top (|> x (n.max y) (n.max z))
         bottom (|> x (n.min y) (n.min z))]]
  ($_ seq
      (test "Constant values don't change."
            (n.= x (&;infix x)))

      (test "Can call infix functions."
            (n.= (&;gcd y x) (&;infix [x &;gcd y])))

      (test "Can use regular syntax in the middle of infix code."
            (n.= (&;gcd +450 (n.* +3 +9))
                 (&;infix [(n.* +3 +9) &;gcd +450])))

      (test "Can use non-numerical functions/macros as operators."
            (b/= (and (n.< y x) (n.< z y))
                 (&;infix [[x n.< y] and [y n.< z]])))

      (test "Can combine boolean operations in special ways via special keywords."
            (and (b/= (and (n.< y x) (n.< z y))
                      (&;infix [#and x n.< y n.< z]))
                 (b/= (and (n.< y x) (n.> z y))
                      (&;infix [#and x n.< y n.> z]))))
      ))
