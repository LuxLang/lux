... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.def# provenance#dummy
       ["" 0 0]
       #0)

(.def# prelude
       "library/lux"
       #1)

... (every .public Any
...   (for_some (_ a) a))
(.def# Any
       (.is_type#
        {9 #1
           [..prelude "Any"]
           {8 #0
              {#0}
              {4 #0 1}}})
       #1)

... (every .public Nothing
...   (for_any (_ a) a))
(.def# Nothing
       (.is_type#
        {9 #1
           [..prelude "Nothing"]
           {7 #0
              {#0}
              {4 #0 1}}})
       #1)

(.def# Bit
       (.is_type#
        {9 #1
           [..prelude "Bit"]
           {#0 "#Bit" {#0}}})
       #1)

(.def# I64
       (.is_type#
        {9 #1
           [..prelude "I64"]
           {7 #0
              {#0}
              {#0 "#I64" {#1 {4 #0 1} {#0}}}}})
       #1)

(.def# Natural
       (.is_type#
        {9 #1
           [..prelude "Natural"]
           {#0 "#I64" {#1 {#0 "#Nat" {#0}} {#0}}}})
       #1)

(.def# Integer
       (.is_type#
        {9 #1
           [..prelude "Integer"]
           {#0 "#I64" {#1 {#0 "#Int" {#0}} {#0}}}})
       #1)

(.def# Revolution
       (.is_type#
        {9 #1
           [..prelude "Revolution"]
           {#0 "#I64" {#1 {#0 "#Rev" {#0}} {#0}}}})
       #1)

(.def# Decimal
       (.is_type#
        {9 #1
           [..prelude "Decimal"]
           {#0 "#Dec" {#0}}})
       #1)

(.def# Text
       (.is_type#
        {9 #1
           [..prelude "Text"]
           {#0 "#Text" {#0}}})
       #1)

(.def# Name
       (.is_type#
        {9 #1
           [..prelude "Name"]
           {2 #0 Text Text}})
       #1)

(.def# Error
       (.is_type#
        {9 #1
           [..prelude "Error"]
           ..Text})
       #1)

... (every .public (List of)
...   (Variant
...    {#End}
...    {#Item of (List of)}))
(.def# List
       (.is_type#
        {9 #1
           [..prelude "List"]
           {7 #0
              {#0}
              {1 #0
                 ... End
                 Any
                 ... Item
                 {2 #0
                    {4 #0 1}
                    {9 #0
                       {4 #0 1}
                       {4 #0 0}}}}}})
       #1)

... (every .public (Maybe of)
...   (Variant
...    {#None}
...    {#Some of}))
(.def# Maybe
       (.is_type#
        {9 #1
           [..prelude "Maybe"]
           {7 #0
              {#0}
              {1 #0
                 ... None
                 Any
                 ... Some
                 {4 #0 1}}}})
       #1)

... https://en.wikipedia.org/wiki/Quantifier_(logic)
... (every .public (Quantification of)
...   [(List of) of])
(.def# Quantification
       (.is_type#
        {9 #1
           [..prelude "Quantification"]
           {7 #0
              {#0}
              {2 #0
                 {9 #0 {4 #0 1} List}
                 {4 #0 1}}}})
       #1)

... (every .public Type
...   (Rec Type
...     (Variant
...      {#Nominal Text (List Type)}
...      {#Sum Type Type}
...      {#Product Type Type}
...      {#Function Type Type}
...      {#Parameter Natural}
...      {#Variable Natural}
...      {#Opaque Natural}
...      {#Universal (List Type) Type}
...      {#Existential (List Type) Type}
...      {#Reification Type Type}
...      {#Named Name Type})))
(.def# Type
       (.is_type#
        {9 #1 [..prelude "Type"]
           (.when# (.is_type# {9 #0 {#0 ["" {#0}]} {4 #0 0}})
                   Type
                   (.when# (.is_type# {2 #0 Type Type})
                           Type_Pair
                           {9 #0
                              {#0 ["" {#0}]}
                              {7 #0
                                 {#0}
                                 {1 #0
                                    ... Nominal
                                    {2 #0 Text {9 #0 Type List}}
                                    {1 #0
                                       ... Sum
                                       Type_Pair
                                       {1 #0
                                          ... Product
                                          Type_Pair
                                          {1 #0
                                             ... Function
                                             Type_Pair
                                             {1 #0
                                                ... Parameter
                                                Natural
                                                {1 #0
                                                   ... Var
                                                   Natural
                                                   {1 #0
                                                      ... Ex
                                                      Natural
                                                      {1 #0
                                                         ... Universal
                                                         {9 #0 Type Quantification}
                                                         {1 #0
                                                            ... Existential
                                                            {9 #0 Type Quantification}
                                                            {1 #0
                                                               ... Reification
                                                               Type_Pair
                                                               ... Named
                                                               {2 #0 Name Type}}}}}}}}}}}}}))})
       #1)

... (every .public Label
...   [(Maybe [Natural Bit (List Name)])
...    Type])
(.def# Label
       (.is_type#
        {9 #1 [..prelude "Label"]
           {2 #0
              {9 #0 {2 #0 Natural
                       {2 #0 Bit
                          {9 #0 Name List}}}
                 Maybe}
              Type}})
       #1)

... (every .public Tag
...   (Nominal "#Tag"))
(.def# Tag
       (.is_type#
        {9 #1 [..prelude "Tag"]
           {#0 "#Tag" {#0}}})
       #1)

... (every .public Slot
...   (Nominal "#Slot"))
(.def# Slot
       (.is_type#
        {9 #1 [..prelude "Slot"]
           {#0 "#Slot" {#0}}})
       #1)

(.def# tag
       (.is# {3 #0 Label
                Tag}
             (.function# _ it (.as# Tag it)))
       #0)

(.def# slot
       (.is# {3 #0 Label
                Slot}
             (.function# _ it (.as# Slot it)))
       #0)

(.def# list_tags
       (.is# {9 #0 Name List}
             {#1 [[..prelude "#End"]
                  {#1 [[..prelude "#Item"]
                       {#0}]}]})
       #0)
(.def# #End (..tag [{#1 [0 #0 ..list_tags]} List]) #1)
(.def# #Item (..tag [{#1 [0 #1 ..list_tags]} List]) #1)

(.def# maybe_tags
       (.is# {9 #0 Name List}
             {#1 [[..prelude "#None"]
                  {#1 [[..prelude "#Some"]
                       {#0}]}]})
       #0)
(.def# #None (..tag [{#1 [0 #0 ..maybe_tags]} Maybe]) #1)
(.def# #Some (..tag [{#1 [0 #1 ..maybe_tags]} Maybe]) #1)

(.def# type_tags
       (.is# {9 #0 Name List}
             {#1 [[..prelude "#Nominal"]
                  {#1 [[..prelude "#Sum"]
                       {#1 [[..prelude "#Product"]
                            {#1 [[..prelude "#Function"]
                                 {#1 [[..prelude "#Parameter"]
                                      {#1 [[..prelude "#Variable"]
                                           {#1 [[..prelude "#Opaque"]
                                                {#1 [[..prelude "#Universal"]
                                                     {#1 [[..prelude "#Existential"]
                                                          {#1 [[..prelude "#Reification"]
                                                               {#1 [[..prelude "#Named"]
                                                                    {#0}]}]}]}]}]}]}]}]}]}]}]})
       #0)
(.def# #Nominal (..tag [{#Some [0 #0 ..type_tags]} Type]) #1)
(.def# #Sum (..tag [{#Some [1 #0 ..type_tags]} Type]) #1)
(.def# #Product (..tag [{#Some [2 #0 ..type_tags]} Type]) #1)
(.def# #Function (..tag [{#Some [3 #0 ..type_tags]} Type]) #1)
(.def# #Parameter (..tag [{#Some [4 #0 ..type_tags]} Type]) #1)
(.def# #Variable (..tag [{#Some [5 #0 ..type_tags]} Type]) #1)
... https://en.wikipedia.org/wiki/Opaque_data_type
(.def# #Opaque (..tag [{#Some [6 #0 ..type_tags]} Type]) #1)
... https://en.wikipedia.org/wiki/Universal_quantification
(.def# #Universal (..tag [{#Some [7 #0 ..type_tags]} Type]) #1)
... https://en.wikipedia.org/wiki/Existential_quantification
(.def# #Existential (..tag [{#Some [8 #0 ..type_tags]} Type]) #1)
(.def# #Reification (..tag [{#Some [9 #0 ..type_tags]} Type]) #1)
(.def# #Named (..tag [{#Some [9 #1 ..type_tags]} Type]) #1)

... (every .public Provenance
...   (Record
...    [#module Text
...     #line   Natural
...     #column Natural]))
(.def# Provenance
       (.is# Type
             {#Named [..prelude "Provenance"]
                     {#Product Text {#Product Natural Natural}}})
       #1)
(.def# provenance_slots
       (.is# {#Reification Name List}
             {#Item [..prelude "#module"]
                    {#Item [..prelude "#line"]
                           {#Item [..prelude "#column"]
                                  {#End}}}})
       #0)
(.def# #module (slot [{#Some [0 #0 ..provenance_slots]} Provenance]) #1)
(.def# #line (slot [{#Some [1 #0 ..provenance_slots]} Provenance]) #1)
(.def# #column (slot [{#Some [1 #1 ..provenance_slots]} Provenance]) #1)

... (every .public (Annotated m v)
...   (Record
...    [#meta  m
...     #datum v]))
(.def# Annotated
       (.is# Type
             {#Named [..prelude "Annotated"]
                     {#Universal {#End}
                                 {#Universal {#End}
                                             {#Product
                                              {#Parameter 3}
                                              {#Parameter 1}}}}})
       #1)
(.def# ann_slots
       (.is# {#Reification Name List}
             {#Item [..prelude "#meta"]
                    {#Item [..prelude "#datum"]
                           {#End}}})
       #0)
(.def# #meta (slot [{#Some [0 #0 ..ann_slots]} Annotated]) #1)
(.def# #datum (slot [{#Some [0 #1 ..ann_slots]} Annotated]) #1)

... (every .public (Code' w)
...   (Variant
...    {#Bit Bit}
...    {#Natural Natural}
...    {#Integer Integer}
...    {#Revolution Revolution}
...    {#Decimal Decimal}
...    {#Text Text}
...    {#Name Name}
...    {#Form (List (w (Code' w)))}
...    {#Variant (List (w (Code' w)))}
...    {#Tuple (List (w (Code' w)))}))
(.def# Code'
       (.is# Type
             {#Named [..prelude "Code'"]
                     (.when# (.is# Type
                                   {#Reification {#Reification {#Parameter 1}
                                                               {#Parameter 0}}
                                                 {#Parameter 1}})
                             Code
                             (.when# (.is# Type
                                           {#Reification Code List})
                                     Code_List
                                     {#Universal {#End}
                                                 {#Sum
                                                  ... Bit
                                                  Bit
                                                  {#Sum
                                                   ... Natural
                                                   Natural
                                                   {#Sum
                                                    ... Integer
                                                    Integer
                                                    {#Sum
                                                     ... Revolution
                                                     Revolution
                                                     {#Sum
                                                      ... Decimal
                                                      Decimal
                                                      {#Sum
                                                       ... Text
                                                       Text
                                                       {#Sum
                                                        ... Name
                                                        Name
                                                        {#Sum
                                                         ... Form
                                                         Code_List
                                                         {#Sum
                                                          ... Variant
                                                          Code_List
                                                          ... Tuple
                                                          Code_List
                                                          }}}}}}}}}
                                                 }))})
       #1)
(.def# code'_tags
       (.is# {#Reification Name List}
             {#Item [..prelude "#Bit"]
                    {#Item [..prelude "#Natural"]
                           {#Item [..prelude "#Integer"]
                                  {#Item [..prelude "#Revolution"]
                                         {#Item [..prelude "#Decimal"]
                                                {#Item [..prelude "#Text"]
                                                       {#Item [..prelude "#Name"]
                                                              {#Item [..prelude "#Form"]
                                                                     {#Item [..prelude "#Variant"]
                                                                            {#Item [..prelude "#Tuple"]
                                                                                   {#End}}}}}}}}}}})
       #0)
(.def# #Bit (tag [{#Some [0 #0 ..code'_tags]} Code']) #1)
(.def# #Natural (tag [{#Some [1 #0 ..code'_tags]} Code']) #1)
(.def# #Integer (tag [{#Some [2 #0 ..code'_tags]} Code']) #1)
(.def# #Revolution (tag [{#Some [3 #0 ..code'_tags]} Code']) #1)
(.def# #Decimal (tag [{#Some [4 #0 ..code'_tags]} Code']) #1)
(.def# #Text (tag [{#Some [5 #0 ..code'_tags]} Code']) #1)
(.def# #Name (tag [{#Some [6 #0 ..code'_tags]} Code']) #1)
(.def# #Form (tag [{#Some [7 #0 ..code'_tags]} Code']) #1)
(.def# #Variant (tag [{#Some [8 #0 ..code'_tags]} Code']) #1)
(.def# #Tuple (tag [{#Some [8 #1 ..code'_tags]} Code']) #1)

... (every .public Code
...   (Annotated Provenance (Code' (Annotated Provenance))))
(.def# Code
       (.is# Type
             {#Named [..prelude "Code"]
                     (.when# (.is# Type {#Reification Provenance Annotated})
                             w
                             {#Reification {#Reification w Code'} w})})
       #1)

(.def# private
       #0
       #1)

(.def# public
       #1
       #1)

(.def# local
       #0
       #1)

(.def# global
       #1
       #1)

(.def# annotated
       (.is# {#Function {#Reification {#Reification Provenance Annotated} Code'}
                        Code}
             (.function# _ data
                         [provenance#dummy data]))
       #0)

(.def# as_bit
       (.is# {#Function Bit
                        Code}
             (.function# _ value (annotated {#Bit value})))
       #0)

(.def# as_natural
       (.is# {#Function Natural
                        Code}
             (.function# _ value (annotated {#Natural value})))
       #0)

(.def# as_integer
       (.is# {#Function Integer
                        Code}
             (.function# _ value (annotated {#Integer value})))
       #0)

(.def# as_revolution
       (.is# {#Function Revolution
                        Code}
             (.function# _ value (annotated {#Revolution value})))
       #0)

(.def# as_decimal
       (.is# {#Function Decimal
                        Code}
             (.function# _ value (annotated {#Decimal value})))
       #0)

(.def# as_text
       (.is# {#Function Text
                        Code}
             (.function# _ text (annotated {#Text text})))
       #0)

(.def# as_name
       (.is# {#Function Name
                        Code}
             (.function# _ name (annotated {#Name name})))
       #0)

(.def# as_local
       (.is# {#Function Text
                        Code}
             (.function# _ name (annotated {#Name ["" name]})))
       #0)

(.def# as_form
       (.is# {#Function {#Reification Code List}
                        Code}
             (.function# _ tokens (annotated {#Form tokens})))
       #0)

(.def# as_variant
       (.is# {#Function {#Reification Code List}
                        Code}
             (.function# _ tokens (annotated {#Variant tokens})))
       #0)

(.def# as_tuple
       (.is# {#Function {#Reification Code List}
                        Code}
             (.function# _ tokens (annotated {#Tuple tokens})))
       #0)

... (every .public Definition
...   [Type Any])
(.def# Definition
       (.is# Type
             {#Named [..prelude "Definition"]
                     {#Product Type Any}})
       .public)

... (every .public Default
...   Definition)
(.def# Default
       (.is# Type
             {#Named [..prelude "Default"]
                     Definition})
       .public)

... (every .public Alias
...   Name)
(.def# Alias
       (.is# Type
             {#Named [..prelude "Alias"]
                     Name})
       .public)

... (every .public Global
...   (Variant
...    {#Definition Definition}
...    {#Alias Alias}
...    {#Default Default}))
(.def# Global
       (.is# Type
             {#Named [..prelude "Global"]
                     {#Sum Definition
                           {#Sum Alias
                                 Default}}})
       .public)

(.def# global_tags
       (.is# {#Reification Name List}
             {#Item [..prelude "#Definition"]
                    {#Item [..prelude "#Alias"]
                           {#Item [..prelude "#Default"]
                                  {#End}}}})
       #0)
(.def# #Definition (tag [{#Some [0 #0 ..global_tags]} Global]) .public)
(.def# #Alias (tag [{#Some [1 #0 ..global_tags]} Global]) .public)
(.def# #Default (tag [{#Some [1 #1 ..global_tags]} Global]) .public)

... (every .public (Bindings k v)
...   (Record
...    [#counter Natural
...     #mappings (List [k v])]))
(.def# Bindings
       (.is# Type
             {#Named [..prelude "Bindings"]
                     {#Universal {#End}
                                 {#Universal {#End}
                                             {#Product
                                              ... counter
                                              Natural
                                              ... mappings
                                              {#Reification {#Product {#Parameter 3}
                                                                      {#Parameter 1}}
                                                            List}}}}})
       .public)

(.def# bindings_slots
       (.is# {#Reification Name List}
             {#Item [..prelude "#counter"]
                    {#Item [..prelude "#mappings"]
                           {#End}}})
       #0)
(.def# #counter (slot [{#Some [0 #0 ..bindings_slots]} Bindings]) .public)
(.def# #mappings (slot [{#Some [0 #1 ..bindings_slots]} Bindings]) .public)

... (every .public Ref
...   {#Local Natural}
...   {#Captured Natural})
(.def# Ref
       (.is# Type
             {#Named [..prelude "Ref"]
                     {#Sum
                      ... Local
                      Natural
                      ... Captured
                      Natural}})
       .public)

(.def# ref_tags
       (.is# {#Reification Name List}
             {#Item [..prelude "#Local"]
                    {#Item [..prelude "#Captured"]
                           {#End}}})
       #0)
(.def# #Local (tag [{#Some [0 #0 ..ref_tags]} Ref]) .public)
(.def# #Captured (tag [{#Some [0 #1 ..ref_tags]} Ref]) .public)

... TODO: Get rid of both #scope_name & #inner_scopes
... (every .public Scope
...   (Record
...    [#scope_name (List Text)
...     #inner_scopes Natural
...     #locals (Bindings Text [Type Natural])
...     #captured (Bindings Text [Type Ref])]))
(.def# Scope
       (.is# Type
             {#Named [..prelude "Scope"]
                     {#Product
                      ... name
                      {#Reification Text List}
                      {#Product
                       ... inner
                       Natural
                       {#Product
                        ... locals
                        {#Reification {#Product Type Natural} {#Reification Text Bindings}}
                        ... captured
                        {#Reification {#Product Type Ref} {#Reification Text Bindings}}}}}})
       .public)

(.def# scope_slots
       (.is# {#Reification Name List}
             {#Item [..prelude "#scope_name"]
                    {#Item [..prelude "#inner_scopes"]
                           {#Item [..prelude "#locals"]
                                  {#Item [..prelude "#captured"]
                                         {#End}}}}})
       #0)
(.def# #scope_name (slot [{#Some [0 #0 ..scope_slots]} Scope]) .public)
(.def# #inner_scopes (slot [{#Some [1 #0 ..scope_slots]} Scope]) .public)
(.def# #locals (slot [{#Some [2 #0 ..scope_slots]} Scope]) .public)
(.def# #captured (slot [{#Some [2 #1 ..scope_slots]} Scope]) .public)

(.def# Code_List
       (.is# Type
             {#Reification Code List})
       #0)

... (every .public (Either l r)
...   (Variant
...    {#Left l}
...    {#Right r}))
(.def# Either
       (.is# Type
             {#Named [..prelude "Either"]
                     {#Universal {#End}
                                 {#Universal {#End}
                                             {#Sum
                                              ... Left
                                              {#Parameter 3}
                                              ... Right
                                              {#Parameter 1}}}}})
       .public)

(.def# either_tags
       (.is# {#Reification Name List}
             {#Item [..prelude "#Left"]
                    {#Item [..prelude "#Right"]
                           {#End}}})
       #0)
(.def# #Left (tag [{#Some [0 #0 ..either_tags]} Either]) .public)
(.def# #Right (tag [{#Some [0 #1 ..either_tags]} Either]) .public)

... (every .public Source
...   [Provenance Natural Text])
(.def# Source
       (.is# Type
             {#Named [..prelude "Source"]
                     {#Product Provenance {#Product Natural Text}}})
       .public)

... (every .public Module_State
...   (Variant
...    #Active
...    #Compiled
...    #Cached))
(.def# Module_State
       (.is# Type
             {#Named [..prelude "Module_State"]
                     {#Sum
                      ... #Active
                      Any
                      {#Sum
                       ... #Compiled
                       Any
                       ... #Cached
                       Any}}})
       .public)

(.def# module_state_tags
       (.is# {#Reification Name List}
             {#Item [..prelude "#Active"]
                    {#Item [..prelude "#Compiled"]
                           {#Item [..prelude "#Cached"]
                                  {#End}}}})
       #0)
(.def# #Active (tag [{#Some [0 #0 ..module_state_tags]} Module_State]) .public)
(.def# #Compiled (tag [{#Some [1 #0 ..module_state_tags]} Module_State]) .public)
(.def# #Cached (tag [{#Some [1 #1 ..module_state_tags]} Module_State]) .public)

... (every .public Module
...   (Record
...    [#module_hash        Natural
...     #module_aliases     (List [Text Text])
...     #definitions        (List [Text [Bit Global]])
...     #imports            (List Text)
...     #module_state       Module_State]))
(.def# Module
       (.is# Type
             {#Named [..prelude "Module"]
                     {#Product
                      ... module_hash
                      Natural
                      {#Product
                       ... module_aliases
                       {#Reification {#Product Text Text} List}
                       {#Product
                        ... definitions
                        {#Reification {#Product Text {#Product Bit Global}} List}
                        {#Product
                         ... imports
                         {#Reification Text List}
                         ... module_state
                         Module_State
                         }}}}})
       .public)

(.def# module_slots
       (.is# {#Reification Name List}
             {#Item [..prelude "#module_hash"]
                    {#Item [..prelude "#module_aliases"]
                           {#Item [..prelude "#definitions"]
                                  {#Item [..prelude "#imports"]
                                         {#Item [..prelude "#module_state"]
                                                {#End}}}}}})
       #0)
(.def# #module_hash (slot [{#Some [0 #0 ..module_slots]} Module]) .public)
(.def# #module_aliases (slot [{#Some [1 #0 ..module_slots]} Module]) .public)
(.def# #definitions (slot [{#Some [2 #0 ..module_slots]} Module]) .public)
(.def# #imports (slot [{#Some [3 #0 ..module_slots]} Module]) .public)
(.def# #module_state (slot [{#Some [3 #1 ..module_slots]} Module]) .public)

... (every .public Type_Context
...   (Record
...    [#ex_counter Natural
...     #var_counter Natural
...     #var_bindings (List [Natural (Maybe Type)])]))
(.def# Type_Context
       (.is# Type
             {#Named [..prelude "Type_Context"]
                     {#Product ... ex_counter
                      Natural
                      {#Product ... var_counter
                       Natural
                       ... var_bindings
                       {#Reification {#Product Natural {#Reification Type Maybe}}
                                     List}}}})
       .public)

(.def# type_context_slots
       (.is# {#Reification Name List}
             {#Item [..prelude "#ex_counter"]
                    {#Item [..prelude "#var_counter"]
                           {#Item [..prelude "#var_bindings"]
                                  {#End}}}})
       #0)
(.def# #ex_counter (slot [{#Some [0 #0 ..type_context_slots]} Type_Context]) .public)
(.def# #var_counter (slot [{#Some [1 #0 ..type_context_slots]} Type_Context]) .public)
(.def# #var_bindings (slot [{#Some [1 #1 ..type_context_slots]} Type_Context]) .public)

... (every .public Mode
...   (Variant
...    {#Build}
...    {#Eval}
...    {#Interpreter}))
(.def# Mode
       (.is# Type
             {#Named [..prelude "Mode"]
                     {#Sum
                      ... Build
                      Any
                      {#Sum
                       ... Eval
                       Any
                       ... Interpreter
                       Any}}})
       .public)

(.def# mode_tags
       (.is# {#Reification Name List}
             {#Item [..prelude "#Build"]
                    {#Item [..prelude "#Eval"]
                           {#Item [..prelude "#Interpreter"]
                                  {#End}}}})
       #0)
(.def# #Build (tag [{#Some [0 #0 ..mode_tags]} Mode]) .public)
(.def# #Eval (tag [{#Some [1 #0 ..mode_tags]} Mode]) .public)
(.def# #Interpreter (tag [{#Some [1 #1 ..mode_tags]} Mode]) .public)

... (every .public Info
...   (Record
...    [#target        Text
...     #version       Text
...     #mode          Mode
...     #configuration (List [Text Text])]))
(.def# Info
       (.is# Type
             {#Named [..prelude "Info"]
                     {#Product
                      ... target
                      Text
                      {#Product
                       ... version
                       Text
                       {#Product
                        ... mode
                        Mode
                        ... configuration
                        {#Reification {#Product Text Text} List}}}}})
       .public)

(.def# info_slots
       (.is# {#Reification Name List}
             {#Item [..prelude "#target"]
                    {#Item [..prelude "#version"]
                           {#Item [..prelude "#mode"]
                                  {#Item [..prelude "#configuration"]
                                         {#End}}}}})
       #0)
(.def# #target (slot [{#Some [0 #0 ..info_slots]} Info]) .public)
(.def# #version (slot [{#Some [1 #0 ..info_slots]} Info]) .public)
(.def# #mode (slot [{#Some [2 #0 ..info_slots]} Info]) .public)
(.def# #configuration (slot [{#Some [2 #1 ..info_slots]} Info]) .public)

... (every .public Lux
...   (Rec Lux
...     (Record
...      [#info            Info
...       #source          Source
...       #provenance      Provenance
...       #current_module  (Maybe Text)
...       #modules         (List [Text Module])
...       #scopes          (List Scope)
...       #type_context    Type_Context
...       #expected        (Maybe Type)
...       #seed            Natural
...       #scope_type_vars (List Natural)
...       #extensions      Any
...       #eval            (-> Type Code (-> Lux (Either Text [Lux Any])))
...       #host            Any])))
(.def# Lux
       (.is# Type
             {#Named [..prelude "Lux"]
                     (.when# {#Reification {#0 ["" {#End}]} {#Parameter 0}}
                             Lux
                             {#Reification {#0 ["" {#End}]}
                                           {#Universal {#End}
                                                       {#Product
                                                        ... info
                                                        Info
                                                        {#Product
                                                         ... source
                                                         Source
                                                         {#Product
                                                          ... provenance
                                                          Provenance
                                                          {#Product
                                                           ... current_module
                                                           {#Reification Text Maybe}
                                                           {#Product
                                                            ... modules
                                                            {#Reification {#Product Text Module} List}
                                                            {#Product
                                                             ... scopes
                                                             {#Reification Scope List}
                                                             {#Product
                                                              ... type_context
                                                              Type_Context
                                                              {#Product
                                                               ... expected
                                                               {#Reification Type Maybe}
                                                               {#Product
                                                                ... seed
                                                                Natural
                                                                {#Product
                                                                 ... scope_type_vars
                                                                 {#Reification Natural List}
                                                                 {#Product
                                                                  ... extensions
                                                                  Any
                                                                  {#Product
                                                                   ... eval
                                                                   {#Function Type
                                                                              {#Function Code
                                                                                         {#Function Lux
                                                                                                    {#Sum Text {#Product Lux Any}}}}}
                                                                   ... host
                                                                   Any}}}}}}}}}}}}}})})
       .public)

(.def# lux_slots
       (.is# {#Reification Name List}
             {#Item [..prelude "#info"]
                    {#Item [..prelude "#source"]
                           {#Item [..prelude "#provenance"]
                                  {#Item [..prelude "#current_module"]
                                         {#Item [..prelude "#modules"]
                                                {#Item [..prelude "#scopes"]
                                                       {#Item [..prelude "#type_context"]
                                                              {#Item [..prelude "#expected"]
                                                                     {#Item [..prelude "#seed"]
                                                                            {#Item [..prelude "#scope_type_vars"]
                                                                                   {#Item [..prelude "#extensions"]
                                                                                          {#Item [..prelude "#eval"]
                                                                                                 {#Item [..prelude "#host"]
                                                                                                        {#End}}}}}}}}}}}}}})
       #0)
(.def# #info (slot [{#Some [0 #0 ..lux_slots]} Lux]) .public)
(.def# #source (slot [{#Some [1 #0 ..lux_slots]} Lux]) .public)
(.def# #provenance (slot [{#Some [2 #0 ..lux_slots]} Lux]) .public)
(.def# #current_module (slot [{#Some [3 #0 ..lux_slots]} Lux]) .public)
(.def# #modules (slot [{#Some [4 #0 ..lux_slots]} Lux]) .public)
(.def# #scopes (slot [{#Some [5 #0 ..lux_slots]} Lux]) .public)
(.def# #type_context (slot [{#Some [6 #0 ..lux_slots]} Lux]) .public)
(.def# #expected (slot [{#Some [7 #0 ..lux_slots]} Lux]) .public)
(.def# #seed (slot [{#Some [8 #0 ..lux_slots]} Lux]) .public)
(.def# #scope_type_vars (slot [{#Some [9 #0 ..lux_slots]} Lux]) .public)
(.def# #extensions (slot [{#Some [10 #0 ..lux_slots]} Lux]) .public)
(.def# #eval (slot [{#Some [11 #0 ..lux_slots]} Lux]) .public)
(.def# #host (slot [{#Some [11 #1 ..lux_slots]} Lux]) .public)

... (every .public (Meta a)
...   (-> Lux (Either Text [Lux a])))
(.def# Meta
       (.is# Type
             {#Named [..prelude "Meta"]
                     {#Universal {#End}
                                 {#Function Lux
                                            {#Reification {#Product Lux {#Parameter 1}}
                                                          {#Reification Text Either}}}}})
       .public)

... (every .public Macro'
...   (-> (List Code) (Meta (List Code))))
(.def# Macro'
       (.is# Type
             {#Named [..prelude "Macro'"]
                     {#Function Code_List {#Reification Code_List Meta}}})
       .public)

... (every .public Macro
...   (Nominal "#Macro"))
(.def# Macro
       (.is# Type
             {#Named [..prelude "Macro"]
                     {#Nominal "#Macro" {#End}}})
       .public)

... Base functions & macros
(.def# meta#in
       (.is# {#Universal {#End}
                         {#Function {#Parameter 1}
                                    {#Reification {#Parameter 1} Meta}}}
             (.function# _ val
                         (.function# _ state
                                     {#Right [state val]})))
       #0)

(.def# failure
       (.is# {#Universal {#End}
                         {#Function Text
                                    {#Reification {#Parameter 1} Meta}}}
             (.function# _ msg
                         (.function# _ state
                                     {#Left msg})))
       #0)

(.def# text#composite
       (.is# {#Function Text {#Function Text Text}}
             (.function# _ x
                         (.function# _ y
                                     (.text_composite# x y))))
       #0)

... https://en.wikipedia.org/wiki/Delimiter
(.def# name_delimiter
       (.is# Text
             ".")
       #0)

(.def# name#as
       (.is# {#Function Name
                        Text}
             (.function# _ full_name
                         (.when# full_name
                                 [module name]
                                 (.when# module
                                         "" name
                                         _  (.text_composite# module ..name_delimiter name)))))
       #0)

(.def# \''
       (.is# Text
             (.int_char# +34))
       #0)

(.def# wrong_syntax
       (.is# Error
             "Wrong syntax.")
       #1)

(.def# let''
       (.as# Macro
             (.is# Macro'
                   (.function# _ tokens
                               (.when# tokens
                                       {#Item lhs {#Item rhs {#Item body {#End}}}}
                                       (meta#in {#Item (as_form {#Item (as_name [..prelude "when#"])
                                                                       {#Item rhs
                                                                              {#Item lhs
                                                                                     {#Item body
                                                                                            {#End}}}}})
                                                       {#End}})

                                       _
                                       (failure ..wrong_syntax)))))
       #0)

(.def# function''
       (.as# Macro
             (.is# Macro'
                   (.function# _ tokens
                               (.when# tokens
                                       {#Item [_ {#Tuple {#Item arg args'}}] {#Item body {#End}}}
                                       (meta#in {#Item (annotated {#Form {#Item (as_name [..prelude "function#"])
                                                                                {#Item (annotated {#Name ["" ""]})
                                                                                       {#Item arg
                                                                                              {#Item (.when# args'
                                                                                                             {#End}
                                                                                                             body

                                                                                                             _
                                                                                                             (annotated {#Form {#Item (annotated {#Name [..prelude "function''"]})
                                                                                                                                      {#Item (annotated {#Tuple args'})
                                                                                                                                             {#Item body {#End}}}}}))
                                                                                                     {#End}}}}}})
                                                       {#End}})

                                       {#Item [_ {#Name ["" self]}] {#Item [_ {#Tuple {#Item arg args'}}] {#Item body {#End}}}}
                                       (meta#in {#Item (annotated {#Form {#Item (as_name [..prelude "function#"])
                                                                                {#Item (annotated {#Name ["" self]})
                                                                                       {#Item arg
                                                                                              {#Item (.when# args'
                                                                                                             {#End}
                                                                                                             body

                                                                                                             _
                                                                                                             (annotated {#Form {#Item (annotated {#Name [..prelude "function''"]})
                                                                                                                                      {#Item (annotated {#Tuple args'})
                                                                                                                                             {#Item body {#End}}}}}))
                                                                                                     {#End}}}}}})
                                                       {#End}})

                                       _
                                       (failure ..wrong_syntax)))))
       #0)

(.def# as_def
       (.is# {#Function Code {#Function Code {#Function Code Code}}}
             (function'' [name value export_policy]
                         (as_form {#Item (as_name [..prelude "def#"]) {#Item name {#Item value {#Item export_policy {#End}}}}})))
       #0)

(.def# as_checked
       (.is# {#Function Code {#Function Code Code}}
             (function'' [type value]
                         (as_form {#Item (as_name [..prelude "is#"]) {#Item type {#Item value {#End}}}})))
       #0)

(.def# as_function
       (.is# {#Function Code {#Function {#Reification Code List} {#Function Code Code}}}
             (function'' as_function [self inputs output]
                         (.when# inputs
                                 {#End}
                                 output

                                 {#Item head tail}
                                 (annotated {#Form {#Item (as_name [..prelude "function#"])
                                                          {#Item self
                                                                 {#Item head
                                                                        {#Item (as_function (annotated {#Name ["" ""]}) tail output)
                                                                               {#End}}}}}}))))
       #0)

(.def# as_macro
       (.is# {#Function Code
                        Code}
             (function'' [expression]
                         (as_form {#Item (as_name [..prelude "as#"])
                                         {#Item (as_name [..prelude "Macro"])
                                                {#Item (as_form {#Item (as_name [..prelude "is#"])
                                                                       {#Item (as_name [..prelude "Macro'"])
                                                                              {#Item expression
                                                                                     {#End}}}})
                                                       {#End}}}})))
       #0)

(.def# def'
       (.as# Macro
             (.is# Macro'
                   (function'' [tokens]
                               (.when# tokens
                                       {#Item [export_policy
                                               {#Item [[_ {#Form {#Item [name args]}}]
                                                       {#Item [type {#Item [body {#End}]}]}]}]}
                                       (meta#in {#Item [(as_def name
                                                                (as_checked type (as_function name args body))
                                                                export_policy)
                                                        {#End}]})

                                       {#Item [export_policy {#Item [name {#Item [type {#Item [body {#End}]}]}]}]}
                                       (meta#in {#Item [(as_def name (as_checked type body)
                                                                export_policy)
                                                        {#End}]})

                                       _
                                       (failure ..wrong_syntax)))))
       #0)

(.def# macro
       (.as# Macro
             (.is# Macro'
                   (function'' [tokens]
                               (.when# tokens
                                       {#Item [_ {#Form {#Item name {#Item head tail}}}] {#Item body {#End}}}
                                       (meta#in {#Item (as_macro (as_function name {#Item head tail} body))
                                                       {#End}})

                                       _
                                       (failure ..wrong_syntax)))))
       #1)

(def' .public comment
      Macro
      (macro (_ tokens)
        (meta#in {#End})))

(def' .private $
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item x {#End}}
                (meta#in tokens)

                {#Item x {#Item y xs}}
                (meta#in {#Item (as_form {#Item (as_name [..prelude "$"])
                                                {#Item (as_variant {#Item (as_name [..prelude "#Reification"])
                                                                          {#Item y {#Item x {#End}}}})
                                                       xs}})
                                {#End}})

                _
                (failure ..wrong_syntax))))

(def' .private (list#mix f init xs)
      ... (for_any (_ a b) (-> (-> b a a) a (List b) a))
      {#Universal {#End}
                  {#Universal {#End}
                              {#Function {#Function {#Parameter 1}
                                                    {#Function {#Parameter 3}
                                                               {#Parameter 3}}}
                                         {#Function {#Parameter 3}
                                                    {#Function ($ List {#Parameter 1})
                                                               {#Parameter 3}}}}}}
      (.when# xs
              {#End}
              init

              {#Item x xs'}
              (list#mix f (f x init) xs')))

(def' .private (list#reversed list)
      {#Universal {#End}
                  {#Function ($ List {#Parameter 1})
                             ($ List {#Parameter 1})}}
      (list#mix (.is# {#Universal {#End}
                                  {#Function {#Parameter 1} {#Function ($ List {#Parameter 1}) ($ List {#Parameter 1})}}}
                      (function'' [head tail] {#Item head tail}))
                {#End}
                list))

(def' .private (list#each f xs)
      {#Universal {#End}
                  {#Universal {#End}
                              {#Function {#Function {#Parameter 3} {#Parameter 1}}
                                         {#Function ($ List {#Parameter 3})
                                                    ($ List {#Parameter 1})}}}}
      (list#mix (function'' [head tail] {#Item (f head) tail})
                {#End}
                (list#reversed xs)))

(def' .private Replacement_Environment
      Type
      ($ List {#Product Text Code}))

(def' .private (replacement_environment xs ys)
      {#Function ($ List Text) {#Function ($ List Code) Replacement_Environment}}
      (.when# [xs ys]
              [{#Item x xs'} {#Item y ys'}]
              {#Item [x y] (replacement_environment xs' ys')}

              _
              {#End}))

(def' .private (text#= expected actual)
      {#Function Text {#Function Text Bit}}
      (.text_=# expected actual))

(def' .private (replacement for environment)
      {#Function Text {#Function Replacement_Environment ($ Maybe Code)}}
      (.when# environment
              {#End}
              {#None}

              {#Item [k v] environment'}
              (.when# (text#= k for)
                      [#1] {#Some v}
                      [#0] (replacement for environment'))))

(def' .private (with_replacements reps syntax)
      {#Function Replacement_Environment {#Function Code Code}}
      (.when# syntax
              [_ {#Name "" name}]
              (.when# (..replacement name reps)
                      {#Some replacement}
                      replacement

                      {#None}
                      syntax)

              [meta {#Form parts}]
              [meta {#Form (list#each (with_replacements reps) parts)}]

              [meta {#Variant members}]
              [meta {#Variant (list#each (with_replacements reps) members)}]

              [meta {#Tuple members}]
              [meta {#Tuple (list#each (with_replacements reps) members)}]
              
              _
              syntax))

(def' .private (n#* param subject)
      {#Function Natural {#Function Natural Natural}}
      (.as# Natural
            (.int_*# (.as# Integer param)
                     (.as# Integer subject))))

(def' .private (list#size list)
      {#Universal {#End}
                  {#Function ($ List {#Parameter 1})
                             Natural}}
      (list#mix (function'' [_ acc] (.i64_+# 1 acc)) 0 list))

(def' .private (as_let binding value body)
      {#Function Code {#Function Code {#Function Code Code}}}
      (as_form {#Item (as_name [..prelude "when#"])
                      {#Item value
                             {#Item binding
                                    {#Item body
                                           {#End}}}}}))

(def' .private |#End|
      Code
      (as_variant {#Item (as_name [..prelude "#End"]) {#End}}))

(def' .private (|#Item| head tail)
      {#Function Code {#Function Code Code}}
      (as_variant {#Item (as_name [..prelude "#Item"])
                         {#Item head
                                {#Item tail
                                       {#End}}}}))

(def' .private (as_Universal body)
      {#Function Code
                 Code}
      (as_variant {#Item (as_name [..prelude "#Universal"]) {#Item ..|#End| {#Item body {#End}}}}))

(def' .private (as_Existential body)
      {#Function Code
                 Code}
      (as_variant {#Item (as_name [..prelude "#Existential"]) {#Item ..|#End| {#Item body {#End}}}}))

(def' .private quantification_level
      Text
      (.text_composite# \'' "quantification_level" \''))

(def' .private quantified
      {#Function Code
                 Code}
      (as_let (as_local ..quantification_level) (as_natural 0)))

(def' .private (quantified_type_parameter idx)
      {#Function Natural
                 Code}
      (as_variant {#Item (as_name [..prelude "#Parameter"])
                         {#Item (as_form {#Item (as_name [..prelude "i64_+#"])
                                                {#Item (as_local ..quantification_level)
                                                       {#Item (as_natural idx)
                                                              {#End}}}})
                                {#End}}}))

(def' .private (next_level depth)
      {#Function Natural
                 Natural}
      (.i64_+# 2 depth))

(def' .private (self_id? id)
      {#Function Natural
                 Bit}
      (.i64_=# id (.as# Natural
                        (.int_*# +2
                                 (.int_/# +2
                                          (.as# Integer
                                                id))))))

(def' .public (__adjusted_quantified_type__ permission depth type)
      {#Function Natural {#Function Natural {#Function Type Type}}}
      (.when# permission
              0
              (.when# type
                      ... Jackpot!
                      {#Parameter id}
                      (.when# (.i64_-# (.i64_-# depth id) 0)
                              id'
                              (.when# (self_id? id)
                                      [#0] {#Parameter id'}
                                      [#1] {#Parameter (.i64_-# 2 id')}))
                      
                      ... Recur
                      {#Nominal name parameters}
                      {#Nominal name (list#each (__adjusted_quantified_type__ permission depth)
                                                parameters)}

                      {#Sum left right}
                      {#Sum (__adjusted_quantified_type__ permission depth left)
                            (__adjusted_quantified_type__ permission depth right)}
                      
                      {#Product left right}
                      {#Product (__adjusted_quantified_type__ permission depth left)
                                (__adjusted_quantified_type__ permission depth right)}
                      
                      {#Function input output}
                      {#Function (__adjusted_quantified_type__ permission depth input)
                                 (__adjusted_quantified_type__ permission depth output)}
                      
                      {#Universal environment body}
                      {#Universal environment
                                  (__adjusted_quantified_type__ permission (next_level depth) body)}
                      
                      {#Existential environment body}
                      {#Existential environment
                                    (__adjusted_quantified_type__ permission (next_level depth) body)}
                      
                      {#Reification parameter function}
                      {#Reification (__adjusted_quantified_type__ permission depth parameter)
                                    (__adjusted_quantified_type__ permission depth function)}

                      ... Leave these alone.
                      {#Named name anonymous} type
                      {#Variable id} type
                      {#Opaque id} type)

              _
              type))

(def' .private (with_correct_quantification body)
      {#Function Code
                 Code}
      (as_form {#Item (as_name [prelude "__adjusted_quantified_type__"])
                      {#Item (as_local ..quantification_level)
                             {#Item (as_natural 0)
                                    {#Item body
                                           {#End}}}}}))

(def' .private (with_quantification depth body)
      {#Function Natural {#Function Code Code}}
      (.when# (as_local ..quantification_level)
              'level
              (as_let 'level
                      (as_form {#Item (as_name [..prelude "i64_+#"])
                                      {#Item 'level
                                             {#Item (as_natural (.as# Natural
                                                                      (.int_*# +2
                                                                               (.as# Integer
                                                                                     depth))))
                                                    {#End}}}})
                      body)))

(def' .private (initialized_quantification? lux)
      {#Function Lux
                 Bit}
      (.when# lux
              [..#info _ ..#source _ ..#current_module _ ..#modules _
               ..#scopes scopes ..#type_context _ ..#host _
               ..#seed _ ..#expected _ ..#provenance _ ..#extensions _
               ..#scope_type_vars _ ..#eval _]
              (list#mix (function'' [scope verdict]
                                    (.when# verdict
                                            [#1] #1
                                            _ (.when# scope
                                                      [..#scope_name _ ..#inner_scopes _ ..#captured _
                                                       ..#locals [..#counter _
                                                                  ..#mappings locals]]
                                                      (list#mix (function'' [local verdict]
                                                                            (.when# local
                                                                                    [local _]
                                                                                    (.when# verdict
                                                                                            [#1] #1
                                                                                            _ (.text_=# ..quantification_level local))))
                                                                #0
                                                                locals))))
                        #0
                        scopes)))

(def' .public for_any
      Macro
      (macro (_ tokens lux)
        (.when# tokens
                {#Item [_ {#Form {#Item self_name args}}]
                       {#Item body {#End}}}
                {#Right [lux
                         {#Item (.when# (.when# args
                                                {#End}
                                                body

                                                {#Item head tail}
                                                (with_correct_quantification
                                                  (as_let self_name (quantified_type_parameter 0)
                                                          (.when# (list#mix (function'' [parameter offset,body']
                                                                                        (.when# offset,body'
                                                                                                [offset body']
                                                                                                [(.i64_+# 2 offset)
                                                                                                 (as_let parameter (quantified_type_parameter (.i64_+# offset 1))
                                                                                                         (as_Universal body'))]))
                                                                            [0 (with_quantification (list#size args)
                                                                                 body)]
                                                                            args)
                                                                  [_ raw]
                                                                  raw))))
                                        raw
                                        (.when# (initialized_quantification? lux)
                                                [#1] raw
                                                [#0] (..quantified raw)))
                                {#End}}]}
                
                _
                {#Left ..wrong_syntax})))

(def' .public for_some
      Macro
      (macro (_ tokens lux)
        (.when# tokens
                {#Item [_ {#Form {#Item self_name args}}]
                       {#Item body {#End}}}
                {#Right [lux
                         {#Item (.when# (.when# args
                                                {#End}
                                                body

                                                {#Item head tail}
                                                (with_correct_quantification
                                                  (as_let self_name (quantified_type_parameter 0)
                                                          (.when# (list#mix (function'' [parameter offset,body']
                                                                                        (.when# offset,body'
                                                                                                [offset body']
                                                                                                [(.i64_+# 2 offset)
                                                                                                 (as_let parameter (quantified_type_parameter (.i64_+# offset 1))
                                                                                                         (as_Existential body'))]))
                                                                            [0 (with_quantification (list#size args)
                                                                                 body)]
                                                                            args)
                                                                  [_ raw]
                                                                  raw))))
                                        raw
                                        (.when# (initialized_quantification? lux)
                                                [#1] raw
                                                [#0] (..quantified raw)))
                                {#End}}]}
                
                _
                {#Left ..wrong_syntax})))

(def' .public ->
      Macro
      (macro (_ tokens)
        (.when# (list#reversed tokens)
                {#Item output inputs}
                (meta#in {#Item (list#mix (.is# {#Function Code {#Function Code Code}}
                                                (function'' [i o] (as_variant {#Item (as_name [..prelude "#Function"]) {#Item i {#Item o {#End}}}})))
                                          output
                                          inputs)
                                {#End}})
                
                _
                (failure ..wrong_syntax))))

(def' .public list
      Macro
      (macro (_ xs)
        (meta#in {#Item (list#mix |#Item| |#End| (list#reversed xs))
                        {#End}})))

(def' .private list#partial
      Macro
      (macro (_ xs)
        (.when# (list#reversed xs)
                {#Item last init}
                (meta#in (list (list#mix |#Item| last init)))

                _
                (failure ..wrong_syntax))))

(def' .public Union
      Macro
      (macro (_ tokens)
        (.when# (list#reversed tokens)
                {#End}
                (meta#in (list (as_name [..prelude "Nothing"])))

                {#Item last prevs}
                (meta#in (list (list#mix (function'' [left right] (as_variant (list (as_name [..prelude "#Sum"]) left right)))
                                         last
                                         prevs))))))

(def' .public Tuple
      Macro
      (macro (_ tokens)
        (.when# (list#reversed tokens)
                {#End}
                (meta#in (list (as_name [..prelude "Any"])))

                {#Item last prevs}
                (meta#in (list (list#mix (function'' [left right] (as_variant (list (as_name [..prelude "#Product"]) left right)))
                                         last
                                         prevs))))))

(def' .private function'
      Macro
      (macro (_ tokens)
        (let'' [name tokens'] (.when# tokens
                                      {#Item [[_ {#Name ["" name]}] tokens']}
                                      [name tokens']

                                      _
                                      ["" tokens])
               (.when# tokens'
                       {#Item [[_ {#Tuple args}] {#Item [body {#End}]}]}
                       (.when# args
                               {#End}
                               (failure "function' requires a non-empty arguments tuple.")
                               
                               {#Item [harg targs]}
                               (meta#in (list (as_form (list (as_name [..prelude "function#"])
                                                             (as_local name)
                                                             harg
                                                             (list#mix (function'' [arg body']
                                                                                   (as_form (list (as_name [..prelude "function#"])
                                                                                                  (as_local "")
                                                                                                  arg
                                                                                                  body')))
                                                                       body
                                                                       (list#reversed targs)))))))

                       _
                       (failure ..wrong_syntax)))))

(def' .public Or
      Macro
      ..Union)

(def' .public And
      Macro
      ..Tuple)

(def' .private (pairs xs)
      (for_any (_ of)
        (-> ($ List of)
            ($ Maybe ($ List (Tuple of of)))))
      (.when# xs
              {#Item x {#Item y xs'}}
              (.when# (pairs xs')
                      {#Some tail}
                      {#Some {#Item [x y] tail}}
                      
                      {#None}
                      {#None})

              {#End}
              {#Some {#End}}

              _
              {#None}))

(def' .private let'
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item [_ {#Tuple bindings}] {#Item body {#End}}}
                (.when# (pairs bindings)
                        {#Some bindings}
                        (meta#in (list (list#mix (.is# (-> (Tuple Code Code) Code
                                                           Code)
                                                       (function' [binding body]
                                                                  (.when# binding
                                                                          [label value]
                                                                          (as_form (list (as_name [..prelude "when#"])
                                                                                         value
                                                                                         label
                                                                                         body)))))
                                                 body
                                                 (list#reversed bindings))))
                        
                        {#None}
                        (failure ..wrong_syntax))

                _
                (failure ..wrong_syntax))))

(def' .private (any? p xs)
      (for_any (_ of)
        (-> (-> of Bit) ($ List of)
            Bit))
      (.when# xs
              {#End}
              #0

              {#Item x xs'}
              (.when# (p x)
                      [#1] #1
                      [#0] (any? p xs'))))

(def' .private (with_provenance @ content)
      (-> Provenance Code
          Code)
      (let' [[module line column] @]
            (as_tuple (list (as_tuple (list (as_text module) (as_natural line) (as_natural column)))
                            content))))

(def' .private (untemplated_list tokens)
      (-> ($ List Code)
          Code)
      (.when# tokens
              {#End}
              |#End|

              {#Item token tokens'}
              (|#Item| token (untemplated_list tokens'))))

(def' .private (list#composite xs ys)
      (for_any (_ of)
        (-> ($ List of) ($ List of)
            ($ List of)))
      (list#mix (function' [head tail] {#Item head tail})
                ys
                (list#reversed xs)))

(def' .private (right_associativity op a1 a2)
      (-> Code Code Code
          Code)
      (.when# op
              [_ {#Form parts}]
              (as_form (list#composite parts (list a1 a2)))

              _
              (as_form (list op a1 a2))))

(def' .private (function#flipped func)
      (for_any (_ left right output)
        (-> (-> left right output)
            (-> right left output)))
      (function' [right left]
                 (func left right)))

... https://en.wikipedia.org/wiki/Operator_associativity
(def' .public left_associative
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item op tokens'}
                (.when# tokens'
                        {#Item first nexts}
                        (meta#in (list (list#mix (function#flipped (right_associativity op)) first nexts)))

                        _
                        (failure ..wrong_syntax))
                
                _
                (failure ..wrong_syntax))))

(def' .public right_associative
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item op tokens'}
                (.when# (list#reversed tokens')
                        {#Item last prevs}
                        (meta#in (list (list#mix (right_associativity op) last prevs)))

                        _
                        (failure ..wrong_syntax))
                
                _
                (failure ..wrong_syntax))))

(def' .public all Macro ..right_associative)

... (every (Monad m)
...   (Interface
...    (is (for_any (_ a) (-> a (m a)))
...        #in)
...    (is (for_any (_ a b) (-> (-> a (m b)) (m a) (m b)))
...        #then)))
(.def# Monad
       (.is# Type
             {#Named [..prelude "Monad"]
                     (for_any (_ !)
                       (Tuple (for_any (_ a)
                                (-> a ($ ! a)))
                              (for_any (_ a b)
                                (-> (-> a ($ ! b))
                                    (-> ($ ! a) ($ ! b))))))})
       #0)

(.def# monad_slots
       (.is# {#Reification Name List}
             {#Item [..prelude "#in"]
                    {#Item [..prelude "#then"]
                           {#End}}})
       #0)
(.def# #in (slot [{#Some [0 #0 ..monad_slots]} Monad]) .private)
(.def# #then (slot [{#Some [0 #1 ..monad_slots]} Monad]) .private)

(def' .private maybe#monad
      ($ Monad Maybe)
      [#in
       (function' [x] {#Some x})
       
       #then
       (function' [f ma]
                  (.when# ma
                          {#None}
                          {#None}
                          
                          {#Some a}
                          (f a)))])

(def' .private meta#monad
      ($ Monad Meta)
      [#in
       (function' [x]
                  (function' [state]
                             {#Right state x}))
       
       #then
       (function' [f ma]
                  (function' [state]
                             (.when# (ma state)
                                     {#Right [state' a]}
                                     (f a state')

                                     {#Left msg}
                                     {#Left msg})))])

(def' .private do
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item monad {#Item [_ {#Tuple bindings}] {#Item body {#End}}}}
                (.when# (pairs bindings)
                        {#Some bindings}
                        (let' ['in (as_local "in")
                               'then (as_local " then ")
                               body' (list#mix (.is# (-> (Tuple Code Code) Code Code)
                                                     (function' [binding body']
                                                                (let' [[var value] binding]
                                                                      (.when# var
                                                                              [_ {#Name [module proper]}]
                                                                              (.when# module
                                                                                      ""
                                                                                      (as_form (list 'then
                                                                                                     (as_form (list (as_name [..prelude "function#"])
                                                                                                                    (as_local "")
                                                                                                                    var
                                                                                                                    body'))
                                                                                                     value))

                                                                                      _
                                                                                      (as_form (list var value body')))
                                                                              

                                                                              _
                                                                              (as_form (list 'then
                                                                                             (as_form (list (as_name [..prelude "function#"])
                                                                                                            (as_local "")
                                                                                                            var
                                                                                                            body'))
                                                                                             value))))))
                                               body
                                               (list#reversed bindings))]
                              (meta#in (list (as_form (list (as_name [..prelude "when#"])
                                                            monad
                                                            (as_tuple (list 'in 'then))
                                                            body')))))
                        
                        {#None}
                        (failure ..wrong_syntax))

                _
                (failure ..wrong_syntax))))

(def' .private (monad#each m f xs)
      (for_any (_ ! input output)
        (-> ($ Monad !)
            (-> input ($ ! output))
            ($ List input)
            ($ ! ($ List output))))
      (let' [[..#in in ..#then _] m]
            (.when# xs
                    {#End}
                    (in {#End})
                    
                    {#Item x xs'}
                    (do m
                      [y (f x)
                       ys (monad#each m f xs')]
                      (in {#Item y ys})))))

(def' .private (monad#each#meta $ items)
      (for_any (_ input output)
        (-> (-> input
                ($ Meta output))
            (-> ($ List input)
                ($ Meta ($ List output)))))
      (function' [lux]
                 ((.is# (for_any (_ input output)
                          (-> Lux (-> input ($ Meta output)) ($ List input) ($ List output)
                              ($ Either Text (And Lux ($ List output)))))
                        (function' again [lux $ items output]
                                   (.when# items
                                           {#End}
                                           {#Right [lux (list#reversed output)]}
                                           
                                           {#Item head tail}
                                           (.when# ($ head lux)
                                                   {#Right [lux head]}
                                                   (again lux $ tail {#Item head output})

                                                   {#Left failure}
                                                   {#Left failure}))))
                  lux
                  $
                  items
                  {#End})))

(def' .private (monad#mix m f y xs)
      (for_any (_ ! mix input)
        (-> ($ Monad !)
            (-> input mix ($ ! mix))
            mix
            ($ List input)
            ($ ! mix)))
      (let' [[..#in in ..#then _] m]
            (.when# xs
                    {#End}
                    (in y)
                    
                    {#Item x xs'}
                    (do m
                      [y' (f x y)]
                      (monad#mix m f y' xs')))))

(def' .public if
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item else {#End}}
                (meta#in (list else))

                {#Item test {#Item then else}}
                (meta#in (list (as_form (list (as_name [..prelude "when#"])
                                              test
                                              (as_bit #1) then
                                              (as_bit #0) (as_form (list#partial (as_name [..prelude "if"]) else))))))

                _
                (failure ..wrong_syntax))))

(def' .private Property_List
      Type
      (for_any (_ of)
        ($ List (Tuple Text of))))

(def' .private (property#value k property_list)
      (for_any (_ of)
        (-> Text ($ Property_List of)
            ($ Maybe of)))
      (.when# property_list
              {#Item [[k' v] property_list']}
              (if (text#= k k')
                {#Some v}
                (property#value k property_list'))

              {#End}
              {#None}))

(def' .private (property#with k v property_list)
      (for_any (_ of)
        (-> Text of ($ Property_List of)
            ($ Property_List of)))
      (.when# property_list
              {#Item [k' v'] property_list'}
              (if (text#= k k')
                (list#partial [k v] property_list')
                (list#partial [k' v'] (property#with k v property_list')))

              {#End}
              (list [k v])))

(def' .private (global_name full_name state)
      (-> Name
          ($ Meta Name))
      (let' [[module name] full_name
             [..#info info ..#source    source  ..#current_module _ ..#modules modules
              ..#scopes scopes ..#type_context types    ..#host    host
              ..#seed seed ..#expected  expected ..#provenance  provenance ..#extensions extensions
              ..#scope_type_vars scope_type_vars ..#eval _eval] state]
            (.when# (property#value module modules)
                    {#Some [..#module_hash _ ..#module_aliases _ ..#definitions definitions ..#imports _ ..#module_state _]}
                    (.when# (property#value name definitions)
                            {#Some [_ constant]}
                            (.when# constant
                                    {#Definition _}
                                    {#Right [state full_name]}

                                    {#Alias real_name}
                                    {#Right [state real_name]}

                                    {#Default _}
                                    {#Left (.text_composite# "Unknown definition: " (name#as full_name))})

                            {#None}
                            {#Left (.text_composite# "Unknown definition: " (name#as full_name))})
                    
                    {#None}
                    {#Left (.text_composite# "Unknown module: " module " @ " (name#as full_name))})))

(def' .private (|List<Code>| expression)
      (-> Code
          Code)
      (let' [type (as_variant (list (as_name [..prelude "#Reification"])
                                    (as_name [..prelude "Code"])
                                    (as_name [..prelude "List"])))]
            (as_form (list (as_name [..prelude "is#"]) type expression))))

(def' .private (untemplated_text value)
      (-> Text
          Code)
      (with_provenance ..provenance#dummy
        (as_variant (list (as_name [..prelude "#Text"]) (as_text value)))))

(def' .public UnQuote
      Type
      {#Named [..prelude "UnQuote"]
              {#Nominal "#Macro/UnQuote" {#End}}})

(def' .public (unquote it)
      (-> Macro
          UnQuote)
      (.as# UnQuote it))

(def' .public (unquote_macro it)
      (-> UnQuote
          Macro')
      (.as# Macro' it))

(def' .public Spliced_UnQuote
      Type
      {#Named [..prelude "Spliced_UnQuote"]
              {#Nominal "#Macro/Spliced_UnQuote" {#End}}})

(def' .public (spliced_unquote it)
      (-> Macro
          Spliced_UnQuote)
      (.as# Spliced_UnQuote it))

(def' .public (spliced_unquote_macro it)
      (-> Spliced_UnQuote
          Macro')
      (.as# Macro' it))

(def' .private (list#one f xs)
      (for_any (_ input output)
        (-> (-> input ($ Maybe output)) ($ List input)
            ($ Maybe output)))
      (.when# xs
              {#End}
              {#None}

              {#Item x xs'}
              (.when# (f x)
                      {#None}
                      (list#one f xs')

                      {#Some y}
                      {#Some y})))

(def' .private (in_env name state)
      (-> Text Lux
          ($ Maybe Type))
      (let' [[..#info info ..#source    source  ..#current_module _ ..#modules modules
              ..#scopes scopes ..#type_context types    ..#host    host
              ..#seed seed ..#expected  expected ..#provenance  provenance ..#extensions extensions
              ..#scope_type_vars scope_type_vars ..#eval _eval] state]
            (list#one (.is# (-> Scope ($ Maybe Type))
                            (function' [env]
                                       (let' [[..#scope_name _
                                               ..#inner_scopes _
                                               ..#locals [..#counter _ ..#mappings locals]
                                               ..#captured _] env]
                                             (list#one (.is# (-> (Tuple Text (Tuple Type Any)) ($ Maybe Type))
                                                             (function' [it]
                                                                        (let' [[bname [type _]] it]
                                                                              (if (text#= name bname)
                                                                                {#Some type}
                                                                                {#None}))))
                                                       locals))))
                      scopes)))

(def' .private (available? expected_module current_module exported?)
      (-> Text ($ Maybe Text) Bit
          Bit)
      (if exported?
        #1
        (.when# current_module
                {.#None}
                #0
                
                {.#Some current_module}
                (text#= expected_module current_module))))

(def' .private (definition_value name state)
      (-> Name
          ($ Meta (Tuple Type Any)))
      (let' [[expected_module expected_proper] name
             [..#info info
              ..#source source
              ..#current_module current_module
              ..#modules modules
              ..#scopes scopes
              ..#type_context types
              ..#host host
              ..#seed seed
              ..#expected expected
              ..#provenance provenance
              ..#extensions extensions
              ..#scope_type_vars scope_type_vars
              ..#eval _eval] state]
            (.when# (property#value expected_module modules)
                    {#None}
                    {#Left (.text_composite# "Unknown definition: " (name#as name))}

                    {#Some [..#definitions definitions
                            ..#module_hash _
                            ..#module_aliases _
                            ..#imports _
                            ..#module_state _]}
                    (.when# (property#value expected_proper definitions)
                            {#None}
                            {#Left (.text_composite# "Unknown definition: " (name#as name))}

                            {#Some [exported? definition]}
                            (.when# definition
                                    {#Alias real_name}
                                    (definition_value real_name state)
                                    
                                    {#Definition [def_type def_value]}
                                    (if (available? expected_module current_module exported?)
                                      {#Right [state [def_type def_value]]}
                                      {#Left (.text_composite# "Unavailable definition: " (name#as name))})

                                    {#Default _}
                                    {#Left (.text_composite# "Unknown definition: " (name#as name))})))))

(def' .private (global_value global lux)
      (-> Name
          ($ Meta ($ Maybe (Tuple Type Any))))
      (let' [[module proper] global]
            (.when# (.when# module
                            "" (.when# (in_env proper lux)
                                       {#None}
                                       (definition_value global lux)
                                       
                                       {#Some _}
                                       {#Left (.text_composite# "Not a global value: " (name#as global))})

                            _
                            (definition_value global lux))
                    {#Right [lux' type,value]}
                    {#Right [lux' {#Some type,value}]}
                    
                    {#Left error}
                    {#Right [lux {#None}]})))

(def' .private (and' left right)
      (-> Bit Bit
          Bit)
      (if left
        right
        #0))

(def' .private (name#= left right)
      (-> Name Name
          Bit)
      (let' [[moduleL properL] left
             [moduleR properR] right]
            (all and'
                 (text#= moduleL moduleR)
                 (text#= properL properR))))

(def' .private (every? ?)
      (for_any (_ of)
        (-> (-> of Bit) ($ List of)
            Bit))
      (list#mix (function' [_2 _1] (if _1 (? _2) #0)) #1))

(def' .private (zipped_2 xs ys)
      (for_any (_ left right)
        (-> ($ List left) ($ List right)
            ($ List (Tuple left right))))
      (.when# xs
              {#Item x xs'}
              (.when# ys
                      {#Item y ys'}
                      (list#partial [x y] (zipped_2 xs' ys'))

                      _
                      (list))
              
              _
              (list)))

(def' .private (type#= left right)
      (-> Type Type
          Bit)
      (.when# [left right]
              [{#Nominal nameL parametersL} {#Nominal nameR parametersR}]
              (all and'
                   (text#= nameL nameR)
                   (.i64_=# (list#size parametersL) (list#size parametersR))
                   (every? (function' [l,r] (let' [[itL itR] l,r] (type#= itL itR)))
                           (zipped_2 parametersL parametersR)))

              [{#Sum leftL rightL} {#Sum leftR rightR}]
              (all and'
                   (type#= leftL leftR)
                   (type#= rightL rightR))

              [{#Product leftL rightL} {#Product leftR rightR}]
              (all and'
                   (type#= leftL leftR)
                   (type#= rightL rightR))

              [{#Function leftL rightL} {#Function leftR rightR}]
              (all and'
                   (type#= leftL leftR)
                   (type#= rightL rightR))

              [{#Reification leftL rightL} {#Reification leftR rightR}]
              (all and'
                   (type#= leftL leftR)
                   (type#= rightL rightR))

              [{#Parameter idL} {#Parameter idR}]
              (.i64_=# idL idR)

              [{#Variable idL} {#Variable idR}]
              (.i64_=# idL idR)

              [{#Opaque idL} {#Opaque idR}]
              (.i64_=# idL idR)

              [{#Universal envL bodyL} {#Universal envR bodyR}]
              (all and'
                   (.i64_=# (list#size envL) (list#size envR))
                   (every? (function' [l,r] (let' [[itL itR] l,r] (type#= itL itR)))
                           (zipped_2 envL envR))
                   (type#= bodyL bodyR))

              [{#Existential envL bodyL} {#Existential envR bodyR}]
              (all and'
                   (.i64_=# (list#size envL) (list#size envR))
                   (every? (function' [l,r] (let' [[itL itR] l,r] (type#= itL itR)))
                           (zipped_2 envL envR))
                   (type#= bodyL bodyR))

              [{#Named nameL anonL} {#Named nameR anonR}]
              (all and'
                   (name#= nameL nameR)
                   (type#= anonL anonR))

              _
              #0))

(def' .private (one_expansion it)
      (-> ($ Meta ($ List Code))
          ($ Meta Code))
      (do meta#monad
        [it it]
        (.when# it
                {#Item it {#End}}
                (in it)

                _
                (failure "Must expand to 1 element."))))

(def' .private (current_module_name state)
      ($ Meta Text)
      (.when# state
              [..#info info ..#source    source  ..#current_module current_module ..#modules modules
               ..#scopes scopes ..#type_context types    ..#host    host
               ..#seed seed ..#expected  expected ..#provenance  provenance ..#extensions extensions
               ..#scope_type_vars scope_type_vars ..#eval _eval]
              (.when# current_module
                      {#Some module_name}
                      {#Right [state module_name]}

                      _
                      {#Left "Cannot get the module name without a module!"})))

(def' .private (normal name)
      (-> Name
          ($ Meta Name))
      (.when# name
              ["" name]
              (do meta#monad
                [module_name ..current_module_name]
                (in [module_name name]))

              _
              (meta#in name)))

(def' .private (untemplated_composite tag @composite untemplated replace? subst elements)
      (-> Text Provenance (-> Bit Text Code ($ Meta Code)) Bit Text ($ List Code)
          ($ Meta Code))
      (do meta#monad
        [.let' [cons (.is# (-> Code Code ($ Meta Code))
                           (function' [head tail]
                                      (do meta#monad
                                        [head (untemplated replace? subst head)]
                                        (in (|#Item| head tail)))))]
         output (if replace?
                  (monad#mix meta#monad
                             (function' [head tail]
                                        (.when# head
                                                [@composite {#Form {#Item [@name {#Name global}] parameters}}]
                                                (do meta#monad
                                                  [|global| (..normal global)
                                                   ?type,value (global_value |global|)]
                                                  (.when# ?type,value
                                                          {#Some [type value]}
                                                          (if (type#= Spliced_UnQuote type)
                                                            (do meta#monad
                                                              [.let' [it (spliced_unquote_macro (.as# Spliced_UnQuote value))]
                                                               output (one_expansion (it {#Item tail parameters}))
                                                               .let' [[_ output] output]]
                                                              (in [@composite output]))
                                                            (cons head tail))
                                                          
                                                          {#None}
                                                          (cons head tail)))

                                                _
                                                (cons head tail)))
                             |#End|
                             (list#reversed elements))
                  (do meta#monad
                    [=elements (monad#each#meta (untemplated replace? subst) elements)]
                    (in (untemplated_list =elements))))
         .let' [[_ output'] (with_provenance ..provenance#dummy
                              (as_variant (list (as_name [..prelude tag]) output)))]]
        (in [@composite output'])))

(def' .private untemplated_form
      (-> Provenance (-> Bit Text Code ($ Meta Code)) Bit Text ($ List Code)
          ($ Meta Code))
      (untemplated_composite "#Form"))

(def' .private untemplated_variant
      (-> Provenance (-> Bit Text Code ($ Meta Code)) Bit Text ($ List Code)
          ($ Meta Code))
      (untemplated_composite "#Variant"))

(def' .private untemplated_tuple
      (-> Provenance (-> Bit Text Code ($ Meta Code)) Bit Text ($ List Code)
          ($ Meta Code))
      (untemplated_composite "#Tuple"))

(def' .private (untemplated replace? subst token)
      (-> Bit Text Code
          ($ Meta Code))
      (.when# [replace? token]
              [_ [@token {#Bit value}]]
              (meta#in (with_provenance ..provenance#dummy
                         (as_variant (list (as_name [..prelude "#Bit"]) (as_bit value)))))

              [_ [@token {#Natural value}]]
              (meta#in (with_provenance ..provenance#dummy
                         (as_variant (list (as_name [..prelude "#Natural"]) (as_natural value)))))

              [_ [@token {#Integer value}]]
              (meta#in (with_provenance ..provenance#dummy
                         (as_variant (list (as_name [..prelude "#Integer"]) (as_integer value)))))

              [_ [@token {#Revolution value}]]
              (meta#in (with_provenance ..provenance#dummy
                         (as_variant (list (as_name [..prelude "#Revolution"]) (as_revolution value)))))
              
              [_ [@token {#Decimal value}]]
              (meta#in (with_provenance ..provenance#dummy
                         (as_variant (list (as_name [..prelude "#Decimal"]) (as_decimal value)))))

              [_ [@token {#Text value}]]
              (meta#in (untemplated_text value))

              [#1 [@token {#Name [module name]}]]
              (do meta#monad
                [real_name (.when# module
                                   ""
                                   (if (text#= "" subst)
                                     (in [module name])
                                     (global_name [subst name]))

                                   _
                                   (in [module name]))
                 .let' [[module name] real_name]]
                (meta#in (with_provenance [module 0 0]
                           (as_variant (list (as_name [..prelude "#Name"]) (as_tuple (list (as_text module) (as_text name))))))))

              [#0 [@token {#Name [module name]}]]
              (meta#in (with_provenance @token
                         (as_variant (list (as_name [..prelude "#Name"]) (as_tuple (list (as_text module) (as_text name)))))))

              [#1 [@composite {#Form {#Item [@name {#Name global}] parameters}}]]
              (do meta#monad
                [|global| (..normal global)
                 ?type,value (global_value |global|)]
                (.when# ?type,value
                        {#Some [type value]}
                        (if (type#= UnQuote type)
                          (do meta#monad
                            [.let' [it (unquote_macro (.as# UnQuote value))]
                             output (one_expansion (it parameters))
                             .let' [[_ output] output]]
                            (in [@composite output]))
                          (untemplated_form @composite untemplated replace? subst {#Item [@name {#Name global}] parameters}))
                        
                        {#None}
                        (untemplated_form @composite untemplated replace? subst {#Item [@name {#Name global}] parameters})))

              [_ [@composite {#Form elements}]]
              (untemplated_form @composite untemplated replace? subst elements)

              [_ [@composite {#Variant elements}]]
              (untemplated_variant @composite untemplated replace? subst elements)

              [_ [@composite {#Tuple elements}]]
              (untemplated_tuple @composite untemplated replace? subst elements)))

(def' .public Nominal
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item [_ {#Text class_name}] {#End}}
                (meta#in (list (as_variant (list (as_name [..prelude "#Nominal"]) (as_text class_name) |#End|))))

                {#Item [_ {#Text class_name}] {#Item [_ {#Tuple params}] {#End}}}
                (meta#in (list (as_variant (list (as_name [..prelude "#Nominal"]) (as_text class_name) (untemplated_list params)))))

                _
                (failure ..wrong_syntax))))

(def' .public `
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item template {#End}}
                (do meta#monad
                  [current_module current_module_name
                   =template (untemplated #1 current_module template)]
                  (in (list (as_form (list (as_name [..prelude "is#"])
                                           (as_name [..prelude "Code"])
                                           =template)))))

                _
                (failure ..wrong_syntax))))

(def' .public complete_quote Macro `)

(def' .public `'
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item template {#End}}
                (do meta#monad
                  [=template (untemplated #1 "" template)]
                  (in (list (as_form (list (as_name [..prelude "is#"])
                                           (as_name [..prelude "Code"])
                                           =template)))))

                _
                (failure ..wrong_syntax))))

(def' .public incomplete_quote Macro `')

(def' .public '
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item template {#End}}
                (do meta#monad
                  [=template (untemplated #0 "" template)]
                  (in (list (as_form (list (as_name [..prelude "is#"])
                                           (as_name [..prelude "Code"])
                                           =template)))))

                _
                (failure ..wrong_syntax))))

(def' .public quote Macro ')

(def' .public ,
      UnQuote
      (..unquote
       (macro (_ tokens)
         (.when# tokens
                 {#Item it {#End}}
                 (meta#in (list (as_form (list (as_name [..prelude "is#"])
                                               (as_name [..prelude "Code"])
                                               it))))

                 _
                 (failure ..wrong_syntax)))))

(def' .public but UnQuote ,)

(def' .public ,'
      UnQuote
      (..unquote
       (macro (_ tokens)
         (.when# tokens
                 {#Item it {#End}}
                 (do meta#monad
                   [current_module ..current_module_name
                    it (untemplated #0 current_module it)]
                   (in (list it)))

                 _
                 (failure ..wrong_syntax)))))

(def' .public verbatim UnQuote ,')

(def' .public ,*
      Spliced_UnQuote
      (let' ['list#composite (as_form (list (as_name [..prelude "in_module#"])
                                            (as_text ..prelude)
                                            (as_name [..prelude "list#composite"])))]
            (..spliced_unquote
             (macro (_ tokens)
               (.when# tokens
                       {#Item tail {#Item it {#End}}}
                       (meta#in (list (as_form (list 'list#composite (|List<Code>| it) tail))))

                       _
                       (failure ..wrong_syntax))))))

(def' .public also Spliced_UnQuote ,*)

(def' .public |>
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item [init apps]}
                (meta#in (list (list#mix (.is# (-> Code Code Code)
                                               (function' [app acc]
                                                          (.when# app
                                                                  [_ {#Variant parts}]
                                                                  (as_variant (list#composite parts (list acc)))

                                                                  [_ {#Tuple parts}]
                                                                  (as_tuple (list#composite parts (list acc)))

                                                                  [_ {#Form parts}]
                                                                  (as_form (list#composite parts (list acc)))

                                                                  _
                                                                  (` ((, app) (, acc))))))
                                         init
                                         apps)))

                _
                (failure ..wrong_syntax))))

(def' .public <|
      Macro
      (macro (_ tokens)
        (.when# (list#reversed tokens)
                {#Item [init apps]}
                (meta#in (list (list#mix (.is# (-> Code Code Code)
                                               (function' [app acc]
                                                          (.when# app
                                                                  [_ {#Variant parts}]
                                                                  (as_variant (list#composite parts (list acc)))

                                                                  [_ {#Tuple parts}]
                                                                  (as_tuple (list#composite parts (list acc)))

                                                                  [_ {#Form parts}]
                                                                  (as_form (list#composite parts (list acc)))

                                                                  _
                                                                  (` ((, app) (, acc))))))
                                         init
                                         apps)))

                _
                (failure ..wrong_syntax))))

(def' .private meta#failure
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item 'error {#End}}
                (meta#in (list (` {.#Left (, 'error)})))

                _
                (failure ..wrong_syntax))))

(def' .private meta#return
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item 'lux {#Item 'term {#End}}}
                (meta#in (list (` {.#Right [(, 'lux) (, 'term)]})))

                _
                (failure ..wrong_syntax))))

(def' .private meta#let
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item 'lux {#Item [_ {#Tuple {#Item 'binding {#Item 'term {#End}}}}]
                                   {#Item 'body {#End}}}}
                (meta#in (list (` (.when# ((, 'term) (, 'lux))
                                          {.#Right [(, 'lux) (, 'binding)]}
                                          (, 'body)

                                          {.#Left (, 'lux)}
                                          {.#Left (, 'lux)}))))

                _
                (failure ..wrong_syntax))))

(def' .private (function#composite f g)
      (for_any (_ start middle end)
        (-> (-> middle end) (-> start middle)
            (-> start end)))
      (function' [x] (f (g x))))

(def' .private (name_name x)
      (-> Code
          ($ Maybe Name))
      (.when# x
              [_ {#Name sname}]
              {#Some sname}

              _
              {#None}))

(def' .private (name_proper x)
      (-> Code
          ($ Maybe Text))
      (.when# x
              [_ {#Name "" sname}]
              {#Some sname}

              _
              {#None}))

(def' .private (tuple_list tuple)
      (-> Code
          ($ Maybe ($ List Code)))
      (.when# tuple
              [_ {#Tuple members}]
              {#Some members}

              _
              {#None}))

(def' .private (realized_template env template)
      (-> Replacement_Environment Code
          Code)
      (.when# template
              [_ {#Name "" sname}]
              (.when# (..replacement sname env)
                      {#Some subst}
                      subst

                      _
                      template)

              [meta {#Form elems}]
              [meta {#Form (list#each (realized_template env) elems)}]

              [meta {#Tuple elems}]
              [meta {#Tuple (list#each (realized_template env) elems)}]

              [meta {#Variant elems}]
              [meta {#Variant (list#each (realized_template env) elems)}]

              _
              template))

(def' .private (high_bits value)
      (-> ($ I64 Any)
          I64)
      (.i64_right# 32 value))

(def' .private low_mask
      I64
      (|> 1 (.i64_left# 32) (.i64_-# 1)))

(def' .private (low_bits value)
      (-> ($ I64 Any)
          I64)
      (.i64_and# low_mask value))

(def' .private (n#< reference it)
      (-> Natural Natural
          Bit)
      (let' [referenceH (high_bits reference)
             itH (high_bits it)]
            (if (.int_<# referenceH itH)
              #1
              (if (.i64_=# referenceH itH)
                (.int_<# (low_bits reference)
                         (low_bits it))
                #0))))

(def' .private (list#conjoint xs)
      (for_any (_ of)
        (-> ($ List ($ List of))
            ($ List of)))
      (list#mix list#composite {#End} (list#reversed xs)))

(def' .public name
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item [_ {#Name [module name]}] {#End}}
                (meta#in (list (as_tuple (list (as_text module) (as_text name)))))

                _
                (failure ..wrong_syntax))))

(def' .private with_template
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item [[_ {#Tuple bindings}] {#Item [[_ {#Tuple templates}] data]}]}
                (.when# [(monad#each maybe#monad name_proper bindings)
                         (monad#each maybe#monad tuple_list data)]
                        [{#Some bindings'} {#Some data'}]
                        (let' [reification (.is# (-> Replacement_Environment ($ List Code))
                                                 (function' [env] (list#each (realized_template env) templates)))
                               num_bindings (list#size bindings')]
                              (if (every? (function' [size] (.i64_=# num_bindings size))
                                          (list#each list#size data'))
                                (|> data'
                                    (list#each (function#composite reification (replacement_environment bindings')))
                                    list#conjoint
                                    meta#in)
                                (failure ..wrong_syntax)))

                        _
                        (failure ..wrong_syntax))

                _
                (failure ..wrong_syntax))))

(def' .private (n#/ param subject)
      (-> Natural Natural
          Natural)
      (if (.int_<# +0 (.as# Integer param))
        (if (n#< param subject)
          0
          1)
        (let' [quotient (|> subject
                            (.i64_right# 1)
                            (.int_/# (.as# Integer param))
                            (.i64_left# 1))
               flat (.int_*# (.as# Integer param)
                             (.as# Integer quotient))
               remainder (.i64_-# flat subject)]
              (if (n#< param remainder)
                quotient
                (.i64_+# 1 quotient)))))

(def' .private (n#% param subject)
      (-> Natural Natural
          Natural)
      (let' [flat (.int_*# (.as# Integer param)
                           (.as# Integer (n#/ param subject)))]
            (.i64_-# flat subject)))

(def' .private (bit#as x)
      (-> Bit
          Text)
      (if x "#1" "#0"))

(def' .private (digit::injection digit)
      (-> Natural
          Text)
      (.when# digit
              [0] "0"
              [1] "1" [2] "2" [3] "3"
              [4] "4" [5] "5" [6] "6"
              [7] "7" [8] "8" [9] "9"
              _ (.error# "@digit::injection Undefined behavior.")))

(def' .private (natural#as value)
      (-> Natural
          Text)
      (.when# value
              [0] "0"
              _ (let' [loop (.is# (-> Natural Text Text)
                                  (function' again [input output]
                                             (if (.i64_=# 0 input)
                                               output
                                               (again (n#/ 10 input)
                                                      (.text_composite# (|> input (n#% 10) digit::injection)
                                                                        output)))))]
                      (loop value ""))))

(def' .private (integer#abs value)
      (-> Integer
          Integer)
      (if (.int_<# +0 value)
        (.int_*# -1 value)
        value))

(def' .private (integer#as value)
      (-> Integer
          Text)
      (if (.i64_=# +0 value)
        "+0"
        (let' [sign (if (.int_<# value +0)
                      "+"
                      "-")]
              ((.is# (-> Integer Text Text)
                     (function' again [input output]
                                (if (.i64_=# +0 input)
                                  (.text_composite# sign output)
                                  (again (.int_/# +10 input)
                                         (.text_composite# (|> input (.int_%# +10) (.as# Natural) digit::injection)
                                                           output)))))
               (|> value (.int_/# +10) integer#abs)
               (|> value (.int_%# +10) integer#abs (.as# Natural) digit::injection)))))

(def' .public (not x)
      (-> Bit
          Bit)
      (if x #0 #1))

(def' .private (named_macro' modules current_module module name)
      (-> ($ List (Tuple Text Module)) Text Text Text
          ($ Maybe Macro))
      (do maybe#monad
        [$module (property#value module modules)
         exported?,gdef (let' [[..#module_hash _ ..#module_aliases _ ..#definitions bindings ..#imports _ ..#module_state _] (.is# Module $module)]
                              (property#value name bindings))
         .let' [[exported? gdef] exported?,gdef]]
        (.when# (.is# Global gdef)
                {#Alias [r_module r_name]}
                (named_macro' modules current_module r_module r_name)

                {#Definition [def_type def_value]}
                (if (type#= ..Macro def_type)
                  (if exported?
                    {#Some (.as# Macro def_value)}
                    (if (text#= module current_module)
                      {#Some (.as# Macro def_value)}
                      {#None}))
                  {#None})

                {#Default _}
                {#None})))

(def' .private (named_macro full_name)
      (-> Name
          ($ Meta ($ Maybe Macro)))
      (<| (function' [lux])
          (meta#let lux [current_module current_module_name])
          (let' [[module name] full_name
                 [..#info info ..#source source ..#current_module _ ..#modules modules
                  ..#scopes   scopes   ..#type_context   types   ..#host host
                  ..#seed   seed ..#expected expected
                  ..#provenance provenance ..#extensions extensions
                  ..#scope_type_vars scope_type_vars ..#eval _eval] lux])
          (meta#return lux (named_macro' modules current_module module name))))

(def' .private (macro? name)
      (-> Name
          ($ Meta Bit))
      (<| (function' [lux])
          (meta#let lux [name (normal name)])
          (meta#let lux [output (named_macro name)])
          (meta#return lux (.when# output
                                   {#Some _} #1
                                   {#None}   #0))))

(def' .private (list#interposed sep xs)
      (for_any (_ of)
        (-> of ($ List of)
            ($ List of)))
      (.when# xs
              {#End}
              xs

              {#Item [x {#End}]}
              xs

              {#Item [x xs']}
              (list#partial x sep (list#interposed sep xs'))))

(def' .private (text#as original)
      (-> Text
          Text)
      (.text_composite# \'' original \''))

(def' .private (code#as code)
      (-> Code
          Text)
      (.when# code
              [_ {#Bit value}]
              (bit#as value)

              [_ {#Natural value}]
              (natural#as value)

              [_ {#Integer value}]
              (integer#as value)

              [_ {#Revolution value}]
              (.error# "@code#as Undefined behavior.")
              
              [_ {#Decimal value}]
              (.error# "@code#as Undefined behavior.")

              [_ {#Text value}]
              (text#as value)
              
              [_ {#Name [module name]}]
              (name#as [module name])
              
              [_ {#Form xs}]
              (.text_composite#
               "("
               (|> xs
                   (list#each code#as)
                   (list#interposed " ")
                   list#reversed
                   (list#mix text#composite ""))
               ")")
              
              [_ {#Tuple xs}]
              (.text_composite#
               "["
               (|> xs
                   (list#each code#as)
                   (list#interposed " ")
                   list#reversed
                   (list#mix text#composite ""))
               "]")
              
              [_ {#Variant xs}]
              (.text_composite#
               "{"
               (|> xs
                   (list#each code#as)
                   (list#interposed " ")
                   list#reversed
                   (list#mix text#composite ""))
               "}")))

(def' .private (single_expansion token)
      (-> Code
          ($ Meta ($ List Code)))
      (.when# token
              [_ {#Form {#Item [_ {#Name name}] args}}]
              (<| (function' [lux])
                  (meta#let lux [name' (normal name)])
                  (meta#let lux [?macro (named_macro name')])
                  (.when# ?macro
                          {#Some macro}
                          (((.as# Macro' macro) args) lux)
                          
                          {#None}
                          (meta#return lux (list token))))

              _
              (meta#in (list token))))

(def' .private (complete_expansion token)
      (-> Code
          ($ Meta ($ List Code)))
      (.when# token
              [_ {#Form {#Item [_ {#Name name}] args}}]
              (<| (function' [lux])
                  (meta#let lux [name' (normal name)])
                  (meta#let lux [?macro (named_macro name')])
                  (.when# ?macro
                          {#Some macro}
                          (<| (meta#let lux [maximum_level_expansion ((.as# Macro' macro) args)])
                              (meta#let lux [recursive_expansion (monad#each#meta complete_expansion maximum_level_expansion)])
                              (meta#return lux (list#conjoint recursive_expansion)))
                          
                          {#None}
                          (meta#return lux (list token))))

              _
              (meta#in (list token))))

(def' .public exec
      Macro
      (macro (_ tokens)
        (.when# (list#reversed tokens)
                {#Item value actions}
                (let' [dummy (as_local "")]
                      (meta#in (list (list#mix (.is# (-> Code Code Code)
                                                     (function' [pre post] (` (.when# (, pre) (, dummy) (, post)))))
                                               value
                                               actions))))

                _
                (failure ..wrong_syntax))))

(def' .private (type_reification abstraction parameters)
      (-> Code ($ List Code)
          Code)
      (list#mix (.is# (-> Code Code Code)
                      (function' [parameter abstraction]
                                 (` {.#Reification (, parameter) (, abstraction)})))
                abstraction
                parameters))

(def' .private (normal_type_reification normal_type abstraction parameters)
      (-> (-> Code ($ Meta Code)) Code ($ List Code)
          ($ Meta Code))
      (<| (function' [lux])
          (meta#let lux [parameters (monad#each#meta normal_type parameters)])
          (meta#return lux (type_reification abstraction parameters))))

(def' .public type_must_have_singular_expansion
      Error
      "The expansion of the type-syntax has to yield a single element.")

(def' .private (expanded_type_reification normal_type @ binding parameters)
      (-> (-> Code ($ Meta Code)) Provenance Name ($ List Code)
          ($ Meta Code))
      (<| (function' [lux])
          (meta#let lux [binding (normal binding)])
          (meta#let lux [?macro (named_macro binding)])
          (.when# ?macro
                  {#Some macro}
                  (<| (meta#let lux [expansion ((.as# Macro' macro) parameters)])
                      (.when# expansion
                              {.#Item singular {.#End}}
                              (meta#return lux (` (..type (, singular))))

                              else
                              (meta#failure ..type_must_have_singular_expansion)))
                  
                  {#None}
                  ((normal_type_reification normal_type [@ {#Name binding}] parameters) lux))))

(def' .private (normal_type type' it)
      (-> (-> Code ($ Meta Code)) Code
          ($ Meta Code))
      (.when# it
              [_ {#Variant {#Item [_ {#Name ["library/lux" "#Named"]}] {#Item name {#Item anonymous {#End}}}}}]
              (<| (function' [lux])
                  (meta#let lux [anonymous (normal_type type' anonymous)])
                  (meta#return lux (` {..#Named (, name) (, anonymous)})))

              [_ {#Variant {#Item [_ {#Name name}] parts}}]
              (<| (function' [lux])
                  (meta#let lux [parts (monad#each#meta (normal_type type') parts)])
                  (meta#return lux (` {(, (as_name name)) (,* parts)})))

              [_ {#Tuple members}]
              (<| (function' [lux])
                  (meta#let lux [members (monad#each#meta (normal_type type') members)])
                  (meta#return lux (` (Tuple (,* members)))))

              [_ {#Form {#Item [_ {#Name ["library/lux" "in_module#"]}]
                               {#Item [_ {#Text module}]
                                      {#Item it'
                                             {#End}}}}}]
              (<| (function' [lux])
                  (meta#let lux [it' (normal_type type' it')])
                  (meta#return lux (` (.in_module# (, (as_text module)) (, it')))))

              [_ {#Form {#Item [_ {#Name ["" ","]}] {#Item expression {#End}}}}]
              (meta#in expression)

              [_0 {#Form {#Item [_ {#Name ["library/lux" "when#"]}]
                                {#Item value
                                       {#Item binding
                                              {#Item body
                                                     {#End}}}}}}]
              (<| (function' [lux])
                  (meta#return lux [_0 {#Form (list [_ {#Name ["library/lux" "when#"]}]
                                                    value
                                                    binding
                                                    (` (..type (, body))))}]))

              [_0 {#Form {#Item [_1 {#Name ["library/lux" "__adjusted_quantified_type__"]}]
                                {#Item _permission
                                       {#Item _level
                                              {#Item body
                                                     {#End}}}}}}]
              (<| (function' [lux])
                  (meta#let lux [body (normal_type type' body)])
                  (meta#return lux [_0 {#Form (list [_1 {#Name [..prelude "__adjusted_quantified_type__"]}]
                                                    _permission
                                                    _level
                                                    body)}]))

              [_ {#Form {#Item [_ {#Form {#Item [_ {#Name ["library/lux" "in_module#"]}]
                                                {#Item [_ {#Text "library/lux"}]
                                                       {#Item [_ {#Name ["library/lux" "type"]}]
                                                              {#End}}}}}]
                               {#Item it'
                                      {#End}}}}]
              (type' it')

              [_0 {#Form {#Item [_1 {#Name binding}]
                                parameters}}]
              (<| (function' [lux])
                  (.when# binding
                          ["" local]
                          (.when# (in_env local lux)
                                  {.#Some type_of_local}
                                  ((normal_type_reification (normal_type type') [_1 {#Name binding}] parameters) lux)
                                  
                                  not_a_local
                                  ((expanded_type_reification (normal_type type') _1 binding parameters) lux))
                          
                          [module global]
                          ((expanded_type_reification (normal_type type') _1 binding parameters) lux)))

              [_ {#Form {#Item type_fn args}}]
              (<| (function' [lux])
                  (meta#let lux [type_fn (normal_type type' type_fn)])
                  (meta#let lux [args (monad#each#meta (normal_type type') args)])
                  (meta#return lux (type_reification type_fn args)))

              _
              (meta#in it)))

(def' .private (with_quantification' body lux)
      (-> ($ Meta Code)
          ($ Meta Code))
      (let' [[..#info info/pre
              ..#source source/pre
              ..#current_module current_module/pre
              ..#modules modules/pre
              ..#scopes scopes/pre
              ..#type_context type_context/pre
              ..#host host/pre
              ..#seed seed/pre
              ..#expected expected/pre
              ..#provenance provenance/pre
              ..#extensions extensions/pre
              ..#scope_type_vars scope_type_vars/pre
              ..#eval eval/pre] lux]
            (.when# (body [..#info info/pre
                           ..#source source/pre
                           ..#current_module current_module/pre
                           ..#modules modules/pre
                           ..#scopes (list#partial [#scope_name (list)
                                                    #inner_scopes 0
                                                    #locals [#counter 0
                                                             #mappings (list [..quantification_level [.Natural (.as# Natural -1)]])]
                                                    #captured [#counter 0
                                                               #mappings (list)]]
                                                   scopes/pre)
                           ..#type_context type_context/pre
                           ..#host host/pre
                           ..#seed seed/pre
                           ..#expected expected/pre
                           ..#provenance provenance/pre
                           ..#extensions extensions/pre
                           ..#scope_type_vars scope_type_vars/pre
                           ..#eval eval/pre])
                    {..#Right [lux/post output]}
                    (let' [[..#info info/post
                            ..#source source/post
                            ..#current_module current_module/post
                            ..#modules modules/post
                            ..#scopes scopes/post
                            ..#type_context type_context/post
                            ..#host host/post
                            ..#seed seed/post
                            ..#expected expected/post
                            ..#provenance provenance/post
                            ..#extensions extensions/post
                            ..#scope_type_vars scope_type_vars/post
                            ..#eval eval/post] lux/post]
                          {..#Right [[..#info info/post
                                      ..#source source/post
                                      ..#current_module current_module/post
                                      ..#modules modules/post
                                      ..#scopes scopes/pre
                                      ..#type_context type_context/post
                                      ..#host host/post
                                      ..#seed seed/post
                                      ..#expected expected/post
                                      ..#provenance provenance/post
                                      ..#extensions extensions/post
                                      ..#scope_type_vars scope_type_vars/post
                                      ..#eval eval/post]
                                     output]})
                    
                    failure
                    failure)))

(def' .private (type' it)
      (-> Code
          ($ Meta Code))
      (<| (function' [lux])
          (if (initialized_quantification? lux)
            ((normal_type type' it) lux)
            (<| (meta#let lux [it (with_quantification'
                                    (type' it))])
                (meta#return lux (..quantified it))))))

(def' .public type
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item it {#End}}
                (<| (function' [lux])
                    (meta#let lux [it (type' it)])
                    (meta#return lux (list it)))

                _
                (failure ..wrong_syntax))))

(def' .public is
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item type {#Item value {#End}}}
                (meta#in (list (` (.is# (..type (, type))
                                        (, value)))))

                _
                (failure ..wrong_syntax))))

(def' .public as
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item type {#Item value {#End}}}
                (meta#in (list (` (.as# (..type (, type))
                                        (, value)))))

                _
                (failure ..wrong_syntax))))

(def' .private (empty? xs)
      (for_any (_ of)
        (-> ($ List of)
            Bit))
      (.when# xs
              {#End} #1
              _      #0))

(with_template [<name> <side>]
  [(def' .private (<name> left,right)
         (for_any (_ left right)
           (-> (Tuple left right)
               <side>))
         (let' [[left right] left,right]
               <side>))]

  [product#left  left]
  [product#right right]
  )

(def' .private (generated_name prefix state)
      (-> Text
          ($ Meta Code))
      (.when# state
              [..#info info ..#source source ..#current_module _ ..#modules modules
               ..#scopes   scopes   ..#type_context   types   ..#host host
               ..#seed   seed ..#expected expected
               ..#provenance provenance ..#extensions extensions
               ..#scope_type_vars scope_type_vars ..#eval _eval]
              {#Right [..#info info ..#source source ..#current_module _ ..#modules modules
                       ..#scopes   scopes   ..#type_context   types   ..#host host
                       ..#seed   (.i64_+# 1 seed) ..#expected expected
                       ..#provenance provenance ..#extensions extensions
                       ..#scope_type_vars scope_type_vars ..#eval _eval]
                      (as_local (.text_composite# "__gensym__" prefix (natural#as seed)))}))

(with_template [<name> <tag>]
  [(def' .private (<name> type)
         (type (-> Type
                   (List Type)))
         (.when# type
                 {<tag> left right}
                 (list#partial left (<name> right))

                 _
                 (list type)))]

  [flat_variant #Sum]
  [flat_tuple   #Product]
  [flat_lambda  #Function]
  )

(def' .private (flat_application type)
      (type (-> Type
                [Type (List Type)]))
      (.when# type
              {#Reification head func'}
              (let' [[func tail] (flat_application func')]
                    [func {#Item head tail}])

              _
              [type (list)]))

(def' .private (type#as type)
      (-> Type
          Text)
      (.when# type
              {#Nominal name params}
              (.text_composite#
               "(Nominal " (text#as name)
               (|> params
                   (list#each (function' [it] (|> it type#as (.text_composite# " "))))
                   list#reversed
                   (list#mix text#composite ""))
               ")")

              {#Sum _}
              (.text_composite# "{" (|> (flat_variant type) (list#each type#as) (list#interposed " ") list#reversed (list#mix text#composite "")) "}")
              
              {#Product _}
              (.text_composite# "[" (|> (flat_tuple type) (list#each type#as) (list#interposed " ") list#reversed (list#mix text#composite "")) "]")

              {#Function _}
              (.text_composite# "(-> " (|> (flat_lambda type) (list#each type#as) (list#interposed " ") list#reversed (list#mix text#composite "")) ")")

              {#Parameter id}
              (natural#as id)
              
              {#Variable id}
              (.text_composite# "-" (natural#as id))
              
              {#Opaque id}
              (.text_composite# "+" (natural#as id))
              
              {#Universal env body}
              (.text_composite# "(for_any " (type#as body) ")")

              {#Existential env body}
              (.text_composite# "(for_some " (type#as body) ")")
              
              {#Reification _}
              (let' [[func args] (flat_application type)]
                    (.text_composite#
                     "(" (type#as func) " "
                     (|> args (list#each type#as) (list#interposed " ") list#reversed (list#mix text#composite ""))
                     ")"))
              
              {#Named name _}
              (name#as name)))

(def' .private (meta#try it)
      (type (for_any (_ of)
              (-> (Meta of)
                  (Meta (Either Text of)))))
      (function' [state]
                 (.when# (it state)
                         {#Left error}
                         {#Right [state {#Left error}]}
                         
                         {#Right [state output]}
                         {#Right [state {#Right output}]})))

(def' .private (anonymous_type it)
      (-> Type
          Type)
      (.when# it
              {#Named _ it}
              (anonymous_type it)
              
              _
              it))

(def' .private static'
      (type (-> Bit Code
                (Meta Code)))
      (let' [simple_literal (is (-> Name
                                    (Meta Code))
                                (function' [name]
                                           (do meta#monad
                                             [type+value (meta#try (definition_value name))]
                                             (.when# type+value
                                                     {#Left error}
                                                     (in (as_name name))
                                                     
                                                     {#Right [type value]}
                                                     (.when# (anonymous_type type)
                                                             {#Nominal "#Bit" {#End}}
                                                             (in (as_bit (as Bit value)))

                                                             {#Nominal "#Dec" {#End}}
                                                             (in (as_decimal (as Decimal value)))

                                                             {#Nominal "#Text" {#End}}
                                                             (in (as_text (as Text value)))

                                                             {#Nominal "#I64" {#Item {#Nominal "#Nat" {#End}} {#End}}}
                                                             (in (as_natural (as Natural value)))

                                                             {#Nominal "#I64" {#Item {#Nominal "#Int" {#End}} {#End}}}
                                                             (in (as_integer (as Integer value)))

                                                             {#Nominal "#I64" {#Item {#Nominal "#Rev" {#End}} {#End}}}
                                                             (in (as_revolution (as Revolution value)))

                                                             {#Nominal "#Tag" {#End}}
                                                             (in (as_name name))
                                                             
                                                             {#Nominal "#Slot" {#End}}
                                                             (in (as_name name))
                                                             
                                                             _
                                                             (failure (.text_composite# "Invalid static value: " (name#as name)
                                                                                        " : " (type#as type))))))))]
            (function' literal [only_global? token]
                       (.when# token
                               [_ {#Name [def_module def_name]}]
                               (if (text#= "" def_module)
                                 (if only_global?
                                   (meta#in (as_name [def_module def_name]))
                                   (do meta#monad
                                     [current_module current_module_name]
                                     (simple_literal [current_module def_name])))
                                 (simple_literal [def_module def_name]))

                               [meta {#Form parts}]
                               (do meta#monad
                                 [=parts (monad#each#meta (literal only_global?) parts)]
                                 (in [meta {#Form =parts}]))

                               [meta {#Variant parts}]
                               (do meta#monad
                                 [=parts (monad#each#meta (literal only_global?) parts)]
                                 (in [meta {#Variant =parts}]))

                               [meta {#Tuple parts}]
                               (do meta#monad
                                 [=parts (monad#each#meta (literal only_global?) parts)]
                                 (in [meta {#Tuple =parts}]))

                               _
                               ... TODO: Figure out why this doesn't work:
                               ... (meta#in token)
                               (meta#in token)))))

(def' .public static
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item pattern {#End}}
                (do meta#monad
                  [pattern' (static' #0 pattern)]
                  (in (list pattern')))
                
                _
                (failure ..wrong_syntax))))

(def' .public Pattern
      Type
      {#Named [..prelude "Pattern"]
              {#Nominal "#Macro/Pattern" {#End}}})

(def' .public (pattern it)
      (-> Macro
          Pattern)
      (.as# Pattern it))

(def' .public (pattern_macro it)
      (-> Pattern
          Macro')
      (.as# Macro' it))

(def' .private (total_expansion' total_expansion @name name args)
      (-> (-> Code ($ Meta ($ List Code))) Provenance Name ($ List Code)
          ($ Meta ($ List Code)))
      (<| (function' [lux])
          (meta#let lux [name' (normal name)])
          (meta#let lux [?macro (named_macro name')])
          (.when# ?macro
                  {#Some macro}
                  (<| (meta#let lux [expansion ((.as# Macro' macro) args)])
                      (meta#let lux [expansion' (monad#each#meta total_expansion expansion)])
                      (meta#return lux (list#conjoint expansion')))
                  
                  {#None}
                  (<| (meta#let lux [args' (monad#each#meta total_expansion args)])
                      (meta#return lux (list (as_form {#Item [@name {#Name name}] (list#conjoint args')})))))))

(def' .private (total_expansion syntax)
      (-> Code
          ($ Meta ($ List Code)))
      (.when# syntax
              [_ {#Form {#Item head tail}}]
              (.when# head
                      [@name {#Name name}]
                      (..total_expansion' total_expansion @name name tail)

                      _
                      (<| (function' [lux])
                          (meta#let lux [members' (monad#each#meta total_expansion {#Item head tail})])
                          (meta#return lux (list (as_form (list#conjoint members'))))))

              [_ {#Variant members}]
              (<| (function' [lux])
                  (meta#let lux [members' (monad#each#meta total_expansion members)])
                  (meta#return lux (list (as_variant (list#conjoint members')))))

              [_ {#Tuple members}]
              (<| (function' [lux])
                  (meta#let lux [members' (monad#each#meta total_expansion members)])
                  (meta#return lux (list (as_tuple (list#conjoint members')))))

              _
              (meta#in (list syntax))))

(def' .private (when_expansion#macro when_expansion pattern body branches)
      (type (-> (-> (List Code) (Meta (List Code))) Code Code (List Code)
                (Meta (List Code))))
      (do meta#monad
        [pattern (one_expansion (total_expansion pattern))
         pattern (static' #1 pattern)
         branches (when_expansion branches)]
        (in (list#partial pattern body branches))))

(def' .private (when_expansion branches)
      (type (-> (List Code)
                (Meta (List Code))))
      (.when# branches
              {#Item [@composite {#Form {#Item [@name {#Name global}] parameters}}]
                     {#Item body
                            branches'}}
              (do meta#monad
                [|global| (..normal global)
                 ?type,value (global_value |global|)]
                (.when# ?type,value
                        {#Some [type value]}
                        (if (type#= Pattern type)
                          (do meta#monad
                            [branches'' ((pattern_macro (.as# Pattern value))
                                         (list#partial (as_form parameters) body branches'))]
                            (when_expansion branches''))
                          (when_expansion#macro when_expansion [@composite {#Form {#Item [@name {#Name global}] parameters}}] body branches'))
                        
                        {#None}
                        (when_expansion#macro when_expansion [@composite {#Form {#Item [@name {#Name global}] parameters}}] body branches')))

              {#Item pattern {#Item body branches'}}
              (when_expansion#macro when_expansion pattern body branches')

              {#End}
              (meta#in (list))

              _
              (failure (.text_composite# "'when' expects an even number of tokens: "
                                         (|> branches
                                             (list#each code#as)
                                             (list#interposed " ")
                                             list#reversed
                                             (list#mix text#composite ""))))))

(def' .public when
      Macro
      (macro (_ tokens)
        (.when# tokens
                {#Item value branches}
                (do meta#monad
                  [expansion (when_expansion branches)]
                  (in (list (` (.when# (, value) (,* expansion))))))

                _
                (failure ..wrong_syntax))))

(def' .private pattern#or
      Pattern
      (pattern
       (macro (_ tokens)
         (when tokens
           (list#partial [_ {#Form patterns}] body branches)
           (when patterns
             {#End}
             (failure "pattern#or cannot have 0 patterns")

             _
             (let' [pairs (|> patterns
                              (list#each (function' [pattern] (list pattern body)))
                              (list#conjoint))]
                   (meta#in (list#composite pairs branches))))
           _
           (failure ..wrong_syntax)))))

(def' .private (name? code)
      (type (-> Code
                Bit))
      (when code
        [_ {#Name _}]
        #1

        _
        #0))

(def' .public bindings_must_be_pairs
      Error
      "Bindings must be pairs (i.e. they must be an even number).")

(def' .public let
      Macro
      (macro (_ tokens)
        (when tokens
          (list [_ {#Tuple bindings}] body)
          (when (..pairs bindings)
            {#Some bindings}
            (|>  bindings
                 list#reversed
                 (list#mix (is (-> [Code Code] Code Code)
                               (function' [lr body']
                                          (let' [[l r] lr]
                                                (if (name? l)
                                                  (` (.when# (, r) (, l) (, body')))
                                                  (` (when (, r) (, l) (, body')))))))
                           body)
                 list
                 meta#in)

            {#None}
            (failure ..bindings_must_be_pairs))

          _
          (failure ..wrong_syntax))))

(def' .public function
      Macro
      (macro (_ tokens)
        (when (is (Maybe [Text Code (List Code) Code])
                  (when tokens
                    (list [_ {#Form (list#partial [_ {#Name ["" name]}] head tail)}] body)
                    {#Some name head tail body}
                    
                    _
                    {#None}))
          {#Some 'name head tail body}
          (let ['blank (as_local "")
                nest (is (-> Code (-> Code Code Code))
                         (function' ['name]
                                    (function' [arg body']
                                               (if (name? arg)
                                                 (` (.function# (, 'name) (, arg) (, body')))
                                                 (` (.function# (, 'name) (, 'blank)
                                                                (.when (, 'blank) (, arg) (, body'))))))))]
            (meta#in (list (nest (..as_local 'name) head
                                 (list#mix (nest 'blank) body (list#reversed tail))))))

          {#None}
          (failure ..wrong_syntax))))

(def' .private Projection
      Type
      {#Named [..prelude "Projection"]
              (type (for_any (_ of)
                      (-> (List Code)
                          (Maybe [(List Code) of]))))})

(def' .private (parsed projection tokens)
      (type (for_any (_ of)
              (-> (Projection of) (List Code)
                  (Maybe of))))
      (when (projection tokens)
        {#Some [(list) it]}
        {#Some it}

        _
        {#None}))

(def' .private (inP it tokens)
      (type (for_any (_ of)
              (-> of
                  (Projection of))))
      {#Some [tokens it]})

(def' .private (orP leftP rightP tokens)
      (type (for_any (_ left right)
              (-> (Projection left)
                  (Projection right)
                  (Projection (Or left right)))))
      (when (leftP tokens)
        {#Some [tokens left]}
        {#Some [tokens {#Left left}]}

        _
        (when (rightP tokens)
          {#Some [tokens right]}
          {#Some [tokens {#Right right}]}

          _
          {#None})))

(def' .private (eitherP leftP rightP tokens)
      (type (for_any (_ of)
              (-> (Projection of) (Projection of)
                  (Projection of))))
      (when (leftP tokens)
        {#None}
        (rightP tokens)

        it
        it))

(def' .private (andP leftP rightP tokens)
      (type (for_any (_ left right)
              (-> (Projection left) (Projection right)
                  (Projection (And left right)))))
      (do maybe#monad
        [left (leftP tokens)
         .let [[tokens left] left]
         right (rightP tokens)
         .let [[tokens right] right]]
        (in [tokens [left right]])))

(def' .private (afterP leftP rightP tokens)
      (type (for_any (_ _ of)
              (-> (Projection _) (Projection of)
                  (Projection of))))
      (do maybe#monad
        [left (leftP tokens)
         .let [[tokens left] left]]
        (rightP tokens)))

(def' .private (someP itP tokens)
      (type (for_any (_ of)
              (-> (Projection of)
                  (Projection (List of)))))
      (when (itP tokens)
        {#Some [tokens head]}
        (do maybe#monad
          [it (someP itP tokens)
           .let [[tokens tail] it]]
          (in [tokens (list#partial head tail)]))

        {#None}
        {#Some [tokens (list)]}))

(def' .private (manyP itP tokens)
      (type (for_any (_ of)
              (-> (Projection of)
                  (Projection (List of)))))
      (do maybe#monad
        [it (itP tokens)
         .let [[tokens head] it]
         it (someP itP tokens)
         .let [[tokens tail] it]]
        (in [tokens (list#partial head tail)])))

(def' .private (maybeP itP tokens)
      (type (for_any (_ of)
              (-> (Projection of)
                  (Projection (Maybe of)))))
      (when (itP tokens)
        {#Some [tokens it]}
        {#Some [tokens {#Some it}]}

        {#None}
        {#Some [tokens {#None}]}))

(def' .private (tupleP itP tokens)
      (type (for_any (_ of)
              (-> (Projection of)
                  (Projection of))))
      (when tokens
        (list#partial [_ {#Tuple input}] tokens')
        (do maybe#monad
          [it (parsed itP input)]
          (in [tokens' it]))

        _
        {#None}))

(def' .private (formP itP tokens)
      (type (for_any (_ of)
              (-> (Projection of)
                  (Projection of))))
      (when tokens
        (list#partial [_ {#Form input}] tokens')
        (do maybe#monad
          [it (parsed itP input)]
          (in [tokens' it]))

        _
        {#None}))

(def' .private (bindingP tokens)
      (type (Projection [Text Code]))
      (when tokens
        (list#partial [_ {#Name ["" name]}] value &rest)
        {#Some [&rest [name value]]}

        _
        {#None}))

(def' .private (endP tokens)
      (type (Projection Any))
      (when tokens
        (list)
        {#Some [tokens []]}

        _
        {#None}))

(def' .private (anyP tokens)
      (type (Projection Code))
      (when tokens
        (list#partial code tokens')
        {#Some [tokens' code]}

        _
        {#None}))

(def' .private (localP tokens)
      (type (Projection Text))
      (when tokens
        (list#partial [_ {#Name ["" local]}] tokens')
        {#Some [tokens' local]}

        _
        {#None}))

(def' .private (nameP tokens)
      (type (Projection Name))
      (when tokens
        (list#partial [_ {#Name it}] tokens')
        {#Some [tokens' it]}

        _
        {#None}))

(with_template [<projection> <item_type> <item_of>]
  [(def' .private (<projection> tokens)
         (type (-> (List Code)
                   (Maybe (List <item_type>))))
         (when tokens
           {#End}
           {#Some {#End}}

           _
           (do maybe#monad
             [% (<item_of> tokens)
              .let [[tokens head] %]
              tail (<projection> tokens)]
             (in {#Item head tail}))))]

  [parametersP Text localP]
  [enhanced_parametersP Code anyP]
  )

(with_template [<projection> <parameter_type> <parameters_of>]
  [(def' .private (<projection> tokens)
         (type (Projection [Text (List <parameter_type>)]))
         (when tokens
           (list#partial [_ {#Form local_declaration}] tokens')
           (do maybe#monad
             [% (localP local_declaration)
              .let [[local_declaration name] %]
              parameters (<parameters_of> local_declaration)]
             (in [tokens' [name parameters]]))
           
           _
           (do maybe#monad
             [% (localP tokens)
              .let [[tokens' name] %]]
             (in [tokens' [name {#End}]]))))]

  [local_declarationP Text parametersP]
  [enhanced_local_declarationP Code enhanced_parametersP]
  )

(def' .private (export_policyP tokens)
      (type (Projection Code))
      {.#Some (when tokens
                (list#partial candidate tokens')
                (when candidate
                  [_ {#Bit it}]
                  [tokens' candidate]
                  
                  [_ {#Name ["" _]}]
                  [tokens (` .private)]
                  
                  [_ {#Name it}]
                  [tokens' candidate]

                  _
                  [tokens (` .private)])

                _
                [tokens (` .private)])})

(with_template [<projection> <parameter_type> <local>]
  [(def' .private (<projection> tokens)
         (type (Projection [Code Text (List <parameter_type>)]))
         (do maybe#monad
           [% (export_policyP tokens)
            .let [[tokens export_policy] %]
            % (<local> tokens)
            .let [[tokens [name parameters]] %]]
           (in [tokens [export_policy name parameters]])))]

  [declarationP Text local_declarationP]
  [enhanced_declarationP Code enhanced_local_declarationP]
  )

(def' .private (bodyP tokens)
      (type (Projection [(Maybe Code) Code]))
      (when tokens
        ... TB
        (list#partial type body tokens')
        {#Some [tokens' [{#Some type} body]]}

        ... B
        (list#partial body tokens')
        {#Some [tokens' [{#None} body]]}

        _
        {#None}))

(def' .private (definitionP tokens)
      (type (-> (List Code)
                (Maybe [Code
                        Text
                        (List Code)
                        (Maybe Code)
                        Code])))
      (do maybe#monad
        [% (enhanced_declarationP tokens)
         .let [[tokens [export_policy name parameters]] %]
         % (bodyP tokens)
         .let [[tokens [?type body]] %]
         _ (endP tokens)]
        (in [export_policy name parameters ?type body])))

... https://en.wikipedia.org/wiki/List_of_English_determiners
(def' .public the
      Macro
      (macro (_ tokens)
        (when (definitionP tokens)
          {#Some [export_policy name parameters ?type body]}
          (let [body (when parameters
                       {#End}
                       body

                       _
                       (` (function ((, (..as_local name)) (,* parameters))
                            (, body))))
                body (when ?type
                       {#Some type}
                       (` (is (, type)
                              (, body)))
                       
                       {#None}
                       body)]
            (meta#in (list (` (.def# (, (..as_local name))
                                     (, body)
                                     (, export_policy))))))
          
          {#None}
          (failure ..wrong_syntax))))

(with_template [<name> <nullary> <form>]
  [(the .public <name>
     (macro (_ tokens)
       (when (list#reversed tokens)
         (list#partial last init)
         (meta#in (list (list#mix (is (-> Code Code Code)
                                      (function (_ pre post) (` <form>)))
                                  last
                                  init)))
         
         _
         (meta#in (list (` <nullary>))))))]

  [and #1 (if (, pre) (, post) #0)]
  [or  #0 (if (, pre) #1 (, post))]
  )

(the (index part text)
  (-> Text Text
      (Maybe Natural))
  (.text_index# 0 part text))

... https://en.wikipedia.org/wiki/Halting_problem
(the .public (halt! message)
  (-> Text
      Nothing)
  (.error# message))

(the maybe#else
  (macro (_ tokens state)
    (when tokens
      (list else maybe)
      (let ['temp (is Code [provenance#dummy {#Name ["" ""]}])
            code (` (when (, maybe)
                      {.#Some (, 'temp)}
                      (, 'temp)

                      {.#None}
                      (, else)))]
        {#Right [state (list code)]})

      _
      {#Left ..wrong_syntax})))

(the (text#all_split_by splitter input)
  (-> Text Text
      (List Text))
  (when (..index splitter input)
    {#None}
    (list input)

    {#Some idx}
    (list#partial (.text_clip# 0 idx input)
                  (text#all_split_by splitter
                                     (let [after_offset (.i64_+# 1 idx)
                                           after_length (.i64_-# after_offset
                                                                 (.text_size# input))]
                                       (.text_clip# after_offset after_length input))))))

(the (item idx xs)
  (for_any (_ of)
    (-> Natural (List of)
        (Maybe of)))
  (when xs
    {#End}
    {#None}

    {#Item x xs'}
    (if (.i64_=# 0 idx)
      {#Some x}
      (item (.i64_-# 1 idx) xs'))))

... https://en.wikipedia.org/wiki/Lambda_calculus#%CE%B2-reduction
(the (reduced env type)
  (-> (List Type) Type
      Type)
  (when type
    {#Sum left right}
    {#Sum (reduced env left) (reduced env right)}

    {#Product left right}
    {#Product (reduced env left) (reduced env right)}
    
    {#Reification arg func}
    {#Reification (reduced env arg) (reduced env func)}

    {#Universal ?local_env ?local_def}
    (when ?local_env
      {#End}
      {#Universal env ?local_def}

      _
      type)

    {#Existential ?local_env ?local_def}
    (when ?local_env
      {#End}
      {#Existential env ?local_def}

      _
      type)

    {#Function ?input ?output}
    {#Function (reduced env ?input) (reduced env ?output)}

    {#Parameter idx}
    (when (item idx env)
      {#Some parameter}
      parameter

      _
      type)
    
    {#Named name type}
    (reduced env type)

    _
    type
    ))

(the (applied_type param type_fn)
  (-> Type Type
      (Maybe Type))
  (when type_fn
    {#Universal env body}
    {#Some (reduced (list#partial type_fn param env) body)}

    {#Existential env body}
    {#Some (reduced (list#partial type_fn param env) body)}

    {#Reification A F}
    (do maybe#monad
      [type_fn* (applied_type A F)]
      (applied_type param type_fn*))

    {#Named name type}
    (applied_type param type)
    
    _
    {#None}))

(the (interface_methods type)
  (-> Type (Maybe (List Type)))
  (when type
    {#Product _}
    {#Some (flat_tuple type)}

    {#Reification arg func}
    (do maybe#monad
      [output (applied_type arg func)]
      (interface_methods output))

    {#Universal _ body}
    (interface_methods body)

    {#Existential _ body}
    (interface_methods body)

    {#Named name type}
    (interface_methods type)

    {#Sum _}
    {#None}
    
    _
    {#Some (list type)}))

(the (module name)
  (-> Text
      (Meta Module))
  (function (_ state)
    (let [[..#info info ..#source    source  ..#current_module _ ..#modules modules
           ..#scopes scopes ..#type_context types    ..#host    host
           ..#seed seed ..#expected  expected ..#provenance  provenance ..#extensions extensions
           ..#scope_type_vars scope_type_vars ..#eval _eval] state]
      (when (property#value name modules)
        {#Some module}
        {#Right state module}

        _
        {#Left (.text_composite# "Unknown module: " name)}))))

(the (type_slot [module name])
  (-> Name
      (Meta [Bit Label]))
  (do meta#monad
    [=module (..module module)
     .let [[..#module_hash _
            ..#module_aliases _
            ..#definitions definitions
            ..#imports _
            ..#module_state _] =module]]
    (when (property#value name definitions)
      {#Some [exported {#Definition [type value]}]}
      (meta#in [exported (as Label value)])

      _
      (failure (.text_composite# "Unknown slot: " (name#as [module name]))))))

(the (slot_family expected_module expected_record)
  (-> Text Type
      (Meta (Maybe (List Name))))
  (do meta#monad
    [module (..module expected_module)
     actual_module ..current_module_name
     .let [[..#module_hash _
            ..#module_aliases _
            ..#definitions definitions
            ..#imports _
            ..#module_state _] module]]
    (in ((is (-> (List [Text [Bit Global]])
                 (Maybe (List Name)))
             (function (again remaining)
               (when remaining
                 {#Item [slot head] tail}
                 (when head
                   [exported? {#Definition [type value]}]
                   (if (and (type#= Slot type)
                            (or exported?
                                (text#= expected_module actual_module)))
                     (let [[label actual_record] (as Label value)]
                       (if (type#= expected_record actual_record)
                         (when label
                           {#Some [lefts right? family]}
                           {#Some family}
                           
                           {#None}
                           {#Some (list [expected_module slot])})
                         (again tail)))
                     (again tail))

                   _
                   (again tail))

                 {#End}
                 {#None})))
         definitions))))

(the (record_slots type)
  (-> Type
      (Meta (Maybe [(List Name) (List Type)])))
  (when type
    {#Reification arg func}
    (record_slots func)

    {#Universal env body}
    (record_slots body)

    {#Existential env body}
    (record_slots body)

    {#Named [module name] unnamed}
    (do meta#monad
      [=module (..module module)
       .let [[..#module_hash _
              ..#module_aliases _
              ..#definitions definitions
              ..#imports _
              ..#module_state _] =module]]
      (when (property#value name definitions)
        {#Some [exported? {#Definition [type value]}]}
        (if (type#= Type type)
          (do meta#monad
            [slots (slot_family module (as Type value))]
            (when [slots (interface_methods (as Type value))]
              [{#Some slots} {#Some members}]
              (in {#Some [slots members]})

              _
              (record_slots unnamed)))
          (in {#None}))

        _
        (record_slots unnamed)))
    
    _
    (meta#in {#None})))

(the expected_type
  (Meta Type)
  (function (_ state)
    (let [[..#info info ..#source    source  ..#current_module _ ..#modules modules
           ..#scopes scopes ..#type_context types    ..#host    host
           ..#seed seed ..#expected  expected ..#provenance  provenance ..#extensions extensions
           ..#scope_type_vars scope_type_vars ..#eval _eval] state]
      (when expected
        {#Some type}
        {#Right state type}

        {#None}
        {#Left "Not expecting any type."}))))

(the \n
  Text
  (.int_char# +10))

(the (provenance#as it)
  (-> Provenance
      Text)
  (let [... https://en.wikipedia.org/wiki/Delimiter
        delimiter ","
        [file line column] it]
    (.text_composite# "@"
                      (text#as file) delimiter
                      (natural#as line) delimiter
                      (natural#as column))))

(the (provenance#with provenance error)
  (-> Provenance Text
      Text)
  (.text_composite# (provenance#as provenance) \n
                    error))

(the provenance#here
  (macro (_ tokens compiler)
    (when tokens
      {.#End}
      (let [[..#info _
             ..#source _
             ..#current_module _
             ..#modules _
             ..#scopes _
             ..#type_context _
             ..#host _
             ..#seed _
             ..#expected _
             ..#provenance provenance
             ..#extensions _
             ..#scope_type_vars _
             ..#eval _] compiler
             [.#module module .#line line .#column column] provenance]
        {.#Right [compiler
                  (list (` (.is .Provenance
                                [.#module (, [..provenance#dummy {.#Text module}])
                                 .#line (, [..provenance#dummy {.#Natural line}])
                                 .#column (, [..provenance#dummy {.#Natural column}])])))]})

      _
      {.#Left ..wrong_syntax})))

(the .public invalid_implementation_member
  Error
  (provenance#with (provenance#here)
                   "Invalid implementation member."))

(the .public implementation
  (macro (_ tokens)
    (do meta#monad
      [tokens' (monad#each#meta complete_expansion tokens)
       implementation_type ..expected_type
       tags+type (record_slots implementation_type)
       tags (is (Meta (List Name))
                (when tags+type
                  {#Some [tags _]}
                  (meta#in tags)

                  _
                  (failure (.text_composite# "No tags available for type: "
                                             (type#as implementation_type)))))
       .let [tag_mappings (is (List [Text Code])
                              (list#each (function (_ tag)
                                           [(product#right tag)
                                            (as_name tag)])
                                         tags))]
       members (monad#each#meta (is (-> Code (Meta (List Code)))
                                    (function (_ token)
                                      (when token
                                        [_ {#Form (list [_ {#Name [..prelude "def#"]}] [_ {#Name ["" slot_name]}] value export_policy)}]
                                        (when (property#value slot_name tag_mappings)
                                          {#Some tag}
                                          (in (list tag value))

                                          _
                                          (failure (.text_composite# "Unknown implementation member: " slot_name)))

                                        _
                                        (failure ..invalid_implementation_member))))
                                (list#conjoint tokens'))]
      (in (list (as_tuple (list#conjoint members)))))))

(the (text#interposed delimiter parts)
  (-> Text (List Text)
      Text)
  (when parts
    {#End}
    ""
    
    {#Item head tail}
    (list#mix (function (_ right left)
                (.text_composite# left delimiter right))
              head
              tail)))

(the (everyP itP tokens)
  (for_any (_ of)
    (-> (-> (List Code) (Maybe [(List Code) of]))
        (-> (List Code) (Maybe (List of)))))
  (when tokens
    {#Item _}
    (do maybe#monad
      [% (itP tokens)
       .let [[tokens' head] %]
       tail (when tokens'
              {#Item _}
              (everyP itP tokens')

              {#End}
              (in (list)))]
      (in (list#partial head tail)))

    {#End}
    {#Some (list)}))

(the (whenP tokens)
  (-> (List Code)
      (Maybe [(List Code) [Text Code]]))
  (when tokens
    (list#partial [_ {#Variant (list [_ {#Name ["" niladic]}])}] tokens')
    {#Some [tokens' [niladic (` .Any)]]}

    (list#partial [_ {#Variant (list#partial [_ {#Name ["" polyadic]}] whenT)}] tokens')
    {#Some [tokens' [polyadic (` (..Tuple (,* whenT)))]]}

    _
    {#None}))

(the .public Variant
  (macro (_ tokens)
    (when (everyP whenP tokens)
      {#Some whens}
      (meta#in (list (` (..Union (,* (list#each product#right whens))))
                     (as_variant (list#each (function (_ when)
                                              (as_text (product#left when)))
                                            whens))))
      
      {#None}
      (failure ..wrong_syntax))))

(the (slotP tokens)
  (-> (List Code)
      (Maybe [(List Code) [Text Code]]))
  (when tokens
    (list#partial [_ {#Name ["" slot]}] type tokens')
    {#Some [tokens' [slot type]]}

    _
    {#None}))

(the .public Record
  (macro (_ tokens)
    (when tokens
      (list [_ {#Tuple record}])
      (when (everyP slotP record)
        {#Some slots}
        (meta#in (list (` (..Tuple (,* (list#each product#right slots))))
                       (as_tuple (list#each (function (_ slot)
                                              (as_text (product#left slot)))
                                            slots))))
        
        {#None}
        (failure ..wrong_syntax))

      _
      (failure ..wrong_syntax))))

(the (typeP tokens)
  (-> (List Code)
      (Maybe [Code Text (List Text) Code]))
  (do maybe#monad
    [% (declarationP tokens)
     .let [[tokens [export_policy name parameters]] %]
     % (anyP tokens)
     .let [[tokens definition] %]
     _ (endP tokens)]
    (in [export_policy name parameters definition])))

(the (textP tokens)
  (-> (List Code)
      (Maybe [(List Code) Text]))
  (when tokens
    (list#partial [_ {#Text it}] tokens')
    {#Some [tokens' it]}

    _
    {#None}))

(the .public improper_type_definition
  Error
  (provenance#with (provenance#here)
                   "Improper type definition syntax."))

(the (type_declaration it)
  (-> Code
      (Meta [Code (Maybe (Either (List Text) (List Text)))]))
  (when it
    [_ {#Form {#Item [_ {#Name declarer}] parameters}}]
    (do meta#monad
      [declaration (single_expansion (as_form (list#partial (as_name declarer) parameters)))]
      (when declaration
        (list type [_ {#Variant tags}])
        (when (everyP textP tags)
          {#Some tags}
          (meta#in [type {#Some {#Left tags}}])
          
          {#None}
          (failure ..improper_type_definition))

        (list type [_ {#Tuple slots}])
        (when (everyP textP slots)
          {#Some slots}
          (meta#in [type {#Some {#Right slots}}])
          
          {#None}
          (failure ..improper_type_definition))

        (list type)
        (meta#in [it {#None}])

        _
        (failure ..improper_type_definition)))

    type
    (meta#in [type {#None}])))

(the (enumeration' idx xs)
  (for_any (_ of)
    (-> Natural (List of)
        (List [Natural of])))
  (when xs
    {#Item x xs'}
    {#Item [idx x] (enumeration' (.i64_+# 1 idx) xs')}

    {#End}
    {#End}))

(the (enumeration xs)
  (for_any (_ of)
    (-> (List of)
        (List [Natural of])))
  (enumeration' 0 xs))

(the (label_definitions module export_policy associated_type label_type family labels)
  (-> Text Code Code Code Code (List Text)
      (List Code))
  (when (list#reversed labels)
    (list single)
    (list (` (the (, export_policy) (, (as_local single))
               (<| (as (, label_type))
                   (is Label)
                   [{#None} (, associated_type)]))))

    (list#partial right lefts)
    (list#partial
     (` (the (, family)
          (List Name)
          (list (,* (list#each (function (_ it)
                                 (` [(, (as_text module)) (, (as_text it))]))
                               labels)))))
     (` (the (, export_policy) (, (as_local right))
          (<| (as (, label_type))
              (is Label)
              [{#Some [(, (as_natural (.i64_-# 1 (list#size lefts)))) #1 (, family)]} (, associated_type)])))
     (list#each (function (_ [lefts it])
                  (` (the (, export_policy) (, (as_local it))
                       (<| (as (, label_type))
                           (is Label)
                           [{#Some [(, (as_natural lefts)) #0 (, family)]} (, associated_type)]))))
                (enumeration (list#reversed lefts))))

    _
    (list)))

... https://en.wikipedia.org/wiki/List_of_English_determiners
(the .public every
  (macro (_ tokens)
    (when (typeP tokens)
      {#Some [export_policy name args type_codes]}
      (do meta#monad
        [type+labels?? (..type_declaration type_codes)
         module_name current_module_name
         'cohort (..generated_name "'cohort")
         .let [type_name (as_local name)
               [it labels??] type+labels??
               it' (is (Maybe Code)
                       (when args
                         {#End}
                         {#Some it}

                         _
                         {#Some (` (.for_any ((, type_name) (,* (list#each as_local args)))
                                     (, it)))}))]]
        (when it'
          {#Some it''}
          (do meta#monad
            [it'' (type' it'')
             .let [itC (` {.#Named [(, (as_text module_name))
                                    (, (as_text name))]
                                   (, it'')})
                   type_definition (` (the (, export_policy) (, type_name)
                                        Type
                                        (, itC)))]]
            (in (when labels??
                  {#Some labels}
                  (list#partial type_definition
                                (when labels
                                  {#Left tags}
                                  (label_definitions module_name export_policy type_name (` Tag) 'cohort tags)
                                  
                                  {#Right slots}
                                  (label_definitions module_name export_policy type_name (` Slot) 'cohort slots)))
                  
                  _
                  (list type_definition))))

          {#None}
          (failure ..wrong_syntax)))

      {#None}
      (failure ..wrong_syntax))))

(every Referral
  [Name (List Code)])

(every Importation
  (Record
   [#import_name Text
    #import_alias (Maybe Text)
    #import_referrals (List Referral)]))

(the referral_of
  (Projection Referral)
  (formP (andP nameP (someP anyP))))

(the (referrals_of aliased?)
  (-> Bit
      (Projection (List Referral)))
  (all eitherP
       (manyP referral_of)
       (afterP endP
               (inP (if aliased?
                      (list [(name ..only) (list)])
                      (list))))
       (inP (list))))

(the (text#split_at' at x)
  (-> Natural Text
      [Text Text])
  [(.text_clip# 0 at x)
   (.text_clip# at (|> x .text_size# (.i64_-# at)) x)])

(the (text#split_by token it)
  (-> Text Text
      (Maybe [Text Text]))
  (do ..maybe#monad
    [index (..index token it)
     .let [[pre post'] (text#split_at' index it)
           [_ post] (text#split_at' (.text_size# token) post')]]
    (in [pre post])))

(the (replaced pattern replacement template)
  (-> Text Text Text
      Text)
  ((is (-> Text Text Text)
       (function (again left right)
         (when (..text#split_by pattern right)
           {#Some [pre post]}
           (again (.text_composite# left pre replacement) post)

           {#None}
           (.text_composite# left right))))
   "" template))

(the (alias_stand_in index)
  (-> Natural
      Text)
  (.text_composite# "[" (natural#as index) "]"))

(the (module_alias context aliased)
  (-> (List Text) Text
      Text)
  (product#right
   (list#mix (function (_ replacement [index aliased])
               [(.i64_+# 1 index)
                (replaced (alias_stand_in index) replacement aliased)])
             [0 aliased]
             context)))

... https://en.wikipedia.org/wiki/Delimiter
(the .public module_delimiter
  "/")

(the parallel_hierarchy_sigil
  "\")

(the (normal_parallel_path' hierarchy root)
  (-> Text Text
      Text)
  (when [(text#split_by ..module_delimiter hierarchy)
         (text#split_by ..parallel_hierarchy_sigil root)]
    [{#Some [_ hierarchy']}
     {#Some ["" root']}]
    (normal_parallel_path' hierarchy' root')

    _
    (when root
      "" hierarchy
      _ (.text_composite# root ..module_delimiter hierarchy))))

(the (normal_parallel_path hierarchy root)
  (-> Text Text
      (Maybe Text))
  (when (text#split_by ..parallel_hierarchy_sigil root)
    {#Some ["" root']}
    {#Some (normal_parallel_path' hierarchy root')}

    _
    {#None}))

(the (relative_ups relatives input)
  (-> Natural Text
      Natural)
  (when (.text_index# relatives ..module_delimiter input)
    {#None}
    relatives

    {#Some found}
    (if (.i64_=# relatives found)
      (relative_ups (.i64_+# 1 relatives) input)
      relatives)))

(the (list#after amount list)
  (for_any (_ of)
    (-> Natural (List of)
        (List of)))
  (when [amount list]
    (pattern#or [0 _]
                [_ {#End}])
    list

    [_ {#Item _ tail}]
    (list#after (.i64_-# 1 amount) tail)))

(the (absolute_module_name nested? relative_root module)
  (-> Bit Text Text
      (Meta Text))
  (when (relative_ups 0 module)
    0
    (meta#in (if nested?
               (.text_composite# relative_root ..module_delimiter module)
               module))
    
    relatives
    (let [parts (text#all_split_by ..module_delimiter relative_root)
          jumps (.i64_-# 1 relatives)]
      (if (n#< (list#size parts) jumps)
        (let [prefix (|> parts
                         list#reversed
                         (list#after jumps)
                         list#reversed
                         (text#interposed ..module_delimiter))
              clean (.text_clip# relatives (|> module .text_size# (.i64_-# relatives)) module)
              output (when (.text_size# clean)
                       0 prefix
                       _ (.text_composite# prefix ..module_delimiter clean))]
          (meta#in output))
        (failure (.text_composite# "Cannot climb the module hierarchy..." \n
                                   "Importing module: " module \n
                                   "   Relative Root: " relative_root \n))))))

(the (imports_of nested? relative_root context imports)
  (-> Bit Text (List Text) (List Code)
      (Meta (List Importation)))
  (do meta#monad
    [imports' (monad#each#meta (is (-> Code (Meta (List Importation)))
                                   (function (_ token)
                                     (when token
                                       ... Nested
                                       [_ {#Tuple (list#partial [_ {#Name ["" module_name]}] extra)}]
                                       (do meta#monad
                                         [absolute_module_name (when (normal_parallel_path relative_root module_name)
                                                                 {#Some parallel_path}
                                                                 (in parallel_path)

                                                                 {#None}
                                                                 (..absolute_module_name nested? relative_root module_name))
                                          extra,referral (when (referrals_of #0 extra)
                                                           {#Some extra,referral}
                                                           (in extra,referral)

                                                           {#None}
                                                           (failure ""))
                                          .let [[extra referral] extra,referral]
                                          sub_imports (imports_of #1 absolute_module_name context extra)]
                                         (in (when referral
                                               {#End}
                                               sub_imports
                                               
                                               _
                                               (list#partial [#import_name absolute_module_name
                                                              #import_alias {#None}
                                                              #import_referrals referral]
                                                             sub_imports))))

                                       [_ {#Tuple (list#partial [_ {#Text alias}] [_ {#Name ["" module_name]}] extra)}]
                                       (do meta#monad
                                         [absolute_module_name (when (normal_parallel_path relative_root module_name)
                                                                 {#Some parallel_path}
                                                                 (in parallel_path)

                                                                 {#None}
                                                                 (..absolute_module_name nested? relative_root module_name))
                                          extra,referral (when (referrals_of #1 extra)
                                                           {#Some extra,referral}
                                                           (in extra,referral)

                                                           {#None}
                                                           (failure ""))
                                          .let [[extra referral] extra,referral]
                                          .let [module_alias (..module_alias {#Item module_name context} alias)]
                                          sub_imports (imports_of #1 absolute_module_name {#Item module_alias context} extra)]
                                         (in (when referral
                                               {#End}
                                               sub_imports
                                               
                                               _
                                               (list#partial [#import_name absolute_module_name
                                                              #import_alias {#Some module_alias}
                                                              #import_referrals referral]
                                                             sub_imports))))

                                       ... Unrecognized syntax.
                                       _
                                       (do meta#monad
                                         [current_module current_module_name]
                                         (failure (.text_composite# "Wrong syntax for import @ " current_module
                                                                    \n (code#as token)))))))
                               imports)]
    (in (list#conjoint imports'))))

(the (exported_definitions module state)
  (-> Text
      (Meta (List Text)))
  (let [[current_module modules] (when state
                                   [..#info info ..#source    source  ..#current_module current_module ..#modules modules
                                    ..#scopes scopes ..#type_context types    ..#host    host
                                    ..#seed seed ..#expected  expected ..#provenance  provenance ..#extensions extensions
                                    ..#scope_type_vars scope_type_vars ..#eval _eval]
                                   [current_module modules])]
    (when (property#value module modules)
      {#Some =module}
      (let [to_alias (list#each (is (-> [Text [Bit Global]]
                                        (List Text))
                                    (function (_ [name [exported? definition]])
                                      (when definition
                                        {#Alias _}
                                        (if exported?
                                          (list name)
                                          (list))

                                        {#Definition [def_type def_value]}
                                        (if exported?
                                          (list name)
                                          (list))

                                        {#Default _}
                                        (list))))
                                (let [[..#module_hash _ ..#module_aliases _ ..#definitions definitions ..#imports _ ..#module_state _] =module]
                                  definitions))]
        {#Right state (list#conjoint to_alias)})
      
      {#None}
      {#Left (.text_composite# "Unknown module: " (text#as module) \n
                               "Current module: " (when current_module
                                                    {#Some current_module}
                                                    (text#as current_module)

                                                    {#None}
                                                    "???") \n
                                                    "Known modules: " (|> modules
                                                                          (list#each (function (_ [name module])
                                                                                       (as_text name)))
                                                                          as_tuple
                                                                          code#as))})
    ))

(the (list#only p xs)
  (for_any (_ of)
    (-> (-> of Bit) (List of)
        (List of)))
  (when xs
    {#End}
    (list)

    {#Item x xs'}
    (if (p x)
      {#Item x (list#only p xs')}
      (list#only p xs'))))

(the (is_member? whens name)
  (-> (List Text) Text
      Bit)
  (let [output (list#mix (function (_ when prev)
                           (or prev
                               (text#= when name)))
                         #0
                         whens)]
    output))

(the (test_referrals current_module imported_module all_defs referred_defs)
  (-> Text Text (List Text) (List Text)
      (Meta (List Any)))
  (monad#each#meta (is (-> Text (Meta Any))
                       (function (_ _def)
                         (if (is_member? all_defs _def)
                           (meta#in [])
                           (failure (.text_composite# _def " is not defined in module " imported_module " @ " current_module)))))
                   referred_defs))

(the (alias_definition imported_module def)
  (-> Text Text
      Code)
  (` (.def# (, (as_local def))
            (, (as_name [imported_module def]))
            .private)))

(the .public only
  (macro (_ tokens)
    (when (..parsed (all ..andP
                         ..textP
                         ..textP
                         ..textP
                         (..someP ..localP))
                    tokens)
      {.#Some [current_module imported_module import_alias actual]}
      (do meta#monad
        [expected (exported_definitions imported_module)
         _ (test_referrals current_module imported_module expected actual)]
        (in (list#each (..alias_definition imported_module) actual)))

      {.#None}
      (failure ..wrong_syntax))))

(the .public |>>
  (macro (_ tokens)
    (do meta#monad
      ['_ (..generated_name "_")
       'arg (..generated_name "arg")]
      (meta#in (list (` (function ((, '_) (, 'arg)) (|> (, 'arg) (,* tokens)))))))))

(the .public <<|
  (macro (_ tokens)
    (do meta#monad
      ['_ (..generated_name "_")
       'arg (..generated_name "arg")]
      (meta#in (list (` (function ((, '_) (, 'arg)) (<| (,* tokens) (, 'arg)))))))))

(the .public except
  (macro (_ tokens)
    (when (..parsed (all ..andP
                         ..textP
                         ..textP
                         ..textP
                         (..someP ..localP))
                    tokens)
      {.#Some [current_module imported_module import_alias actual]}
      (do meta#monad
        [expected (exported_definitions imported_module)
         _ (test_referrals current_module imported_module expected actual)]
        (in (|> expected
                (..list#only (|>> (is_member? actual) not))
                (list#each (..alias_definition imported_module)))))

      {.#None}
      (failure ..wrong_syntax))))

(the (definition_type name state)
  (-> Name Lux
      (Maybe Type))
  (let [[expected_module expected_proper] name
        [..#info info ..#source    source  ..#current_module _ ..#modules modules
         ..#scopes scopes ..#type_context types    ..#host    host
         ..#seed seed ..#expected  expected ..#provenance  provenance ..#extensions extensions
         ..#scope_type_vars scope_type_vars ..#eval _eval] state]
    (when (property#value expected_module modules)
      {#None}
      {#None}

      {#Some [..#definitions definitions
              ..#module_hash _
              ..#module_aliases _
              ..#imports _
              ..#module_state _]}
      (when (property#value expected_proper definitions)
        {#None}
        {#None}

        {#Some [exported? definition]}
        (when definition
          {#Alias real_name}
          (definition_type real_name state)
          
          {#Definition [def_type def_value]}
          {#Some def_type}

          {#Default _}
          {#None})))))

(the (type_variable idx bindings)
  (-> Natural (List [Natural (Maybe Type)])
      (Maybe Type))
  (when bindings
    {#End}
    {#End}
    
    {#Item [var bound] bindings'}
    (if (.i64_=# idx var)
      bound
      (type_variable idx bindings'))))

(the (clean_type variables it)
  (-> (List [Natural (Maybe Type)]) Type
      Type)
  (when it
    {#Nominal name parameters}
    {#Nominal name (list#each (clean_type variables) parameters)}
    
    {#Sum left right}
    {#Sum (clean_type variables left)
          (clean_type variables right)}

    {#Product left right}
    {#Product (clean_type variables left)
              (clean_type variables right)}

    {#Function left right}
    {#Function (clean_type variables left)
               (clean_type variables right)}

    {#Reification left right}
    {#Reification (clean_type variables left)
                  (clean_type variables right)}

    {#Universal environment unquantified}
    {#Universal (list#each (clean_type variables) environment)
                (clean_type variables unquantified)}
    
    {#Existential environment unquantified}
    {#Existential (list#each (clean_type variables) environment)
                  (clean_type variables unquantified)}
    
    {#Named name anonymous}
    it

    {#Parameter _}
    it
    
    {#Opaque _}
    it
    
    {#Variable id}
    (when (type_variable id variables)
      {#None}
      it

      {#Some {#Variable _}}
      it

      {#Some it}
      (clean_type variables it))))

(the (type_definition full_name)
  (-> Name
      (Meta Type))
  (do meta#monad
    [.let [[module name] full_name]
     current_module current_module_name]
    (function (_ compiler)
      (let [temp (is (Either Text [Lux Type])
                     (if (text#= "" module)
                       (when (in_env name compiler)
                         {#Some implementation_type}
                         {#Right [compiler implementation_type]}

                         _
                         (when (definition_type [current_module name] compiler)
                           {#Some implementation_type}
                           {#Right [compiler implementation_type]}

                           _
                           {#Left (.text_composite# "Unknown var: " (name#as full_name))}))
                       (when (definition_type full_name compiler)
                         {#Some implementation_type}
                         {#Right [compiler implementation_type]}

                         _
                         {#Left (.text_composite# "Unknown var: " (name#as full_name))})))]
        (when temp
          {#Right [compiler temp]}
          (let [[..#info _ ..#source _ ..#current_module _ ..#modules _
                 ..#scopes _ ..#type_context type_context ..#host _
                 ..#seed _ ..#expected _ ..#provenance _ ..#extensions extensions
                 ..#scope_type_vars _ ..#eval _eval] compiler
                 [..#ex_counter _ ..#var_counter _ ..#var_bindings var_bindings] type_context]
            {#Right [compiler (clean_type var_bindings temp)]})

          _
          temp)))))

(the (list#all choice items)
  (for_any (_ input output)
    (-> (-> input (Maybe output)) (List input)
        (List output)))
  (when items
    {#Item head tail}
    (when (choice head)
      {#Some head}
      {#Item head (list#all choice tail)}
      
      {#None}
      (list#all choice tail))
    
    {#End}
    {#End}))

(every Implementation_Interface
  [(List Name) (List Type)])

(the (open_layer alias [tags members])
  (-> Text Implementation_Interface
      (Meta [Code (List [Name Implementation_Interface])]))
  (do meta#monad
    [pattern (monad#each#meta (function (_ [slot slot_type])
                                (do meta#monad
                                  [.let [[_ slot_name] slot
                                         local ["" (..module_alias (list slot_name) alias)]]
                                   implementation (record_slots slot_type)]
                                  (in [(list (as_name slot)
                                             (as_name local))
                                       [local implementation]])))
                              (zipped_2 tags members))]
    (in [(|> pattern
             (list#each product#left)
             list#conjoint
             as_tuple)
         (list#all (function (_ [_ [sub_binding sub_implementation]])
                     (do maybe#monad
                       [sub_implementation sub_implementation]
                       (in [sub_binding sub_implementation])))
                   pattern)])))

(the (open_layers alias interfaces body)
  (-> Text (List Implementation_Interface) Code
      (Meta [Code Code]))
  (do meta#monad
    [layer (monad#each#meta (open_layer alias) interfaces)
     .let [pattern (as_tuple (list#each product#left layer))
           next (|> layer
                    (list#each product#right)
                    list#conjoint)]]
    (when next
      {#End}
      (in [pattern body])

      _
      (do meta#monad
        [.let [sub_value (as_tuple (list#each (|>> product#left as_name) next))]
         sub_pattern,sub_body (open_layers alias (list#each product#right next) body)
         .let [[sub_pattern sub_body] sub_pattern,sub_body]]
        (in [pattern (` (when (, sub_value)
                          (, sub_pattern)
                          (, sub_body)))])))))

(the .public open
  (pattern
   (macro (_ tokens)
     (when tokens
       (list#partial [_ {#Form (list [_ {#Text alias}])}] body branches)
       (do meta#monad
         ['temp (..generated_name "temp")]
         (in (list#partial 'temp
                           (` (..when (, 'temp)
                                (..open (, 'temp) (, (as_text alias)))
                                (, body)))
                           branches)))

       (list#partial [_ {#Form (list [@temp_var {#Name name}] [_ {#Text alias}])}]
                     body
                     branches)
       (do meta#monad
         [init_type (type_definition name)
          implementation_evidence (record_slots init_type)]
         (when implementation_evidence
           {#None}
           (failure (.text_composite# "Can only 'open' implementations: " (type#as init_type)))

           {#Some tags,members}
           (do meta#monad
             [pattern,body (open_layers alias (list tags,members) body)
              .let [[pattern body] pattern,body]]
             (in (list#partial pattern body branches)))))

       _
       (failure ..wrong_syntax)))))

(every (Try of)
  (Variant
   {#Failure Text}
   {#Success of}))

(the .public cannot_access
  Error
  "Cannot synthesize access pattern.")

(the (access_pattern '_ 'output lefts right? members)
  (-> Code Code Natural Bit (List Type)
      (Try (List Code)))
  (when ((is (-> Natural (List Type) (List Code)
                 (List Code))
             (function (again index input output)
               (when input
                 (list#partial head tail)
                 (if (.i64_=# index (if right?
                                      (.i64_+# 1 lefts)
                                      lefts))
                   (list#reversed (list#partial 'output output))
                   (again (.i64_+# 1 index) tail (list#partial '_ output)))

                 (list)
                 (list))))
         0 members (list))
    (list)
    {#Failure ..cannot_access}

    pattern
    {#Success pattern}))

(the .public its
  (macro (_ tokens)
    (when tokens
      (list [_ {#Name slot'}] record)
      (do meta#monad
        [slot (normal slot')
         output (..type_slot slot)
         .let [[exported? [label' type]] output]]
        (when label'
          {.#None}
          (in (list record))
          
          {.#Some [lefts right? family]}
          (do meta#monad
            ['_ (..generated_name "_")
             'output (..generated_name "")
             .let [idx (if right?
                         (is Natural (.i64_+# 1 lefts))
                         lefts)
                   pattern (|> (enumeration family)
                               (list#each (is (-> [Natural Name] (List Code))
                                              (function (_ [r_idx slot])
                                                (list (as_name slot)
                                                      (if (.i64_=# idx r_idx)
                                                        'output
                                                        '_)))))
                               list#conjoint)]]
            (in (list (` (..when (, record) [(,* pattern)] (, 'output))))))))

      (list [_ {#Tuple slots}] record)
      (meta#in (list (list#mix (is (-> Code Code Code)
                                   (function (_ slot inner)
                                     (` (..its (, slot) (, inner)))))
                               record
                               slots)))

      (list selector)
      (do meta#monad
        ['_ (..generated_name "_")
         'record (..generated_name "record")]
        (in (list (` (function ((, '_) (, 'record)) (..its (, selector) (, 'record)))))))
      
      _
      (failure ..wrong_syntax))))

(the (open_declaration imported_module alias tags my_tag_index [module proper] source type)
  (-> Text Text (List Name) Natural Name Code Type
      (Meta (List Code)))
  (do meta#monad
    [output (record_slots type)
     '_ (..generated_name "'_")
     .let ['output (as_local proper)
           pattern (|> tags
                       enumeration
                       (list#each (function (_ [tag_idx tag])
                                    (if (.i64_=# my_tag_index tag_idx)
                                      'output
                                      '_)))
                       as_tuple)
           source+ (` (..when (, source) (, pattern) (, 'output)))]]
    (when output
      {#Some [tags' members']}
      (do meta#monad
        [decls' (monad#each#meta (is (-> [Natural Name Type] (Meta (List Code)))
                                     (function (_ [sub_tag_index sname stype])
                                       (open_declaration imported_module alias tags' sub_tag_index sname source+ stype)))
                                 (enumeration (zipped_2 tags' members')))]
        (in (list#conjoint decls')))

      _
      (in (list (` (.def# (, (as_local (..module_alias (list proper imported_module) alias)))
                          (, source+)
                          #0)))))))

(the (implementation_declarations imported_module alias implementation)
  (-> Text Text Name
      (Meta (List Code)))
  (do meta#monad
    [interface (type_definition implementation)
     output (record_slots interface)]
    (when output
      {#Some [slots terms]}
      (do meta#monad
        [.let ['implementation (as_name implementation)]
         declarations (monad#each#meta (is (-> [Natural Name Type] (Meta (List Code)))
                                           (function (_ [index slot_label slot_type])
                                             (open_declaration imported_module alias slots index slot_label 'implementation slot_type)))
                                       (enumeration (zipped_2 slots terms)))]
        (in (list#conjoint declarations)))

      _
      (failure (.text_composite# "Can only 'use' implementations: " (name#as implementation)
                                 " : " (type#as interface))))))

(the (localized module global)
  (-> Text Name
      Name)
  (when global
    ["" local]
    [module local]

    _
    global))

(the .public use
  (macro (_ tokens)
    (when (..parsed (all ..andP
                         (..maybeP (all ..andP
                                        ..textP
                                        ..textP
                                        ..textP))
                         ..textP
                         (..orP (..manyP ..nameP)
                                (..manyP ..anyP)))
                    tokens)
      {.#Some [current_module,imported_module,import_alias alias implementations]}
      (let [[current_module imported_module import_alias]
            (when current_module,imported_module,import_alias
              {#Some [current_module imported_module import_alias]}
              [current_module imported_module import_alias]
              
              {#None}
              ["" "" ""])]
        (when implementations
          {#Left implementations}
          (do meta#monad
            [declarations (|> implementations
                              (list#each (localized imported_module))
                              (monad#each#meta (implementation_declarations import_alias alias)))]
            (in (list#conjoint declarations)))
          
          {#Right implementations}
          (do meta#monad
            [pre_defs,implementations (is (Meta [(List Code) (List Code)])
                                          (monad#mix meta#monad
                                                     (function (_ it [pre_defs implementations])
                                                       (when it
                                                         [_ {#Name _}]
                                                         (in [pre_defs
                                                              {#Item it implementations}])
                                                         
                                                         _
                                                         (do meta#monad
                                                           ['implementation (..generated_name "implementation")]
                                                           (in [{#Item (` (.def# (, 'implementation) (, it) #0)) pre_defs}
                                                                {#Item 'implementation implementations}]))))
                                                     [(list) (list)]
                                                     implementations))
             .let [[pre_defs implementations] pre_defs,implementations]]
            (in (|> pre_defs
                    {#Item (` (..use
                               (, (as_text current_module))
                               (, (as_text imported_module))
                               (, (as_text import_alias))
                               (, (as_text alias))
                               (,* implementations)))}
                    list#reversed)))))
      

      {.#None}
      (failure ..wrong_syntax))))

(the (imported_by? import_name module_name)
  (-> Text Text
      (Meta Bit))
  (do meta#monad
    [module (module module_name)
     .let [[..#module_hash _ ..#module_aliases _ ..#definitions _ ..#imports imports ..#module_state _] module]]
    (in (is_member? imports import_name))))

(the (referrals module_name extra)
  (-> Text (List Code)
      (Meta (List Referral)))
  (do meta#monad
    [extra,referral (when (referrals_of #0 extra)
                      {#Some extra,referral}
                      (in extra,referral)

                      {#None}
                      (failure ""))
     .let [[extra referral] extra,referral]
     current_module current_module_name]
    (when extra
      {#End}
      (in referral)

      _
      (failure (.text_composite# ..wrong_syntax
                                 \n "@ " current_module
                                 \n (|> extra
                                        (list#each code#as)
                                        (list#interposed " ")
                                        (list#mix text#composite "")))))))

(the .public refer
  (macro (_ tokens)
    (when tokens
      (list#partial [_ {#Text imported_module}] [_ {#Text alias}] options)
      (do meta#monad
        [referrals (..referrals imported_module options)
         current_module ..current_module_name]
        (in (list#each (function (_ [macro parameters])
                         (` ((, (as_name macro))
                             (, (as_text current_module))
                             (, (as_text imported_module))
                             (, (as_text alias))
                             (,* parameters))))
                       referrals)))

      _
      (failure ..wrong_syntax))))

(the .public with
  (macro (_ tokens)
    (when (..parsed (..andP ..anyP ..anyP)
                    tokens)
      {.#Some [implementation expression]}
      (meta#in (list (` (..let [(..open (, (as_text (alias_stand_in 0)))) (, implementation)]
                          (, expression)))))

      {.#None}
      (failure ..wrong_syntax))))

(the .public by
  (macro (_ tokens)
    (when tokens
      (list implementation [_ {#Name member}])
      (meta#in (list (` (..with (, implementation) (, (as_name member))))))

      (list#partial implementation member args)
      (meta#in (list (` ((..by (, implementation) (, member)) (,* args)))))
      
      _
      (failure ..wrong_syntax))))

(the .public has
  (macro (_ tokens)
    (when tokens
      (list [_ {#Name slot'}] value record)
      (do meta#monad
        [slot (normal slot')
         output (..type_slot slot)
         .let [[exported? [label' type]] output]]
        (when label'
          {.#None}
          (in (list value))

          {.#Some [lefts right? family]}
          (do meta#monad
            [pattern' (monad#each#meta (is (-> [Natural Name] (Meta [Name Natural Code]))
                                           (function (_ [r_idx r_slot_name])
                                             (do meta#monad
                                               ['slot (..generated_name "")]
                                               (in [r_slot_name r_idx 'slot]))))
                                       (enumeration family))
             .let [pattern (|> pattern'
                               (list#each (is (-> [Name Natural Code] (List Code))
                                              (function (_ [r_slot_name r_idx r_var])
                                                (list (as_name r_slot_name)
                                                      r_var))))
                               list#conjoint)
                   idx (if right?
                         (is Natural (.i64_+# 1 lefts))
                         lefts)
                   output (|> pattern'
                              (list#each (is (-> [Name Natural Code] (List Code))
                                             (function (_ [r_slot_name r_idx r_var])
                                               (list (as_name r_slot_name)
                                                     (if (.i64_=# idx r_idx)
                                                       value
                                                       r_var)))))
                              list#conjoint)]]
            (in (list (` (..when (, record) [(,* pattern)] [(,* output)])))))))

      (list [_ {#Tuple slots}] value record)
      (when slots
        {#End}
        (failure ..wrong_syntax)

        _
        (do meta#monad
          [bindings (monad#each#meta (is (-> Code (Meta Code))
                                         (function (_ _) (..generated_name "temp")))
                                     slots)
           .let [pairs (zipped_2 slots bindings)
                 update_expr (list#mix (is (-> [Code Code] Code Code)
                                           (function (_ [s b] v)
                                             (` (..has (, s) (, v) (, b)))))
                                       value
                                       (list#reversed pairs))
                 [_ accesses'] (list#mix (is (-> [Code Code] [Code (List (List Code))] [Code (List (List Code))])
                                             (function (_ [new_slot new_binding] [old_record accesses'])
                                               [(` (its (, new_slot) (, new_binding)))
                                                {#Item (list new_binding old_record) accesses'}]))
                                         [record (is (List (List Code)) {#End})]
                                         pairs)
                 accesses (list#conjoint (list#reversed accesses'))]]
          (in (list (` (let [(,* accesses)]
                         (, update_expr)))))))
      
      (list selector value)
      (do meta#monad
        ['_ (..generated_name "_")
         'record (..generated_name "record")]
        (in (list (` (function ((, '_) (, 'record))
                       (..has (, selector) (, value) (, 'record)))))))

      (list selector)
      (do meta#monad
        ['_ (..generated_name "_")
         'value (..generated_name "value")
         'record (..generated_name "record")]
        (in (list (` (function ((, '_) (, 'value) (, 'record))
                       (..has (, selector) (, 'value) (, 'record)))))))

      _
      (failure ..wrong_syntax))))

(the .public revised
  (macro (_ tokens)
    (when tokens
      (list [_ {#Name slot'}] fun record)
      (do meta#monad
        [slot (normal slot')
         output (..type_slot slot)
         .let [[exported? [label' type]] output]]
        (when label'
          {.#None}
          (in (list (` ((, fun) (, record)))))

          {.#Some [lefts right? family]}
          (do meta#monad
            [pattern' (monad#each#meta (is (-> [Natural Name] (Meta [Name Natural Code]))
                                           (function (_ [r_idx r_slot_name])
                                             (do meta#monad
                                               ['slot (..generated_name "")]
                                               (in [r_slot_name r_idx 'slot]))))
                                       (enumeration family))
             .let [pattern (|> pattern'
                               (list#each (is (-> [Name Natural Code] (List Code))
                                              (function (_ [r_slot_name r_idx r_var])
                                                (list (as_name r_slot_name)
                                                      r_var))))
                               list#conjoint)
                   idx (if right?
                         (is Natural (.i64_+# 1 lefts))
                         lefts)
                   output (|> pattern'
                              (list#each (is (-> [Name Natural Code] (List Code))
                                             (function (_ [r_slot_name r_idx r_var])
                                               (list (as_name r_slot_name)
                                                     (if (.i64_=# idx r_idx)
                                                       (` ((, fun) (, r_var)))
                                                       r_var)))))
                              list#conjoint)]]
            (in (list (` (..when (, record) [(,* pattern)] [(,* output)])))))))

      (list [_ {#Tuple slots}] fun record)
      (when slots
        {#End}
        (failure ..wrong_syntax)

        _
        (do meta#monad
          ['record (..generated_name "record")
           'temp (..generated_name "temp")]
          (in (list (` (let [(, 'record) (, record)
                             (, 'temp) (its [(,* slots)] (, 'record))]
                         (has [(,* slots)] ((, fun) (, 'temp)) (, 'record))))))))

      (list selector fun)
      (do meta#monad
        ['_ (..generated_name "_")
         'record (..generated_name "record")]
        (in (list (` (function ((, '_) (, 'record))
                       (..revised (, selector) (, fun) (, 'record)))))))

      (list selector)
      (do meta#monad
        ['_ (..generated_name "_")
         'fun (..generated_name "fun")
         'record (..generated_name "record")]
        (in (list (` (function ((, '_) (, 'fun) (, 'record))
                       (..revised (, selector) (, 'fun) (, 'record)))))))
      
      _
      (failure ..wrong_syntax))))

(the .private with_template#pattern
  (pattern
   (macro (_ tokens)
     (when tokens
       (list#partial [_ {#Form (list [_ {#Tuple bindings}]
                                     [_ {#Tuple templates}])}]
                     [_ {#Form data}]
                     branches)
       (when (is (Maybe (List Code))
                 (do maybe#monad
                   [bindings' (monad#each maybe#monad name_proper bindings)
                    data' (monad#each maybe#monad tuple_list data)]
                   (let [num_bindings (list#size bindings')]
                     (if (every? (|>> (.i64_=# num_bindings))
                                 (list#each list#size data'))
                       (let [reification (is (-> Replacement_Environment (List Code))
                                             (function (_ env) (list#each (realized_template env) templates)))]
                         (|> data'
                             (list#each (function#composite reification (replacement_environment bindings')))
                             list#conjoint
                             in))
                       {#None}))))
         {#Some output}
         (meta#in (list#composite output branches))
         
         {#None}
         (failure ..wrong_syntax))
       
       _
       (failure ..wrong_syntax)))))

(with_template [<name> <extension>]
  [(the .public <name>
     (for_any (_ of)
       (-> (I64 of)
           (I64 of)))
     (|>> (<extension> 1)))]

  [++ .i64_+#]
  [-- .i64_-#]
  )

(the (interleaved xs ys)
  (for_any (_ of)
    (-> (List of) (List of)
        (List of)))
  (when xs
    {#End}
    {#End}
    
    {#Item x xs'}
    (when ys
      {#End}
      {#End}
      
      {#Item y ys'}
      (list#partial x y (interleaved xs' ys')))))

(the (type_code type)
  (-> Type
      Code)
  (when type
    {#Nominal name params}
    (` {.#Nominal (, (as_text name)) (, (untemplated_list (list#each type_code params)))})

    (with_template#pattern [<tag>]
      [{<tag> left right}
       (` {<tag> (, (type_code left)) (, (type_code right))})])
    ([.#Sum] [.#Product]
     [.#Function]
     [.#Reification])

    (with_template#pattern [<tag>]
      [{<tag> id}
       (` {<tag> (, (as_natural id))})])
    ([.#Parameter] [.#Variable] [.#Opaque])

    (with_template#pattern [<tag>]
      [{<tag> env type}
       (let [env' (untemplated_list (list#each type_code env))]
         (` {<tag> (, env') (, (type_code type))}))])
    ([.#Universal] [.#Existential])
    
    {#Named [module name] anonymous}
    ... TODO: Generate the explicit type definition instead of using
    ... the "as_name" shortcut below.
    ... (` {.#Named [(, (as_text module)) (, (as_text name))]
    ...     (, (type_code anonymous))})
    (as_name [module name])))

(the .public loop
  (macro (_ tokens)
    (let [?params (when tokens
                    (list [_ {#Form (list name [_ {#Tuple bindings}])}] body)
                    {#Some [name bindings body]}

                    _
                    {#None})]
      (when ?params
        {#Some [name bindings body]}
        (when (pairs bindings)
          {#Some pairs}
          (let [vars (list#each product#left pairs)
                inits (list#each product#right pairs)]
            (if (every? name? inits)
              (do meta#monad
                [inits' (is (Meta (List Name))
                            (when (monad#each maybe#monad name_name inits)
                              {#Some inits'} (meta#in inits')
                              {#None}        (failure ..wrong_syntax)))
                 init_types (monad#each#meta type_definition inits')
                 expected ..expected_type]
                (meta#in (list (` ((.is# (-> (,* (list#each type_code init_types))
                                             (, (type_code expected)))
                                         (function ((, name) (,* vars))
                                           (, body)))
                                   (,* inits))))))
              (do meta#monad
                [aliases (monad#each#meta (is (-> Code (Meta Code))
                                              (function (_ _) (..generated_name "")))
                                          inits)]
                (meta#in (list (` (..let [(,* (..interleaved aliases inits))]
                                    (..loop ((, name) [(,* (..interleaved vars aliases))])
                                      (, body)))))))))

          {#None}
          (failure ..wrong_syntax))

        {#None}
        (failure ..wrong_syntax)))))

(the expansion#let
  (let [expansion#let' (is (-> Text (List Code) Code
                               (List Code))
                           (function (expansion#let' label tokens target)
                             (when target
                               (pattern#or [_ {#Bit _}]
                                           [_ {#Natural _}]
                                           [_ {#Integer _}]
                                           [_ {#Revolution _}]
                                           [_ {#Decimal _}]
                                           [_ {#Text _}])
                               (list target)

                               [_ {#Name [module name]}]
                               (if (and (text#= "" module)
                                        (text#= label name))
                                 tokens
                                 (list target))

                               (with_template#pattern [<tag>]
                                 [[provenance {<tag> elems}]
                                  (list [provenance {<tag> (list#conjoint (list#each (expansion#let' label tokens) elems))}])])
                               ([#Form]
                                [#Variant]
                                [#Tuple]))))]
    (macro (_ tokens)
      (when (parsed (andP (tupleP (someP bindingP)) anyP)
                    tokens)
        {#Some [bindings body]}
        (loop (again [bindings bindings
                      map (is (Property_List (List Code))
                              (list))])
          (let [normal (is (-> Code (List Code))
                           (function (_ it)
                             (list#mix (function (_ [binding expansion] it)
                                         (list#conjoint (list#each (expansion#let' binding expansion) it)))
                                       (list it)
                                       map)))]
            (when bindings
              {#Item [var_name expr] &rest}
              (do meta#monad
                [expansion (when (normal expr)
                             (list expr)
                             (single_expansion expr)

                             _
                             (failure (.text_composite# "Incorrect expansion in expansion#let"
                                                        " | Binding: " (text#as var_name)
                                                        " | Expression: " (code#as expr))))]
                (again &rest (property#with var_name expansion map)))
              
              {#End}
              (meta#in (normal body)))))
        
        {#None}
        (failure ..wrong_syntax)))))

(the .public (same? expected actual)
  (for_any (_ of)
    (-> of of
        Bit))
  (.is?# expected actual))

(the .public as_expected
  (macro (_ tokens)
    (when tokens
      (list expr)
      (do meta#monad
        [type ..expected_type]
        (in (list (` (.as# (, (type_code type))
                           (, expr))))))
      
      _
      (failure ..wrong_syntax))))

(the .public undefined
  (let [provenance (is (Meta Provenance)
                       (function (_ compiler)
                         {#Right [compiler (its #provenance compiler)]}))]
    (macro (_ tokens)
      (when tokens
        {#End}
        (do meta#monad
          [provenance provenance
           .let [message (.text_composite# "Undefined behavior at " (provenance#as provenance))]]
          (exec
            (.log!# (.text_composite# "WARNING: " message))
            (in (list (` (..halt! (, (as_text message))))))))
        
        _
        (failure ..wrong_syntax)))))

(the .public type_of
  (macro (_ tokens)
    (when tokens
      (list [_ {#Name ["" local]}])
      (do meta#monad
        [type (type_definition ["" local])]
        (in (list (` (..type ((,' ,) (, (type_code type))))))))

      (list [_ {#Name global}])
      (do meta#monad
        [type (type_definition global)
         .let [as_name (is (-> Name Code)
                           (function (_ [module name])
                             (as_tuple (list (as_text module) (as_text name)))))]]
        (in (list (` (..type ((,' ,) {..#Reification {..#Named (, (as_name global))
                                                               (, (type_code type))}
                                                     {..#Named (, (as_name (name ..type_of)))
                                                               (, (type_code ..Nothing))}}))))))

      (list expression)
      (do meta#monad
        ['temp (..generated_name "'temp")]
        (in (list (` (let [(, 'temp) (, expression)]
                       (..type_of (, 'temp)))))))

      _
      (failure ..wrong_syntax))))

(the template#macro
  (let [templateP (is (-> (List Code)
                          (Maybe [Text (List Text) (List Code)]))
                      (function (_ tokens)
                        (do maybe#monad
                          [% (local_declarationP tokens)
                           .let [[tokens [name parameters]] %]
                           % (tupleP (someP anyP) tokens)
                           .let [[tokens templates] %]
                           _ (endP tokens)]
                          (in [name parameters templates]))))
        simple_replacement_environment (is (-> (List Text)
                                               Replacement_Environment)
                                           (list#each (function (_ arg)
                                                        [arg (` ((,' ,) (, (as_local arg))))])))
        instantiated_template (is (-> Replacement_Environment Code
                                      Code)
                                  (function (_ replacement_environment template)
                                    (` (`' (, (with_replacements replacement_environment
                                                template))))))]
    (macro (_ tokens)
      (when (templateP tokens)
        {#Some [name args input_templates]}
        (do meta#monad
          ['tokens (..generated_name "tokens")
           'compiler (..generated_name "compiler")
           '_ (..generated_name "_")
           this_module ..current_module_name]
          (in (list (` (..macro ((, (as_local name)) (, 'tokens) (, 'compiler))
                         (when (, 'tokens)
                           (list (,* (list#each as_local args)))
                           {.#Right [(, 'compiler)
                                     (list (,* (list#each (instantiated_template (simple_replacement_environment args))
                                                          input_templates)))]}
                           
                           (, '_)
                           {.#Left ..wrong_syntax}))))))

        {#None}
        (failure ..wrong_syntax)))))

(with_template [<name> <to>]
  [(the .public <name>
     (template#macro
      (<name> it)
      [(..|> it
             (..is (..I64 ..Any))
             (..as <to>))]))]

  [i64 ..I64]
  [natural ..Natural]
  [integer ..Integer]
  [revolution ..Revolution]
  )

(the .public these
  (macro (_ tokens compiler)
    {#Right [compiler tokens]}))

(the .public character
  (macro (_ tokens compiler)
    (when tokens
      (list [_ {#Text input}])
      (if (|> input .text_size# (.i64_=# 1))
        (|> input (.text_char# 0)
            as_natural list
            [compiler] {#Right})
        {#Left ..wrong_syntax})

      _
      {#Left ..wrong_syntax})))

(the (eval type code)
  (-> Type Code
      (Meta Any))
  (do meta#monad
    [eval (function (_ lux)
            {#Right [lux (its #eval lux)]})]
    (eval type code)))

(every .public Target
  Text)

(with_template [<name> <value>]
  [(the .public <name>
     Target
     <value>)]
  
  ... Available.
  [js          "JavaScript"]
  [jvm         "JVM"]
  [lua         "Lua"]
  [python      "Python"]
  [ruby        "Ruby"]
  ... Not available yet.
  [c++         "C++"]
  [common_lisp "Common Lisp"]
  [php         "PHP"]
  [r           "R"]
  )

(the .public for
  (let [target (is (Meta Target)
                   (function (_ compiler)
                     {#Right [compiler (its [#info #target] compiler)]}))
        platform_name (is (-> Code
                              (Meta Target))
                          (function (_ it)
                            (do meta#monad
                              [it (eval Target it)]
                              (in (as Target it)))))
        target_pick (is (-> Text (List [Code Code]) (Maybe Code)
                            (Meta (List Code)))
                        (function (target_pick target options default)
                          (when options
                            {#End}
                            (when default
                              {#None}
                              (failure (.text_composite# "No code for target platform: " target))

                              {#Some default}
                              (meta#in (list default)))

                            {#Item [key pick] options'}
                            (do meta#monad
                              [platform (platform_name key)]
                              (if (text#= target platform)
                                (meta#in (list pick))
                                (target_pick target options' default))))))]
    (macro (_ tokens)
      (when (..parsed (..andP (..someP (..andP ..anyP ..anyP))
                              (..maybeP ..anyP))
                      tokens)
        {.#Some [options default]}
        (do meta#monad
          [target target]
          (target_pick target options default))

        {.#None}
        (failure ..wrong_syntax)))))

(the .public using
  (let [refer_code (is (-> Text Text (List Referral)
                           Code)
                       (function (_ imported_module alias referrals)
                         (` (..refer (, (as_text imported_module))
                                     (, (as_text alias))
                                     (,* (list#each (function (_ [macro parameters])
                                                      (` ((, (as_name macro)) (,* parameters))))
                                                    referrals))))))]
    (macro (_ _imports)
      (do meta#monad
        [current_module ..current_module_name
         imports (imports_of #0 current_module {#End} _imports)
         .let [=imports (|> imports
                            (list#each (is (-> Importation
                                               Code)
                                           (function (_ [module_name m_alias =refer])
                                             (` [(, (as_text module_name)) (, (as_text (..maybe#else "" m_alias)))]))))
                            as_tuple)
               =refers (list#each (is (-> Importation
                                          Code)
                                      (function (_ [module_name m_alias =refer])
                                        (refer_code module_name (..maybe#else "" m_alias) =refer)))
                                  imports)
               =module (` (.module# (, =imports)))]
         '_ (..generated_name "")]
        (in {#Item =module =refers})))))

(every Immediate_UnQuote
  (Nominal "#Macro/Immediate_UnQuote"))

(the immediate_unquote
  (-> Macro Immediate_UnQuote)
  (|>> (as Immediate_UnQuote)))

(the immediate_unquote_macro
  (-> Immediate_UnQuote Macro')
  (|>> (as Macro')))

(the .public ,,
  (..immediate_unquote
   (macro (_ it)
     (when it
       (list it)
       (meta#in (list it))

       _
       (failure ..wrong_syntax)))))

(the aggregate_embedded_expansions
  (template#macro
   (_ embedded_expansions <@> <tag> <*>)
   [(do meta#monad
      [<*>' (monad#each#meta embedded_expansions <*>)]
      (in [(|> <*>'
               list#reversed
               (list#each product#left)
               (list#mix list#composite (list)))
           [<@> {<tag> (list#each product#right <*>')}]]))]))

(the (embedded_expansions code)
  (-> Code (Meta [(List Code) Code]))
  (when code
    [@ {#Form (list#partial [@name {#Name original_name}] parameters)}]
    (<| (expansion#let [<failure> (aggregate_embedded_expansions embedded_expansions @ #Form (list#partial [@name {#Name original_name}] parameters))])
        (do meta#monad
          [resolved_name (..normal original_name)
           ?resolved_name (meta#try (..global_name resolved_name))]
          (when ?resolved_name
            {#Left _}
            <failure>

            {#Right resolved_name}
            (do meta#monad
              [?type,value (meta#try (..definition_value resolved_name))]
              (when ?type,value
                {#Left _}
                <failure>

                {#Right [type value]}
                (if (type#= ..Immediate_UnQuote type)
                  (do meta#monad
                    [bound ((immediate_unquote_macro (as Immediate_UnQuote value)) parameters)
                     'expansion (..generated_name "'expansion")]
                    (in [{#Item 'expansion bound}
                         'expansion]))
                  <failure>))))))

    (with_template#pattern [<tag>]
      [[@ {<tag> parts}]
       (aggregate_embedded_expansions embedded_expansions @ <tag> parts)])
    ([#Form]
     [#Variant]
     [#Tuple])
    
    _
    (meta#in [(list) code])))

(the .public ``
  (macro (_ tokens)
    (when tokens
      (list raw)
      (do meta#monad
        [=raw (..embedded_expansions raw)
         .let [[labels labelled] =raw]]
        ((as Macro' ..expansion#let)
         (list (as_tuple labels)
               labelled)))

      _
      (failure ..wrong_syntax))))

(with_template [<bit> <name>]
  [(the .public <name> Bit <bit>)]

  [#0 false]
  [#1 true]
  )

(the .public try
  (macro (_ tokens)
    (when tokens
      (list expression)
      (do meta#monad
        ['_ (..generated_name "'_")]
        (in (list (` (.try# (.function ((, '_) (, '_))
                              (, expression)))))))

      _
      (..failure ..wrong_syntax))))

(the (methodP tokens)
  (-> (List Code)
      (Maybe [(List Code) [Text Code]]))
  (when tokens
    (list#partial [_ {#Form (list [_ {#Name [..prelude "is#"]}]
                                  type
                                  [_ {#Name ["" name]}])}]
                  tokens')
    {#Some [tokens' [name type]]}

    _
    {#None}))

... [Scrap your type classes](https://www.haskellforall.com/2012/05/scrap-your-type-classes.html)
(the .public Interface
  (macro (_ tokens)
    (do meta#monad
      [methods' (monad#each#meta complete_expansion tokens)]
      (when (everyP methodP (list#conjoint methods'))
        {#Some methods}
        (in (list (` (..Tuple (,* (list#each product#right methods))))
                  (as_tuple (list#each (|>> product#left as_text) methods))))

        {#None}
        (failure ..wrong_syntax)))))

(the .public Rec
  (let [recursive_type (is (-> Code Code Text Code
                               Code)
                           (function (recursive_type 'self 'dummy name body)
                             (` {.#Reification (..Nominal "")
                                               (.for_any ((, 'self) (, 'dummy))
                                                 (, (as_let (as_local name) (` {.#Reification (..Nominal "") (, 'self)})
                                                            body)))})))]
    (macro (_ tokens)
      (when tokens
        (list [_ {#Name "" name}] body)
        (do meta#monad
          [body' (complete_expansion body)
           'self (generated_name "'self")
           'dummy (generated_name "'dummy")]
          (when body'
            (list body' labels)
            (in (list (recursive_type 'self 'dummy name body')
                      labels))

            (list body')
            (in (list (recursive_type 'self 'dummy name body')))

            _
            (failure ..wrong_syntax)))

        _
        (failure ..wrong_syntax)))))

(with_template [<type>]
  [(the .public <type>
     Type
     (let [[_ proper] (name <type>)]
       {#Named [..prelude proper]
               {.#Nominal (.text_composite# "#Extension/" proper) (list)}}))]

  [Analysis]
  [Synthesis]
  [Translation]
  [Declaration]
  )

(the .public alias
  (macro (_ tokens)
    (when (parsed (andP (tupleP (manyP localP))
                        nameP)
                  tokens)
      {#Some [alias/+ original]}
      (meta#in (list#each (function (_ it)
                            (` (the .public (, (as_local it))
                                 (, (as_name original)))))
                          alias/+))
      
      {#None}
      (failure ..wrong_syntax))))

(alias [F64 Double]
       ..Decimal)

(alias [alias? identical?]
       ..same?)

(the .public variant
  (macro (_ it)
    (meta#in (list (` {(,* it)})))))

(alias [|]
       ..variant)

(the .public tuple
  (macro (_ it)
    (meta#in (list (` [(,* it)])))))

(alias [& record]
       ..tuple)

(the .public unless
  (template#macro
   (_ ,when ,else ,then)
   [(..if ,when ,then ,else)]))

(alias [for_all for_every]
       ..for_any)

(alias [there_exists]
       ..for_some)

... https://en.wikipedia.org/wiki/Up_tack
(alias [False Falsum]
       ..Nothing)

... https://en.wikipedia.org/wiki/Tee_(symbol)
(alias [True Verum]
       ..Any)

(the .public <-
  (macro (_ parameters)
    (meta#in (list (` (.-> (,* parameters)))))))

... https://en.wikipedia.org/wiki/Number#Classification
(alias [N Nat]
       ..Natural)

(alias [Z Int]
       ..Integer)

(alias [Rev]
       ..Revolution)

(the .public match?
  (macro (_ tokens)
    (when (parsed (andP anyP
                        anyP)
                  tokens)
      {#Some [pattern term]}
      (do meta#monad
        ['else (generated_name "'else")]
        (in (list (` (..when (, term)
                       (, pattern)
                       ..true

                       (, 'else)
                       ..false)))))
      
      {#None}
      (failure ..wrong_syntax))))

(every .public (Can ability)
  (for_some (_ entity)
    [entity
     (ability entity)]))

(the .public (can ability entity)
  (for_any (_ ability of)
    (-> (ability of) of
        (Can ability of)))
  [entity ability])

(the .public (ability [entity ability])
  (for_any (_ ability of)
    (-> (Can ability of)
        (ability of)))
  ability)

(the .public (entity [entity ability])
  (for_any (_ ability of)
    (-> (Can ability of)
        of))
  entity)
