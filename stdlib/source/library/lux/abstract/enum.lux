... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)]]
 [//
  ["[0]" order (.only Order)]])

(every .public (Enum of)
  (Interface
   (is (Order of)
       order)
   (is (-> of of)
       succ)
   (is (-> of of)
       pred)))

(the .public (range enum from to)
  (for_any (_ of)
    (-> (Enum of) of of
        (List of)))
  (let [(open "/#[0]") enum]
    (loop (again [end to
                  output (`` (is (List (,, (type_of from)))
                                 {.#End}))])
      (cond (/#< end from)
            (again (/#pred end) {.#Item end output})

            (/#< from end)
            (again (/#succ end) {.#Item end output})

            ... (= end from)
            {.#Item end output}))))
