... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)]]
 [//
  ["[0]" order (.only Order)]])

(every .public (Enum it)
  (Interface
   (is (Order it)
       order)
   (is (-> it it)
       succ)
   (is (-> it it)
       pred)))

(the .public (range enum from to)
  (for_any (_ it)
    (-> (Enum it) it it
        (List it)))
  (let [(open "/:[0]") enum]
    (if (/:= from to)
        (list from)
        (loop (of [end to
                   output (`` (is (List (,, (type_of from)))
                                  (list)))])
          (if (/:< end from)
              (of (/:pred end) (.list_composite# (list end) output))

              (/:< from end)
              (of (/:succ end) (.list_composite# (list end) output))

              ... (/:= end from)
              (.list_composite# (list end) output))))))
