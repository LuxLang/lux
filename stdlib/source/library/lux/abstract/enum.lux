(.module:
  [library
   [lux #*]]
  [//
   ["." order (#+ Order)]])

(interface: #export (Enum e)
  {#.doc "Enumerable types, with a notion of moving forward and backwards through a type's instances."}
  (: (Order e) &order)
  (: (-> e e) succ)
  (: (-> e e) pred))

(def: #export (range enum from to)
  {#.doc "An inclusive [from, to] range of values."}
  (All [a] (-> (Enum a) a a (List a)))
  (let [(^open "/\.") enum]
    (loop [end to
           output #.Nil]
      (cond (/\< end from)
            (recur (/\pred end) (#.Cons end output))

            (/\< from end)
            (recur (/\succ end) (#.Cons end output))

            ## (/\= end from)
            (#.Cons end output)))))
