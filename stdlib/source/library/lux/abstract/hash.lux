... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)]]
 [//
  ["[0]" equivalence (.only Equivalence)]
  [functor
   ["[0]" contravariant]]])

(every .public (Hash of)
  (Interface
   (is (Equivalence of)
       equivalence)
   (is (-> of
           Natural)
       hash)))

(the .public functor
  (contravariant.Functor Hash)
  (implementation
   (the (each f super)
     (implementation
      (the equivalence
        (of equivalence.functor each f
            (of super equivalence)))
      
      (the hash
        (|>> f (of super hash)))))))
