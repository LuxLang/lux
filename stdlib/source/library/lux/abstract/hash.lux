... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)]]
 [//
  ["[0]" equivalence (.only Equivalence)]
  [functor
   ["[0]" contravariant]]])

(type .public (Hash a)
  (Interface
   (is (Equivalence a)
       equivalence)
   (is (-> a Nat)
       hash)))

(def .public functor
  (contravariant.Functor Hash)
  (implementation
   (def (each f super)
     (implementation
      (def equivalence
        (of equivalence.functor each f
            (of super equivalence)))
      
      (def hash
        (|>> f (of super hash)))))))
