... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)]]
 [//
  [functor
   ["[0]" contravariant]]])

(every .public (Equivalence of)
  (Interface
   (is (-> of of
           Bit)
       =)))

(def .public (rec sub)
  (All (_ of)
    (-> (-> (Equivalence of) (Equivalence of))
        (Equivalence of)))
  (implementation
   (def (= left right)
     (sub = left right))))

(def .public functor
  (contravariant.Functor Equivalence)
  (implementation
   (def (each value it)
     (implementation
      (def (= reference sample)
        (of it = (value reference) (value sample)))))))
