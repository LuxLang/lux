... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)]]
 [///
  [functor (.only Functor)]
  [apply (.only Apply)]
  [monad (.only Monad)]])

(every .public (Free ! of)
  (Variant
   {#Pure of}
   {#Impure (! (Free ! of))}))

(the .public (functor dsl)
  (for_any (_ !)
    (-> (Functor !)
        (Functor (Free !))))
  (implementation
   (the (each $ !it)
     (when !it
       {#Pure it}
       {#Pure ($ it)}
       
       {#Impure value}
       {#Impure (of dsl each (each $) value)}))))

(the .public (apply dsl)
  (for_any (_ !)
    (-> (Functor !)
        (Apply (Free !))))
  (implementation
   (the functor
     (..functor dsl))

   (the (on !it !$)
     (when [!$ !it]
       [{#Pure $} {#Pure it}]
       {#Pure ($ it)}

       [{#Pure $} {#Impure !it}]
       {#Impure (of dsl each
                    (of (..functor dsl) each $)
                    !it)}

       [{#Impure !$} _]
       {#Impure (of dsl each (on !it) !$)}
       ))))

(the .public (monad dsl)
  (for_any (_ !)
    (-> (Functor !)
        (Monad (Free !))))
  (implementation
   (the functor (..functor dsl))

   (the in
     (|>> {#Pure}))

   (the (conjoint !!it)
     (when !!it
       {#Pure !it}
       !it
       
       {#Impure !it}
       {#Impure (of dsl each
                    (of (monad dsl) conjoint)
                    !it)}
       ))))
