(.require
 [library
  [lux (.except)]]
 [///
  [functor (.only Functor)]
  [apply (.only Apply)]
  [monad (.only Monad)]])

(type: .public (Free F a)
  (Variant
   {#Pure a}
   {#Effect (F (Free F a))}))

(def .public (functor dsl)
  (All (_ F) (-> (Functor F) (Functor (Free F))))
  (implementation
   (def (each f ea)
     (case ea
       {#Pure a}
       {#Pure (f a)}
       
       {#Effect value}
       {#Effect (at dsl each (each f) value)}))))

(def .public (apply dsl)
  (All (_ F) (-> (Functor F) (Apply (Free F))))
  (implementation
   (def functor
     (..functor dsl))

   (def (on ea ef)
     (case [ef ea]
       [{#Pure f} {#Pure a}]
       {#Pure (f a)}

       [{#Pure f} {#Effect fa}]
       {#Effect (at dsl each
                    (at (..functor dsl) each f)
                    fa)}

       [{#Effect ff} _]
       {#Effect (at dsl each (on ea) ff)}
       ))))

(def .public (monad dsl)
  (All (_ F) (-> (Functor F) (Monad (Free F))))
  (implementation
   (def functor (..functor dsl))

   (def (in a)
     {#Pure a})

   (def (conjoint efefa)
     (case efefa
       {#Pure efa}
       (case efa
         {#Pure a}
         {#Pure a}

         {#Effect fa}
         {#Effect fa})
       
       {#Effect fefa}
       {#Effect (at dsl each
                    (at (monad dsl) conjoint)
                    fefa)}
       ))))
