... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)]]
 [///
  [functor (.only Functor)]
  [apply (.only Apply)]
  [monad (.only Monad)]])

(every .public (Free ! of)
  (Variant
   {#Pure of}
   {#Impure (! (Free ! of))}))

(the .public (functor dsl)
  (All (_ !)
    (-> (Functor !)
        (Functor (Free !))))
  (implementation
   (the (each f ea)
     (when ea
       {#Pure a}
       {#Pure (f a)}
       
       {#Impure value}
       {#Impure (of dsl each (each f) value)}))))

(the .public (apply dsl)
  (All (_ !)
    (-> (Functor !)
        (Apply (Free !))))
  (implementation
   (the functor
     (..functor dsl))

   (the (on ea ef)
     (when [ef ea]
       [{#Pure f} {#Pure a}]
       {#Pure (f a)}

       [{#Pure f} {#Impure fa}]
       {#Impure (of dsl each
                    (of (..functor dsl) each f)
                    fa)}

       [{#Impure ff} _]
       {#Impure (of dsl each (on ea) ff)}
       ))))

(the .public (monad dsl)
  (All (_ !)
    (-> (Functor !)
        (Monad (Free !))))
  (implementation
   (the functor (..functor dsl))

   (the (in a)
     {#Pure a})

   (the (conjoint efefa)
     (when efefa
       {#Pure efa}
       (when efa
         {#Pure a}
         {#Pure a}

         {#Impure fa}
         {#Impure fa})
       
       {#Impure fefa}
       {#Impure (of dsl each
                    (of (monad dsl) conjoint)
                    fefa)}
       ))))
