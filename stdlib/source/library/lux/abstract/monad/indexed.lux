... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except global)
   [abstract
    ["?" projection]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix)
      ["?[1]" \\projection]]
     ["[0]" stack (.use "[1]#[0]" functor mix)]]]
   ["[0]" macro (.only)
    ["[0]" syntax]]
   [meta
    ["[0]" code (.only)
     ["?[1]" \\projection (.only Projection)]]]]]
 ["[0]" //])

(every .public (Monad !)
  (Interface
   (is (for_any (_ condition value)
         (-> value
             (! condition condition value)))
       in)

   (is (for_any (_ pre interim post input output)
         (-> (-> input
                 (! interim post output))
             (-> (! pre interim input)
                 (! pre post output))))
       then)))

(every Binding
  [Code Code])

(the binding
  (Projection Binding)
  (?.and ?list.any
         ?list.any))

(every Context
  (Variant
   {#Macro Name Code}
   {#Binding Binding}))

(the global
  (Projection Name)
  (//.do ?.monad
    [[module proper] ?code.name
     _ (?.assertion "" (when module "" false _ true))]
    (in [module proper])))

(the context
  (Projection Context)
  (?.or (?.and ..global
               ?list.any)
        binding))

(the (pair_stack [binding value])
  (for_any (_ it)
    (-> [it it]
        (Stack it)))
  (stack binding value))

(the named_monad
  (Projection [(Maybe Text) Code])
  (?.either (?code.tuple (?.and (by ?.monad each (|>> {.#Some})
                                    ?code.local)
                                ?list.any))
            (by ?.monad each (|>> [{.#None}])
                ?list.any)))

(the .public do
  (syntax.macro (_ [[?name monad] ..named_monad
                    context (?code.tuple (?.some context))
                    expression ?list.any])
    (macro.with_names ['_ 'then]
      (let [body (list#mix (function (_ context next)
                             (when context
                               {#Macro macro parameter}
                               (` ((, (code.name macro))
                                   (, parameter)
                                   (, next)))
                               
                               {#Binding [binding value]}
                               (` ((, 'then)
                                   (.function ((, '_) (, binding))
                                     (, next))
                                   (, value)))))
                           expression
                           (list.reversed context))]
        (in (list (when ?name
                    {.#Some name}
                    (let [name (code.local name)]
                      (` (let [(, name) (, monad)
                               [..in (,' in) ..then (, 'then)] (, name)]
                           (, body))))

                    {.#None}
                    (` (let [[..in (,' in) ..then (, 'then)] (, monad)]
                         (, body))))))))))
