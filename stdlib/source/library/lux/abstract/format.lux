... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... https://en.wikipedia.org/wiki/Content_format
(.require
 [library
  [lux (.except)
   [control
    ["[0]" try (.only Try)]]]]
 [//
  [monad (.only do)]
  ["[0]" functor]])

(every .public (Format super sub)
  (Interface
   (is (-> sub
           super)
       injection)
   (is (-> super
           (Try sub))
       projection)))

(the .public (composite format_21 format_10)
  (for_any (_ @0 @1 @2)
    (-> (Format @2 @1) (Format @1 @0)
        (Format @2 @0)))
  (implementation
   (the injection
     (|>> (of format_10 injection)
          (of format_21 injection)))

   (the (projection cy)
     (do try.monad
       [by (of format_21 projection cy)]
       (of format_10 projection by)))))
