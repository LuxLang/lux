(.module:
  [library
   [lux (#- or and)
    [control
     ["." function]]]]
  [//
   [monoid (#+ Monoid)]
   [functor
    ["." contravariant]]])

(type: .public (Predicate a)
  {#.doc (doc "A question that can be asked of a value, yield either false (#0) or true (#1).")}
  (-> a Bit))

(template [<identity_name> <identity_value> <composition_name> <composition>
           <identity_doc> <composition_doc>]
  [(def: .public <identity_name>
     {#.doc <identity_doc>}
     Predicate
     (function.constant <identity_value>))

   (def: .public (<composition_name> left right)
     {#.doc <composition_doc>}
     (All [a] (-> (Predicate a) (Predicate a) (Predicate a)))
     (function (_ value)
       (<composition> (left value)
                      (right value))))]

  [none #0 or .or
   (doc "A predicate that always fails.")
   (doc "A predicate that meets either predecessor.")]
  [all  #1 and .and
   (doc "A predicate that always succeeds.")
   (doc "A predicate that meets both predecessors.")]
  )

(template [<name> <identity> <composition>]
  [(implementation: .public <name>
     (All [a] (Monoid (Predicate a)))
     
     (def: identity <identity>)
     (def: compose <composition>))]

  [union        ..none ..or]
  [intersection ..all  ..and]
  )

(def: .public (complement predicate)
  {#.doc (doc "The opposite of a predicate.")}
  (All [a] (-> (Predicate a) (Predicate a)))
  (|>> predicate not))

(def: .public (difference sub base)
  {#.doc (doc "A predicate that meeds 'base', but not 'sub'.")}
  (All [a] (-> (Predicate a) (Predicate a) (Predicate a)))
  (function (_ value)
    (.and (base value)
          (not (sub value)))))

(def: .public (rec predicate)
  {#.doc (doc "Ties the knot for a recursive predicate.")}
  (All [a]
    (-> (-> (Predicate a) (Predicate a))
        (Predicate a)))
  (function (recur input)
    (predicate recur input)))

(implementation: .public functor
  (contravariant.Functor Predicate)
  
  (def: (map f fb)
    (|>> f fb)))
