... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)]]
 [// (.only CoMonad)
  [//
   [functor (.only Functor)]]])

(every .public (Free ! of)
  [of (! (Free ! of))])

(the .public (functor dsl)
  (All (_ !)
    (-> (Functor !)
        (Functor (Free !))))
  (implementation
   (the (each $ [head tail])
     [($ head) (of dsl each (each $) tail)])))

(the .public (comonad dsl)
  (All (_ !)
    (-> (Functor !)
        (CoMonad (Free !))))
  (implementation
   (the functor (..functor dsl))

   (the (out [head tail])
     head)

   (the (disjoint [head tail])
     [[head tail]
      (of dsl each disjoint tail)])))
