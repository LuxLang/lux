... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)]]
 [// (.only CoMonad)
  [//
   [functor (.only Functor)]]])

(every .public (Free F a)
  [a (F (Free F a))])

(def .public (functor dsl)
  (All (_ F) (-> (Functor F) (Functor (Free F))))
  (implementation
   (def (each f [head tail])
     [(f head) (of dsl each (each f) tail)])))

(def .public (comonad dsl)
  (All (_ F) (-> (Functor F) (CoMonad (Free F))))
  (implementation
   (def functor (..functor dsl))

   (def (out [head tail])
     head)

   (def (disjoint [head tail])
     [[head tail]
      (of dsl each disjoint tail)])))
