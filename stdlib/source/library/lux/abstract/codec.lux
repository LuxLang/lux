... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [control
    ["[0]" try (.only Try)]]]]
 [//
  [monad (.only do)]
  ["[0]" functor]])

(every .public (Codec m a)
  (Interface
   (is (-> a m)
       encoded)
   (is (-> m (Try a))
       decoded)))

(the .public (composite cb_codec ba_codec)
  (All (_ a b c)
    (-> (Codec c b) (Codec b a)
        (Codec c a)))
  (implementation
   (the encoded
     (|>> (of ba_codec encoded)
          (of cb_codec encoded)))

   (the (decoded cy)
     (do try.monad
       [by (of cb_codec decoded cy)]
       (of ba_codec decoded by)))))
