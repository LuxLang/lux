(.module:
  [library
   [lux #*
    [control
     ["." try (#+ Try)]]]]
  [//
   [monad (#+ do)]
   ["." functor]])

(interface: .public (Codec m a)
  (: (-> a m)
     encode)
  (: (-> m (Try a))
     decode))

(implementation: .public (composite cb_codec ba_codec)
  (All [a b c]
    (-> (Codec c b) (Codec b a)
        (Codec c a)))
  (def: encode
    (|>> (\ ba_codec encode)
         (\ cb_codec encode)))

  (def: (decode cy)
    (do try.monad
      [by (\ cb_codec decode cy)]
      (\ ba_codec decode by))))
