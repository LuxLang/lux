... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [control
    ["[0]" try (.only Try)]]]]
 [//
  [monad (.only do)]
  ["[0]" functor]])

(every .public (Codec super sub)
  (Interface
   (is (-> sub
           super)
       encoded)
   (is (-> super
           (Try sub))
       decoded)))

(the .public (composite codec_21 codec_10)
  (All (_ @0 @1 @2)
    (-> (Codec @2 @1) (Codec @1 @0)
        (Codec @2 @0)))
  (implementation
   (the encoded
     (|>> (of codec_10 encoded)
          (of codec_21 encoded)))

   (the (decoded cy)
     (do try.monad
       [by (of codec_21 decoded cy)]
       (of codec_10 decoded by)))))
