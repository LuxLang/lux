... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [control
    ["[0]" try (.only Try)]]]]
 [//
  [monad (.only do)]
  ["[0]" functor]])

(every .public (Codec super sub)
  (Interface
   (is (-> sub
           super)
       injection)
   (is (-> super
           (Try sub))
       projection)))

(the .public (composite codec_21 codec_10)
  (for_any (_ @0 @1 @2)
    (-> (Codec @2 @1) (Codec @1 @0)
        (Codec @2 @0)))
  (implementation
   (the injection
     (|>> (of codec_10 injection)
          (of codec_21 injection)))

   (the (projection cy)
     (do try.monad
       [by (of codec_21 projection cy)]
       (of codec_10 projection by)))))
