... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)]]
 [//
  [monad (.only Monad
                do)]
  ["[0]" functor (.only Functor)]])

(every .public (Apply !)
  (Interface
   (is (Functor !)
       functor)
   (is (for_any (_ @0 @1)
         (-> (! @0) (! (-> @0 @1))
             (! @1)))
       on)))

(the .public (composite outer_monad outer_apply inner_apply)
  (for_any (_ outer inner)
    (-> (Monad outer) (Apply outer) (Apply inner)
        ... TODO: Replace (for_any (_ of) (outer (inner of))) with (functor.Then outer inner)
        (Apply (for_any (_ of) (outer (inner of))))))
  (implementation
   (the functor
     (functor.composite (its ..functor outer_apply)
                        (its ..functor inner_apply)))
   (the (on oix oif)
     ... TODO: Switch from this version to the one below (in comments) ASAP.
     (let [oif' (by outer_apply on
                    oif
                    (by outer_monad in (function (_ if ix) (by inner_apply on ix if))))]
       (by outer_apply on oix oif'))
     ... (let [outer_apply (by outer_apply on)
     ...       inner_apply (by inner_apply on)]
     ...   (all outer_apply
     ...       oif
     ...       (by outer_monad in inner_apply)
     ...       oix))
     )))
