... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [meta
    [compiler
     ["@" target]]]]]
 [//
  [monad (.only Monad do)]
  ["[0]" functor (.only Functor)]])

(every .public (Apply f)
  (Interface
   (is (Functor f)
       functor)
   (is (All (_ a b)
         (-> (f a) (f (-> a b)) (f b)))
       on)))

(the .public (composite f_monad f_apply g_apply)
  (All (_ F G)
    (-> (Monad F) (Apply F) (Apply G)
        ... TODO: Replace (All (_ a) (F (G a))) with (functor.Then F G)
        (Apply (All (_ a) (F (G a))))))
  (implementation
   (the functor
     (functor.composite (its ..functor f_apply)
                        (its ..functor g_apply)))
   (the (on fgx fgf)
     ... TODO: Switch from this version to the one below (in comments) ASAP.
     (for @.old (let [fgf' (of f_apply on
                               fgf
                               (of f_monad in (function (_ gf gx) (of g_apply on gx gf))))]
                  (as_expected (of f_apply on (as_expected fgx) (as_expected fgf'))))
          (let [fgf' (of f_apply on
                         fgf
                         (of f_monad in (function (_ gf gx) (of g_apply on gx gf))))]
            (of f_apply on fgx fgf')))
     ... (let [applyF (of f_apply on)
     ...       applyG (of g_apply on)]
     ...   (all applyF
     ...       fgf
     ...       (of f_monad in applyG)
     ...       fgx))
     )))
