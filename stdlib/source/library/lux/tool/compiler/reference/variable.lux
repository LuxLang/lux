(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [control
    ["[0]" pipe]]
   [data
    [text
     ["%" \\format (.only Format)]]]
   [macro
    ["^" pattern]]
   [math
    [number
     ["n" nat]
     ["i" int]]]]])

(type: .public Register
  Nat)

(type: .public Variable
  (Variant
   {#Local Register}
   {#Foreign Register}))

(def .public equivalence
  (Equivalence Variable)
  (implementation
   (def (= reference sample)
     (case [reference sample]
       (^.with_template [<tag>]
         [[{<tag> reference'} {<tag> sample'}]
          (n.= reference' sample')])
       ([#Local] [#Foreign])

       _
       #0))))

(def .public hash
  (Hash Variable)
  (implementation
   (def equivalence
     ..equivalence)
   
   (def hash
     (|>> (pipe.case
            (^.with_template [<factor> <tag>]
              [{<tag> register}
               (|> register
                   (at n.hash hash)
                   (n.* <factor>))])
            ([2 #Local]
             [3 #Foreign]))))))

(def .public self
  (template (self)
    [{..#Local 0}]))

(def .public self?
  (-> Variable Bit)
  (|>> (pipe.case
         (pattern (..self))
         true

         _
         false)))

(def .public format
  (Format Variable)
  (|>> (pipe.case
         {#Local local}
         (%.format "+" (%.nat local))
         
         {#Foreign foreign}
         (%.format "-" (%.nat foreign)))))
