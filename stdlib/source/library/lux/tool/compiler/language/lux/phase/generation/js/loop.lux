(.module:
  [library
   [lux {"-" [Scope]}
    [abstract
     ["[0]" monad {"+" [do]}]]
    [data
     ["[0]" product]
     ["[0]" text
      ["%" format {"+" [format]}]]
     [collection
      ["[0]" list ("[1]#[0]" functor mix)]]]
    [math
     [number
      ["n" nat]]]
    [target
     ["_" js {"+" [Computation Var Expression Statement]}]]]]
  ["[0]" // "_"
   [runtime {"+" [Operation Phase Phase! Generator Generator!]}]
   ["[1][0]" case]
   ["///[1]" //// "_"
    [synthesis {"+" [Scope Synthesis]}]
    ["[1][0]" generation]
    ["//[1]" /// "_"
     ["[1][0]" phase]
     [reference
      [variable {"+" [Register]}]]]]])

(def: @scope
  (-> Nat Text)
  (|>> %.nat (format "scope")))

(def: (setup initial? offset bindings body)
  (-> Bit Register (List Expression) Statement Statement)
  (|> bindings
      list.enumeration
      (list#each (function (_ [register value])
                   (let [variable (//case.register (n.+ offset register))]
                     (if initial?
                       (_.define variable value)
                       (_.set variable value)))))
      list.reversed
      (list#mix _.then body)))

(def: .public (scope! statement expression archive [start initsS+ bodyS])
  (Generator! (Scope Synthesis))
  (case initsS+
    ... function/false/non-independent loop
    {.#End}
    (statement expression archive bodyS)

    ... true loop
    _
    (do [! ///////phase.monad]
      [@scope (# ! each ..@scope /////generation.next)
       initsO+ (monad.each ! (expression archive) initsS+)
       body! (/////generation.with_anchor [start @scope]
               (statement expression archive bodyS))]
      (in (..setup true start initsO+
                   (_.with_label (_.label @scope)
                     (_.do_while (_.boolean true)
                                 body!)))))))

(def: .public (scope statement expression archive [start initsS+ bodyS])
  (-> Phase! (Generator (Scope Synthesis)))
  (case initsS+
    ... function/false/non-independent loop
    {.#End}
    (expression archive bodyS)

    ... true loop
    _
    (do [! ///////phase.monad]
      [loop! (scope! statement expression archive [start initsS+ bodyS])]
      (in (_.apply/* (_.closure (list) loop!) (list))))))

(def: @temp
  (_.var "lux_recur_values"))

(def: .public (recur! statement expression archive argsS+)
  (Generator! (List Synthesis))
  (do [! ///////phase.monad]
    [[offset @scope] /////generation.anchor
     argsO+ (monad.each ! (expression archive) argsS+)]
    (in ($_ _.then
            (_.define @temp (_.array argsO+))
            (..setup false offset
                     (|> argsO+
                         list.enumeration
                         (list#each (function (_ [idx _])
                                      (_.at (_.i32 (.int idx)) @temp))))
                     (_.continue_at (_.label @scope)))))))
