(.require
 [library
  [lux (.except Module)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" monoid)]]]]]
 [//
  ["[0]" analysis]
  ["[0]" synthesis]
  ["[0]" generation]
  [phase
   ["[0]" extension]]
  [///
   ["[0]" phase]
   [meta
    [archive
     [module
      [descriptor (.only Module)]]]]]])

(type: .public (Component state phase)
  (Record
   [#state state
    #phase phase]))

(type: .public (State anchor expression directive)
  (Record
   [#analysis (Component analysis.State+
                         analysis.Phase)
    #synthesis (Component synthesis.State+
                          synthesis.Phase)
    #generation (Component (generation.State+ anchor expression directive)
                           (generation.Phase anchor expression directive))]))

(type: .public Import
  (Record
   [#module Module
    #alias Text]))

(type: .public Requirements
  (Record
   [#imports (List Import)
    #referrals (List Code)]))

(def .public no_requirements
  Requirements
  [#imports (list)
   #referrals (list)])

(def .public (merge_requirements left right)
  (-> Requirements Requirements Requirements)
  [#imports (list#composite (the #imports left) (the #imports right))
   #referrals (list#composite (the #referrals left) (the #referrals right))])

(with_template [<special> <general>]
  [(type: .public (<special> anchor expression directive)
     (<general> (..State anchor expression directive) Code Requirements))]

  [State+    extension.State]
  [Operation extension.Operation]
  [Phase     extension.Phase]
  [Handler   extension.Handler]
  [Bundle    extension.Bundle]
  )

(with_template [<name> <component> <phase>]
  [(def .public <name>
     (All (_ anchor expression directive)
       (Operation anchor expression directive <phase>))
     (function (_ [bundle state])
       {try.#Success [[bundle state] (the [<component> ..#phase] state)]}))]

  [analysis   ..#analysis   analysis.Phase]
  [synthesis  ..#synthesis  synthesis.Phase]
  [generation ..#generation (generation.Phase anchor expression directive)]
  )

(with_template [<name> <component> <operation>]
  [(def .public <name>
     (All (_ anchor expression directive output)
       (-> (<operation> output)
           (Operation anchor expression directive output)))
     (|>> (phase.sub [(the [<component> ..#state])
                      (has [<component> ..#state])])
          extension.lifted))]

  [lifted_analysis   ..#analysis   analysis.Operation]
  [lifted_synthesis  ..#synthesis  synthesis.Operation]
  [lifted_generation ..#generation (generation.Operation anchor expression directive)]
  )

(def .public (set_current_module module)
  (All (_ anchor expression directive)
    (-> Module (Operation anchor expression directive Any)))
  (do phase.monad
    [_ (..lifted_analysis
        (analysis.set_current_module module))]
    (..lifted_generation
     (generation.enter_module module))))
