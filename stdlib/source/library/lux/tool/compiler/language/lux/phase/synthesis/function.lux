(.using
 [library
  [lux "*"
   [abstract
    ["[0]" monad {"+" do}]
    ["[0]" enum]]
   [control
    [pipe {"+" case>}]
    ["[0]" maybe ("[1]#[0]" functor)]
    ["[0]" exception {"+" exception:}]]
   [data
    ["[0]" text
     ["%" format {"+" format}]]
    [collection
     ["[0]" list ("[1]#[0]" functor monoid)]]]
   [math
    [number
     ["n" nat]]]]]
 ["[0]" // "_"
  ["[1][0]" loop {"+" Transform}]
  ["//[1]" /// "_"
   ["[1][0]" analysis {"+" Environment Analysis}
    ["[1]/[0]" complex]]
   ["/" synthesis {"+" Path Abstraction Synthesis Operation Phase}]
   [///
    [arity {"+" Arity}]
    ["[1][0]" reference
     ["[1]/[0]" variable {"+" Register Variable}]]
    ["[0]" phase ("[1]#[0]" monad)]]]])

(exception: .public (cannot_find_foreign_variable_in_environment [foreign Register
                                                                  environment (Environment Synthesis)])
  (exception.report
   ["Foreign" (%.nat foreign)]
   ["Environment" (exception.listing /.%synthesis environment)]))

(def: arity_arguments
  (-> Arity (List Synthesis))
  (|>> --
       (enum.range n.enum 1)
       (list#each (|>> /.variable/local))))

(template: .public (self_reference)
  [(/.variable/local 0)])

(def: (expanded_nested_self_reference arity)
  (-> Arity Synthesis)
  (/.function/apply [(..self_reference) (arity_arguments arity)]))

(def: .public (apply phase)
  (-> Phase Phase)
  (function (_ archive exprA)
    (let [[funcA argsA] (////analysis.reification exprA)]
      (do [! phase.monad]
        [funcS (phase archive funcA)
         argsS (monad.each ! (phase archive) argsA)]
        (with_expansions [<apply> (as_is (/.function/apply [funcS argsS]))]
          (case funcS
            (^ (/.function/abstraction functionS))
            (if (n.= (value@ /.#arity functionS)
                     (list.size argsS))
              (do !
                [locals /.locals]
                (in (|> functionS
                        (//loop.optimization true locals argsS)
                        (maybe#each (: (-> [Nat (List Synthesis) Synthesis] Synthesis)
                                       (function (_ [start inits iteration])
                                         (case iteration
                                           (^ (/.loop/scope [start' inits' output]))
                                           (if (and (n.= start start')
                                                    (list.empty? inits'))
                                             (/.loop/scope [start inits output])
                                             (/.loop/scope [start inits iteration]))

                                           _
                                           (/.loop/scope [start inits iteration])))))
                        (maybe.else <apply>))))
              (in <apply>))

            (^ (/.function/apply [funcS' argsS']))
            (in (/.function/apply [funcS' (list#composite argsS' argsS)]))

            _
            (in <apply>)))))))

(def: (find_foreign environment register)
  (-> (Environment Synthesis) Register (Operation Synthesis))
  (case (list.item register environment)
    {.#Some aliased}
    (phase#in aliased)

    {.#None}
    (phase.except ..cannot_find_foreign_variable_in_environment [register environment])))

(def: (grow_path grow path)
  (-> (-> Synthesis (Operation Synthesis)) Path (Operation Path))
  (case path
    {/.#Bind register}
    (phase#in {/.#Bind (++ register)})

    (^template [<tag>]
      [{<tag> left right}
       (do phase.monad
         [left' (grow_path grow left)
          right' (grow_path grow right)]
         (in {<tag> left' right'}))])
    ([/.#Alt] [/.#Seq])

    {/.#Bit_Fork when then else}
    (do [! phase.monad]
      [then (grow_path grow then)
       else (case else
              {.#Some else}
              (# ! each (|>> {.#Some}) (grow_path grow else))

              {.#None}
              (in {.#None}))]
      (in {/.#Bit_Fork when then else}))
    
    (^template [<tag>]
      [{<tag> [[test then] elses]}
       (do [! phase.monad]
         [then (grow_path grow then)
          elses (monad.each ! (function (_ [else_test else_then])
                                (do !
                                  [else_then (grow_path grow else_then)]
                                  (in [else_test else_then])))
                            elses)]
         (in {<tag> [[test then] elses]}))])
    ([/.#I64_Fork]
     [/.#F64_Fork]
     [/.#Text_Fork])
    
    {/.#Then thenS}
    (|> thenS
        grow
        (phase#each (|>> {/.#Then})))

    _
    (phase#in path)))

(def: (grow environment expression)
  (-> (Environment Synthesis) Synthesis (Operation Synthesis))
  (case expression
    {/.#Structure structure}
    (case structure
      {////analysis/complex.#Variant [lefts right? subS]}
      (|> subS
          (grow environment)
          (phase#each (|>> [lefts right?] /.variant)))
      
      {////analysis/complex.#Tuple membersS+}
      (|> membersS+
          (monad.each phase.monad (grow environment))
          (phase#each (|>> /.tuple))))

    (^ (..self_reference))
    (phase#in (/.function/apply [expression (list (/.variable/local 1))]))
    
    {/.#Reference reference}
    (case reference
      {////reference.#Variable variable}
      (case variable
        {////reference/variable.#Local register}
        (phase#in (/.variable/local (++ register)))
        
        {////reference/variable.#Foreign register}
        (..find_foreign environment register))
      
      {////reference.#Constant constant}
      (phase#in expression))
    
    {/.#Control control}
    (case control
      {/.#Branch branch}
      (case branch
        {/.#Exec [this that]}
        (do phase.monad
          [this (grow environment this)
           that (grow environment that)]
          (in (/.branch/exec [this that])))
        
        {/.#Let [inputS register bodyS]}
        (do phase.monad
          [inputS' (grow environment inputS)
           bodyS' (grow environment bodyS)]
          (in (/.branch/let [inputS' (++ register) bodyS'])))
        
        {/.#If [testS thenS elseS]}
        (do phase.monad
          [testS' (grow environment testS)
           thenS' (grow environment thenS)
           elseS' (grow environment elseS)]
          (in (/.branch/if [testS' thenS' elseS'])))

        {/.#Get members inputS}
        (do phase.monad
          [inputS' (grow environment inputS)]
          (in (/.branch/get [members inputS'])))
        
        {/.#Case [inputS pathS]}
        (do phase.monad
          [inputS' (grow environment inputS)
           pathS' (grow_path (grow environment) pathS)]
          (in (/.branch/case [inputS' pathS']))))
      
      {/.#Loop loop}
      (case loop
        {/.#Scope [start initsS+ iterationS]}
        (do [! phase.monad]
          [initsS+' (monad.each ! (grow environment) initsS+)
           iterationS' (grow environment iterationS)]
          (in (/.loop/scope [(++ start) initsS+' iterationS'])))
        
        {/.#Again argumentsS+}
        (|> argumentsS+
            (monad.each phase.monad (grow environment))
            (phase#each (|>> /.loop/again))))
      
      {/.#Function function}
      (case function
        {/.#Abstraction [_env _arity _body]}
        (do [! phase.monad]
          [_env' (monad.each !
                             (|>> (case> {/.#Reference {////reference.#Variable {////reference/variable.#Foreign register}}}
                                         (..find_foreign environment register)

                                         captured
                                         (grow environment captured)))
                             _env)]
          (in (/.function/abstraction [_env' _arity _body])))
        
        {/.#Apply funcS argsS+}
        (do [! phase.monad]
          [funcS (grow environment funcS)
           argsS+ (monad.each ! (grow environment) argsS+)]
          (in (/.function/apply (case funcS
                                  (^ (/.function/apply [(..self_reference) pre_argsS+]))
                                  [(..self_reference)
                                   (list#composite pre_argsS+ argsS+)]

                                  _
                                  [funcS
                                   argsS+]))))))
    
    {/.#Extension name argumentsS+}
    (|> argumentsS+
        (monad.each phase.monad (grow environment))
        (phase#each (|>> {/.#Extension name})))

    {/.#Primitive _}
    (phase#in expression)))

(def: .public (abstraction phase environment archive bodyA)
  (-> Phase (Environment Analysis) Phase)
  (do [! phase.monad]
    [currying? /.currying?
     environment (monad.each ! (phase archive) environment)
     bodyS (/.with_currying? true
             (/.with_locals 2
               (phase archive bodyA)))
     abstraction (: (Operation Abstraction)
                    (case bodyS
                      (^ (/.function/abstraction [env' down_arity' bodyS']))
                      (|> bodyS'
                          (grow env')
                          (# ! each (function (_ body)
                                      [/.#environment environment
                                       /.#arity (++ down_arity')
                                       /.#body body])))
                      
                      _
                      (in [/.#environment environment
                           /.#arity 1
                           /.#body bodyS])))]
    (in (if currying?
          (/.function/abstraction abstraction)
          (case (//loop.optimization false 1 (list) abstraction)
            {.#Some [startL initsL bodyL]}
            (/.function/abstraction [/.#environment environment
                                     /.#arity (value@ /.#arity abstraction)
                                     /.#body (/.loop/scope [startL initsL bodyL])])
            
            {.#None}
            (/.function/abstraction abstraction))))))
