(.module:
  [library
   [lux #*
    [abstract
     ["." monad (#+ do)]]
    [control
     ["." try]
     ["." exception (#+ exception:)]
     ["<>" parser
      ["<.>" code (#+ Parser)]]]
    [data
     ["." maybe]
     ["." text
      ["%" format (#+ format)]]
     [collection
      ["." list ("#\." functor)]
      ["." dictionary (#+ Dictionary)]]]
    [math
     [number
      ["n" nat]]]
    [type
     ["." check]]
    ["." meta]]]
  ["." ///
   ["#." bundle]
   ["/#" // #_
    [analysis
     [".A" type]]
    [//
     ["#." analysis (#+ Analysis Operation Phase Handler Bundle)
      [evaluation (#+ Eval)]]
     [///
      ["#" phase]
      [meta
       [archive (#+ Archive)]]]]]])

(def: #export (custom [syntax handler])
  (All [s]
    (-> [(Parser s)
         (-> Text Phase Archive s (Operation Analysis))]
        Handler))
  (function (_ extension_name analyse archive args)
    (case (<code>.run syntax args)
      (#try.Success inputs)
      (handler extension_name analyse archive inputs)

      (#try.Failure _)
      (////analysis.throw ///.invalid_syntax [extension_name %.code args]))))

(def: (simple inputsT+ outputT)
  (-> (List Type) Type Handler)
  (let [num_expected (list.size inputsT+)]
    (function (_ extension_name analyse archive args)
      (let [num_actual (list.size args)]
        (if (n.= num_expected num_actual)
          (do {! ////.monad}
            [_ (typeA.infer outputT)
             argsA (monad.map !
                              (function (_ [argT argC])
                                (typeA.with_type argT
                                  (analyse archive argC)))
                              (list.zip/2 inputsT+ args))]
            (wrap (#////analysis.Extension extension_name argsA)))
          (////analysis.throw ///.incorrect_arity [extension_name num_expected num_actual]))))))

(def: #export (nullary valueT)
  (-> Type Handler)
  (simple (list) valueT))

(def: #export (unary inputT outputT)
  (-> Type Type Handler)
  (simple (list inputT) outputT))

(def: #export (binary subjectT paramT outputT)
  (-> Type Type Type Handler)
  (simple (list subjectT paramT) outputT))

(def: #export (trinary subjectT param0T param1T outputT)
  (-> Type Type Type Type Handler)
  (simple (list subjectT param0T param1T) outputT))

## TODO: Get rid of this ASAP
(as_is
 (exception: #export (char_text_must_be_size_1 {text Text})
   (exception.report
    ["Text" (%.text text)]))
 
 (def: text_char
   (Parser text.Char)
   (do <>.monad
     [raw <code>.text]
     (case (text.size raw)
       1 (wrap (|> raw (text.nth 0) maybe.assume))
       _ (<>.fail (exception.construct ..char_text_must_be_size_1 [raw])))))

 (def: lux::syntax_char_case!
   (..custom
    [($_ <>.and
         <code>.any
         (<code>.tuple (<>.some (<>.and (<code>.tuple (<>.many ..text_char))
                                        <code>.any)))
         <code>.any)
     (function (_ extension_name phase archive [input conditionals else])
       (do {! ////.monad}
         [input (typeA.with_type text.Char
                  (phase archive input))
          expectedT (///.lift meta.expected_type)
          conditionals (monad.map ! (function (_ [cases branch])
                                      (do !
                                        [branch (typeA.with_type expectedT
                                                  (phase archive branch))]
                                        (wrap [cases branch])))
                                  conditionals)
          else (typeA.with_type expectedT
                 (phase archive else))]
         (wrap (|> conditionals
                   (list\map (function (_ [cases branch])
                               (////analysis.tuple
                                (list (////analysis.tuple (list\map (|>> ////analysis.nat) cases))
                                      branch))))
                   (list& input else)
                   (#////analysis.Extension extension_name)))))])))

## "lux is" represents reference/pointer equality.
(def: lux::is
  Handler
  (function (_ extension_name analyse archive args)
    (do ////.monad
      [[var_id varT] (typeA.with_env check.var)]
      ((binary varT varT Bit extension_name)
       analyse archive args))))

## "lux try" provides a simple way to interact with the host platform's
## error_handling facilities.
(def: lux::try
  Handler
  (function (_ extension_name analyse archive args)
    (case args
      (^ (list opC))
      (do ////.monad
        [[var_id varT] (typeA.with_env check.var)
         _ (typeA.infer (type (Either Text varT)))
         opA (typeA.with_type (type (-> .Any varT))
               (analyse archive opC))]
        (wrap (#////analysis.Extension extension_name (list opA))))
      
      _
      (////analysis.throw ///.incorrect_arity [extension_name 1 (list.size args)]))))

(def: lux::in_module
  Handler
  (function (_ extension_name analyse archive argsC+)
    (case argsC+
      (^ (list [_ (#.Text module_name)] exprC))
      (////analysis.with_current_module module_name
        (analyse archive exprC))
      
      _
      (////analysis.throw ///.invalid_syntax [extension_name %.code argsC+]))))

(def: (lux::type::check eval)
  (-> Eval Handler)
  (function (_ extension_name analyse archive args)
    (case args
      (^ (list typeC valueC))
      (do {! ////.monad}
        [count (///.lift meta.count)
         actualT (\ ! map (|>> (:as Type))
                    (eval archive count Type typeC))
         _ (typeA.infer actualT)]
        (typeA.with_type actualT
          (analyse archive valueC)))

      _
      (////analysis.throw ///.incorrect_arity [extension_name 2 (list.size args)]))))

(def: (lux::type::as eval)
  (-> Eval Handler)
  (function (_ extension_name analyse archive args)
    (case args
      (^ (list typeC valueC))
      (do {! ////.monad}
        [count (///.lift meta.count)
         actualT (\ ! map (|>> (:as Type))
                    (eval archive count Type typeC))
         _ (typeA.infer actualT)
         [valueT valueA] (typeA.with_inference
                           (analyse archive valueC))]
        (wrap valueA))

      _
      (////analysis.throw ///.incorrect_arity [extension_name 2 (list.size args)]))))

(def: (caster input output)
  (-> Type Type Handler)
  (..custom
   [<code>.any
    (function (_ extension_name phase archive valueC)
      (do {! ////.monad}
        [_ (typeA.infer output)]
        (typeA.with_type input
          (phase archive valueC))))]))

(def: lux::macro
  Handler
  (..custom
   [<code>.any
    (function (_ extension_name phase archive valueC)
      (do {! ////.monad}
        [_ (typeA.infer .Macro)
         input_type (loop [input_name (name_of .Macro')]
                      (do !
                        [input_type (///.lift (meta.find_def (name_of .Macro')))]
                        (case input_type
                          (#.Definition [exported? def_type def_data def_value])
                          (wrap (:as Type def_value))

                          (#.Alias real_name)
                          (recur real_name))))]
        (typeA.with_type input_type
          (phase archive valueC))))]))

(def: (bundle::lux eval)
  (-> Eval Bundle)
  (|> ///bundle.empty
      (///bundle.install "syntax char case!" lux::syntax_char_case!)
      (///bundle.install "is" lux::is)
      (///bundle.install "try" lux::try)
      (///bundle.install "type check" (lux::type::check eval))
      (///bundle.install "type as" (lux::type::as eval))
      (///bundle.install "macro" ..lux::macro)
      (///bundle.install "type check type" (..caster .Type .Type))
      (///bundle.install "in-module" lux::in_module)))

(def: bundle::io
  Bundle
  (<| (///bundle.prefix "io")
      (|> ///bundle.empty
          (///bundle.install "log" (unary Text Any))
          (///bundle.install "error" (unary Text Nothing))
          (///bundle.install "exit" (unary Int Nothing)))))

(def: I64* (type (I64 Any)))

(def: bundle::i64
  Bundle
  (<| (///bundle.prefix "i64")
      (|> ///bundle.empty
          (///bundle.install "and" (binary I64* I64* I64))
          (///bundle.install "or" (binary I64* I64* I64))
          (///bundle.install "xor" (binary I64* I64* I64))
          (///bundle.install "left-shift" (binary Nat I64* I64))
          (///bundle.install "right-shift" (binary Nat I64* I64))
          (///bundle.install "=" (binary I64* I64* Bit))
          (///bundle.install "<" (binary Int Int Bit))
          (///bundle.install "+" (binary I64* I64* I64))
          (///bundle.install "-" (binary I64* I64* I64))
          (///bundle.install "*" (binary Int Int Int))
          (///bundle.install "/" (binary Int Int Int))
          (///bundle.install "%" (binary Int Int Int))
          (///bundle.install "f64" (unary Int Frac))
          (///bundle.install "char" (unary Int Text)))))

(def: bundle::f64
  Bundle
  (<| (///bundle.prefix "f64")
      (|> ///bundle.empty
          (///bundle.install "+" (binary Frac Frac Frac))
          (///bundle.install "-" (binary Frac Frac Frac))
          (///bundle.install "*" (binary Frac Frac Frac))
          (///bundle.install "/" (binary Frac Frac Frac))
          (///bundle.install "%" (binary Frac Frac Frac))
          (///bundle.install "=" (binary Frac Frac Bit))
          (///bundle.install "<" (binary Frac Frac Bit))
          (///bundle.install "i64" (unary Frac Int))
          (///bundle.install "encode" (unary Frac Text))
          (///bundle.install "decode" (unary Text (type (Maybe Frac)))))))

(def: bundle::text
  Bundle
  (<| (///bundle.prefix "text")
      (|> ///bundle.empty
          (///bundle.install "=" (binary Text Text Bit))
          (///bundle.install "<" (binary Text Text Bit))
          (///bundle.install "concat" (binary Text Text Text))
          (///bundle.install "index" (trinary Nat Text Text (type (Maybe Nat))))
          (///bundle.install "size" (unary Text Nat))
          (///bundle.install "char" (binary Nat Text Nat))
          (///bundle.install "clip" (trinary Nat Nat Text Text))
          )))

(def: #export (bundle eval)
  (-> Eval Bundle)
  (<| (///bundle.prefix "lux")
      (|> ///bundle.empty
          (dictionary.merge (bundle::lux eval))
          (dictionary.merge bundle::i64)
          (dictionary.merge bundle::f64)
          (dictionary.merge bundle::text)
          (dictionary.merge bundle::io)
          )))
