(.module:
  [library
   [lux (#- Type type)
    [data
     [collection
      ["." list ("#\." functor)]]]
    [target
     [jvm
      ["." method (#+ Method)]
      ["_" bytecode (#+ Bytecode)]
      [constant
       [pool (#+ Resource)]]
      ["." type (#+ Type)
       ["." category (#+ Class)]]]]]]
  ["." //
   ["#." new]
   ["/#" // #_
    [field
     [variable
      ["#." foreign]]]
    ["/#" // #_
     ["#." reference]
     [////
      [analysis (#+ Environment)]
      [synthesis (#+ Synthesis)]
      [///
       ["." arity (#+ Arity)]]]]]])

(def: .public name "reset")

(def: .public (type class)
  (-> (Type Class) (Type category.Method))
  (type.method [(list) class (list)]))

(def: (current_environment class)
  (-> (Type Class) (Environment Synthesis) (List (Bytecode Any)))
  (|>> list.size
       list.indices
       (list\map (///foreign.get class))))

(def: .public (method class environment arity)
  (-> (Type Class) (Environment Synthesis) Arity (Resource Method))
  (method.method //.modifier ..name
                 (..type class)
                 (list)
                 (#.Some ($_ _.compose
                             (if (arity.multiary? arity)
                               (//new.instance' (..current_environment class environment) class environment arity)
                               ////reference.this)
                             _.areturn))))
