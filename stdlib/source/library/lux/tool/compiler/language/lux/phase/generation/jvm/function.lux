(.using
 [library
  [lux (.except Type Label with)
   [abstract
    ["[0]" monad (.only do)]]
   [data
    ["[0]" product]
    [binary
     ["[0]" \\format]]
    [collection
     ["[0]" list (.open: "[1]#[0]" monoid functor)]
     ["[0]" sequence]]]
   [math
    [number
     ["n" nat]
     ["[0]" i32]]]
   [target
    [jvm
     ["_" bytecode (.only Label Bytecode) (.open: "[1]#[0]" monad)]
     ["[0]" version]
     ["[0]" modifier (.only Modifier) (.open: "[1]#[0]" monoid)]
     ["[0]" field (.only Field)]
     ["[0]" method (.only Method)]
     ["[0]" class (.only Class)]
     ["[0]" type (.only Type)
      [category (.only Return' Value')]
      ["[0]" reflection]]
     ["[0]" constant (.only)
      [pool (.only Resource)]]
     [encoding
      ["[0]" name (.only External Internal)]
      ["[0]" unsigned]]]]
   [tool
    [compiler
     [meta
      ["[0]" archive (.only Archive)]
      ["[0]" cache
       [dependency
        ["[1]/[0]" artifact]]]]]]]]
 ["[0]" /
  ["[1][0]" abstract]
  [field
   [constant
    ["[1][0]" arity]]
   [variable
    ["[1][0]" foreign]
    ["[1][0]" partial]]]
  [method
   ["[1][0]" init]
   ["[1][0]" new]
   ["[1][0]" implementation]
   ["[1][0]" reset]
   ["[1][0]" apply]]
  ["/[1]" //
   ["[1][0]" runtime (.only Operation Phase Generator)]
   ["[1][0]" reference]
   [////
    [analysis (.only Environment)]
    ["[0]" synthesis (.only Synthesis Abstraction Apply)]
    ["[0]" generation]
    [///
     ["[0]" arity (.only Arity)]
     ["[0]" phase]
     [meta
      [archive
       ["[0]" unit]]]
     [reference
      [variable (.only Register)]]]]]])

(def .public (with generate archive @begin class environment arity body)
  (-> Phase Archive Label External (Environment Synthesis) Arity (Bytecode Any)
      (Operation [(List (Resource Field))
                  (List (Resource Method))
                  (Bytecode Any)]))
  (let [classT (type.class class (list))
        fields (is (List (Resource Field))
                   (list#composite (/foreign.variables environment)
                                   (/partial.variables arity)))
        methods (is (List (Resource Method))
                    (list.partial (/init.method classT environment arity)
                                  (/reset.method classT environment arity)
                                  (if (arity.multiary? arity)
                                    (|> (n.min arity /arity.maximum)
                                        list.indices
                                        (list#each (|>> ++ (/apply.method classT environment arity @begin body)))
                                        (list.partial (/implementation.method classT arity @begin body)))
                                    (list (/implementation.method classT arity @begin body)
                                          (/apply.method classT environment arity @begin body 1)))))]
    (do phase.monad
      [instance (/new.instance generate archive classT environment arity)]
      (in [fields methods instance]))))

(def modifier
  (Modifier Class)
  (all modifier#composite
       class.public
       class.final))

(def this_offset 1)

(def internal
  (All (_ category)
    (-> (Type (<| Return' Value' category))
        Internal))
  (|>> type.reflection reflection.reflection name.internal))

(def .public (abstraction generate archive [environment arity bodyS])
  (Generator Abstraction)
  (do phase.monad
    [dependencies (cache/artifact.dependencies archive bodyS)
     @begin //runtime.forge_label
     [function_context bodyG] (generation.with_new_context archive dependencies
                                (generation.with_anchor [@begin ..this_offset]
                                  (generate archive bodyS)))
     .let [function_class (//runtime.class_name function_context)]
     [fields methods instance] (..with generate archive @begin function_class environment arity bodyG)
     class (phase.lifted (class.class version.v6_0
                                      ..modifier
                                      (name.internal function_class)
                                      {.#None}
                                      (..internal /abstract.class) (list)
                                      fields
                                      methods
                                      (sequence.sequence)))
     .let [bytecode [function_class (\\format.result class.writer class)]]
     _ (generation.execute! bytecode)
     _ (generation.save! (product.right function_context) {.#None} bytecode)]
    (in instance)))

(def (apply/?' generate archive [abstractionG inputsS])
  (Generator [(Bytecode Any) (List Synthesis)])
  (do [! phase.monad]
    [inputsG (monad.each ! (generate archive) inputsS)]
    (in (all _.composite
             abstractionG
             (|> inputsG
                 (list.sub /arity.maximum)
                 (monad.each _.monad
                             (function (_ batchG)
                               (all _.composite
                                    (_.checkcast /abstract.class)
                                    (monad.all _.monad batchG)
                                    (_.invokevirtual /abstract.class //runtime.apply::name (//runtime.apply::type (list.size batchG)))
                                    ))))
             ))))

(def (apply/? generate archive [abstractionS inputsS])
  (Generator Apply)
  (do [! phase.monad]
    [abstractionG (generate archive abstractionS)]
    (apply/?' generate archive [abstractionG inputsS])))

(def (apply/= generate archive [$abstraction @abstraction arity inputsS])
  (Generator [Symbol unit.ID Arity (List Synthesis)])
  (do [! phase.monad]
    [.let [:abstraction: (type.class (//runtime.class_name @abstraction) (list))]
     abstractionG (//reference.constant archive $abstraction)
     inputsG (monad.each ! (generate archive) inputsS)]
    (in (all _.composite
             abstractionG
             (monad.all _.monad inputsG)
             (/implementation.call :abstraction: arity)
             ))))

(def (apply/> generate archive [$abstraction @abstraction arity inputsS])
  (Generator [Symbol unit.ID Arity (List Synthesis)])
  (do [! phase.monad]
    [=G (apply/= generate archive [$abstraction @abstraction arity (list.first arity inputsS)])]
    (apply/?' generate archive [=G (list.after arity inputsS)])))

(def .public (apply generate archive [abstractionS inputsS])
  (Generator Apply)
  (case abstractionS
    (pattern (synthesis.constant $abstraction))
    (do [! phase.monad]
      [[@definition |abstraction|] (generation.definition archive $abstraction)
       .let [actual_arity (list.size inputsS)]]
      (case |abstraction|
        {.#Some [_ {.#Some [expected_arity @abstraction]}]}
        (cond (n.< expected_arity actual_arity)
              (apply/? generate archive [abstractionS inputsS])
              
              (n.= expected_arity actual_arity)
              (apply/= generate archive [$abstraction @abstraction expected_arity inputsS])
              
              ... (n.> expected_arity actual_arity)
              (apply/> generate archive [$abstraction @abstraction expected_arity inputsS]))
        
        _
        (apply/? generate archive [abstractionS inputsS])))

    _
    (apply/? generate archive [abstractionS inputsS])))
