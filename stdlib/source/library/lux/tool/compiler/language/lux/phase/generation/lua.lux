(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" exception (.only exception:)]]
   [macro
    ["^" pattern]]
   [target
    ["_" lua]]]]
 ["[0]" /
  [runtime (.only Phase)]
  ["[1][0]" primitive]
  ["[1][0]" structure]
  ["[1][0]" reference]
  ["[1][0]" case]
  ["[1][0]" loop]
  ["[1][0]" function]
  ["/[1]" //
   ["[1][0]" reference]
   ["/[1]" //
    ["[1][0]" extension (.only)
     [generation
      [lua
       ["[1]/[0]" common]]]]
    ["/[1]" //
     [analysis (.only)]
     ["[0]" synthesis]
     ["//[1]" ///
      ["[1][0]" phase (.open: "[1]#[0]" monad)]
      [reference (.only)
       [variable (.only)]]]]]]])

(exception: .public cannot_recur_as_an_expression)

(def (expression archive synthesis)
  Phase
  (case synthesis
    (^.with_template [<tag> <generator>]
      [(pattern (<tag> value))
       (//////phase#in (<generator> value))])
    ([synthesis.bit  /primitive.bit]
     [synthesis.i64  /primitive.i64]
     [synthesis.f64  /primitive.f64]
     [synthesis.text /primitive.text])

    (pattern (synthesis.variant variantS))
    (/structure.variant expression archive variantS)

    (pattern (synthesis.tuple members))
    (/structure.tuple expression archive members)

    {synthesis.#Reference value}
    (//reference.reference /reference.system archive value)

    (pattern (synthesis.branch/case case))
    (/case.case ///extension/common.statement expression archive case)

    (pattern (synthesis.branch/exec it))
    (/case.exec expression archive it)

    (pattern (synthesis.branch/let let))
    (/case.let expression archive let)

    (pattern (synthesis.branch/if if))
    (/case.if expression archive if)

    (pattern (synthesis.branch/get get))
    (/case.get expression archive get)

    (pattern (synthesis.loop/scope scope))
    (/loop.scope ///extension/common.statement expression archive scope)

    (pattern (synthesis.loop/again updates))
    (//////phase.except ..cannot_recur_as_an_expression [])

    (pattern (synthesis.function/abstraction abstraction))
    (/function.function ///extension/common.statement expression archive abstraction)

    (pattern (synthesis.function/apply application))
    (/function.apply expression archive application)

    {synthesis.#Extension extension}
    (///extension.apply archive expression extension)))

(def .public generate
  Phase
  ..expression)
