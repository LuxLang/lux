(.module:
  [library
   [lux (#- Module)
    [abstract
     ["." monad (#+ do)]]
    [control
     ["." maybe]
     ["." try (#+ Try)]
     ["." exception (#+ exception:)]]
    [data
     ["." product]
     [text
      ["%" format (#+ format)]]
     [collection
      ["." list ("#\." functor)]]]]]
  [//
   [generation (#+ Context)]
   [///
    [meta
     ["." archive (#+ Archive)
      ["." descriptor (#+ Module)]
      ["." artifact]]]]])

(type: .public (Program expression directive)
  (-> Context expression directive))

(def: .public name
  Text
  "")

(exception: .public (cannot_find_program {modules (List Module)})
  (exception.report
   ["Modules" (exception.listing %.text modules)]))

(def: .public (context archive)
  (-> Archive (Try Context))
  (do {! try.monad}
    [registries (|> archive
                    archive.archived
                    (monad.map !
                               (function (_ module)
                                 (do !
                                   [id (archive.id module archive)
                                    [descriptor document] (archive.find module archive)]
                                   (in [[module id] (value@ #descriptor.registry descriptor)])))))]
    (case (list.one (function (_ [[module module_id] registry])
                      (do maybe.monad
                        [program_id (artifact.remember ..name registry)]
                        (in [module_id program_id])))
                    registries)
      (#.Some program_context)
      (in program_context)
      
      #.None
      (|> registries
          (list\map (|>> product.left product.left))
          (exception.except ..cannot_find_program)))))
