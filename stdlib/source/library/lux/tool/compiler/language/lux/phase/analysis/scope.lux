(.module:
  [library
   [lux {"-" local}
    [abstract
     monad]
    [control
     ["[0]" maybe ("[1]#[0]" monad)]
     ["[0]" try]
     ["[0]" exception {"+" exception:}]]
    [data
     ["[0]" text ("[1]#[0]" equivalence)]
     ["[0]" product]
     [collection
      ["[0]" list ("[1]#[0]" functor mix monoid)]
      [dictionary
       ["[0]" plist]]]]]]
  ["[0]" /// "_"
   ["[1][0]" extension]
   [//
    ["/" analysis {"+" Operation Phase}]
    [///
     [reference
      ["[0]" variable {"+" Register Variable}]]
     ["[1]" phase]]]])

(type: Local
  (Bindings Text [Type Register]))

(type: Foreign
  (Bindings Text [Type Variable]))

(def: (local? name scope)
  (-> Text Scope Bit)
  (|> scope
      (value@ [.#locals .#mappings])
      (plist.contains? name)))

(def: (local name scope)
  (-> Text Scope (Maybe [Type Variable]))
  (|> scope
      (value@ [.#locals .#mappings])
      (plist.value name)
      (maybe#each (function (_ [type value])
                    [type {variable.#Local value}]))))

(def: (captured? name scope)
  (-> Text Scope Bit)
  (|> scope
      (value@ [.#captured .#mappings])
      (plist.contains? name)))

(def: (captured name scope)
  (-> Text Scope (Maybe [Type Variable]))
  (loop [idx 0
         mappings (value@ [.#captured .#mappings] scope)]
    (case mappings
      {.#Item [_name [_source_type _source_ref]] mappings'}
      (if (text#= name _name)
        {.#Some [_source_type {variable.#Foreign idx}]}
        (recur (++ idx) mappings'))

      {.#End}
      {.#None})))

(def: (reference? name scope)
  (-> Text Scope Bit)
  (or (local? name scope)
      (captured? name scope)))

(def: (reference name scope)
  (-> Text Scope (Maybe [Type Variable]))
  (case (..local name scope)
    {.#Some type}
    {.#Some type}

    _
    (..captured name scope)))

(def: .public (find name)
  (-> Text (Operation (Maybe [Type Variable])))
  (///extension.lifted
   (function (_ state)
     (let [[inner outer] (|> state
                             (value@ .#scopes)
                             (list.split_when (|>> (reference? name))))]
       (case outer
         {.#End}
         {.#Right [state {.#None}]}

         {.#Item top_outer _}
         (let [[ref_type init_ref] (maybe.else (undefined)
                                               (..reference name top_outer))
               [ref inner'] (list#mix (: (-> Scope [Variable (List Scope)] [Variable (List Scope)])
                                         (function (_ scope ref+inner)
                                           [{variable.#Foreign (value@ [.#captured .#counter] scope)}
                                            {.#Item (revised@ .#captured
                                                              (: (-> Foreign Foreign)
                                                                 (|>> (revised@ .#counter ++)
                                                                      (revised@ .#mappings (plist.has name [ref_type (product.left ref+inner)]))))
                                                              scope)
                                                    (product.right ref+inner)}]))
                                      [init_ref {.#End}]
                                      (list.reversed inner))
               scopes (list#composite inner' outer)]
           {.#Right [(with@ .#scopes scopes state)
                     {.#Some [ref_type ref]}]})
         )))))

(exception: .public cannot_create_local_binding_without_a_scope)
(exception: .public invalid_scope_alteration)

(def: .public (with_local [name type] action)
  (All (_ a) (-> [Text Type] (Operation a) (Operation a)))
  (function (_ [bundle state])
    (case (value@ .#scopes state)
      {.#Item head tail}
      (let [old_mappings (value@ [.#locals .#mappings] head)
            new_var_id (value@ [.#locals .#counter] head)
            new_head (revised@ .#locals
                               (: (-> Local Local)
                                  (|>> (revised@ .#counter ++)
                                       (revised@ .#mappings (plist.has name [type new_var_id]))))
                               head)]
        (case (///.result' [bundle (with@ .#scopes {.#Item new_head tail} state)]
                           action)
          {try.#Success [[bundle' state'] output]}
          (case (value@ .#scopes state')
            {.#Item head' tail'}
            (let [scopes' {.#Item (with@ .#locals (value@ .#locals head) head')
                                  tail'}]
              {try.#Success [[bundle' (with@ .#scopes scopes' state')]
                             output]})

            _
            (exception.except ..invalid_scope_alteration []))

          {try.#Failure error}
          {try.#Failure error}))

      _
      (exception.except ..cannot_create_local_binding_without_a_scope []))
    ))

(template [<name> <val_type>]
  [(def: <name>
     (Bindings Text [Type <val_type>])
     [.#counter 0
      .#mappings (list)])]

  [init_locals   Nat]
  [init_captured Variable]
  )

(def: (scope parent_name child_name)
  (-> (List Text) Text Scope)
  [.#name     (list& child_name parent_name)
   .#inner    0
   .#locals   init_locals
   .#captured init_captured])

(def: .public (with_scope name action)
  (All (_ a) (-> Text (Operation a) (Operation a)))
  (function (_ [bundle state])
    (let [parent_name (case (value@ .#scopes state)
                        {.#End}
                        (list)
                        
                        {.#Item top _}
                        (value@ .#name top))]
      (case (action [bundle (revised@ .#scopes
                                      (|>> {.#Item (scope parent_name name)})
                                      state)])
        {try.#Success [[bundle' state'] output]}
        {try.#Success [[bundle' (revised@ .#scopes
                                          (|>> list.tail (maybe.else (list)))
                                          state')]
                       output]}

        {try.#Failure error}
        {try.#Failure error}))))

(exception: .public cannot_get_next_reference_when_there_is_no_scope)

(def: .public next_local
  (Operation Register)
  (///extension.lifted
   (function (_ state)
     (case (value@ .#scopes state)
       {.#Item top _}
       {try.#Success [state (value@ [.#locals .#counter] top)]}

       {.#End}
       (exception.except ..cannot_get_next_reference_when_there_is_no_scope [])))))

(def: (ref_variable ref)
  (-> Ref Variable)
  (case ref
    {.#Local register}
    {variable.#Local register}
    
    {.#Captured register}
    {variable.#Foreign register}))

(def: .public (environment scope)
  (-> Scope (List Variable))
  (|> scope
      (value@ [.#captured .#mappings])
      (list#each (function (_ [_ [_ ref]]) (ref_variable ref)))))
