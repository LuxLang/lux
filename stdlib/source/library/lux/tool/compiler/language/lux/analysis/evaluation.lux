(.using
 [library
  [lux "*"
   [type {"+" sharing}]
   ["[0]" meta]
   [abstract
    [monad {"+" do}]]
   [control
    ["[0]" maybe]
    ["[0]" try]
    ["[0]" io]
    [concurrency
     ["[0]" atom {"+" Atom}]]]
   [data
    [collection
     ["[0]" dictionary {"+" Dictionary}]]]
   [math
    [number
     ["n" nat]]]]]
 ["[0]" // {"+" Operation}
  [macro {"+" Expander}]
  ["[1][0]" type]
  ["[1][0]" scope]
  [//
   [phase
    ["[0]P" extension]
    ["[0]P" synthesis]
    ["[0]P" analysis]
    [//
     ["[0]" synthesis]
     ["[0]" generation]
     [///
      ["[0]" phase]
      [meta
       ["[0]" archive {"+" Archive}
        ["[0]" module]]]]]]]])

(type: .public Eval
  (-> Archive Type Code (Operation Any)))

(def: evals
  (Atom (Dictionary module.ID Nat))
  (atom.atom (dictionary.empty n.hash)))

(def: .public (evaluator expander synthesis_state generation_state generate)
  (All (_ anchor expression artifact)
    (-> Expander
        synthesis.State+
        (generation.State+ anchor expression artifact)
        (generation.Phase anchor expression artifact)
        Eval))
  (let [analyze (analysisP.phase expander)]
    (function (eval archive type exprC)
      (do phase.monad
        [exprA (<| (//type.expecting type)
                   //scope.reset
                   (analyze archive exprC))
         module (extensionP.lifted
                 meta.current_module_name)]
        (<| phase.lifted
            (do try.monad
              [exprS (|> exprA (synthesisP.phase archive) (phase.result synthesis_state))])
            (phase.result generation_state)
            (do phase.monad
              [@module (sharing [anchor expression artifact]
                                (generation.Phase anchor expression artifact)
                                generate

                                (generation.Operation anchor expression artifact module.ID)
                                (generation.module_id module archive))
               .let [[evals _] (io.run! (atom.update! (dictionary.revised' @module 0 ++) ..evals))
                     @eval (maybe.else 0 (dictionary.value @module evals))]
               exprO (<| (generation.with_registry_shift (|> @module
                                                             ("lux i64 left-shift" 16)
                                                             ("lux i64 or" @eval)
                                                             ("lux i64 left-shift" 32)))
                         (generate archive exprS))]
              (generation.evaluate! [@module @eval] [{.#None} exprO])))))))
