(.require
 [library
  [lux (.except Variant Tuple)
   [abstract
    ["[0]" monad (.only do)]]
   [target
    ["_" python (.only Expression)]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Generator)]
  ["[1][0]" primitive]
  ["///[1]" ////
   [analysis
    [complex (.only Variant Tuple)]]
   ["[1][0]" synthesis (.only Synthesis)]
   ["//[1]" ///
    ["[1][0]" phase (.use "[1]#[0]" monad)]]]])

(def .public (tuple generate archive elemsS+)
  (Generator (Tuple Synthesis))
  (case elemsS+
    {.#End}
    (///////phase#in (//primitive.text /////synthesis.unit))

    {.#Item singletonS {.#End}}
    (generate archive singletonS)

    _
    (|> elemsS+
        (monad.each ///////phase.monad (generate archive))
        (///////phase#each _.list))))

(def .public (variant generate archive [lefts right? valueS])
  (Generator (Variant Synthesis))
  (///////phase#each (//runtime.variant lefts right?)
                     (generate archive valueS)))
