(.module:
  [library
   [lux #*
    [abstract
     ["." monad (#+ do)]]
    [data
     [collection
      ["." list]]]
    [target
     ["_" scheme (#+ Expression)]]]]
  ["." // #_
   ["#." runtime (#+ Operation Phase Generator)]
   ["#." primitive]
   ["///#" //// #_
    [analysis (#+ Variant Tuple)]
    ["#." synthesis (#+ Synthesis)]
    ["//#" /// #_
     ["#." phase ("#\." monad)]]]])

(def: #export (tuple expression archive elemsS+)
  (Generator (Tuple Synthesis))
  (case elemsS+
    #.End
    (///////phase\in (//primitive.text /////synthesis.unit))

    (#.Item singletonS #.End)
    (expression archive singletonS)

    _
    (|> elemsS+
        (monad.map ///////phase.monad (expression archive))
        (///////phase\map _.vector/*))))

(def: #export (variant expression archive [lefts right? valueS])
  (Generator (Variant Synthesis))
  (let [tag (if right?
              (inc lefts)
              lefts)]
    (///////phase\map (|>> [tag right?] //runtime.variant)
                      (expression archive valueS))))
