(.using
 [library
  [lux "*"
   ["[0]" ffi]
   [abstract
    ["[0]" monad {"+" do}]]
   [control
    ["<>" parser
     ["<[0]>" code {"+" Parser}]]]
   [data
    [collection
     ["[0]" array {"+" Array}]
     ["[0]" dictionary]
     ["[0]" list]]]
   ["[0]" type
    ["[0]" check]]
   ["@" target
    ["_" js]]]]
 [//
  ["/" lux {"+" custom}]
  [//
   ["[0]" bundle]
   [///
    ["[0]" analysis {"+" Analysis Operation Phase Handler Bundle}
     ["[1]/[0]" type]]
    [///
     ["[0]" phase]]]]])

(def: array::new
  Handler
  (custom
   [<code>.any
    (function (_ extension phase archive lengthC)
      (<| analysis/type.with_var
          (function (_ [@var :var:]))
          (do phase.monad
            [lengthA (analysis/type.expecting Nat
                                              (phase archive lengthC))
             _ (analysis/type.inference (type (Array :var:)))]
            (in {analysis.#Extension extension (list lengthA)}))))]))

(def: array::length
  Handler
  (custom
   [<code>.any
    (function (_ extension phase archive arrayC)
      (<| analysis/type.with_var
          (function (_ [@var :var:]))
          (do phase.monad
            [arrayA (analysis/type.expecting (type (Array :var:))
                                             (phase archive arrayC))
             _ (analysis/type.inference Nat)]
            (in {analysis.#Extension extension (list arrayA)}))))]))

(def: array::read
  Handler
  (custom
   [(<>.and <code>.any <code>.any)
    (function (_ extension phase archive [indexC arrayC])
      (<| analysis/type.with_var
          (function (_ [@var :var:]))
          (do phase.monad
            [indexA (analysis/type.expecting Nat
                                             (phase archive indexC))
             arrayA (analysis/type.expecting (type (Array :var:))
                                             (phase archive arrayC))
             _ (analysis/type.inference :var:)]
            (in {analysis.#Extension extension (list indexA arrayA)}))))]))

(def: array::write
  Handler
  (custom
   [($_ <>.and <code>.any <code>.any <code>.any)
    (function (_ extension phase archive [indexC valueC arrayC])
      (<| analysis/type.with_var
          (function (_ [@var :var:]))
          (do phase.monad
            [indexA (analysis/type.expecting Nat
                                             (phase archive indexC))
             valueA (analysis/type.expecting :var:
                                             (phase archive valueC))
             arrayA (analysis/type.expecting (type (Array :var:))
                                             (phase archive arrayC))
             _ (analysis/type.inference (type (Array :var:)))]
            (in {analysis.#Extension extension (list indexA valueA arrayA)}))))]))

(def: array::delete
  Handler
  (custom
   [($_ <>.and <code>.any <code>.any)
    (function (_ extension phase archive [indexC arrayC])
      (<| analysis/type.with_var
          (function (_ [@var :var:]))
          (do phase.monad
            [indexA (analysis/type.expecting Nat
                                             (phase archive indexC))
             arrayA (analysis/type.expecting (type (Array :var:))
                                             (phase archive arrayC))
             _ (analysis/type.inference (type (Array :var:)))]
            (in {analysis.#Extension extension (list indexA arrayA)}))))]))

(def: bundle::array
  Bundle
  (<| (bundle.prefix "array")
      (|> bundle.empty
          (bundle.install "new" array::new)
          (bundle.install "length" array::length)
          (bundle.install "read" array::read)
          (bundle.install "write" array::write)
          (bundle.install "delete" array::delete)
          )))

(def: object::new
  Handler
  (custom
   [($_ <>.and <code>.any (<code>.tuple (<>.some <code>.any)))
    (function (_ extension phase archive [constructorC inputsC])
      (do [! phase.monad]
        [constructorA (analysis/type.expecting Any
                                               (phase archive constructorC))
         inputsA (monad.each ! (|>> (phase archive) (analysis/type.expecting Any)) inputsC)
         _ (analysis/type.inference .Any)]
        (in {analysis.#Extension extension (list& constructorA inputsA)})))]))

(def: object::get
  Handler
  (custom
   [($_ <>.and <code>.text <code>.any)
    (function (_ extension phase archive [fieldC objectC])
      (do phase.monad
        [objectA (analysis/type.expecting Any
                                          (phase archive objectC))
         _ (analysis/type.inference .Any)]
        (in {analysis.#Extension extension (list (analysis.text fieldC)
                                                 objectA)})))]))

(def: object::do
  Handler
  (custom
   [($_ <>.and <code>.text <code>.any (<code>.tuple (<>.some <code>.any)))
    (function (_ extension phase archive [methodC objectC inputsC])
      (do [! phase.monad]
        [objectA (analysis/type.expecting Any
                                          (phase archive objectC))
         inputsA (monad.each ! (|>> (phase archive) (analysis/type.expecting Any)) inputsC)
         _ (analysis/type.inference .Any)]
        (in {analysis.#Extension extension (list& (analysis.text methodC)
                                                  objectA
                                                  inputsA)})))]))

(def: bundle::object
  Bundle
  (<| (bundle.prefix "object")
      (|> bundle.empty
          (bundle.install "new" object::new)
          (bundle.install "get" object::get)
          (bundle.install "do" object::do)
          (bundle.install "null" (/.nullary Any))
          (bundle.install "null?" (/.unary Any Bit))
          (bundle.install "undefined" (/.nullary Any))
          (bundle.install "undefined?" (/.unary Any Bit))
          )))

(def: js::constant
  Handler
  (custom
   [<code>.text
    (function (_ extension phase archive name)
      (do phase.monad
        [_ (analysis/type.inference Any)]
        (in {analysis.#Extension extension (list (analysis.text name))})))]))

(def: js::apply
  Handler
  (custom
   [($_ <>.and <code>.any (<code>.tuple (<>.some <code>.any)))
    (function (_ extension phase archive [abstractionC inputsC])
      (do [! phase.monad]
        [abstractionA (analysis/type.expecting Any
                                               (phase archive abstractionC))
         inputsA (monad.each ! (|>> (phase archive) (analysis/type.expecting Any)) inputsC)
         _ (analysis/type.inference Any)]
        (in {analysis.#Extension extension (list& abstractionA inputsA)})))]))

(def: js::type_of
  Handler
  (custom
   [<code>.any
    (function (_ extension phase archive objectC)
      (do phase.monad
        [objectA (analysis/type.expecting Any
                                          (phase archive objectC))
         _ (analysis/type.inference .Text)]
        (in {analysis.#Extension extension (list objectA)})))]))

(def: js::function
  Handler
  (custom
   [($_ <>.and <code>.nat <code>.any)
    (function (_ extension phase archive [arity abstractionC])
      (do phase.monad
        [.let [inputT (type.tuple (list.repeated arity Any))]
         abstractionA (analysis/type.expecting (-> inputT Any)
                                               (phase archive abstractionC))
         _ (analysis/type.inference (for @.js ffi.Function
                                         Any))]
        (in {analysis.#Extension extension (list (analysis.nat arity)
                                                 abstractionA)})))]))

(def: .public bundle
  Bundle
  (<| (bundle.prefix "js")
      (|> bundle.empty
          (dictionary.merged bundle::array)
          (dictionary.merged bundle::object)

          (bundle.install "constant" js::constant)
          (bundle.install "apply" js::apply)
          (bundle.install "type-of" js::type_of)
          (bundle.install "function" js::function)
          )))
