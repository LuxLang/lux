(.module:
  [library
   [lux #*
    [abstract
     ["." monad (#+ do)]]
    [control
     ["." function]]
    [data
     ["." product]
     ["." text]
     [number
      ["f" frac]]
     [collection
      ["." dictionary]]]
    [target
     ["_" php (#+ Expression)]]]]
  ["." /// #_
   ["#." runtime (#+ Operation Phase Handler Bundle)]
   ["#." primitive]
   [//
    [extension (#+ Nullary Unary Binary Trinary
                   nullary unary binary trinary)]
    [//
     [extension
      ["." bundle]]]]])

(def: lux_procs
  Bundle
  (|> bundle.empty
      (bundle.install "is" (binary (product.uncurry _.=)))
      (bundle.install "try" (unary ///runtime.lux//try))))

(def: i64_procs
  Bundle
  (<| (bundle.prefix "i64")
      (|> bundle.empty
          (bundle.install "and" (binary (product.uncurry _.bit_and)))
          (bundle.install "or" (binary (product.uncurry _.bit_or)))
          (bundle.install "xor" (binary (product.uncurry _.bit_xor)))
          (bundle.install "left-shift" (binary (product.uncurry _.bit_shl)))
          (bundle.install "logical-right-shift" (binary (product.uncurry ///runtime.i64//logic_right_shift)))
          (bundle.install "arithmetic-right-shift" (binary (product.uncurry _.bit_shr)))
          (bundle.install "=" (binary (product.uncurry _.=)))
          (bundle.install "+" (binary (product.uncurry _.+)))
          (bundle.install "-" (binary (product.uncurry _.-)))
          )))

(def: int_procs
  Bundle
  (<| (bundle.prefix "int")
      (|> bundle.empty
          (bundle.install "<" (binary (product.uncurry _.<)))
          (bundle.install "*" (binary (product.uncurry _.*)))
          (bundle.install "/" (binary (product.uncurry _./)))
          (bundle.install "%" (binary (product.uncurry _.%)))
          (bundle.install "frac" (unary _.floatval/1))
          (bundle.install "char" (unary _.chr/1)))))

(def: frac_procs
  Bundle
  (<| (bundle.prefix "frac")
      (|> bundle.empty
          (bundle.install "+" (binary (product.uncurry _.+)))
          (bundle.install "-" (binary (product.uncurry _.-)))
          (bundle.install "*" (binary (product.uncurry _.*)))
          (bundle.install "/" (binary (product.uncurry _./)))
          (bundle.install "%" (binary (product.uncurry _.%)))
          (bundle.install "=" (binary (product.uncurry _.=)))
          (bundle.install "<" (binary (product.uncurry _.<)))
          (bundle.install "int" (unary _.intval/1))
          (bundle.install "encode" (unary _.strval/1))
          (bundle.install "decode" (unary (|>> _.floatval/1 ///runtime.some)))
          )))

(def: (text//index [startO partO textO])
  (Trinary (Expression Any))
  (///runtime.text//index textO partO startO))

(def: text_procs
  Bundle
  (<| (bundle.prefix "text")
      (|> bundle.empty
          (bundle.install "=" (binary (product.uncurry _.=)))
          (bundle.install "<" (binary (product.uncurry _.<)))
          (bundle.install "concat" (binary (product.uncurry _.concat)))
          (bundle.install "index" (trinary text//index))
          (bundle.install "size" (unary _.strlen/1))
          (bundle.install "char" (binary (function (text//char [text idx])
                                           (|> text (_.nth idx) _.ord/1))))
          (bundle.install "clip" (trinary (function (text//clip [from to text])
                                            (_.substr/3 [text from (_.- from to)]))))
          )))

(def: io_procs
  Bundle
  (<| (bundle.prefix "io")
      (|> bundle.empty
          (bundle.install "log" (unary (|>> (_.concat (_.string text.new_line)) _.print/1)))
          (bundle.install "error" (unary ///runtime.io//throw!))
          (bundle.install "exit" (unary _.exit/1))
          (bundle.install "current-time" (nullary (|>> _.time/0 (_.* (_.int +1,000))))))))

(def: #export bundle
  Bundle
  (<| (bundle.prefix "lux")
      (|> lux_procs
          (dictionary.merged i64_procs)
          (dictionary.merged int_procs)
          (dictionary.merged frac_procs)
          (dictionary.merged text_procs)
          (dictionary.merged io_procs)
          )))
