(.module:
  [library
   [lux "*"
    [abstract
     [monad {"+" [do]}]]
    [data
     ["." text
      ["%" format {"+" [format]}]]
     [collection
      ["." list ("#\." functor)]
      ["." dictionary {"+" [Dictionary]}]]]]]
  [// {"+" [Handler Bundle]}])

(def: .public empty
  Bundle
  (dictionary.empty text.hash))

(def: .public (install name anonymous)
  (All (_ s i o)
    (-> Text (Handler s i o)
        (-> (Bundle s i o) (Bundle s i o))))
  (dictionary.has name anonymous))

(def: .public (prefix prefix)
  (All (_ s i o)
    (-> Text (-> (Bundle s i o) (Bundle s i o))))
  (|>> dictionary.entries
       (list\each (function (_ [key val]) [(format prefix " " key) val]))
       (dictionary.of_list text.hash)))
