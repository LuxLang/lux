(.module:
  [library
   [lux (#- nat int rev)
    [abstract
     monad]]]
  ["." // #_
   ["#." type]
   ["/#" // #_
    [//
     ["/" analysis (#+ Analysis Operation)]
     [///
      ["#" phase]]]]])

(template [<name> <type> <tag>]
  [(def: .public (<name> value)
     (-> <type> (Operation Analysis))
     (do ///.monad
       [_ (//type.infer <type>)]
       (in (#/.Primitive (<tag> value)))))]

  [bit  .Bit  #/.Bit]
  [nat  .Nat  #/.Nat]
  [int  .Int  #/.Int]
  [rev  .Rev  #/.Rev]
  [frac .Frac #/.Frac]
  [text .Text #/.Text]
  )

(def: .public unit
  (Operation Analysis)
  (do ///.monad
    [_ (//type.infer .Any)]
    (in (#/.Primitive #/.Unit))))
