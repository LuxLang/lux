(.using
 [library
  [lux {"-" Type Label Primitive}
   [abstract
    ["[0]" monad {"+" do}]]
   [control
    ["[0]" try]
    ["[0]" exception {"+" exception:}]
    ["<>" parser
     ["<[0]>" synthesis {"+" Parser}]]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list ("[1]#[0]" monad)]
     ["[0]" dictionary]]]
   [math
    [number
     ["f" frac]
     ["[0]" i32]]]
   [target
    [jvm
     ["_" bytecode {"+" Label Bytecode} ("[1]#[0]" monad)]
     [encoding
      ["[0]" signed {"+" S4}]]
     ["[0]" type {"+" Type}
      [category {"+" Primitive Class}]]]]]]
 ["[0]" ///// "_"
  [generation
   [extension {"+" Nullary Unary Binary Trinary Variadic
               nullary unary binary trinary variadic}]
   ["///" jvm "_"
    ["[1][0]" value]
    ["[1][0]" runtime {"+" Operation Phase Bundle Handler}]
    ["[1][0]" function "_"
     ["[1]" abstract]]]]
  [extension
   ["[1]extension" /]
   ["[1][0]" bundle]]
  [//
   ["/[1][0]" synthesis {"+" Synthesis %synthesis}]
   [///
    ["[1]" phase]
    [meta
     [archive {"+" Archive}]]]]])

(def: .public (custom [parser handler])
  (All (_ s)
    (-> [(Parser s)
         (-> Text Phase Archive s (Operation (Bytecode Any)))]
        Handler))
  (function (_ extension_name phase archive input)
    (case (<synthesis>.result parser input)
      {try.#Success input'}
      (handler extension_name phase archive input')

      {try.#Failure error}
      (/////.except /////extension.invalid_syntax [extension_name //////synthesis.%synthesis input]))))

(def: $Boolean (type.class "java.lang.Boolean" (list)))
(def: $Double (type.class "java.lang.Double" (list)))
(def: $Character (type.class "java.lang.Character" (list)))
(def: $String (type.class "java.lang.String" (list)))
(def: $CharSequence (type.class "java.lang.CharSequence" (list)))
(def: $Object (type.class "java.lang.Object" (list)))
(def: $PrintStream (type.class "java.io.PrintStream" (list)))
(def: $System (type.class "java.lang.System" (list)))
(def: $Error (type.class "java.lang.Error" (list)))

(def: lux_int
  (Bytecode Any)
  ($_ _.composite
      _.i2l
      (///value.wrap type.long)))

(def: jvm_int
  (Bytecode Any)
  ($_ _.composite
      (///value.unwrap type.long)
      _.l2i))

(def: (predicate bytecode)
  (-> (-> Label (Bytecode Any))
      (Bytecode Any))
  (do _.monad
    [@then _.new_label
     @end _.new_label]
    ($_ _.composite
        (bytecode @then)
        (_.getstatic $Boolean "FALSE" $Boolean)
        (_.goto @end)
        (_.set_label @then)
        (_.getstatic $Boolean "TRUE" $Boolean)
        (_.set_label @end)
        )))

... TODO: Get rid of this ASAP
(def: lux::syntax_char_case!
  (..custom [($_ <>.and
                 <synthesis>.any
                 <synthesis>.any
                 (<>.some (<synthesis>.tuple ($_ <>.and
                                                 (<synthesis>.tuple (<>.many <synthesis>.i64))
                                                 <synthesis>.any))))
             (function (_ extension_name phase archive [inputS elseS conditionalsS])
               (do [! /////.monad]
                 [@end ///runtime.forge_label
                  inputG (phase archive inputS)
                  elseG (phase archive elseS)
                  conditionalsG+ (: (Operation (List [(List [S4 Label])
                                                      (Bytecode Any)]))
                                    (monad.each ! (function (_ [chars branch])
                                                    (do !
                                                      [branchG (phase archive branch)
                                                       @branch ///runtime.forge_label]
                                                      (in [(list#each (function (_ char)
                                                                        [(try.trusted (signed.s4 (.int char))) @branch])
                                                                      chars)
                                                           ($_ _.composite
                                                               (_.set_label @branch)
                                                               branchG
                                                               (_.goto @end))])))
                                                conditionalsS))
                  .let [table (|> conditionalsG+
                                  (list#each product.left)
                                  list#conjoint)
                        conditionalsG (|> conditionalsG+
                                          (list#each product.right)
                                          (monad.all _.monad))]]
                 (in (do _.monad
                       [@else _.new_label]
                       ($_ _.composite
                           inputG (///value.unwrap type.long) _.l2i
                           (_.lookupswitch @else table)
                           conditionalsG
                           (_.set_label @else)
                           elseG
                           (_.set_label @end)
                           )))))]))

(def: (lux::is [referenceG sampleG])
  (Binary (Bytecode Any))
  ($_ _.composite
      referenceG
      sampleG
      (..predicate _.if_acmpeq)))

(def: (lux::try riskyG)
  (Unary (Bytecode Any))
  ($_ _.composite
      riskyG
      (_.checkcast ///function.class)
      ///runtime.try))

(def: bundle::lux
  Bundle
  (|> (: Bundle /////bundle.empty)
      (/////bundle.install "syntax char case!" ..lux::syntax_char_case!)
      (/////bundle.install "is" (binary ..lux::is))
      (/////bundle.install "try" (unary ..lux::try))))

(template [<name> <op>]
  [(def: (<name> [maskG inputG])
     (Binary (Bytecode Any))
     ($_ _.composite
         inputG (///value.unwrap type.long)
         maskG (///value.unwrap type.long)
         <op> (///value.wrap type.long)))]

  [i64::and _.land]
  [i64::or  _.lor]
  [i64::xor _.lxor]
  )

(template [<name> <op>]
  [(def: (<name> [shiftG inputG])
     (Binary (Bytecode Any))
     ($_ _.composite
         inputG (///value.unwrap type.long)
         shiftG ..jvm_int
         <op> (///value.wrap type.long)))]

  [i64::left_shifted  _.lshl]
  [i64::right_shifted _.lushr]
  )

(template [<name> <type> <op>]
  [(def: (<name> [paramG subjectG])
     (Binary (Bytecode Any))
     ($_ _.composite
         subjectG (///value.unwrap <type>)
         paramG (///value.unwrap <type>)
         <op> (///value.wrap <type>)))]

  [i64::+ type.long   _.ladd]
  [i64::- type.long   _.lsub]
  [i64::* type.long   _.lmul]
  [i64::/ type.long   _.ldiv]
  [i64::% type.long   _.lrem]
  
  [f64::+ type.double _.dadd]
  [f64::- type.double _.dsub]
  [f64::* type.double _.dmul]
  [f64::/ type.double _.ddiv]
  [f64::% type.double _.drem]
  )

(template [<eq> <lt> <type> <cmp>]
  [(template [<name> <reference>]
     [(def: (<name> [paramG subjectG])
        (Binary (Bytecode Any))
        ($_ _.composite
            subjectG (///value.unwrap <type>)
            paramG (///value.unwrap <type>)
            <cmp>
            <reference>
            (..predicate _.if_icmpeq)))]
     
     [<eq> _.iconst_0]
     [<lt> _.iconst_m1])]

  [i64::= i64::< type.long   _.lcmp]
  [f64::= f64::< type.double _.dcmpg]
  )

(def: (::toString class from)
  (-> (Type Class) (Type Primitive) (Bytecode Any))
  (_.invokestatic class "toString" (type.method [(list) (list from) ..$String (list)])))

(template [<name> <prepare> <transform>]
  [(def: (<name> inputG)
     (Unary (Bytecode Any))
     ($_ _.composite
         inputG
         <prepare>
         <transform>))]

  [i64::f64
   (///value.unwrap type.long)
   ($_ _.composite
       _.l2d
       (///value.wrap type.double))]

  [i64::char
   (///value.unwrap type.long)
   ($_ _.composite
       _.l2i
       _.i2c
       (..::toString ..$Character type.char))]

  [f64::i64
   (///value.unwrap type.double)
   ($_ _.composite
       _.d2l
       (///value.wrap type.long))]
  
  [f64::encode
   (///value.unwrap type.double)
   (..::toString ..$Double type.double)]
  
  [f64::decode
   (_.checkcast $String)
   ///runtime.decode_frac]
  )

(def: bundle::i64
  Bundle
  (<| (/////bundle.prefix "i64")
      (|> (: Bundle /////bundle.empty)
          (/////bundle.install "and" (binary ..i64::and))
          (/////bundle.install "or" (binary ..i64::or))
          (/////bundle.install "xor" (binary ..i64::xor))
          (/////bundle.install "left-shift" (binary ..i64::left_shifted))
          (/////bundle.install "right-shift" (binary ..i64::right_shifted))
          (/////bundle.install "=" (binary ..i64::=))
          (/////bundle.install "<" (binary ..i64::<))
          (/////bundle.install "+" (binary ..i64::+))
          (/////bundle.install "-" (binary ..i64::-))
          (/////bundle.install "*" (binary ..i64::*))
          (/////bundle.install "/" (binary ..i64::/))
          (/////bundle.install "%" (binary ..i64::%))
          (/////bundle.install "f64" (unary ..i64::f64))
          (/////bundle.install "char" (unary ..i64::char)))))

(def: bundle::f64
  Bundle
  (<| (/////bundle.prefix "f64")
      (|> (: Bundle /////bundle.empty)
          (/////bundle.install "+" (binary ..f64::+))
          (/////bundle.install "-" (binary ..f64::-))
          (/////bundle.install "*" (binary ..f64::*))
          (/////bundle.install "/" (binary ..f64::/))
          (/////bundle.install "%" (binary ..f64::%))
          (/////bundle.install "=" (binary ..f64::=))
          (/////bundle.install "<" (binary ..f64::<))
          (/////bundle.install "i64" (unary ..f64::i64))
          (/////bundle.install "encode" (unary ..f64::encode))
          (/////bundle.install "decode" (unary ..f64::decode)))))

(def: (text::size inputG)
  (Unary (Bytecode Any))
  ($_ _.composite
      inputG
      (_.checkcast $String)
      (_.invokevirtual ..$String "length" (type.method [(list) (list) type.int (list)]))
      ..lux_int))

(def: no_op (Bytecode Any) (_#in []))

(template [<name> <pre_subject> <pre_param> <op> <post>]
  [(def: (<name> [paramG subjectG])
     (Binary (Bytecode Any))
     ($_ _.composite
         subjectG <pre_subject>
         paramG <pre_param>
         <op> <post>))]

  [text::= ..no_op ..no_op
   (_.invokevirtual ..$Object "equals" (type.method [(list) (list ..$Object) type.boolean (list)]))
   (///value.wrap type.boolean)]
  [text::< (_.checkcast $String) (_.checkcast $String)
   (_.invokevirtual ..$String "compareTo" (type.method [(list) (list ..$String) type.int (list)]))
   (..predicate _.iflt)]
  [text::char (_.checkcast $String) ..jvm_int
   (_.invokevirtual ..$String "charAt" (type.method [(list) (list type.int) type.char (list)]))
   ..lux_int]
  )

(def: (text::concat [leftG rightG])
  (Binary (Bytecode Any))
  ($_ _.composite
      leftG (_.checkcast $String)
      rightG (_.checkcast $String)
      (_.invokevirtual ..$String "concat" (type.method [(list) (list ..$String) ..$String (list)]))))

(def: (text::clip [offset! length! subject!])
  (Trinary (Bytecode Any))
  ($_ _.composite
      subject! (_.checkcast $String)
      offset! ..jvm_int
      _.dup
      length! ..jvm_int
      _.iadd
      (_.invokevirtual ..$String "substring" (type.method [(list) (list type.int type.int) ..$String (list)]))))

(def: index_method (type.method [(list) (list ..$String type.int) type.int (list)]))
(def: (text::index [startG partG textG])
  (Trinary (Bytecode Any))
  (do _.monad
    [@not_found _.new_label
     @end _.new_label]
    ($_ _.composite
        textG (_.checkcast $String)
        partG (_.checkcast $String)
        startG ..jvm_int
        (_.invokevirtual ..$String "indexOf" index_method)
        _.dup
        _.iconst_m1
        (_.if_icmpeq @not_found)
        ..lux_int
        ///runtime.some_injection
        (_.goto @end)
        (_.set_label @not_found)
        _.pop
        ///runtime.none_injection
        (_.set_label @end))))

(def: bundle::text
  Bundle
  (<| (/////bundle.prefix "text")
      (|> (: Bundle /////bundle.empty)
          (/////bundle.install "=" (binary ..text::=))
          (/////bundle.install "<" (binary ..text::<))
          (/////bundle.install "concat" (binary ..text::concat))
          (/////bundle.install "index" (trinary ..text::index))
          (/////bundle.install "size" (unary ..text::size))
          (/////bundle.install "char" (binary ..text::char))
          (/////bundle.install "clip" (trinary ..text::clip)))))

(def: string_method (type.method [(list) (list ..$String) type.void (list)]))
(def: (io::log messageG)
  (Unary (Bytecode Any))
  ($_ _.composite
      (_.getstatic ..$System "out" ..$PrintStream)
      messageG
      (_.checkcast $String)
      (_.invokevirtual ..$PrintStream "println" ..string_method)
      ///runtime.unit))

(def: (io::error messageG)
  (Unary (Bytecode Any))
  ($_ _.composite
      (_.new ..$Error)
      _.dup
      messageG
      (_.checkcast $String)
      (_.invokespecial ..$Error "<init>" ..string_method)
      _.athrow))

(def: bundle::io
  Bundle
  (<| (/////bundle.prefix "io")
      (|> (: Bundle /////bundle.empty)
          (/////bundle.install "log" (unary ..io::log))
          (/////bundle.install "error" (unary ..io::error)))))

(def: .public bundle
  Bundle
  (<| (/////bundle.prefix "lux")
      (|> bundle::lux
          (dictionary.merged ..bundle::i64)
          (dictionary.merged ..bundle::f64)
          (dictionary.merged ..bundle::text)
          (dictionary.merged ..bundle::io))))
