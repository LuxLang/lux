(.using
 [library
  [lux {"-" Definition}
   ["[0]" ffi {"+" import: do_to object}]
   [abstract
    [monad {"+" do}]]
   [control
    ["[0]" maybe]
    ["[0]" try {"+" Try}]
    ["[0]" exception {"+" exception:}]
    ["[0]" io {"+" IO io}]
    [concurrency
     ["[0]" atom {"+" Atom atom}]]]
   [data
    [binary {"+" Binary}]
    ["[0]" product]
    ["[0]" text ("[1]#[0]" hash)
     ["%" format {"+" format}]]
    [collection
     ["[0]" array]
     ["[0]" dictionary {"+" Dictionary}]
     ["[0]" sequence]]
    ["[0]" format "_"
     ["[1]" binary]]]
   [target
    [jvm
     ["_" bytecode {"+" Bytecode}]
     ["[0]" loader {"+" Library}]
     ["[0]" modifier {"+" Modifier} ("[1]#[0]" monoid)]
     ["[0]" field {"+" Field}]
     ["[0]" method {"+" Method}]
     ["[0]" version]
     ["[0]" class {"+" Class}]
     ["[0]" encoding "_"
      ["[1]/[0]" name]]
     ["[0]" type
      ["[0]" descriptor]]]]
   [tool
    [compiler
     [meta
      [io {"+" lux_context}]
      [archive
       ["[0]" unit]]]]]]]
 ["[0]" // "_"
  ["[1][0]" runtime {"+" Definition}]
  ["[1][0]" type]
  ["[1][0]" value]]
 )

(import: java/lang/reflect/Field
  "[1]::[0]"
  (get ["?" java/lang/Object] "try" "?" java/lang/Object))

(import: (java/lang/Class a)
  "[1]::[0]"
  (getField [java/lang/String] "try" java/lang/reflect/Field))

(import: java/lang/Object
  "[1]::[0]"
  (getClass [] (java/lang/Class java/lang/Object)))

(import: java/lang/ClassLoader
  "[1]::[0]")

(def: value::modifier ($_ modifier#composite field.public field.final field.static))

(def: init::type (type.method [(list) (list) type.void (list)]))
(def: init::modifier ($_ modifier#composite method.public method.static method.strict))

(exception: .public (cannot_load [class Text
                                  error Text])
  (exception.report
   ["Class" class]
   ["Error" error]))

(exception: .public (invalid_field [class Text
                                    field Text
                                    error Text])
  (exception.report
   ["Class" class]
   ["Field" field]
   ["Error" error]))

(exception: .public (invalid_value [class Text])
  (exception.report
   ["Class" class]))

(def: (class_value class_name class)
  (-> Text (java/lang/Class java/lang/Object) (Try Any))
  (case (java/lang/Class::getField //value.field class)
    {try.#Success field}
    (case (java/lang/reflect/Field::get {.#None} field)
      {try.#Success ?value}
      (case ?value
        {.#Some value}
        {try.#Success value}
        
        {.#None}
        (exception.except ..invalid_value [class_name]))
      
      {try.#Failure error}
      (exception.except ..cannot_load [class_name error]))
    
    {try.#Failure error}
    (exception.except ..invalid_field [class_name //value.field error])))

(def: class_path_separator
  ".")

(def: (evaluate! library loader eval_class [@it valueG])
  (-> Library java/lang/ClassLoader Text [(Maybe unit.ID) (Bytecode Any)] (Try [Any Definition]))
  (let [bytecode_name (text.replaced class_path_separator .module_separator eval_class)
        :value: (case @it
                  {.#Some @it}
                  (type.class (//runtime.class_name @it) (list))

                  {.#None}
                  //type.value)
        bytecode (class.class version.v6_0
                              class.public
                              (encoding/name.internal bytecode_name)
                              {.#None}
                              (encoding/name.internal "java.lang.Object") (list)
                              (list (field.field ..value::modifier //value.field #0 :value: (sequence.sequence)))
                              (list (method.method ..init::modifier "<clinit>"
                                                   #0 ..init::type
                                                   (list)
                                                   {.#Some
                                                    ($_ _.composite
                                                        valueG
                                                        (_.putstatic (type.class bytecode_name (list)) //value.field :value:)
                                                        _.return)}))
                              (sequence.sequence))]
    (io.run! (do [! (try.with io.monad)]
               [bytecode (# ! each (format.result class.writer)
                            (io.io bytecode))
                _ (loader.store eval_class bytecode library)
                class (loader.load eval_class loader)
                value (# io.monad in (class_value eval_class class))]
               (in [value
                    [eval_class bytecode]])))))

(def: (execute! library loader [class_name class_bytecode])
  (-> Library java/lang/ClassLoader Definition (Try Any))
  (io.run! (do (try.with io.monad)
             [existing_class? (|> (atom.read! library)
                                  (# io.monad each (function (_ library)
                                                     (dictionary.key? library class_name)))
                                  (try.lifted io.monad)
                                  (: (IO (Try Bit))))
              _ (if existing_class?
                  (in [])
                  (loader.store class_name class_bytecode library))]
             (loader.load class_name loader))))

(def: (define! library loader context custom @it,valueG)
  (-> Library java/lang/ClassLoader unit.ID (Maybe Text) [(Maybe unit.ID) (Bytecode Any)] (Try [Text Any Definition]))
  (let [class_name (maybe.else (//runtime.class_name context)
                               custom)]
    (do try.monad
      [[value definition] (evaluate! library loader class_name @it,valueG)]
      (in [class_name value definition]))))

(def: .public host
  (IO [java/lang/ClassLoader //runtime.Host])
  (io (let [library (loader.new_library [])
            loader (loader.memory library)]
        [loader
         (: //runtime.Host
            (implementation
             (def: (evaluate context @it,valueG)
               (# try.monad each product.left
                  (..evaluate! library loader (format "E" (//runtime.class_name context)) @it,valueG)))
             
             (def: execute
               (..execute! library loader))
             
             (def: define
               (..define! library loader))

             (def: (ingest context bytecode)
               [(//runtime.class_name context) bytecode])

             (def: (re_learn context custom [_ bytecode])
               (io.run! (loader.store (maybe.else (//runtime.class_name context) custom) bytecode library)))
             
             (def: (re_load context custom [directive_name bytecode])
               (io.run!
                (do (try.with io.monad)
                  [.let [class_name (maybe.else (//runtime.class_name context)
                                                custom)]
                   _ (loader.store class_name bytecode library)
                   class (loader.load class_name loader)]
                  (# io.monad in (..class_value class_name class)))))
             ))])))
