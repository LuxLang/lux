(.module:
  [library
   [lux (#- Module)
    [abstract
     [monad (#+ do)]]
    [control
     ["." try (#+ Try)]
     ["." exception (#+ exception:)]
     ["." function]]
    [data
     [binary (#+ Binary)]
     ["." product]
     ["." name]
     ["." text ("#\." equivalence)
      ["%" format (#+ format)]]
     [collection
      ["." row (#+ Row)]
      ["." list ("#\." functor)]]]
    [math
     [number
      ["n" nat]]]]]
  [//
   [synthesis (#+ Synthesis)]
   [phase
    ["." extension]]
   [///
    ["." phase]
    [meta
     ["." archive (#+ Archive)
      ["." descriptor (#+ Module)]
      ["." artifact]]]]])

(type: #export Context
  [archive.ID artifact.ID])

(type: #export (Buffer directive)
  (Row [artifact.ID (Maybe Text) directive]))

(exception: #export (cannot_interpret {error Text})
  (exception.report
   ["Error" error]))

(template [<name>]
  [(exception: #export (<name> {artifact_id artifact.ID})
     (exception.report
      ["Artifact ID" (%.nat artifact_id)]))]

  [cannot_overwrite_output]
  [no_buffer_for_saving_code]
  )

(interface: #export (Host expression directive)
  (: (-> Context expression (Try Any))
     evaluate!)
  (: (-> directive (Try Any))
     execute!)
  (: (-> Context (Maybe Text) expression (Try [Text Any directive]))
     define!)

  (: (-> Context Binary directive)
     ingest)
  (: (-> Context (Maybe Text) directive (Try Any))
     re_learn)
  (: (-> Context (Maybe Text) directive (Try Any))
     re_load))

(type: #export (State anchor expression directive)
  {#module Module
   #anchor (Maybe anchor)
   #host (Host expression directive)
   #buffer (Maybe (Buffer directive))
   #registry artifact.Registry
   #counter Nat
   #context (Maybe artifact.ID)
   #log (Row Text)})

(template [<special> <general>]
  [(type: #export (<special> anchor expression directive)
     (<general> (State anchor expression directive) Synthesis expression))]

  [State+    extension.State]
  [Operation extension.Operation]
  [Phase     extension.Phase]
  [Handler   extension.Handler]
  [Bundle    extension.Bundle]
  [Extender  extension.Extender]
  )

(def: #export (state host module)
  (All [anchor expression directive]
    (-> (Host expression directive)
        Module
        (..State anchor expression directive)))
  {#module module
   #anchor #.None
   #host host
   #buffer #.None
   #registry artifact.empty
   #counter 0
   #context #.None
   #log row.empty})

(def: #export empty_buffer Buffer row.empty)

(template [<tag>
           <with_declaration> <with_type> <with_value>
           <set> <get> <get_type> <exception>]
  [(exception: #export <exception>)

   (def: #export <with_declaration>
     (All [anchor expression directive output] <with_type>)
     (function (_ body)
       (function (_ [bundle state])
         (case (body [bundle (set@ <tag> (#.Some <with_value>) state)])
           (#try.Success [[bundle' state'] output])
           (#try.Success [[bundle' (set@ <tag> (get@ <tag> state) state')]
                          output])

           (#try.Failure error)
           (#try.Failure error)))))

   (def: #export <get>
     (All [anchor expression directive]
       (Operation anchor expression directive <get_type>))
     (function (_ (^@ stateE [bundle state]))
       (case (get@ <tag> state)
         (#.Some output)
         (#try.Success [stateE output])

         #.None
         (exception.throw <exception> []))))

   (def: #export (<set> value)
     (All [anchor expression directive]
       (-> <get_type> (Operation anchor expression directive Any)))
     (function (_ [bundle state])
       (#try.Success [[bundle (set@ <tag> (#.Some value) state)]
                      []])))]

  [#anchor
   (with_anchor anchor)
   (-> anchor (Operation anchor expression directive output)
       (Operation anchor expression directive output))
   anchor
   set_anchor anchor anchor no_anchor]

  [#buffer
   with_buffer
   (-> (Operation anchor expression directive output)
       (Operation anchor expression directive output))
   ..empty_buffer
   set_buffer buffer (Buffer directive) no_active_buffer]
  )

(def: #export get_registry
  (All [anchor expression directive]
    (Operation anchor expression directive artifact.Registry))
  (function (_ (^@ stateE [bundle state]))
    (#try.Success [stateE (get@ #registry state)])))

(def: #export (set_registry value)
  (All [anchor expression directive]
    (-> artifact.Registry (Operation anchor expression directive Any)))
  (function (_ [bundle state])
    (#try.Success [[bundle (set@ #registry value state)]
                   []])))

(def: #export next
  (All [anchor expression directive]
    (Operation anchor expression directive Nat))
  (do phase.monad
    [count (extension.read (get@ #counter))
     _ (extension.update (update@ #counter inc))]
    (wrap count)))

(def: #export (gensym prefix)
  (All [anchor expression directive]
    (-> Text (Operation anchor expression directive Text)))
  (\ phase.monad map (|>> %.nat (format prefix)) ..next))

(def: #export (enter_module module)
  (All [anchor expression directive]
    (-> Module (Operation anchor expression directive Any)))
  (extension.update (set@ #module module)))

(def: #export module
  (All [anchor expression directive]
    (Operation anchor expression directive Module))
  (extension.read (get@ #module)))

(def: #export (evaluate! label code)
  (All [anchor expression directive]
    (-> Context expression (Operation anchor expression directive Any)))
  (function (_ (^@ state+ [bundle state]))
    (case (\ (get@ #host state) evaluate! label code)
      (#try.Success output)
      (#try.Success [state+ output])

      (#try.Failure error)
      (exception.throw ..cannot_interpret error))))

(def: #export (execute! code)
  (All [anchor expression directive]
    (-> directive (Operation anchor expression directive Any)))
  (function (_ (^@ state+ [bundle state]))
    (case (\ (get@ #host state) execute! code)
      (#try.Success output)
      (#try.Success [state+ output])

      (#try.Failure error)
      (exception.throw ..cannot_interpret error))))

(def: #export (define! context custom code)
  (All [anchor expression directive]
    (-> Context (Maybe Text) expression (Operation anchor expression directive [Text Any directive])))
  (function (_ (^@ stateE [bundle state]))
    (case (\ (get@ #host state) define! context custom code)
      (#try.Success output)
      (#try.Success [stateE output])

      (#try.Failure error)
      (exception.throw ..cannot_interpret error))))

(def: #export (save! artifact_id custom code)
  (All [anchor expression directive]
    (-> artifact.ID (Maybe Text) directive (Operation anchor expression directive Any)))
  (do {! phase.monad}
    [?buffer (extension.read (get@ #buffer))]
    (case ?buffer
      (#.Some buffer)
      ## TODO: Optimize by no longer checking for overwrites...
      (if (row.any? (|>> product.left (n.= artifact_id)) buffer)
        (phase.throw ..cannot_overwrite_output [artifact_id])
        (extension.update (set@ #buffer (#.Some (row.add [artifact_id custom code] buffer)))))
      
      #.None
      (phase.throw ..no_buffer_for_saving_code [artifact_id]))))

(template [<name> <artifact>]
  [(def: #export (<name> name)
     (All [anchor expression directive]
       (-> Text (Operation anchor expression directive artifact.ID)))
     (function (_ (^@ stateE [bundle state]))
       (let [[id registry'] (<artifact> name (get@ #registry state))]
         (#try.Success [[bundle (set@ #registry registry' state)]
                        id]))))]

  [learn artifact.definition]
  [learn_custom artifact.custom]
  [learn_analyser artifact.analyser]
  [learn_synthesizer artifact.synthesizer]
  [learn_generator artifact.generator]
  [learn_directive artifact.directive]
  )

(exception: #export (unknown_definition {name Name}
                                        {known_definitions (List Text)})
  (exception.report
   ["Definition" (name.short name)]
   ["Module" (name.module name)]
   ["Known Definitions" (exception.enumerate function.identity known_definitions)]))

(def: #export (remember archive name)
  (All [anchor expression directive]
    (-> Archive Name (Operation anchor expression directive Context)))
  (function (_ (^@ stateE [bundle state]))
    (let [[_module _name] name]
      (do try.monad
        [module_id (archive.id _module archive)
         registry (if (text\= (get@ #module state) _module)
                    (#try.Success (get@ #registry state))
                    (do try.monad
                      [[descriptor document] (archive.find _module archive)]
                      (#try.Success (get@ #descriptor.registry descriptor))))]
        (case (artifact.remember _name registry)
          #.None
          (exception.throw ..unknown_definition [name (artifact.definitions registry)])
          
          (#.Some id)
          (#try.Success [stateE [module_id id]]))))))

(exception: #export no_context)

(def: #export (module_id module archive)
  (All [anchor expression directive]
    (-> Module Archive (Operation anchor expression directive archive.ID)))
  (function (_ (^@ stateE [bundle state]))
    (do try.monad
      [module_id (archive.id module archive)]
      (wrap [stateE module_id]))))

(def: #export (context archive)
  (All [anchor expression directive]
    (-> Archive (Operation anchor expression directive Context)))
  (function (_ (^@ stateE [bundle state]))
    (case (get@ #context state)
      #.None
      (exception.throw ..no_context [])
      
      (#.Some id)
      (do try.monad
        [module_id (archive.id (get@ #module state) archive)]
        (wrap [stateE [module_id id]])))))

(def: #export (with_context id body)
  (All [anchor expression directive a]
    (-> artifact.ID
        (Operation anchor expression directive a)
        (Operation anchor expression directive a)))
  (function (_ [bundle state])
    (do try.monad
      [[[bundle' state'] output] (body [bundle (set@ #context (#.Some id) state)])]
      (wrap [[bundle' (set@ #context (get@ #context state) state')]
             output]))))

(def: #export (with_new_context archive body)
  (All [anchor expression directive a]
    (-> Archive (Operation anchor expression directive a)
        (Operation anchor expression directive [Context a])))
  (function (_ (^@ stateE [bundle state]))
    (let [[id registry'] (artifact.resource (get@ #registry state))]
      (do try.monad
        [[[bundle' state'] output] (body [bundle (|> state
                                                     (set@ #registry registry')
                                                     (set@ #context (#.Some id)))])
         module_id (archive.id (get@ #module state) archive)]
        (wrap [[bundle' (set@ #context (get@ #context state) state')]
               [[module_id id]
                output]])))))

(def: #export (log! message)
  (All [anchor expression directive a]
    (-> Text (Operation anchor expression directive Any)))
  (function (_ [bundle state])
    (#try.Success [[bundle
                    (update@ #log (row.add message) state)]
                   []])))
