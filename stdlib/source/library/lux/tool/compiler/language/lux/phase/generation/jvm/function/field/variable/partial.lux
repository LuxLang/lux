(.module:
  [library
   [lux (#- Type)
    [abstract
     ["." monad]]
    [data
     [number
      ["n" nat]]
     [collection
      ["." list ("#\." functor)]
      ["." row]]]
    [target
     [jvm
      ["." field (#+ Field)]
      ["_" bytecode (#+ Label Bytecode) ("#\." monad)]
      [type (#+ Type)
       [category (#+ Class)]]
      [constant
       [pool (#+ Resource)]]]]]]
  ["." / #_
   ["#." count]
   ["/#" //
    ["/#" // #_
     [constant
      ["#." arity]]
     ["//#" /// #_
      ["#." reference]
      [//////
       ["." arity (#+ Arity)]
       [reference
        [variable (#+ Register)]]]]]]])

(def: .public (initial amount)
  (-> Nat (Bytecode Any))
  ($_ _.composite
      (|> _.aconst_null
          (list.repeated amount)
          (monad.seq _.monad))
      (_\in [])))

(def: .public (get class register)
  (-> (Type Class) Register (Bytecode Any))
  (//.get class (/////reference.partial_name register)))

(def: .public (put class register value)
  (-> (Type Class) Register (Bytecode Any) (Bytecode Any))
  (//.put /////reference.partial_name class register value))

(def: .public variables
  (-> Arity (List (Resource Field)))
  (|>> (n.- ///arity.minimum) (//.variables /////reference.partial_name)))

(def: .public (new arity)
  (-> Arity (Bytecode Any))
  (if (arity.multiary? arity)
    ($_ _.composite
        /count.initial
        (initial (n.- ///arity.minimum arity)))
    (_\in [])))
