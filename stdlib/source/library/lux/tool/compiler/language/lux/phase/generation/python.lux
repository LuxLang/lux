(.module:
  [library
   [lux #*
    [abstract
     [monad (#+ do)]]
    [control
     ["." exception (#+ exception:)]]
    [target
     ["_" python]]]]
  ["." / #_
   [runtime (#+ Phase Phase!)]
   ["#." primitive]
   ["#." structure]
   ["#." reference]
   ["#." function]
   ["#." case]
   ["#." loop]
   ["/#" // #_
    ["#." reference]
    ["/#" // #_
     ["#." extension]
     ["/#" // #_
      [analysis (#+)]
      ["#." synthesis]
      ["//#" /// #_
       ["#." phase ("#\." monad)]
       [reference (#+)
        [variable (#+)]]]]]]])

(def: (statement expression archive synthesis)
  Phase!
  (case synthesis
    (^template [<tag>]
      [(^ (<tag> value))
       (//////phase\map _.return (expression archive synthesis))])
    ([////synthesis.bit]
     [////synthesis.i64]
     [////synthesis.f64]
     [////synthesis.text]
     [////synthesis.variant]
     [////synthesis.tuple]
     [#////synthesis.Reference]
     [////synthesis.branch/get]
     [////synthesis.function/apply]
     [#////synthesis.Extension])

    (^ (////synthesis.branch/case case))
    (/case.case! false statement expression archive case)

    (^template [<tag> <generator>]
      [(^ (<tag> value))
       (<generator> statement expression archive value)])
    ([////synthesis.branch/let /case.let!]
     [////synthesis.branch/if /case.if!]
     [////synthesis.loop/scope /loop.scope!]
     [////synthesis.loop/recur /loop.recur!])

    (^ (////synthesis.function/abstraction abstraction))
    (//////phase\map _.return (/function.function statement expression archive abstraction))
    ))

(exception: #export cannot-recur-as-an-expression)

(def: #export (expression archive synthesis)
  Phase
  (case synthesis
    (^template [<tag> <generator>]
      [(^ (<tag> value))
       (//////phase\wrap (<generator> value))])
    ([////synthesis.bit  /primitive.bit]
     [////synthesis.i64  /primitive.i64]
     [////synthesis.f64  /primitive.f64]
     [////synthesis.text /primitive.text])

    (^ (////synthesis.variant variantS))
    (/structure.variant expression archive variantS)

    (^ (////synthesis.tuple members))
    (/structure.tuple expression archive members)

    (#////synthesis.Reference value)
    (//reference.reference /reference.system archive value)

    (^ (////synthesis.branch/case case))
    (/case.case ..statement expression archive case)

    (^ (////synthesis.branch/let let))
    (/case.let expression archive let)

    (^ (////synthesis.branch/if if))
    (/case.if expression archive if)

    (^ (////synthesis.branch/get get))
    (/case.get expression archive get)

    (^ (////synthesis.loop/scope scope))
    (/loop.scope ..statement expression archive scope)

    (^ (////synthesis.loop/recur updates))
    (//////phase.throw ..cannot-recur-as-an-expression [])

    (^ (////synthesis.function/abstraction abstraction))
    (/function.function ..statement expression archive abstraction)

    (^ (////synthesis.function/apply application))
    (/function.apply expression archive application)

    (#////synthesis.Extension extension)
    (///extension.apply archive expression extension)))

(def: #export generate
  Phase
  ..expression)
