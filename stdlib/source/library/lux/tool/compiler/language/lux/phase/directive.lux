(.require
 [library
  [lux (.except)
   ["[0]" meta]
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception (.only exception:)]]
   [data
    [text
     ["%" \\format (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" mix monoid)]]]]]
 ["[0]" //
  ["[1][0]" extension]
  ["[1][0]" analysis]
  ["/[1]" //
   ["/" directive (.only Operation Phase)]
   ["[1][0]" analysis (.only)
    ["[0]" evaluation]
    ["[1]/[0]" macro (.only Expander)]
    ["[1]/[0]" type]]
   [///
    ["//" phase]
    [reference (.only)
     [variable (.only)]]
    [meta
     [archive (.only Archive)]]]]])

(exception: .public (not_a_directive [code Code])
  (exception.report
   "Directive" (%.code code)))

(exception: .public (invalid_macro_call [code Code])
  (exception.report
   "Code" (%.code code)))

(exception: .public (macro_was_not_found [name Symbol])
  (exception.report
   "Name" (%.symbol name)))

(type: Eval
  (-> Type Code (Meta Any)))

(def (meta_eval archive bundle compiler_eval)
  (-> Archive ///analysis.Bundle evaluation.Eval
      Eval)
  (function (_ type code lux)
    (case (compiler_eval archive type code [bundle lux])
      {try.#Success [[_bundle lux'] value]}
      {try.#Success [lux' value]}
      
      {try.#Failure error}
      {try.#Failure error})))

(def (requiring phase archive expansion)
  (All (_ anchor expression directive)
    (-> (Phase anchor expression directive) Archive (List Code)
        (Operation anchor expression directive /.Requirements)))
  (function (_ state)
    (loop (again [state state
                  input expansion
                  output /.no_requirements])
      (case input
        {.#End}
        {try.#Success [state output]}
        
        {.#Item head tail}
        (case (phase archive head state)
          {try.#Success [state' head']}
          (again state' tail (/.merge_requirements head' output))
          
          {try.#Failure error}
          {try.#Failure error})))))

(with_expansions [<lux_def_module> (these [|form_location| {.#Form (list.partial [|text_location| {.#Text "lux def module"}] annotations)}])]
  (def .public (phase wrapper expander)
    (-> //.Wrapper Expander Phase)
    (let [analysis (//analysis.phase expander)]
      (function (again archive code)
        (do [! //.monad]
          [state //.state
           .let [compiler_eval (meta_eval archive
                                          (the [//extension.#state /.#analysis /.#state //extension.#bundle] state)
                                          (evaluation.evaluator expander
                                                                (the [//extension.#state /.#synthesis /.#state] state)
                                                                (the [//extension.#state /.#generation /.#state] state)
                                                                (the [//extension.#state /.#generation /.#phase] state)))
                 extension_eval (as Eval (wrapper (as_expected compiler_eval)))]
           _ (//.with (has [//extension.#state /.#analysis /.#state //extension.#state .#eval] extension_eval state))]
          (case code
            (pattern [_ {.#Form (list.partial [_ {.#Text name}] inputs)}])
            (//extension.apply archive again [name inputs])

            (pattern [_ {.#Form (list.partial macro inputs)}])
            (do !
              [expansion (/.lifted_analysis
                          (do !
                            [macroA (<| (///analysis/type.expecting Macro)
                                        (analysis archive macro))]
                            (case macroA
                              (pattern (///analysis.constant macro_name))
                              (do !
                                [?macro (//extension.lifted (meta.macro macro_name))
                                 macro (case ?macro
                                         {.#Some macro}
                                         (in macro)
                                         
                                         {.#None}
                                         (//.except ..macro_was_not_found macro_name))]
                                (//extension.lifted (///analysis/macro.expansion expander macro_name macro inputs)))
                              
                              _
                              (//.except ..invalid_macro_call code))))]
              (case expansion
                (pattern (list.partial <lux_def_module> referrals))
                (|> (again archive <lux_def_module>)
                    (at ! each (revised /.#referrals (list#composite referrals))))

                _
                (..requiring again archive expansion)))

            _
            (//.except ..not_a_directive code)))))))
