(.using
 [library
  [lux (.except local)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [control
    ["[0]" pipe]]
   [data
    [text
     ["%" format (.only Format)]]]
   [macro
    ["^" pattern]]
   [math
    [number
     ["n" nat]]]
   [meta
    ["[0]" symbol]]]]
 ["[0]" /
  ["[1][0]" variable (.only Variable)]])

(type: .public Constant
  Symbol)

(type: .public Reference
  (Variant
   {#Variable Variable}
   {#Constant Constant}))

(implementation: .public equivalence
  (Equivalence Reference)

  (def: (= reference sample)
    (case [reference sample]
      (^.template [<tag> <equivalence>]
        [[{<tag> reference} {<tag> sample}]
         (# <equivalence> = reference sample)])
      ([#Variable /variable.equivalence]
       [#Constant symbol.equivalence])

      _
      false)))

(implementation: .public hash
  (Hash Reference)

  (def: equivalence
    ..equivalence)

  (def: (hash value)
    (case value
      (^.template [<factor> <tag> <hash>]
        [{<tag> value}
         (|> value
             (# <hash> hash)
             (n.* <factor>))])
      ([2 #Variable /variable.hash]
       [3 #Constant symbol.hash])
      )))

(template [<name> <family> <tag>]
  [(template: .public (<name> content)
     [(<| {<family>}
          {<tag>}
          content)])]

  [local   ..#Variable /variable.#Local]
  [foreign ..#Variable /variable.#Foreign]
  )

(template [<name> <tag>]
  [(template: .public (<name> content)
     [(<| {<tag>}
          content)])]

  [variable ..#Variable]
  [constant ..#Constant]
  )

(`` (template: .public self
      [(..variable (~~ (/variable.self)))]))

(def: .public format
  (Format Reference)
  (|>> (pipe.case
         {#Variable variable}
         (/variable.format variable)
         
         {#Constant constant}
         (%.symbol constant))))
