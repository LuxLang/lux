(.module:
  [library
   [lux {"-" [local]}
    [abstract
     [equivalence {"+" [Equivalence]}]
     [hash {"+" [Hash]}]]
    [control
     [pipe {"+" [case>]}]]
    [data
     ["." name]
     [text
      ["%" format {"+" [Format]}]]]
    [math
     [number
      ["n" nat]]]]]
  ["." / "_"
   ["#." variable {"+" [Variable]}]])

(type: .public Constant
  Name)

(type: .public Reference
  (Variant
   (#Variable Variable)
   (#Constant Constant)))

(implementation: .public equivalence
  (Equivalence Reference)

  (def: (= reference sample)
    (case [reference sample]
      (^template [<tag> <equivalence>]
        [[(<tag> reference) (<tag> sample)]
         (\ <equivalence> = reference sample)])
      ([#Variable /variable.equivalence]
       [#Constant name.equivalence])

      _
      false)))

(implementation: .public hash
  (Hash Reference)

  (def: &equivalence
    ..equivalence)

  (def: (hash value)
    (case value
      (^template [<factor> <tag> <hash>]
        [(<tag> value)
         ($_ n.* <factor>
             (\ <hash> hash value))])
      ([2 #Variable /variable.hash]
       [3 #Constant name.hash])
      )))

(template [<name> <family> <tag>]
  [(template: .public (<name> content)
     [(<| <family>
          <tag>
          content)])]

  [local   #..Variable #/variable.Local]
  [foreign #..Variable #/variable.Foreign]
  )

(template [<name> <tag>]
  [(template: .public (<name> content)
     [(<| <tag>
          content)])]

  [variable #..Variable]
  [constant #..Constant]
  )

(def: .public self
  Reference
  (..local 0))

(def: .public format
  (Format Reference)
  (|>> (case> (#Variable variable)
              (/variable.format variable)
              
              (#Constant constant)
              (%.name constant))))
