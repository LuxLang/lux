(.require
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only Monad do)]]
   [control
    ["[0]" try (.only Try)]]
   [data
    [binary (.only Binary)]
    ["[0]" product]
    [text
     ["%" \\format (.only format)]
     [encoding 
      ["[0]" utf8]]]
    [collection
     ["[0]" sequence]
     ["[0]" set (.only Set)]
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [meta
    [type (.only sharing)]]]]
 ["[0]" // (.only Packager)
  [//
   ["[0]" archive (.only Output)
    ["[0]" artifact]
    ["[0]" unit]
    ["[0]" module (.only)
     ["[0]" descriptor]]]
   ["[0]" cache
    [dependency
     ["[1]/[0]" module]
     ["[1]/[0]" artifact]]]
   ["[0]" io
    ["[1]" archive]]
   [//
    [language
     ["$" lux]]]]])

(def (write_module necessary_dependencies sequence [module_id output] so_far)
  (All (_ declaration)
    (-> (Set unit.ID) (-> declaration declaration declaration) [module.ID Output] declaration
        (Try declaration)))
  (|> output
      sequence.list
      (list.all (function (_ [artifact_id custom content])
                  (if (set.member? necessary_dependencies [module_id artifact_id])
                    {.#Some content}
                    {.#None})))
      (monad.mix try.monad
                 (function (_ content so_far)
                   (|> content
                       (at utf8.codec decoded)
                       (at try.monad each
                           (|>> as_expected
                                (is declaration)
                                (sharing [declaration]
                                  (is declaration
                                      so_far))
                                (sequence so_far)))))
                 so_far)))

(def .public (package header code sequence scope)
  (All (_ declaration)
    (-> declaration
        (-> declaration Text)
        (-> declaration declaration declaration)
        (-> declaration declaration)
        Packager))
  (function (package host_dependencies archive program)
    (do [! try.monad]
      [.let [necessary_dependencies (cache/artifact.necessary_dependencies archive)]
       order (cache/module.load_order $.key archive)]
      (|> order
          (list#each (function (_ [module [module_id entry]])
                       [module_id (the archive.#output entry)]))
          (monad.mix ! (..write_module necessary_dependencies sequence) header)
          (at ! each (|>> scope
                          code
                          (at utf8.codec encoded)
                          {.#Left}))))))
