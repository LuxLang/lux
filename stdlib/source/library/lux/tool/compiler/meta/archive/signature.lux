(.module:
  [library
   [lux #*
    [abstract
     [equivalence (#+ Equivalence)]]
    [control
     ["<>" parser
      ["<b>" binary (#+ Parser)]]]
    [data
     ["." product]
     ["." name]
     ["." text
      ["%" format (#+ format)]]
     [format
      ["." binary (#+ Writer)]]]
    [math
     [number
      ["." nat]]]]]
  [////
   [version (#+ Version)]])

(type: #export Signature
  {#name Name
   #version Version})

(def: #export equivalence
  (Equivalence Signature)
  (product.equivalence name.equivalence nat.equivalence))

(def: #export (description signature)
  (-> Signature Text)
  (format (%.name (get@ #name signature)) " " (%.nat (get@ #version signature))))

(def: #export writer
  (Writer Signature)
  (binary.and (binary.and binary.text binary.text)
              binary.nat))

(def: #export parser
  (Parser Signature)
  (<>.and (<>.and <b>.text <b>.text)
          <b>.nat))
