(.using
 [library
  [lux (.except Module Source)
   [abstract
    [monad (.only do)]
    [equivalence (.only Equivalence)]]
   [control
    ["[0]" pipe]
    ["<>" parser (.only)
     ["<[0]>" cli (.only Parser)]]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\format]
     ["<[1]>" \\parser]]
    [collection
     ["[0]" list (.open: "[1]#[0]" functor)]]]
   [macro
    ["^" pattern]]
   [math
    [number (.only hex)]]
   [meta
    ["[0]" symbol]
    ["[0]" configuration (.only Configuration)]]
   [tool
    [compiler
     [meta
      [archive
       [module
        ["[0]" descriptor]]]]]]
   [world
    [file (.only Path)]]]]
 ["[0]" /
  ["[1][0]" compiler (.only Compiler)]])

(type: .public Host_Dependency
  Path)

(type: .public Library
  Path)

(type: .public Source
  Path)

(type: .public Target
  Path)

(type: .public Module
  descriptor.Module)

(type: .public Compilation
  (Record
   [#host_dependencies (List Host_Dependency)
    #libraries (List Library)
    #compilers (List Compiler)
    #sources (List Source)
    #target Target
    #module Module
    #configuration Configuration]))

(type: .public Interpretation
  ..Compilation)

(type: .public Export
  [(List Source) Target])

(type: .public Service
  (Variant
   {#Compilation Compilation}
   {#Interpretation Interpretation}
   {#Export Export}))

(with_template [<name> <long> <type> <parser>]
  [(def: <name>
     (Parser <type>)
     (<cli>.named <long> <parser>))]

  [host_dependency_parser "--host_dependency" Host_Dependency <cli>.any]
  [library_parser "--library" Library <cli>.any]
  [compiler_parser "--compiler" Compiler (<text>.then /compiler.parser <cli>.any)]
  [source_parser "--source" Source <cli>.any]
  [target_parser "--target" Target <cli>.any]
  [module_parser "--module" Module <cli>.any]
  [configuration_parser "--configuration" Configuration (<text>.then configuration.parser <cli>.any)]
  )

(def: .public service
  (Parser Service)
  (let [compilation (is (Parser Compilation)
                        (all <>.and
                             (<>.some ..host_dependency_parser)
                             (<>.some ..library_parser)
                             (<>.some ..compiler_parser)
                             (<>.some ..source_parser)
                             ..target_parser
                             ..module_parser
                             (<>.else configuration.empty ..configuration_parser)))]
    (all <>.or
         (<>.after (<cli>.this "build")
                   compilation)
         (<>.after (<cli>.this "repl")
                   compilation)
         (<>.after (<cli>.this "export")
                   (all <>.and
                        (<>.some ..source_parser)
                        ..target_parser))
         )))

(def: .public target
  (-> Service Target)
  (|>> (pipe.case
         (^.or {#Compilation [host_dependencies libraries compilers sources target module]}
               {#Interpretation [host_dependencies libraries compilers sources target module]}
               {#Export [sources target]})
         target)))
