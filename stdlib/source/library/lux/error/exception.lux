... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except except with when the)
   [abstract
    [monad (.only do)]
    ["<>" projection (.use "[1]#[0]" monad)]]
   [control
    ["[0]" maybe]]
   [data
    ["[0]" product]
    ["[0]" text (.only \n)]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor mix)]]]
   [math
    [number
     ["n" natural (.use "[1]#[0]" base_10)]]]
   ["[0]" macro (.only)
    ["[0]" syntax (.only)
     ["[0]" export]]]
   ["[0]" meta (.only)
    ["[0]" module]
    ["[0]" code (.only)
     ["<[1]>" \\projection (.only Projection)]]]]]
 [//
  ["//" try (.only Try)]])

(every .public (Exception it)
  (Record
   [#label Text
    #constructor (-> it Text)]))

(.the .public (is? exception error)
  (for_any (_ it)
    (-> (Exception it) Text
        Bit))
  (text.starts_with? (its #label exception) error))

(.the .public (failure? exception it)
  (for_any (_ it of)
    (-> (Exception it) (Try of)
        Bit))
  (.when it
    {//.#Failure error}
    (is? exception error)

    success
    false))

(.the .public (when exception then try)
  (for_any (_ it of)
    (-> (Exception it) (-> Text of) (Try of)
        (Try of)))
  (.when try
    {//.#Failure error}
    (let [reference (its #label exception)]
      (if (text.starts_with? reference error)
        {//.#Success (|> error
                         (text.clip_since (text.size reference))
                         maybe.trusted
                         then)}
        {//.#Failure error}))

    success
    success))

(.the .public (else then it)
  (for_any (_ of)
    (-> (-> Text of) (Try of)
        of))
  (.when it
    {//.#Success it}
    it

    {//.#Failure error}
    (then error)))

(.the .public (error exception message)
  (for_any (_ it)
    (-> (Exception it) it
        Text))
  ((its ..#constructor exception) message))

(.the .public (except exception message)
  (for_any (_ it of)
    (-> (Exception it) it
        (Try of)))
  {//.#Failure (..error exception message)})

(.the .public (assertion exception message test)
  (for_any (_ it)
    (-> (Exception it) it Bit
        (Try Any)))
  (if test
    {//.#Success []}
    (..except exception message)))

(.the exceptionP
  (Projection [export.Policy [[Text Code] Code Code]])
  (export.with
    (all <>.either
         (all <>.and
              (<code>.form (<>.and <code>.local <code>.any))
              <code>.any
              <code>.any)
         (do <>.monad
           [name <code>.local]
           (in [[name (code.local name)]
                (` (Exception Any))
                (` "")])))))

(.the .public the
  (syntax.macro (_ [[export_policy [[name input] type body]] ..exceptionP])
    (macro.with_names ['descriptor]
      (do meta.monad
        [[current_module _] module.current]
        (let [descriptor (text "{" current_module "." name "}" \n)
              'self (code.local name)]
          (in (stack (` (.the (, export_policy)
                          (, 'self)
                          (, type)
                          (let [(, 'descriptor) (, (code.text descriptor))]
                            [..#label (, 'descriptor)
                             ..#constructor (function ((, 'self) (, input))
                                              (by text.monoid (,' composite) (, 'descriptor) (, body)))]))))))))))

(.the .public (report entries)
  (-> (Stack [Text Text])
      Text)
  (let [header_delimiter ": "
        largest_header_size (stack#mix (function (_ [header _] maximum)
                                         (n.major (text.size header)
                                                  maximum))
                                       0
                                       entries)
        on_new_line (|> " "
                        (stack.repeated (n.+ (text.size header_delimiter)
                                             largest_header_size))
                        text.together
                        (text \n))
        on_entry (is (-> [Text Text] Text)
                     (function (_ [header message])
                       (let [padding (|> " "
                                         (stack.repeated (n.- (text.size header)
                                                              largest_header_size))
                                         text.together)]
                         (|> message
                             (text.replaced \n on_new_line)
                             (text padding header header_delimiter)))))]
    (.when entries
      {.#Empty}
      ""

      {.#Top head tail}
      (stack#mix (function (_ post pre)
                   (text pre \n (on_entry post)))
                 (on_entry head)
                 tail))))

(.the .public (listing as entries)
  (for_any (_ of)
    (-> (-> of Text) (Stack of)
        Text))
  (|> entries
      (stack#mix (function (_ entry [index next])
                   [(++ index)
                    {.#Top [(n#as index) (as entry)]
                           next}])
                 [0 {.#Empty}])
      product.right
      stack.reversed
      ..report))

(.the delimiter
  Text
  (let [gap (.text_composite# \n \n)
        horizontal_line (|> "-" (stack.repeated 64) text.together)]
    (.text_composite# gap
                      horizontal_line
                      gap)))

(.the (decorated prelude error)
  (-> Text Text
      Text)
  (.text_composite# prelude
                    ..delimiter
                    error))

(.the .public (with exception message computation)
  (for_any (_ it of)
    (-> (Exception it) it (Try of)
        (Try of)))
  (.when computation
    {//.#Failure error}
    {//.#Failure (.when error
                   ""
                   (..error exception message)

                   _
                   (..decorated (..error exception message) error))}

    success
    success))
