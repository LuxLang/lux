... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [error
    ["[0]" exception (.only Exception)]]
   [data
    [text
     ["%" \\injection]]
    [collection
     ["[0]" list
      ["?[1]" \\projection]]]]
   [math
    [number
     [/64
      ["i" integer]]]]
   [macro (.only with_names)
    ["[0]" syntax]
    ["[0]" template]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]]])

(template.with [<name>]
  [(exception.the .public (<name> condition)
     (Exception Code)
     (exception.report
      (list ["Condition" (code.as_text condition)])))]

  [pre_condition_failed]
  [post_condition_failed]
  )

(the .public (assert! message test)
  (-> Text Bit [])
  (if test
      []
      (halt! message)))

(the .public pre
  (syntax.macro (_ [test ?list.any
                    expr ?list.any])
    (pure (list (` (exec
                     (..assert! (, (code.text (exception.error ..pre_condition_failed test)))
                                (, test))
                     (, expr)))))))

(the .public post
  (syntax.macro (_ [test ?list.any
                    expr ?list.any])
    (with_names ['output]
      (pure (list (` (let [(, 'output) (, expr)]
                       (exec
                         (..assert! (, (code.text (exception.error ..post_condition_failed test)))
                                    ((, test) (, 'output)))
                         (, 'output)))))))))
