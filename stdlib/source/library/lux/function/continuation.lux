... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [functor (.only Functor)]
    [monad (.only Monad)]]
   ["[0]" function]
   [data
    [collection
     ["[0]" list
      ["?[1]" \\projection]]]]
   [macro (.only with_names)
    ["[0]" syntax]
    ["[0]" template]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]]])

(every .public (Context it answer)
  (-> it
      answer))

(the .public empty
  (for_any (_ it)
    (Context it it))
  function.identity)

(every .public (Cont it)
  (for_any (_ answer)
    (-> (Context it answer)
        answer)))

(the .public (continued next cont)
  (for_any (_ it answer)
    (-> (Context it answer) (Cont it answer)
        answer))
  (cont next))

(the .public value
  (for_any (_ it)
    (-> (Cont it)
        it))
  (..continued ..empty))

(the Implementation
  (template.macro (_ <interface>)
    [(for_any (_ answer)
       (<interface> (for_any (_ it)
                      (Cont it answer))))]))

(the .public functor
  (Implementation Functor)
  (implementation
   (the (each $ it)
     (function (_ after)
       (it (function.composite after $))))))

(the .public monad
  (Implementation Monad)
  (implementation
   (the functor ..functor)

   (the (pure value)
     (function (_ after)
       (after value)))

   (the (conjoint ffa)
     (function (_ after)
       (ffa (continued after))))))

(the .public (call/cc context)
  (for_any (_ local_return global_return final_return)
    (-> (-> (-> local_return (Cont global_return final_return))
            (Cont local_return final_return))
        (Cont local_return final_return)))
  (function (_ after_pause)
    (context (function (_ value)
               (function (_ after_resumption)
                 (after_pause value)))
             after_pause)))

(alias [with_current]
       ..call/cc)

(syntax.the .public (pending [expr ?list.any])
  (with_names ['_ 'after]
    (pure (list (` (.function ((, '_) (, 'after))
                     ((, 'after) (, expr))))))))
