... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except all or and not)
   [abstract
    [monoid (.only Monoid)]
    [functor
     ["[0]" contravariant]]]]]
 ["[0]" //])

(the with_template' (.in_module# .prelude .with_template))

(every .public (Predicate it)
  (-> it
      Bit))

(with_template' [<identity_value> <identity_name> <composition_name> <composition>]
  [(the .public <identity_name>
     Predicate
     (//.constant <identity_value>))

   (the .public (<composition_name> left right)
     (for_any (_ it)
       (-> (Predicate it) (Predicate it)
           (Predicate it)))
     (function (_ value)
       (<composition> (left value)
                      (right value))))]

  [#0 none or  .or]
  [#1 all  and .and]
  )

(with_template' [<name> <identity> <composition>]
  [(the .public <name>
     (for_any (_ it)
       (Monoid (Predicate it)))
     (implementation
      (the identity <identity>)
      (the composite <composition>)))]

  [union        ..none ..or]
  [intersection ..all  ..and]
  )

(the .public (complement predicate)
  (for_any (_ it)
    (-> (Predicate it)
        (Predicate it)))
  (|>> predicate
       .not))

(alias [not]
       ..complement)

(the .public (difference sub base)
  (for_any (_ it)
    (-> (Predicate it) (Predicate it)
        (Predicate it)))
  (function (_ value)
    (.and (base value)
          (.not (sub value)))))

(the .public (rec predicate)
  (for_any (_ it)
    (-> (-> (Predicate it) (Predicate it))
        (Predicate it)))
  (function (again input)
    (predicate again input)))

(the .public functor
  (contravariant.Functor Predicate)
  (implementation
   (the (each $ it)
     (|>> $ it))))
