... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... Inspired by;
... "The Different Aspects of Monads and Mixins" by Bruno C. d. S. Oliveira

(.using
 [library
  [lux (.except)
   [abstract
    [monoid (.only Monoid)]
    [monad (.only Monad do)]]]]
 [//
  [predicate (.only Predicate)]])

(every .public (Mixin input output)
  (-> (-> input output) (-> input output)
      (-> input output)))

(the .public (fixed f)
  (for_any (_ input output)
    (-> (Mixin input output)
        (-> input output)))
  (function (mix input)
    ((f mix mix) input)))

(the .public nothing
  Mixin
  (function (_ partial complete)
    partial))

(the .public (mixed child parent)
  (for_any (_ input output)
    (-> (Mixin input output) (Mixin input output)
        (Mixin input output)))
  (function (_ partial complete)
    (child (parent partial complete) complete)))

(the .public monoid
  (for_any (_ input output)
    (Monoid (Mixin input output)))
  (implementation
   (the identity ..nothing)
   (the composite ..mixed)))

(the .public (advice when then)
  (for_any (_ input output)
    (-> (Predicate input) (Mixin input output)
        (Mixin input output)))
  (function (_ partial complete input)
    (if (when input)
      ((then partial complete) input)
      (partial input))))

(the .public (before ! action)
  (for_any (_ ! input output)
    (-> (Monad !) (-> input (! Any))
        (Mixin input (! output))))
  (function (_ partial complete input)
    (do !
      [_ (action input)]
      (partial input))))

(the .public (after ! action)
  (for_any (_ ! input output)
    (-> (Monad !) (-> input output (! Any))
        (Mixin input (! output))))
  (function (_ partial complete input)
    (do !
      [output (partial input)
       _ (action input output)]
      (in output))))

(every .public (Recursive input output)
  (-> (-> input output)
      (-> input output)))

(the .public (of_recursive recursive)
  (for_any (_ input output)
    (-> (Recursive input output)
        (Mixin input output)))
  (function (_ partial complete)
    (recursive complete)))
