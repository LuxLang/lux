... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [functor (.only Functor)]
    [apply (.only Apply)]
    [monad (.only Monad)]
    [comonad (.only CoMonad)]]
   [control
    ["[0]" function]]]])

(type .public (Identity a)
  a)

(def .public functor
  (Functor Identity)
  (implementation
   (def each function.identity)))

(def .public apply
  (Apply Identity)
  (implementation
   (def functor ..functor)
   (def (on fa ff)
     (ff fa))))

(def .public monad
  (Monad Identity)
  (implementation
   (def functor ..functor)
   (def in function.identity)
   (def conjoint function.identity)))

(def .public comonad
  (CoMonad Identity)
  (implementation
   (def functor ..functor)
   (def out function.identity)
   (def disjoint function.identity)))
