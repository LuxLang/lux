... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except has nat int rev list
                or and
                variant record)
   [abstract
    [equivalence (.only Equivalence)]
    [format (.only Format)]
    ["[0]" monad (.only do)]]
   [control
    ["<>" projection (.use "[1]#[0]" monad)]
    ["[0]" pipe]
    ["[0]" maybe]
    ["[0]" try (.only Try) (.use "[1]#[0]" functor)]
    ["[0]" exception (.only Exception)]
    ["[0]" function (.only)
     [predicate (.only Predicate)]]]
   [data
    ["[0]" bit]
    ["[0]" product]
    ["[0]" text (.use "[1]#[0]" equivalence monoid)
     ["<[1]>" \\projection (.only Projection)]]
    [collection
     ["[0]" list (.use "[1]#[0]" mix functor)]
     ["[0]" sequence (.only Sequence sequence) (.use "[1]#[0]" monoid monad)]
     ["[0]" dictionary (.only Dictionary) (.use "[1]#[0]" functor)]]]
   [math
    [number
     ["[0]" nat]
     ["[0]" int]
     ["[0]" rev]
     ["[0]" dec (.use "[1]#[0]" decimal)]
     ["[0]" i64]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro
     ["[0]" syntax]
     ["[0]" template]]]
   [world
    [time
     ["[0]" instant]
     ["[0]" duration]
     ["[0]" date]
     ["[0]" day]
     ["[0]" month]]]]])

(template.with [<name> <type>]
  [(every .public <name>
     <type>)]

  [Null    Any]
  [Boolean Bit]
  [Number  Dec]
  [String  Text]
  )

(every .public JSON
  (Rec JSON
    (Variant
     {#Null    Null}
     {#Boolean Boolean}
     {#Number  Number}
     {#String  String}
     {#Array   (Sequence JSON)}
     {#Object  (Dictionary String JSON)})))

(template.with [<name> <type>]
  [(every .public <name>
     <type>)]

  [Array  (Sequence JSON)]
  [Object (Dictionary String JSON)]
  )

(the .public null?
  (Predicate JSON)
  (|>> (pipe.when
         {#Null}
         true
         
         _
         false)))

(the .public object
  (-> (List [String JSON])
      JSON)
  (|>> (dictionary.of_list text.hash)
       {..#Object}))

(every JSON'
  (Rec JSON'
    (Variant
     {#Null'    Null}
     {#Boolean' Boolean}
     {#Number'  Number}
     {#String'  String}
     {#Array'   (Sequence JSON')}
     {#Object'  (Dictionary String JSON')}
     {#Code'    Code})))

(the jsonP
  (<code>.Projection JSON')
  (<>.rec
   (function (_ jsonP)
     (all <>.or
          (<code>.form (<>#in []))
          <code>.bit
          <code>.dec
          <code>.text
          (<>#each sequence.of_list
                   (<code>.tuple (<>.some jsonP)))
          (<>#each (dictionary.of_list text.hash)
                   (<code>.variant (<>.some (<>.and <code>.text jsonP))))
          <code>.any
          ))))

(the (jsonF token)
  (-> JSON'
      Code)
  (`` (when token
        {#Null' _}
        (` {..#Null})
        
        (,, (template.with [<ctor> <input_tag> <output_tag>]
              [{<input_tag> value}
               (` {<output_tag> (, (<ctor> value))})]

              [code.bit  ..#Boolean' ..#Boolean]
              [code.dec ..#Number' ..#Number]
              [code.text ..#String' ..#String]))
        
        {#Array' members}
        (` {..#Array (sequence.sequence (,* (sequence.list (sequence#each jsonF members))))})

        {#Object' pairs}
        (` {..#Object (dictionary.of_list text.hash
                                          (.list (,* (|> pairs
                                                         dictionary.entries
                                                         (list#each (function (_ [key_name value])
                                                                      (` [(, (code.text key_name)) (, (jsonF value))])))))))})

        {#Code' code}
        code)))

(the .public json
  (syntax.macro (_ [token ..jsonP])
    (in (.list (` (is JSON (, (jsonF token))))))))

(the .public (fields json)
  (-> JSON
      (Try (List String)))
  (when json
    {#Object obj}
    {try.#Success (dictionary.keys obj)}

    _
    {try.#Failure "Cannot get the fields of a non-object."}))

(the .public (field key json)
  (-> String JSON
      (Try JSON))
  (when json
    {#Object obj}
    (when (dictionary.value key obj)
      {try.#Success value}
      {try.#Success value}

      failure
      {try.#Failure (all text#composite "Missing field '" key "' on object.")})

    _
    {try.#Failure (all text#composite "Cannot get field '" key "' on a non-object.")}))

(the .public (has key value json)
  (-> String JSON JSON
      (Try JSON))
  (when json
    {#Object obj}
    {try.#Success {#Object (dictionary.has key value obj)}}

    _
    {try.#Failure (all text#composite "Cannot set field '" key "' on a non-object.")}))

(template.with [<name> <tag> <type>]
  [(the .public (<name> key json)
     (-> Text JSON
         (Try <type>))
     (when (field key json)
       {try.#Success {<tag> value}}
       {try.#Success value}

       {try.#Success _}
       {try.#Failure (all text#composite "Wrong value type at key: " key)}

       {try.#Failure error}
       {try.#Failure error}))]

  [boolean_field #Boolean Boolean]
  [number_field  #Number  Number]
  [string_field  #String  String]
  [array_field   #Array   Array]
  [object_field  #Object  Object]
  )

(the .public equivalence
  (Equivalence JSON)
  (implementation
   (the (= x y)
     (`` (when [x y]
           [{#Null} {#Null}]
           true

           (,, (template.with [<tag> <struct>]
                 [[{<tag> x'} {<tag> y'}]
                  (of <struct> = x' y')]

                 [#Boolean bit.equivalence]
                 [#Number  dec.equivalence]
                 [#String  text.equivalence]))

           [{#Array xs} {#Array ys}]
           (.and (nat.= (sequence.size xs) (sequence.size ys))
                 (list#mix (function (_ idx prev)
                             (.and prev
                                   (maybe.else false
                                               (do maybe.monad
                                                 [x' (sequence.item idx xs)
                                                  y' (sequence.item idx ys)]
                                                 (in (= x' y'))))))
                           true
                           (list.indices (sequence.size xs))))
           
           [{#Object xs} {#Object ys}]
           (.and (nat.= (dictionary.size xs) (dictionary.size ys))
                 (list#mix (function (_ [xk xv] prev)
                             (.and prev
                                   (when (dictionary.value xk ys)
                                     {try.#Success yv}
                                     (= xv yv)

                                     failure
                                     false)))
                           true
                           (dictionary.entries xs)))
           
           _
           false)))))

............................................................
............................................................
............................................................

(the (null_injection _)
  (-> Null
      Text)
  "null")

(the boolean_injection
  (-> Boolean
      Text)
  (|>> (pipe.when
         .false
         "false"
         
         .true
         "true")))

(the number_injection
  (-> Number
      Text)
  (|>> (pipe.when
         +0.0 ... OR -0.0
         "0.0"

         value
         (let [raw (of dec.decimal injection value)]
           (if (dec.< +0.0 value)
             raw
             (|> raw (text.split_at 1) maybe.trusted product.right))))))

(the escape "\")
(the escaped_dq (text#composite ..escape text.double_quote))

(the string_injection
  (-> String
      Text)
  (|>> (text.replaced text.double_quote ..escaped_dq)
       (text.enclosed [text.double_quote text.double_quote])))

(template.with [<token> <name>]
  [(the <name>
     Text
     <token>)]

  ["," value_separator]
  [":" entry_separator]

  ["[" array_start]
  ["]" array_end]

  ["{" object_start]
  ["}" object_end]
  )

(the (array_injection injection)
  (-> (-> JSON Text)
      (-> Array Text))
  (|>> (sequence#each injection)
       sequence.list
       (text.interposed ..value_separator)
       (text.enclosed [..array_start ..array_end])))

(the (kv_injection injection [key value])
  (-> (-> JSON Text)
      (-> [String JSON] Text))
  (all text#composite
       (..string_injection key)
       ..entry_separator
       (injection value)
       ))

(the (object_injection injection)
  (-> (-> JSON Text)
      (-> Object Text))
  (|>> dictionary.entries
       (list#each (..kv_injection injection))
       (text.interposed ..value_separator)
       (text.enclosed [..object_start ..object_end])))

(the .public (injection json)
  (-> JSON
      Text)
  (`` (when json
        (,, (template.with [<tag> <injection>]
              [{<tag> value}
               (<injection> value)]

              [#Null    ..null_injection]
              [#Boolean ..boolean_injection]
              [#Number  ..number_injection]
              [#String  ..string_injection]
              [#Array   (..array_injection injection)]
              [#Object  (..object_injection injection)]))
        )))

............................................................
............................................................
............................................................

(the space_projection
  (Projection Text)
  (<text>.some <text>.space))

(the value_separator_projection
  (Projection [Text Any Text])
  (all <>.and
       ..space_projection
       (<text>.this ..value_separator)
       ..space_projection))

(the null_projection
  (Projection Null)
  (do <>.monad
    [_ (<text>.this "null")]
    (in [])))

(template.with [<name> <token> <value>]
  [(the <name>
     (Projection Boolean)
     (do <>.monad
       [_ (<text>.this <token>)]
       (in <value>)))]

  [false_projection "false" #0]
  [true_projection  "true"  #1]
  )

(the boolean_projection
  (Projection Boolean)
  (all <>.either
       ..true_projection
       ..false_projection))

(the number_projection
  (Projection Number)
  (do [! <>.monad]
    [signed? (<>.parses? (<text>.this "-"))
     digits (<text>.many <text>.decimal)
     decimals (<>.else "0"
                       (do !
                         [_ (<text>.this ".")]
                         (<text>.many <text>.decimal)))
     exp (<>.else ""
                  (do !
                    [mark (<text>.one_of "eE")
                     signed?' (<>.parses? (<text>.this "-"))
                     offset (<text>.many <text>.decimal)]
                    (in (all text#composite mark (if signed?' "-" "") offset))))]
    (when (dec#projection (all text#composite (if signed? "-" "") digits "." decimals exp))
      {try.#Success value}
      (in value)

      {try.#Failure message}
      (<>.failure message))))

(the escaped_projection
  (Projection Text)
  (`` (all <>.either
           (,, (template.with [<when> <then>]
                 [(<>.after (<text>.this <when>)
                            (<>#in <then>))]

                 ["\t" text.tab]
                 ["\b" text.back_space]
                 ["\n" text.new_line]
                 ["\r" text.carriage_return]
                 ["\f" text.form_feed]
                 [(text#composite "\" text.double_quote) text.double_quote]
                 ["\\" "\"]
                 ))
           (<>.after (<text>.this "\u")
                     (|> <text>.hexadecimal!
                         (<text>.exactly! 4)
                         <text>.slice
                         (<>.format nat.hex)
                         (<>#each text.of_char)))
           )))

(the string_projection
  (Projection String)
  (<| (<text>.enclosed [text.double_quote text.double_quote])
      (loop (again [_ []]))
      (do [! <>.monad]
        [chars (<text>.some (<text>.none_of (text#composite "\" text.double_quote)))
         stop <text>.next])
      (if (text#= "\" stop)
        (do !
          [escaped escaped_projection
           next_chars (again [])]
          (in (all text#composite chars escaped next_chars)))
        (in chars))))

(the (kv_projection json_projection)
  (-> (Projection JSON)
      (Projection [String JSON]))
  (do <>.monad
    [key ..string_projection
     _ ..space_projection
     _ (<text>.this ..entry_separator)
     _ ..space_projection
     value json_projection]
    (in [key value])))

(template.with [<name> <type> <open> <close> <elem_projection> <prep>]
  [(the (<name> json_projection)
     (-> (Projection JSON)
         (Projection <type>))
     (do <>.monad
       [_ (<text>.this <open>)
        _ space_projection
        elems (<>.separated_by ..value_separator_projection <elem_projection>)
        _ space_projection
        _ (<text>.this <close>)]
       (in (<prep> elems))))]

  [array_projection  Array  ..array_start ..array_end json_projection sequence.of_list]
  [object_projection Object ..object_start ..object_end (kv_projection json_projection) (dictionary.of_list text.hash)]
  )

(the json_projection
  (Projection JSON)
  (<| (<>.after ..space_projection)
      (<>.before ..space_projection)
      (<>.rec
       (function (_ json_projection)
         (all <>.or
              null_projection
              boolean_projection
              number_projection
              string_projection
              (array_projection json_projection)
              (object_projection json_projection))))))

(the .public format
  (Format Text JSON)
  (implementation
   (the injection ..injection)
   (the projection (<text>.value json_projection))))

(exception.the (cannot_project [it])
  (Exception JSON)
  (exception.report
   (.list ["JSON" (with ..format
                    (injection it))])))

(template.with [,name ,type ,tag]
  [(the .public ,name
     (Format JSON ,type)
     (implementation
      (the injection (|>> {,tag}))
      (the (projection it)
        (when it
          {,tag it}
          {try.#Success it}

          _
          (exception.except ..cannot_project [it])))))]

  [any Any #Null]
  [bit Bit #Boolean]
  [dec Dec #Number]
  [text Text #String]
  )

(the low_mask Nat (|> 1 (i64.left_shifted 32) --))
(the high_mask Nat (|> low_mask (i64.left_shifted 32)))

(the .public nat
  (Format JSON Nat)
  (implementation
   (the (injection input)
     (let [high (|> input (i64.and high_mask) (i64.right_shifted 32))
           low (i64.and low_mask input)]
       {#Array (sequence (|> high .int int.dec {#Number})
                         (|> low .int int.dec {#Number}))}))
   (the (projection it)
     (when it
       {#Array it'}
       (when (sequence.list it')
         (.list {#Number high} {#Number low})
         {try.#Success (nat.+ (|> high dec.int .nat (i64.left_shifted 32))
                              (|> low dec.int .nat))}

         _
         (exception.except ..cannot_project [it]))

       _
       (exception.except ..cannot_project [it])))))

(the .public int
  (Format JSON Int)
  (implementation
   (the injection
     (|>> .nat
          (of ..nat injection)))
   (the projection
     (|>> (of ..nat projection)
          (of try.functor each (|>> .int))))))

(the .public rev
  (Format JSON Rev)
  (implementation
   (the injection
     (|>> .nat
          (of ..nat injection)))
   (the projection
     (|>> (of ..nat projection)
          (of try.functor each (|>> .rev))))))

(template.with [,name ,type ,format]
  [(the .public ,name
     (Format JSON ,type)
     (implementation
      (the injection
        (|>> (of ,format injection)
             {#String}))
      (the (projection it)
        (when it
          {#String it}
          (of ,format projection it)
          
          _
          (exception.except ..cannot_project [it])))))]

  [duration duration.Duration duration.format]
  [instant instant.Instant instant.format]
  [date date.Date date.format]
  [day day.Day day.format]
  [month month.Month month.format]
  )

(the .public (sum left right)
  (for_any (_ left right)
    (-> (Format JSON left) (Format JSON right)
        (Format JSON (Or left right))))
  (implementation
   (the (injection it)
     {#Array (sequence (when it
                         {#0 it}
                         (with left (injection it))
                         
                         {#1 it}
                         (with right (injection it))))})
   (the (projection it)
     (when it
       {#Array it'}
       (`` (when (sequence.list it')
             (,, (template.with [,flag ,format]
                   [(.list {#Boolean ,flag} it)
                    (try#each (|>> {,flag}) (with ,format (projection it)))]

                   [#0 left]
                   [#1 right]
                   ))

             _
             (exception.except ..cannot_project [it])))
       
       _
       (exception.except ..cannot_project [it])))))

(the .public (product left right)
  (for_any (_ left right)
    (-> (Format JSON left) (Format JSON right)
        (Format JSON (And left right))))
  (implementation
   (the (injection [itL itR])
     {#Array (sequence (with left (injection itL))
                       (with right (injection itR)))})
   (the (projection it)
     (when it
       {#Array it'}
       (when (sequence.list it')
         (.list itL itR)
         (do try.monad
           [itL (with left (projection itL))
            itR (with right (projection itR))]
           (in [itL itR]))

         _
         (exception.except ..cannot_project [it]))
       
       _
       (exception.except ..cannot_project [it])))))

(the .public (recursive context)
  (for_any (_ of)
    (-> (-> (Format JSON of)
            (Format JSON of))
        (Format JSON of)))
  (implementation
   (the (injection it)
     (with (context [injection
                     (function (_ _) {try.#Failure ""})])
       (injection it)))
   (the (projection it)
     (with (context [(|>> {#Null})
                     projection])
       (projection it)))))

(the .public (list item)
  (for_any (_ of)
    (-> (Format JSON of)
        (Format JSON (List of))))
  (implementation
   (the injection
     (|>> (list#each (with item injection))
          sequence.of_list
          {#Array}))
   (the (projection it)
     (when it
       {#Array it}
       (|> it
           sequence.list
           (monad.each try.monad (with item projection)))
       
       _
       (exception.except ..cannot_project [it])))))

(the .public (dictionary value)
  (for_any (_ of)
    (-> (Format JSON of)
        (Format JSON (Dictionary Text of))))
  (implementation
   (the injection
     (|>> (dictionary#each (of value injection))
          {#Object}))
   (the (projection it)
     (when it
       {#Object it}
       (|> it
           dictionary.entries
           (monad.each try.monad (function (_ [key it])
                                   (do try.monad
                                     [it (with value (projection it))]
                                     (in [key it]))))
           (try#each (dictionary.of_list text.hash)))
       
       _
       (exception.except ..cannot_project [it])))))

(every .public (Case of)
  (-> Nat
      (Format JSON of)))

(the .public (case format)
  (All (_ of)
    (-> (Format JSON of)
        (Case of)))
  (function (_ expected)
    (let [expected (nat.dec expected)]
      (implementation
       (the (injection it)
         {#Array (sequence {#Number expected}
                           (of format injection it))})
       (the (projection it)
         (when it
           {#Array case}
           (when (sequence.list case)
             (.list {#Number actual} case)
             (if (dec.= expected actual)
               (of format projection case)
               (exception.except ..cannot_project [it]))

             _
             (exception.except ..cannot_project [it]))
           
           _
           (exception.except ..cannot_project [it])))))))

(the .public (or left right)
  (All (_ left right)
    (-> (Case left) (Case right)
        (Case (Or left right))))
  (function (_ expected)
    (implementation
     (the (injection it)
       (when it
         {#0 it} (of (left expected) injection it)
         {#1 it} (of (right (++ expected)) injection it)))
     (the (projection it)
       (when (of (left expected) projection it)
         {try.#Success it}
         {try.#Success {#0 it}}
         
         {try.#Failure _}
         (when (of (right (++ expected)) projection it)
           {try.#Success it}
           {try.#Success {#1 it}}
           
           {try.#Failure error}
           {try.#Failure error}))))))

(the .public variant
  (All (_ of)
    (-> (Case of)
        (Format JSON of)))
  (function.on 0))

(every .public (Member of)
  (-> [Nat Bit]
      (Format (Sequence JSON) of)))

(the .public (member format)
  (All (_ of)
    (-> (Format JSON of)
        (Member of)))
  (function (_ [index end?])
    (implementation
     (the (injection it)
       (sequence (of format injection it)))
     (the (projection it)
       (if (.or (not end?)
                (nat.= (++ index) (sequence.size it)))
         (do try.monad
           [it (sequence.item index it)]
           (of format projection it))
         (exception.except ..cannot_project [{#Array it}]))))))

(the .public (and left right)
  (All (_ left right)
    (-> (Member left) (Member right)
        (Member (And left right))))
  (function (_ [index end?])
    (implementation
     (the (injection it)
       (all sequence#composite
            (of (left [index false]) injection (product.left it))
            (of (right [(++ index) true]) injection (product.right it))))
     (the (projection it)
       (do try.monad
         [left (of (left [index false]) projection it)
          right (of (right [(++ index) true]) projection it)]
         (in [left right]))))))

(the .public (record member)
  (All (_ of)
    (-> (Member of)
        (Format JSON of)))
  (let [format (member [0 true])]
    (implementation
     (the (injection it)
       {#Array (of format injection it)})
     (the (projection it)
       (when it
         {#Array items}
         (of format projection items)

         _
         (exception.except ..cannot_project [it]))))))
