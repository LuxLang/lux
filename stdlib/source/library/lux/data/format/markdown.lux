(.require
 [library
  [lux (.except and)
   [data
    ["[0]" text (.only)
     ["%" \\format (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [meta
    [type
     ["[0]" nominal]]]
   [world
    [net (.only URL)]]]])

... https://www.markdownguide.org/basic-syntax/

(def safe
  (-> Text
      Text)
  (|>> (text.replaced "\" "\\")
       (text.replaced "`" "\`")
       (text.replaced "*" "\*")
       (text.replaced "_" "\_")
       (text.replaced "{" "\{")
       (text.replaced "}" "\}")
       (text.replaced "[" "\[")
       (text.replaced "]" "\]")
       (text.replaced "(" "\(")
       (text.replaced ")" "\)")
       (text.replaced "#" "\#")
       (text.replaced "+" "\+")
       (text.replaced "-" "\-")
       (text.replaced "." "\.")
       (text.replaced "!" "\!")))

(nominal.def .public Span Any)
(nominal.def .public Block Any)

(nominal.def .public (Markdown brand)
  Text

  (def .public empty
    Markdown
    (nominal.abstraction ""))

  (def .public text
    (-> Text
        (Markdown Span))
    (|>> ..safe nominal.abstraction))

  (def blank_line
    (format text.new_line text.new_line))

  (with_template [<name> <prefix>]
    [(def .public (<name> content)
       (-> (Markdown Span)
           (Markdown Block))
       (nominal.abstraction (format <prefix> " " (nominal.representation content) ..blank_line)))]

    [heading/1 "#"]
    [heading/2 "##"]
    [heading/3 "###"]
    [heading/4 "####"]
    [heading/5 "#####"]
    [heading/6 "######"]
    )

  (def (block content)
    (-> Text
        (Markdown Block))
    (nominal.abstraction (format content ..blank_line)))

  (def .public paragraph
    (-> (Markdown Span)
        (Markdown Block))
    (|>> nominal.representation ..block))

  (def .public break
    (Markdown Span)
    (nominal.abstraction (format "  " text.new_line)))

  (with_template [<name> <wrapper>]
    [(def .public <name>
       (-> (Markdown Span)
           (Markdown Span))
       (|>> nominal.representation
            (text.enclosed [<wrapper> <wrapper>])
            nominal.abstraction))]

    [bold "**"]
    [italic "_"]
    )

  (def (prefix with)
    (-> Text
        (-> Text
            Text))
    (|>> (text.all_split_by text.new_line)
         (list#each (function (_ line)
                      (if (text.empty? line)
                        line
                        (format with line))))
         (text.interposed text.new_line)))

  (def indent
    (-> Text
        Text)
    (..prefix text.tab))

  (def .public quote
    (-> (Markdown Block)
        (Markdown Block))
    (|>> nominal.representation
         (..prefix "> ")
         nominal.abstraction))

  (def .public numbered_list
    (-> (List [(Markdown Span) (Maybe (Markdown Block))])
        (Markdown Block))
    (|>> list.enumeration
         (list#each (function (_ [idx [summary detail]])
                      (format "1. " (nominal.representation summary)
                              (when detail
                                {.#Some detail}
                                (|> detail
                                    nominal.representation
                                    ..indent
                                    (text.enclosed [text.new_line text.new_line])
                                    (format text.new_line))
                                
                                {.#None}
                                ""))))
         (text.interposed text.new_line)
         ..block))

  (def .public bullet_list
    (-> (List [(Markdown Span) (Maybe (Markdown Block))])
        (Markdown Block))
    (|>> (list#each (function (_ [summary detail])
                      (format "* " (nominal.representation summary)
                              (when detail
                                {.#Some detail}
                                (|> detail
                                    nominal.representation
                                    ..indent
                                    (text.enclosed [text.new_line text.new_line])
                                    (format text.new_line))
                                
                                {.#None}
                                ""))))
         (text.interposed text.new_line)
         ..block))

  ... A snippet of code.
  (def .public snippet
    (-> Text
        (Markdown Span))
    (|>> (text.enclosed ["`` " " ``"]) nominal.abstraction))

  ... A (generic) block of code.
  (def .public generic_code
    (-> Text
        (Markdown Block))
    (let [open (format "```" text.new_line)
          close (format text.new_line "```")]
      (|>> (text.enclosed [open close]) ..block)))

  ... A block of code of a specific language.
  (def .public (code language block)
    (-> Text Text
        (Markdown Block))
    (let [open (format "```" language text.new_line)
          close (format text.new_line "```")]
      (|> block
          (text.enclosed [open close])
          ..block)))

  (def .public (image description url)
    (-> Text URL
        (Markdown Span))
    (nominal.abstraction (format "![" (..safe description) "](" url ")")))

  (def .public horizontal_rule
    (Markdown Block)
    (..block "___"))

  (def .public (anchor name)
    (-> Text
        (Markdown Span))
    (nominal.abstraction (format "<a name=" name "></a>")))

  (def .public (link description url)
    (-> (Markdown Span) URL
        (Markdown Span))
    (nominal.abstraction (format "[" (nominal.representation description) "](" url ")")))

  (type .public Email
    Text)

  (with_template [<name> <type>]
    [(def .public <name>
       (-> <type>
           (Markdown Span))
       (|>> (text.enclosed ["<" ">"]) nominal.abstraction))]

    [url URL]
    [email Email]
    )

  (with_template [<name> <brand> <infix>]
    [(def .public (<name> pre post)
       (-> (Markdown <brand>) (Markdown <brand>)
           (Markdown <brand>))
       (nominal.abstraction (format (nominal.representation pre) <infix> (nominal.representation post))))]

    [and Span " "]
    [then Block ""]
    )

  (def .public markdown
    (All (_ a)
      (-> (Markdown a)
          Text))
    (|>> nominal.representation))
  )
