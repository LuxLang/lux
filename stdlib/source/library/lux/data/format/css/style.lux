(.using
 [library
  [lux "*"
   [data
    [text
     ["%" format {"+" format}]]
    [collection
     ["[0]" list ("[1]#[0]" mix)]]]
   [type
    [abstract {"-" pattern}]]]]
 ["[0]" // "_"
  ["[1][0]" value {"+" Value}]
  ["[1][0]" property {"+" Property}]])

(abstract: .public Style
  Text

  (def: .public empty
    Style
    (abstraction ""))

  (def: .public (with [property value])
    (All (_ brand)
      (-> [(Property brand) (Value brand)]
          (-> Style Style)))
    (|>> representation
         (format (//property.name property) ": " (//value.value value) ";")
         abstraction))

  (def: .public inline
    (-> Style Text)
    (|>> representation))

  (def: .public (style config)
    (-> (List (Ex (_ brand) [(Property brand) (Value brand)]))
        Style)
    (list#mix ..with ..empty config))
  )
