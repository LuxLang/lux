(.module:
  [library
   [lux (#- or and for is? not)
    [data
     ["." text
      ["%" format (#+ format)]]
     [number
      ["i" int]]]
    [type
     abstract]
    [macro
     ["." template]]
    ["." locale (#+ Locale)]]])

(type: #export Label Text)

(type: #export Tag Label)
(type: #export ID Label)
(type: #export Class Label)
(type: #export Attribute Label)

(abstract: #export (Generic brand) Any)

(template [<generic> <brand>]
  [(abstract: <brand> Any)
   (type: #export <generic> (Generic <brand>))]

  [Can_Chain Can_Chain']
  [Cannot_Chain Cannot_Chain']
  )

(abstract: #export Unique Any)
(abstract: #export Specific Any)
(abstract: #export Composite Any)

(abstract: #export (Selector kind)
  Text

  (def: #export selector
    (-> (Selector Any) Text)
    (|>> :representation))

  (def: #export any
    (Selector Cannot_Chain)
    (:abstraction "*"))

  (def: #export tag
    (-> Tag (Selector Cannot_Chain))
    (|>> :abstraction))

  (template [<name> <type> <prefix> <kind>]
    [(def: #export <name>
       (-> <type> (Selector <kind>))
       (|>> (format <prefix>) :abstraction))]

    [id ID "#" Unique]
    [class Class "." Can_Chain]
    )

  (template [<right> <left> <combo> <combinator>+]
    [(`` (template [<combinator> <name>]
           [(def: #export (<name> right left)
              (-> (Selector <right>) (Selector <left>) (Selector <combo>))
              (:abstraction (format (:representation left)
                                    <combinator>
                                    (:representation right))))]

           (~~ (template.spliced <combinator>+))))]

    [Can_Chain (Generic Any) Can_Chain
     [["" and]]]
    [Unique (Generic Any) Composite
     [["" for]]]
    [Specific (Generic Any) Composite
     [["" at]]]
    [Any Any Composite
     [["," or]
      [" " in]
      [">" sub]
      ["+" next]
      ["~" later]]]
    )

  (type: #export Combinator
    (-> (Selector Any) (Selector Any) (Selector Composite)))

  (def: #export (with? attribute)
    (-> Attribute (Selector Can_Chain))
    (:abstraction (format "[" attribute "]")))

  (template [<check> <name>]
    [(def: #export (<name> attribute value)
       (-> Attribute Text (Selector Can_Chain))
       (:abstraction (format "[" attribute <check> value "]")))]

    ["=" is?]
    ["~=" has?]
    ["|=" has_start?]
    ["^=" starts?]
    ["$=" ends?]
    ["*=" contains?]
    )

  (template [<kind> <pseudo>+]
    [(`` (template [<name> <pseudo>]
           [(def: #export <name>
              (Selector Can_Chain)
              (:abstraction <pseudo>))]

           (~~ (template.spliced <pseudo>+))))]

    [Can_Chain
     [[active ":active"]
      [checked ":checked"]
      [default ":default"]
      [disabled ":disabled"]
      [empty ":empty"]
      [enabled ":enabled"]
      [first_child ":first-child"]
      [first_of_type ":first-of-type"]
      [focused ":focus"]
      [hovered ":hover"]
      [in_range ":in-range"]
      [indeterminate ":indeterminate"]
      [invalid ":invalid"]
      [last_child ":last-child"]
      [last_of_type ":last-of-type"]
      [link ":link"]
      [only_of_type ":only-of-type"]
      [only_child ":only-child"]
      [optional ":optional"]
      [out_of_range ":out-of-range"]
      [read_only ":read-only"]
      [read_write ":read-write"]
      [required ":required"]
      [root ":root"]
      [target ":target"]
      [valid ":valid"]
      [visited ":visited"]]]
    
    [Specific
     [[after "::after"]
      [before "::before"]
      [first_letter "::first-letter"]
      [first_line "::first-line"]
      [placeholder "::placeholder"]
      [selection "::selection"]]]
    )

  (def: #export (language locale)
    (-> Locale (Selector Can_Chain))
    (|> locale
        locale.code
        (text.enclosed ["(" ")"])
        (format ":lang")
        :abstraction))

  (def: #export not
    (-> (Selector Any) (Selector Can_Chain))
    (|>> :representation
         (text.enclosed ["(" ")"])
         (format ":not")
         :abstraction))

  (abstract: #export Index
    Text

    (def: #export index
      (-> Nat Index)
      (|>> %.nat :abstraction))

    (template [<name> <index>]
      [(def: #export <name> Index (:abstraction <index>))]
      
      [odd "odd"]
      [even "even"]
      )

    (type: #export Formula
      {#constant Int
       #variable Int})

    (def: #export (formula input)
      (-> Formula Index)
      (let [(^slots [#constant #variable]) input]
        (:abstraction (format (if (i.< +0 variable)
                                (%.int variable)
                                (%.nat (.nat variable)))
                              (%.int constant)))))
    
    (template [<name> <pseudo>]
      [(def: #export (<name> index)
         (-> Index (Selector Can_Chain))
         (|> (:representation index)
             (text.enclosed ["(" ")"])
             (format <pseudo>)
             (:abstraction Selector)))]

      [nth_child ":nth-child"]
      [nth_last_child ":nth-last-child"]
      [nth_last_of_type ":nth-last-of-type"]
      [nth_of_type ":nth-of-type"]
      )
    )
  )
