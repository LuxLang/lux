(.module:
  [library
   [lux #*
    [abstract
     [functor (#+ Functor)]
     comonad]
    [type
     implicit]]])

(type: .public (Store s a)
  {#cursor s
   #peek (-> s a)})

(def: (extend f wa)
  (All [s a b] (-> (-> (Store s a) b) (Store s a) (Store s b)))
  {#cursor (get@ #cursor wa)
   #peek (function (_ s) (f (set@ #cursor s wa)))})

(implementation: .public functor
  (All [s] (Functor (Store s)))
  
  (def: (map f fa)
    (extend (function (_ store)
              (f (\ store peek (\ store cursor))))
            fa)))

(implementation: .public comonad
  (All [s] (CoMonad (Store s)))
  
  (def: &functor ..functor)

  (def: (out wa) (\\ peek (\\ cursor)))

  (def: split (extend id)))

(def: .public (peeks trans store)
  (All [s a] (-> (-> s s) (Store s a) a))
  (|> (\\ cursor) trans (\\ peek)))

(def: .public (seek cursor store)
  (All [s a] (-> s (Store s a) (Store s a)))
  (\ (\\ split store) peek cursor))

(def: .public (seeks change store)
  (All [s a] (-> (-> s s) (Store s a) (Store s a)))
  (|> store (\\ split) (peeks change)))

(def: .public (experiment Functor<f> change store)
  (All [f s a] (-> (Functor f) (-> s (f s)) (Store s a) (f a)))
  (\ Functor<f> map (\\ peek) (change (\\ cursor))))
