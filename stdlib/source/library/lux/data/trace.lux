(.module:
  [library
   [lux #*
    [abstract
     ["." monoid (#+ Monoid)]
     [functor (#+ Functor)]
     comonad]
    function]])

(type: .public (Trace t a)
  {#monoid (Monoid t)
   #trace (-> t a)})

(implementation: .public functor (All [t] (Functor (Trace t)))
  (def: (map f fa)
    (revised@ #trace (compose f) fa)))

(implementation: .public comonad (All [t] (CoMonad (Trace t)))
  (def: &functor ..functor)

  (def: (out wa)
    ((value@ #trace wa)
     (value@ [#monoid #monoid.identity] wa)))

  (def: (split wa)
    (let [monoid (value@ #monoid wa)]
      {#monoid monoid
       #trace (function (_ t1)
                {#monoid monoid
                 #trace (function (_ t2)
                          ((value@ #trace wa)
                           (\ monoid compose t1 t2)))})})))

(def: .public (result context tracer)
  (All [t a] (-> t (Trace t a) a))
  (\ tracer trace context))
