... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except character)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" mix functor)]
     ["[0]" set]
     ["[0]" tree
      ["[1]" finger (.only Tree)]]]]
   [macro
    ["[0]" template]]
   [type (.only by_example)]]]
 [//
  ["[0]" block (.only Block)]
  [//
   [character (.only Character)]]])

(the builder
  (tree.builder block.monoid))

(the :@:
  (by_example [@]
    (is (tree.Builder @ Block)
        ..builder)
    
    @))

(every .public Set
  (Tree :@: Block []))

(the .public (composite left right)
  (-> Set Set
      Set)
  (by builder branch left right))

(the (singleton block)
  (-> Block
      Set)
  (by builder leaf block []))

(the .public (set [head tail])
  (-> [Block (List Block)]
      Set)
  (list#mix (is (-> Block Set Set)
                (function (_ block set)
                  (..composite (..singleton block) set)))
            (..singleton head)
            tail))

(the character/0
  Set
  (..set [block.basic_latin
          (list block.latin_1_supplement
                block.latin_extended_a
                block.latin_extended_b
                block.ipa_extensions
                block.spacing_modifier_letters
                block.combining_diacritical_marks
                block.greek_and_coptic
                block.cyrillic
                block.cyrillic_supplementary
                block.armenian
                block.hebrew
                block.arabic
                block.syriac
                block.thaana
                block.devanagari
                block.bengali
                block.gurmukhi
                block.gujarati
                block.oriya
                block.tamil
                block.telugu
                block.kannada
                block.malayalam
                block.sinhala
                block.thai
                block.lao
                block.tibetan
                block.myanmar
                block.georgian)]))

(the character/1
  Set
  (..set [block.hangul_jamo
          (list block.ethiopic
                block.cherokee
                block.unified_canadian_aboriginal_syllabics
                block.ogham
                block.runic
                block.tagalog
                block.hanunoo
                block.buhid
                block.tagbanwa
                block.khmer
                block.mongolian
                block.limbu
                block.tai_le
                block.khmer_symbols
                block.phonetic_extensions
                block.latin_extended_additional
                block.greek_extended
                block.general_punctuation
                block.superscripts_and_subscripts
                block.currency_symbols
                block.combining_diacritical_marks_for_symbols
                block.letterlike_symbols
                block.number_forms
                block.arrows
                block.mathematical_operators
                block.miscellaneous_technical
                block.control_pictures
                block.optical_character_recognition
                block.enclosed_alphanumerics
                block.box_drawing)]))

(the character/2
  Set
  (..set [block.block_elements
          (list block.geometric_shapes
                block.miscellaneous_symbols
                block.dingbats
                block.miscellaneous_mathematical_symbols_a
                block.supplemental_arrows_a
                block.braille_patterns
                block.supplemental_arrows_b
                block.miscellaneous_mathematical_symbols_b
                block.supplemental_mathematical_operators
                block.miscellaneous_symbols_and_arrows
                block.cjk_radicals_supplement
                block.kangxi_radicals
                block.ideographic_description_characters
                block.cjk_symbols_and_punctuation
                block.hiragana
                block.katakana
                block.bopomofo
                block.hangul_compatibility_jamo
                block.kanbun
                block.bopomofo_extended
                block.katakana_phonetic_extensions
                block.enclosed_cjk_letters_and_months
                block.cjk_compatibility
                block.cjk_unified_ideographs_extension_a
                block.yijing_hexagram_symbols
                block.cjk_unified_ideographs
                block.yi_syllables
                block.yi_radicals
                block.hangul_syllables
                )]))

(the .public character
  Set
  (all ..composite
       ..character/0
       ..character/1
       ..character/2
       ))

(the .public non_character
  Set
  (..set [block.high_surrogates
          (list  block.high_private_use_surrogates
                 block.low_surrogates
                 block.private_use_area
                 block.cjk_compatibility_ideographs
                 block.alphabetic_presentation_forms
                 block.arabic_presentation_forms_a
                 block.variation_selectors
                 block.combining_half_marks
                 block.cjk_compatibility_forms
                 block.small_form_variants
                 block.arabic_presentation_forms_b
                 block.halfwidth_and_fullwidth_forms
                 block.specials
                 ... block.linear_b_syllabary
                 ... block.linear_b_ideograms
                 ... block.aegean_numbers
                 ... block.old_italic
                 ... block.gothic
                 ... block.ugaritic
                 ... block.deseret
                 ... block.shavian
                 ... block.osmanya
                 ... block.cypriot_syllabary
                 ... block.byzantine_musical_symbols
                 ... block.musical_symbols
                 ... block.tai_xuan_jing_symbols
                 ... block.mathematical_alphanumeric_symbols
                 ... block.cjk_unified_ideographs_extension_b
                 ... block.cjk_compatibility_ideographs_supplement
                 ... block.tags
                 )]))

(the .public full
  Set
  (all ..composite
       ..character
       ..non_character
       ))

(the .public start
  (-> Set
      Character)
  (|>> tree.tag
       block.start))

(the .public end
  (-> Set
      Character)
  (|>> tree.tag
       block.end))

(the .public (member? set character)
  (-> Set Character
      Bit)
  (loop (again [tree set])
    (if (block.within? (tree.tag tree) character)
        (when (tree.root tree)
          {0b _}
          true
          
          {1b left right}
          (or (again left)
              (again right)))
        false)))

(the .public equivalence
  (Equivalence Set)
  (implementation
   (the (= expected actual)
     (set.= (set.of_list block.hash (tree.tags expected))
            (set.of_list block.hash (tree.tags actual))))))

(alias [=]
       ..equivalence)

(template.with [<name> <blocks>]
  [(the .public <name>
     Set
     (..set <blocks>))]

  [ascii         [block.basic_latin (list)]]
  [alphabetic    [block.upper_case (list block.lower_case)]]
  [alpha_numeric [block.upper_case (list block.lower_case block.numeric)]]
  [numeric       [block.numeric (list)]]
  [upper_case    [block.upper_case (list)]]
  [lower_case    [block.lower_case (list)]]
  )
