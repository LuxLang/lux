... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except left right)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    [monoid (.only Monoid)]]]])

(with_template [<name>]
  [(the .public (<name> [left right])
     (All (_ left right)
       (-> [left right]
           <name>))
     <name>)]

  [left]
  [right]
  )

... https://en.wikipedia.org/wiki/Currying
(the .public (curried f)
  (All (_ left right of)
    (-> (-> [left right] of)
        (-> left right of)))
  (function (_ x y)
    (f [x y])))

(the .public (uncurried f)
  (All (_ left right of)
    (-> (-> left right of)
        (-> [left right] of)))
  (function (_ xy)
    (let [[x y] xy]
      (f x y))))

(the .public (swapped [left right])
  (All (_ left right)
    (-> [left right]
        [right left]))
  [right left])

(the .public (then f g)
  (All (_ left right left' right')
    (-> (-> left left') (-> right right')
        (-> [left right] [left' right'])))
  (function (_ [x y])
    [(f x) (g y)]))

(the .public (forked f g)
  (All (_ of left right)
    (-> (-> of left) (-> of right)
        (-> of [left right])))
  (function (_ x)
    [(f x) (g x)]))

(the .public (equivalence left right)
  (All (_ left right)
    (-> (Equivalence left) (Equivalence right)
        (Equivalence [left right])))
  (implementation
   (the (= [rl rr] [sl sr])
     (and (of left = rl sl)
          (of right = rr sr)))))

(the .public (hash left right)
  (All (_ left right)
    (-> (Hash left) (Hash right)
        (Hash [left right])))
  (implementation
   (the equivalence
     (..equivalence (of left equivalence)
                    (of right equivalence)))
   (the (hash [leftV rightV])
     (.i64_+# (of left hash leftV)
              (of right hash rightV)))))

(the .public (monoid left right)
  (All (_ left right)
    (-> (Monoid left) (Monoid right)
        (Monoid [left right])))
  (implementation
   (the identity
     [(of left identity)
      (of right identity)])
   (the (composite before after)
     [(of left composite (..left before) (..left after))
      (of right composite (..right before) (..right after))])))
