(.module:
  [library
   [lux #*
    [abstract
     [equivalence (#+ Equivalence)]
     [hash (#+ Hash)]]]])

(template [<right?> <name>]
  [(def: .public (<name> value)
     (All [left right]
       (-> <name> (Or left right)))
     (0 <right?> value))]

  [#0 left]
  [#1 right])

(def: .public (either on_left on_right)
  (All [a b c]
    (-> (-> a c) (-> b c)
        (-> (Or a b) c)))
  (function (_ input)
    (case input
      (0 #0 l) (on_left l)
      (0 #1 r) (on_right r))))

(def: .public (then on_left on_right)
  (All [l l' r r']
    (-> (-> l l') (-> r r')
        (-> (Or l r) (Or l' r'))))
  (function (_ input)
    (case input
      (0 #0 l) (0 #0 (on_left l))
      (0 #1 r) (0 #1 (on_right r)))))

(template [<name> <side> <right?>]
  [(def: .public (<name> items)
     (All [a b] (-> (List (Or a b)) (List <side>)))
     (case items
       #.End
       #.End
       
       (#.Item (0 <right?> x) items')
       (#.Item [x (<name> items')])
       
       (#.Item _ items')
       (<name> items')))]

  [lefts  a #0]
  [rights b #1]
  )

(def: .public (partition xs)
  (All [a b] (-> (List (Or a b)) [(List a) (List b)]))
  (case xs
    #.End
    [#.End #.End]

    (#.Item x xs')
    (let [[lefts rights] (partition xs')]
      (case x
        (0 #0 x')  [(#.Item x' lefts) rights]
        (0 #1 x') [lefts (#.Item x' rights)]))))

(def: .public (equivalence left right)
  (All [l r] (-> (Equivalence l) (Equivalence r) (Equivalence (Or l r))))
  (implementation
   (def: (= reference sample)
     (case [reference sample]
       [(#.Left reference) (#.Left sample)]
       (\ left = reference sample)

       [(#.Right reference) (#.Right sample)]
       (\ right = reference sample)

       _
       false))))

(def: .public (hash left right)
  (All [l r] (-> (Hash l) (Hash r) (Hash (Or l r))))
  (implementation
   (def: &equivalence
     (..equivalence (\ left &equivalence)
                    (\ right &equivalence)))
   (def: (hash value)
     (case value
       (#.Left value)
       (\ left hash value)

       (#.Right value)
       (\ right hash value)))))
