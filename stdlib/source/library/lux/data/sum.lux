... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" mix)]]]
   [meta
    [macro
     ["[0]" template]]]]])

(template.with [<right?> <name>]
  [(the .public (<name> value)
     (for_any (_ left right)
       (-> <name> (Or left right)))
     {<right?> value})]

  [#0 left]
  [#1 right])

(the .public (either on_left on_right)
  (for_any (_ a b c)
    (-> (-> a c) (-> b c)
        (-> (Or a b) c)))
  (function (_ input)
    (when input
      {#0 l} (on_left l)
      {#1 r} (on_right r))))

(the .public (then on_left on_right)
  (for_any (_ l l' r r')
    (-> (-> l l') (-> r r')
        (-> (Or l r) (Or l' r'))))
  (function (_ input)
    (when input
      {#0 l} {#0 (on_left l)}
      {#1 r} {#1 (on_right r)})))

(template.with [<right?> <side> <name>]
  [(the .public (<name> it)
     (for_any (_ t0 t1) (-> (List (Or t0 t1)) (List <side>)))
     ... TODO: Use the more obvious implementation once "tail recursion modulo cons" is added to the compiler.
     (list#mix (function (_ head tail)
                 (when head
                   {<right?> head}
                   (list.partial head tail)

                   _
                   tail))
               (list)
               (list.reversed it)))]

  [#0 t0 lefts]
  [#1 t1 rights]
  )

(the .public (partition xs)
  (for_any (_ a b) (-> (List (Or a b)) [(List a) (List b)]))
  (when xs
    {.#End}
    [{.#End} {.#End}]

    {.#Item x xs'}
    (let [[lefts rights] (partition xs')]
      (when x
        {#0 x'}  [{.#Item x' lefts} rights]
        {#1 x'} [lefts {.#Item x' rights}]))))

(the .public (equivalence left right)
  (for_any (_ l r) (-> (Equivalence l) (Equivalence r) (Equivalence (Or l r))))
  (implementation
   (the (= expected actual)
     (when [expected actual]
       [{.#Left expected} {.#Left actual}]
       (of left = expected actual)

       [{.#Right expected} {.#Right actual}]
       (of right = expected actual)

       _
       false))))

(the .public (hash left right)
  (for_any (_ l r)
    (-> (Hash l) (Hash r)
        (Hash (Or l r))))
  (implementation
   (the equivalence
     (..equivalence (of left equivalence)
                    (of right equivalence)))
   (the (hash value)
     (.nat (when value
             {.#Left value}
             (.int_*# +2 (.int (of left hash value)))

             {.#Right value}
             (.int_*# +3 (.int (of right hash value))))))))
