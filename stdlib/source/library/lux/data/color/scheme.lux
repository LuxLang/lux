... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    [number
     ["i" int]
     ["d" dec]]]]]
 [//
  [rgb (.only RGB)]
  ["[0]" hsl]
  ["[0]" hsb]])

(the (ratio it)
  (-> Dec
      Dec)
  (cond (d.> +1.0 it)
        (d.% +1.0 it)

        (d.< +0.0 it)
        (|> it (d.% +1.0) (d.+ +1.0))

        ... else
        it))

(with_template [<name> <1> <2>]
  [(`` (the .public (<name> it)
         (-> RGB
             [RGB RGB RGB])
         (let [(open "/[0]") (hsl.of_rgb it)]
           [it
            (hsl.rgb (hsl.hsl (|> /#hue (d.+ <1>) ..ratio)
                              /#saturation
                              /#luminance))
            (hsl.rgb (hsl.hsl (|> /#hue (d.+ <2>) ..ratio)
                              /#saturation
                              /#luminance))])))]

  [triad            (|> +1.0 (d./ +3.0)) (|> +2.0 (d./ +3.0))]
  [clash            (|> +1.0 (d./ +4.0)) (|> +3.0 (d./ +4.0))]
  [split_complement (|> +1.0 (d./ +5.0)) (|> +3.0 (d./ +5.0))]
  )

(with_template [<name> <1> <2> <3>]
  [(`` (the .public (<name> it)
         (-> RGB
             [RGB RGB RGB RGB])
         (let [(open "/[0]") (hsl.of_rgb it)
               of_hue (is (-> hsl.Value
                              RGB)
                          (function (_ hue)
                            (hsl.rgb (hsl.hsl hue /#saturation /#luminance))))]
           [it
            (|> /#hue (d.+ <1>) ..ratio of_hue)
            (|> /#hue (d.+ <2>) ..ratio of_hue)
            (|> /#hue (d.+ <3>) ..ratio of_hue)])))]

  [square   (|> +1.0 (d./ +4.0))  (|> +2.0 (d./ +4.0))  (|> +3.0 (d./ +4.0))]
  [tetradic (|> +2.0 (d./ +12.0)) (|> +6.0 (d./ +12.0)) (|> +8.0 (d./ +12.0))]
  )

(every .public Spread
  Dec)

... https://en.wikipedia.org/wiki/Color_scheme
(every .public Scheme
  (-> Spread Nat RGB
      (List RGB)))

(the .public (analogous spread variations it)
  Scheme
  (let [it (hsl.of_rgb it)
        hue (its hsl.#hue it)
        saturation (its hsl.#saturation it)
        luminance (its hsl.#luminance it)
        spread (..ratio spread)]
    (list#each (function (_ idx)
                 (hsl.rgb (hsl.hsl (|> idx ++ .int i.dec (d.* spread) (d.+ hue) ..ratio)
                                   saturation
                                   luminance)))
               (list.indices variations))))

(the .public (monochromatic spread variations it)
  Scheme
  (let [it (hsb.of_rgb it)
        hue (hsb.hue it)
        saturation (hsb.saturation it)
        brightness (hsb.brightness it)
        spread (..ratio spread)]
    (|> (list.indices variations)
        (list#each (|>> ++ .int i.dec
                        (d.* spread)
                        (d.+ brightness)
                        ..ratio
                        (hsb.hsb hue saturation)
                        hsb.rgb)))))
