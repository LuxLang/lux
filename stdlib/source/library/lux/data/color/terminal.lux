... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... https://en.wikipedia.org/wiki/ANSI_escape_code#Colors
(.require
 [library
  [lux (.except with)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["[0]" char]
     ["%" \\format]]]
   [math
    [number
     ["n" nat]]]
   [meta
    [macro
     ["[0]" template]]
    [type
     ["[0]" nominal]]]]]
 [//
  ["[0]" rgb (.only RGB)]])

(nominal.every .public Command
  [Text Text]

  (the .public equivalence
    (Equivalence Command)
    (implementation
     (the (= reference it)
       (let [[beforeR afterR] (nominal.representation Command reference)
             [beforeI afterI] (nominal.representation Command it)]
         (and (text#= beforeR beforeI)
              (text#= afterR afterI))))))

  (the command
    Text
    (%.format (text.of_char char.escape) "["))

  (the .public (with command text)
    (-> Command Text
        Text)
    (let [[before after] (nominal.representation Command command)]
      (%.format before
                text
                after)))

  (with_template [<code> <name>]
    [(the <name>
       Text
       (%.format ..command (%.nat <code>) "m"))]

    [39 default_foreground_color]
    [49 default_background_color]
    )

  (with_template [<fg> <bg> <color>]
    [(`` (the .public (,, (template.symbol [<color> "_foreground"]))
           Command
           (|> [(%.format ..command (%.nat <fg>) "m")
                ..default_foreground_color]
               (nominal.abstraction Command))))

     (`` (the .public (,, (template.symbol [<color> "_background"]))
           Command
           (|> [(%.format ..command (%.nat <bg>) "m")
                ..default_background_color]
               (nominal.abstraction Command))))]

    [030 040 black]
    [031 041 red]
    [032 042 green]
    [033 043 yellow]
    [034 044 blue]
    [035 045 magenta]
    [036 046 cyan]
    [037 047 white]
    
    [090 100 bright_black]
    [091 101 bright_red]
    [092 102 bright_green]
    [093 103 bright_yellow]
    [094 104 bright_blue]
    [095 105 bright_magenta]
    [096 106 bright_cyan]
    [097 107 bright_white]
    )

  (with_template [<command> <name> <reset>]
    [(the .public (<name> it)
       (-> RGB
           Command)
       (|> [(%.format ..command
                      <command>
                      ";" (%.nat (rgb.red it))
                      ";" (%.nat (rgb.green it))
                      ";" (%.nat (rgb.blue it))
                      "m")
            <reset>]
           (nominal.abstraction Command)))]

    ["38;2" foreground ..default_foreground_color]
    ["48;2" background ..default_background_color]
    )
  )
