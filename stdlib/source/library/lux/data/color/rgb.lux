... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [monoid (.only Monoid)]
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [control
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    [number
     ["n" natural]
     ["i" integer]
     ["d" decimal]
     ["[0]" i64]]]
   [meta
    [macro
     ["[0]" template]]]]])

(the mask
  i64.Mask
  (i64.mask 8))

(the .public limit
  Natural
  (++ (natural ..mask)))

(every .public Value
  Natural)

(template.with [<name> <value>]
  [(the .public <name>
     Value
     <value>)]

  [least 0]
  [most (-- limit)]
  )

(the .public (value? it)
  (Predicate Natural)
  (not (or (n.< ..least it)
           (n.> ..most it))))

(the .public value
  (-> Natural
      Value)
  (i64.and ..mask))

(every .public RGB
  Natural)

(template.with [<section> <name>]
  [(the .public <name>
     (-> RGB
         Value)
     (|>> (i64.right_shifted (n.* 8 <section>))
          (i64.and ..mask)))]

  [2 red]
  [1 green]
  [0 blue]
  )

(the .public (rgb red green blue)
  (-> Natural Natural Natural
      RGB)
  (`` (all i64.or
           (,, (template.with [<section> <name>]
                 [(i64.left_shifted (n.* 8 <section>) (value <name>))]

                 [2 red]
                 [1 green]
                 [0 blue]
                 ))
           )))

(the .public hash
  (Hash RGB)
  n.hash)

(the .public equivalence
  (Equivalence RGB)
  (by ..hash equivalence))

(alias [=]
       ..equivalence)

(the (opposite_intensity value)
  (-> Value
      Value)
  (|> ..most
      (n.- value)))

(the .public (complement it)
  (-> RGB
      RGB)
  (`` (rgb (,, (template.with [<slot>]
                 [(|> it
                      <slot>
                      opposite_intensity)]

                 [red]
                 [green]
                 [blue]
                 )))))

(template.with [<color> <value>]
  [(the .public <color>
     RGB
     (rgb <value> <value> <value>))]

  [black ..least]
  [white ..most]
  )

(template.with [<identity> <composite> <monoid>]
  [(the .public <monoid>
     (Monoid RGB)
     (implementation
      (the identity
        <identity>)

      (the (composite left right)
        (`` (rgb (,, (template.with [<slot>]
                       [(<composite> (<slot> left)
                                     (<slot> right))]

                       [red]
                       [green]
                       [blue]
                       )))))))]

  [..black n.max addition]
  [..white n.min subtraction]
  )

(the (ratio it)
  (-> Decimal
      Decimal)
  (if (d.> +1.0 it)
    (d.% +1.0 it)

    (d.< +0.0 it)
    (|> it (d.% +1.0) (d.+ +1.0))

    ... else
    it))

(the .public (interpolated end ratio start)
  (-> RGB Decimal RGB
      RGB)
  (let [dS (..ratio ratio)
        dE (|> +1.0 (d.- dS))
        interpolated' (is (-> Natural Natural
                              Natural)
                          (function (_ end start)
                            (|> (|> start .integer i.decimal (d.* dS))
                                (d.+ (|> end .integer i.decimal (d.* dE)))
                                d.integer
                                .natural)))]
    (..rgb (interpolated' (red end) (red start))
           (interpolated' (green end) (green start))
           (interpolated' (blue end) (blue start)))))

(template.with [<name> <target>]
  [(the .public <name>
     (-> Decimal RGB
         RGB)
     (..interpolated <target>))]

  [darker   ..black]
  [brighter ..white]
  )

(the .public (injection it)
  (text.Injection RGB)
  (%.message "rgb("
             (%.natural (red it))
             "," (%.natural (green it))
             "," (%.natural (blue it))
             ")"))
