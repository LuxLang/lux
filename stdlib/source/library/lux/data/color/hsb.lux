... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    [function
     [predicate (.only Predicate)]]]
   [data
    [text
     ["%" \\injection]]]
   [math
    [number
     ["d" dec]
     ["[0]" nat]
     ["[0]" int]]]
   [meta
    [type
     ["[0]" nominal]]
    ["[0]" macro
     ["[1]" local]]]]]
 [//
  ["[0]" rgb (.only RGB)]])

(every .public Value
  Dec)

(with_template [<value> <name>]
  [(the .public <name>
     Value
     <value>)]

  [+0.0 least]
  [+1.0 most]
  )

(the .public (value? it)
  (Predicate Dec)
  (not (or (d.< ..least it)
           (d.> ..most it))))

(the .public value
  (-> Dec
      Value)
  (|>> (d.max ..least)
       (d.min ..most)))

(nominal.every .public HSB
  (Record
   [#hue Value
    #saturation Value
    #brightness Value])

  (the .public equivalence
    (Equivalence HSB)
    (implementation
     (the (= left right)
       (`` (and (,, (with_template [<slot>]
                      [(d.= (its <slot> (nominal.representation left))
                            (its <slot> (nominal.representation right)))]

                      [#hue]
                      [#saturation]
                      [#brightness]
                      )))))))

  (with_template [<name> <slot>]
    [(the .public <name>
       (-> HSB
           Value)
       (|>> nominal.representation
            (its <slot>)))]

    [hue #hue]
    [saturation #saturation]
    [brightness #brightness]
    )

  (the .public (hsb hue saturation brightness)
    (-> Dec Dec Dec
        HSB)
    (nominal.abstraction
     [#hue (..value hue)
      #saturation (..value saturation)
      #brightness (..value brightness)]))

  (the top
    (-- rgb.limit))

  (the rgb_factor
    (|> top .int int.dec))

  (the down
    (-> Nat
        Dec)
    (|>> nat.dec (d./ rgb_factor)))

  (the up
    (-> Dec
        Nat)
    (|>> (d.* rgb_factor) d.round d.int .nat))

  (the .public (of_rgb it)
    (-> RGB
        HSB)
    (let [red (..down (rgb.red it))
          green (..down (rgb.green it))
          blue (..down (rgb.blue it))

          brightness (all d.max red green blue)
          range (all d.min red green blue)

          chroma (|> brightness (d.- range))
          saturation (if (d.= +0.0 brightness)
                       +0.0
                       (|> chroma (d./ brightness)))]
      (macro.let [hue_of (template (_ <base> <shift> <adjustment>)
                           [(|> <base> (d.- <shift>)
                                (d./ chroma)
                                <adjustment>
                                (d./ +6.0))])]
        (nominal.abstraction
         [#hue (cond (d.= +0.0 chroma)
                     ... Achromatic
                     +0.0
                     ... Chromatic
                     (d.= brightness red)
                     (hue_of green blue (d.mod +6.0))

                     (d.= brightness green)
                     (hue_of blue red (d.+ +2.0))

                     ... (d.= brightness blue)
                     (hue_of red green (d.+ +4.0)))
          #saturation saturation
          #brightness brightness]))))

  (the .public (rgb it)
    (-> HSB
        RGB)
    (let [[hue saturation brightness] (nominal.representation it)
          hue (|> hue (d.* +6.0))
          
          i (d.floor hue)
          f (|> hue (d.- i))
          p (|> +1.0 (d.- saturation) (d.* brightness))
          q (|> +1.0 (d.- (d.* f saturation)) (d.* brightness))
          t (|> +1.0 (d.- (|> +1.0 (d.- f) (d.* saturation))) (d.* brightness))
          v brightness
          mod (|> i (d.% +6.0) d.int .nat)
          
          red (when mod 0 v 1 q 2 p 3 p 4 t 5 v _ (undefined))
          green (when mod 0 t 1 v 2 v 3 q 4 p 5 p _ (undefined))
          blue (when mod 0 p 1 p 2 t 3 v 4 v 5 q _ (undefined))]
      (rgb.rgb (..up red)
               (..up green)
               (..up blue))))

  (the .public (format it)
    (%.Injection HSB)
    (let [it (nominal.representation it)]
      (%.format "hsb("
                (%.nat (d.nat (d.as_degree (its #hue it))))
                " " (%.nat (d.nat (d.as_percentage (its #saturation it)))) "%"
                " " (%.nat (d.nat (d.as_percentage (its #brightness it)))) "%"
                ")")))
  )
