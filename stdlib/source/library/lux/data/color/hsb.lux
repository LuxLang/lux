... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    [function
     [predicate (.only Predicate)]]]
   [data
    [text
     ["%" \\format]]]
   [math
    [number
     ["f" frac]
     ["[0]" nat]
     ["[0]" int]]]
   [meta
    [type
     ["[0]" nominal]]
    ["[0]" macro
     ["[1]" local]]]]]
 [//
  ["[0]" rgb (.only RGB)]])

(every .public Value
  Frac)

(with_template [<value> <name>]
  [(the .public <name>
     Value
     <value>)]

  [+0.0 least]
  [+1.0 most]
  )

(the .public (value? it)
  (Predicate Frac)
  (not (or (f.< ..least it)
           (f.> ..most it))))

(the .public value
  (-> Frac
      Value)
  (|>> (f.max ..least)
       (f.min ..most)))

(nominal.every .public HSB
  (Record
   [#hue Value
    #saturation Value
    #brightness Value])

  (the .public equivalence
    (Equivalence HSB)
    (implementation
     (the (= left right)
       (`` (and (,, (with_template [<slot>]
                      [(f.= (its <slot> (nominal.representation left))
                            (its <slot> (nominal.representation right)))]

                      [#hue]
                      [#saturation]
                      [#brightness]
                      )))))))

  (with_template [<name> <slot>]
    [(the .public <name>
       (-> HSB
           Value)
       (|>> nominal.representation
            (its <slot>)))]

    [hue #hue]
    [saturation #saturation]
    [brightness #brightness]
    )

  (the .public (hsb hue saturation brightness)
    (-> Frac Frac Frac
        HSB)
    (nominal.abstraction
     [#hue (..value hue)
      #saturation (..value saturation)
      #brightness (..value brightness)]))

  (the top
    (-- rgb.limit))

  (the rgb_factor
    (|> top .int int.frac))

  (the down
    (-> Nat
        Frac)
    (|>> nat.frac (f./ rgb_factor)))

  (the up
    (-> Frac
        Nat)
    (|>> (f.* rgb_factor) f.round f.int .nat))

  (the .public (of_rgb it)
    (-> RGB
        HSB)
    (let [red (..down (rgb.red it))
          green (..down (rgb.green it))
          blue (..down (rgb.blue it))

          brightness (all f.max red green blue)
          range (all f.min red green blue)

          chroma (|> brightness (f.- range))
          saturation (if (f.= +0.0 brightness)
                       +0.0
                       (|> chroma (f./ brightness)))]
      (macro.let [hue_of (template (_ <base> <shift> <adjustment>)
                           [(|> <base> (f.- <shift>)
                                (f./ chroma)
                                <adjustment>
                                (f./ +6.0))])]
        (nominal.abstraction
         [#hue (cond (f.= +0.0 chroma)
                     ... Achromatic
                     +0.0
                     ... Chromatic
                     (f.= brightness red)
                     (hue_of green blue (f.mod +6.0))

                     (f.= brightness green)
                     (hue_of blue red (f.+ +2.0))

                     ... (f.= brightness blue)
                     (hue_of red green (f.+ +4.0)))
          #saturation saturation
          #brightness brightness]))))

  (the .public (rgb it)
    (-> HSB
        RGB)
    (let [[hue saturation brightness] (nominal.representation it)
          hue (|> hue (f.* +6.0))
          
          i (f.floor hue)
          f (|> hue (f.- i))
          p (|> +1.0 (f.- saturation) (f.* brightness))
          q (|> +1.0 (f.- (f.* f saturation)) (f.* brightness))
          t (|> +1.0 (f.- (|> +1.0 (f.- f) (f.* saturation))) (f.* brightness))
          v brightness
          mod (|> i (f.% +6.0) f.int .nat)
          
          red (when mod 0 v 1 q 2 p 3 p 4 t 5 v _ (undefined))
          green (when mod 0 t 1 v 2 v 3 q 4 p 5 p _ (undefined))
          blue (when mod 0 p 1 p 2 t 3 v 4 v 5 q _ (undefined))]
      (rgb.rgb (..up red)
               (..up green)
               (..up blue))))

  (the .public (format it)
    (%.Format HSB)
    (let [it (nominal.representation it)]
      (%.format "hsb("
                (%.nat (f.nat (f.as_degree (its #hue it))))
                " " (%.nat (f.nat (f.as_percentage (its #saturation it)))) "%"
                " " (%.nat (f.nat (f.as_percentage (its #brightness it)))) "%"
                ")")))
  )
