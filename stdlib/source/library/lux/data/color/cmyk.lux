... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" product]]
   [math
    [number
     ["d" dec]
     ["[0]" int]]]
   [meta
    [macro
     ["[0]" template]]]]]
 [//
  ["[0]" rgb (.only RGB)]])

(every .public Value
  Dec)

(template.with [<value> <name>]
  [(the .public <name>
     Value
     <value>)]

  [+0.0 least]
  [+1.0 most]
  )

(the .public (value? it)
  (Predicate Dec)
  (not (or (d.< ..least it)
           (d.> ..most it))))

(the .public value
  (-> Dec
      Value)
  (|>> (d.max ..least)
       (d.min ..most)))

(every .public CMYK
  (Record
   [#cyan Value
    #magenta Value
    #yellow Value
    #key Value]))

(the .public equivalence
  (Equivalence CMYK)
  (all product.equivalence
       d.equivalence
       d.equivalence
       d.equivalence
       d.equivalence
       ))

(alias [=]
       ..equivalence)

(the top
  (-- rgb.limit))

(the rgb_factor
  (|> top .int int.dec))

(the down
  (-> Natural
      Dec)
  (|>> .int int.dec (d./ rgb_factor)))

(the up
  (-> Dec
      Natural)
  (|>> (d.* rgb_factor) d.round d.int .natural))

(the (opposite it)
  (-> Dec
      Dec)
  (d.- it ..most))

(the .public (of_rgb it)
  (-> RGB
      CMYK)
  (let [red (..down (rgb.red it))
        green (..down (rgb.green it))
        blue (..down (rgb.blue it))
        key (opposite (all d.max red green blue))
        f (if (d.< ..most key)
            (d./ (opposite key)
                 ..most)
            ..least)]
    [#cyan (|> ..most (d.- red) (d.- key) (d.* f))
     #magenta (|> ..most (d.- green) (d.- key) (d.* f))
     #yellow (|> ..most (d.- blue) (d.- key) (d.* f))
     #key key]))

(the .public (rgb it)
  (-> CMYK
      RGB)
  (when (its #key it)
    ..most
    rgb.black
    
    key
    (let [~key (opposite key)]
      (rgb.rgb (..up (d.* ~key (opposite (its #cyan it))))
               (..up (d.* ~key (opposite (its #magenta it))))
               (..up (d.* ~key (opposite (its #yellow it))))))))
