(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    ["[0]" try]
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" product]]
   [math
    [number
     ["f" frac]
     ["[0]" int]]]]]
 [//
  ["[0]" rgb (.only RGB)]])

(def .public Value
  Frac)

(with_template [<value> <name>]
  [(def .public <name>
     Value
     <value>)]

  [+0.0 least]
  [+1.0 most]
  )

(def .public (value? it)
  (Predicate Frac)
  (not (or (f.< ..least it)
           (f.> ..most it))))

(def .public value
  (-> Frac
      Value)
  (|>> (f.max ..least)
       (f.min ..most)))

(type .public CMYK
  (Record
   [#cyan Value
    #magenta Value
    #yellow Value
    #key Value]))

(def .public equivalence
  (Equivalence CMYK)
  (all product.equivalence
       f.equivalence
       f.equivalence
       f.equivalence
       f.equivalence
       ))

(def top
  (-- rgb.limit))

(def rgb_factor
  (|> top .int int.frac))

(def down
  (-> Nat
      Frac)
  (|>> .int int.frac (f./ rgb_factor)))

(def up
  (-> Frac
      Nat)
  (|>> (f.* rgb_factor) f.int .nat))

(def (opposite it)
  (-> Frac
      Frac)
  (f.- it ..most))

(def .public (cmyk it)
  (-> RGB
      CMYK)
  (let [red (..down (rgb.number (the rgb.#red it)))
        green (..down (rgb.number (the rgb.#green it)))
        blue (..down (rgb.number (the rgb.#blue it)))
        key (opposite (all f.max red green blue))
        f (if (f.< ..most key)
            (f./ (opposite key)
                 ..most)
            ..least)]
    [#cyan (|> ..most (f.- red) (f.- key) (f.* f))
     #magenta (|> ..most (f.- green) (f.- key) (f.* f))
     #yellow (|> ..most (f.- blue) (f.- key) (f.* f))
     #key key]))

(def .public (rgb it)
  (-> CMYK
      RGB)
  (when (the #key it)
    ..most
    rgb.black
    
    key
    (let [~key (opposite key)
          red (f.* ~key
                   (opposite (the #cyan it)))
          green (f.* ~key
                     (opposite (the #magenta it)))
          blue (f.* ~key
                    (opposite (the #yellow it)))]
      (|> (rgb.rgb (..up red)
                   (..up green)
                   (..up blue))
          try.trusted))))
