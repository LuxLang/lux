... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" product]]
   [math
    [number
     ["f" frac]
     ["[0]" int]]]]]
 [//
  ["[0]" rgb (.only RGB)]])

(every .public Value
  Frac)

(with_template [<value> <name>]
  [(the .public <name>
     Value
     <value>)]

  [+0.0 least]
  [+1.0 most]
  )

(the .public (value? it)
  (Predicate Frac)
  (not (or (f.< ..least it)
           (f.> ..most it))))

(the .public value
  (-> Frac
      Value)
  (|>> (f.max ..least)
       (f.min ..most)))

(every .public CMYK
  (Record
   [#cyan Value
    #magenta Value
    #yellow Value
    #key Value]))

(the .public equivalence
  (Equivalence CMYK)
  (all product.equivalence
       f.equivalence
       f.equivalence
       f.equivalence
       f.equivalence
       ))

(the top
  (-- rgb.limit))

(the rgb_factor
  (|> top .int int.frac))

(the down
  (-> Nat
      Frac)
  (|>> .int int.frac (f./ rgb_factor)))

(the up
  (-> Frac
      Nat)
  (|>> (f.* rgb_factor) f.round f.int .nat))

(the (opposite it)
  (-> Frac
      Frac)
  (f.- it ..most))

(the .public (of_rgb it)
  (-> RGB
      CMYK)
  (let [red (..down (rgb.red it))
        green (..down (rgb.green it))
        blue (..down (rgb.blue it))
        key (opposite (all f.max red green blue))
        f (if (f.< ..most key)
            (f./ (opposite key)
                 ..most)
            ..least)]
    [#cyan (|> ..most (f.- red) (f.- key) (f.* f))
     #magenta (|> ..most (f.- green) (f.- key) (f.* f))
     #yellow (|> ..most (f.- blue) (f.- key) (f.* f))
     #key key]))

(the .public (rgb it)
  (-> CMYK
      RGB)
  (when (its #key it)
    ..most
    rgb.black
    
    key
    (let [~key (opposite key)]
      (rgb.rgb (..up (f.* ~key (opposite (its #cyan it))))
               (..up (f.* ~key (opposite (its #magenta it))))
               (..up (f.* ~key (opposite (its #yellow it))))))))
