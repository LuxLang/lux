... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]]
   [control
    [function
     [predicate (.only Predicate)]]]
   [data
    [text
     ["%" \\format]]]
   [math
    [number
     ["i" int]
     ["f" frac]]]]]
 [//
  ["[0]" rgb (.only RGB)]])

(the top
  (-- rgb.limit))

(the rgb_factor
  (|> top .int i.frac))

(the down
  (-> Nat
      Frac)
  (|>> .int i.frac (f./ rgb_factor)))

(the up
  (-> Frac
      Nat)
  (|>> (f.* rgb_factor) f.round f.int .nat))

(every .public Value
  Frac)

(with_template [<value> <name>]
  [(the .public <name>
     Value
     <value>)]

  [+0.0 least]
  [+1.0 most]
  )

(the .public (value? it)
  (Predicate Frac)
  (not (or (f.< ..least it)
           (f.> ..most it))))

(the .public value
  (-> Frac
      Value)
  (|>> (f.max ..least)
       (f.min ..most)))

(every .public HSL
  (Record
   [#hue Value
    #saturation Value
    #luminance Value]))

(the .public equivalence
  (Equivalence HSL)
  (implementation
   (the (= left right)
     (`` (and (,, (with_template [<slot>]
                    [(f.= (its <slot> left)
                          (its <slot> right))]

                    [#hue]
                    [#saturation]
                    [#luminance]
                    )))))))

(the .public (hsl hue saturation luminance)
  (-> Frac Frac Frac
      HSL)
  [#hue (..value hue)
   #saturation (..value saturation)
   #luminance (..value luminance)])

(the .public (of_rgb it)
  (-> RGB
      HSL)
  (let [red (..down (rgb.red it))
        green (..down (rgb.green it))
        blue (..down (rgb.blue it))

        max (all f.max red green blue)
        min (all f.min red green blue)
        luminance (|> (f.+ max min) (f./ +2.0))]
    (if (f.= max min)
      ... Achromatic
      [#hue ..least
       #saturation ..least
       #luminance luminance]
      ... Chromatic
      (let [diff (|> max (f.- min))
            saturation (|> diff
                           (f./ (if (f.> +0.5 luminance)
                                  (|> +2.0 (f.- max) (f.- min))
                                  (|> max (f.+ min)))))
            hue' (cond (f.= red max)
                       (|> green (f.- blue) (f./ diff)
                           (f.+ (if (f.< blue green) +6.0 +0.0)))
                       
                       (f.= green max)
                       (|> blue (f.- red) (f./ diff)
                           (f.+ +2.0))
                       
                       ... (f.= blue max)
                       (|> red (f.- green) (f./ diff)
                           (f.+ +4.0)))]
        [#hue (|> hue' (f./ +6.0))
         #saturation saturation
         #luminance luminance]))))

(the (hue_rgb p q t)
  (-> Frac Frac Frac
      Nat)
  (let [t (cond (f.< +0.0 t) (f.+ +1.0 t)
                (f.> +1.0 t) (f.- +1.0 t)
                ... else
                t)
        f2/3 (f./ +3.0 +2.0)]
    (..up (cond (f.< (f./ +6.0 +1.0) t)
                (|> q (f.- p) (f.* +6.0) (f.* t) (f.+ p))
                
                (f.< (f./ +2.0 +1.0) t)
                q
                
                (f.< f2/3 t)
                (|> q (f.- p) (f.* (|> f2/3 (f.- t))) (f.* +6.0) (f.+ p))
                
                ... else
                p))))

(the .public (rgb (open "/[0]"))
  (-> HSL
      RGB)
  (if (f.= ..least /#saturation)
    ... Achromatic
    (let [intensity (..up /#luminance)]
      (rgb.rgb intensity intensity intensity))
    ... Chromatic
    (let [q (if (f.< +0.5 /#luminance)
              (|> /#saturation (f.+ +1.0) (f.* /#luminance))
              (|> /#luminance (f.+ /#saturation) (f.- (f.* /#saturation /#luminance))))
          p (|> /#luminance (f.* +2.0) (f.- q))
          third (|> +1.0 (f./ +3.0))]
      (rgb.rgb (|> /#hue (f.+ third) (hue_rgb p q))
               (|> /#hue (hue_rgb p q))
               (|> /#hue (f.- third) (hue_rgb p q))))))

(the (ratio it)
  (-> Frac
      Frac)
  (cond (f.> +1.0 it)
        (f.% +1.0 it)

        (f.< +0.0 it)
        (|> it (f.% +1.0) (f.+ +1.0))

        ... else
        it))

(with_template [<op> <name>]
  [(the .public (<name> ratio (open "/[0]"))
     (-> Frac HSL
         HSL)
     (..hsl /#hue
            (|> /#saturation
                (f.* (|> +1.0 (<op> (..ratio ratio))))
                (f.min +1.0))
            /#luminance))]

  [f.+ saturated]
  [f.- un_saturated]
  )

(the .public gray_scale
  (-> HSL
      HSL)
  (|>> (its #luminance)
       (..hsl +0.0
              +0.0)))

(the .public (format it)
  (%.Format HSL)
  (%.format "hsl("
            (%.nat (f.nat (f.as_degree (its #hue it))))
            " " (%.nat (f.nat (f.as_percentage (its #saturation it)))) "%"
            " " (%.nat (f.nat (f.as_percentage (its #luminance it)))) "%"
            ")"))
