... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monoid (.only Monoid)]
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    [format (.only Format)]]
   [control
    ["[0]" error]
    ["[0]" try]]]])

(the with_template' (.in_module# .prelude .with_template))

(with_template' [<zero> <one>]
  [(the .public <zero> Bit #0)
   (the .public <one> Bit  #1)]

  [no  yes]
  [off on]

  ... https://en.wikipedia.org/wiki/Up_tack
  ... https://en.wikipedia.org/wiki/Tee_(symbol)
  [falsum verum]
  )

(the .public equivalence
  (Equivalence Bit)
  (implementation
   (the (= x y)
     (if x
       y
       (not y)))))

(alias [=]
       ..equivalence)

(the .public hash
  (Hash Bit)
  (implementation
   (the equivalence ..equivalence)
   
   (the (hash value)
     (when value
       #0 2
       #1 3))))

(with_template' [<name> <identity> <op>]
  [(the .public <name>
     (Monoid Bit)
     (implementation
      (the identity <identity>)
      (the (composite x y) (<op> x y))))]

  [disjunction #0 or]
  [conjunction #1 and]
  )

(the .public not_valid
  (error.error "Not valid."))

(the false_text "#0")
(the true_text "#1")

(the .public (as_text it)
  (if it
    ..true_text
    ..false_text))

(the .public text
  (Format Text Bit)
  (implementation
   (the as ..as_text)

   (the (of input)
     (when input
       ..false_text
       {try.#Success #0}
       
       ..true_text
       {try.#Success #1}

       _
       {try.#Failure ..not_valid}))))
