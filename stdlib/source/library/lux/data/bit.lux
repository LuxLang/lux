... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monoid (.only Monoid)]
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    [codec (.only Codec)]]
   [meta
    ["[0]" location]]]])

(with_template [<zero> <one>]
  [(the .public <zero> Bit #0)
   (the .public <one> Bit  #1)]

  [no  yes]
  [off on]

  ... https://en.wikipedia.org/wiki/Up_tack
  ... https://en.wikipedia.org/wiki/Tee_(symbol)
  [falsum verum]
  )

(the .public equivalence
  (Equivalence Bit)
  (implementation
   (the (= x y)
     (if x
       y
       (not y)))))

(the .public hash
  (Hash Bit)
  (implementation
   (the equivalence ..equivalence)
   
   (the (hash value)
     (when value
       #0 2
       #1 3))))

(with_template [<name> <identity> <op>]
  [(the .public <name>
     (Monoid Bit)
     (implementation
      (the identity <identity>)
      (the (composite x y) (<op> x y))))]

  [disjunction #0 or]
  [conjunction #1 and]
  )

(the .public cannot_decode
  Error
  (location.with (location.here)
    "Cannot decode."))

(the false_format "#0")
(the true_format "#1")

(the .public codec
  (Codec Text Bit)
  (implementation
   (the (encoded x)
     (if x
       ..true_format
       ..false_format))

   (the (decoded input)
     (when input
       ..false_format
       {.#Right #0}
       
       ..true_format
       {.#Right #1}

       _
       {.#Left ..cannot_decode}))))
