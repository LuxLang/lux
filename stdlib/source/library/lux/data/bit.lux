... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monoid (.only Monoid)]
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    [codec (.only Codec)]]
   [control
    ["[0]" function]]]])

(with_template [<zero> <one>]
  [(the .public <zero> Bit #0)
   (the .public <one> Bit  #1)]

  [no  yes]
  [off on]
  )

(the .public equivalence
  (Equivalence Bit)
  (implementation
   (the (= x y)
     (if x
       y
       (not y)))))

(the .public hash
  (Hash Bit)
  (implementation
   (the equivalence ..equivalence)
   
   (the (hash value)
     (when value
       #0 2
       #1 3))))

(with_template [<name> <identity> <op>]
  [(the .public <name>
     (Monoid Bit)
     (implementation
      (the identity <identity>)
      (the (composite x y) (<op> x y))))]

  [disjunction #0 or]
  [conjunction #1 and]
  )

(the .public codec
  (Codec Text Bit)
  (implementation
   (the (encoded x)
     (if x
       "#1"
       "#0"))

   (the (decoded input)
     (when input
       "#1" {.#Right #1}
       "#0" {.#Right #0}
       _    {.#Left "Wrong syntax for Bit."}))))

(the .public complement
  (All (_ a) (-> (-> a Bit) (-> a Bit)))
  (function.composite not))
