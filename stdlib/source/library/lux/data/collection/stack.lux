... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except revised all only with when)
   [abstract
    [monoid (.only Monoid)]
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    ["[0]" functor (.only Functor)]
    ["[0]" monad (.only Monad do)]
    ["[0]" enum]]
   [algorithm
    [mix (.only Mix)]]
   [error (.only error)
    ["[0]" try (.only Try)]]
   [function
    [predicate (.only Predicate)]]
   [data
    ["[0]" bit]
    ["[0]" product]]
   [math
    [number
     ["n" natural]]]]])

(the with_template' (.in_module# .prelude .with_template))
(the template#macro (.in_module# .prelude .template#macro))

... (every (Stack it)
...   (Variant
...    {#Empty}
...    {#Top it (Stack it)}))

(the .public mix
  (Mix Stack)
  (implementation
   (the (mix f init xs)
     (.when xs
       {.#Top x xs'}
       (mix f (f x init) xs')

       end
       init))))

(the .public (mixes f init inputs)
  (for_any (_ it mix)
    (-> (-> it mix mix) mix (Stack it)
        (Stack mix)))
  (.when inputs
    {.#Top [head tail]}
    {.#Top [init (mixes f (f head init) tail)]}

    end
    (stack init)))

(the .public (reversed it)
  (for_any (_ it)
    (-> (Stack it)
        (Stack it)))
  (mix (function (_ head tail)
         {.#Top head tail})
       {.#Empty}
       it))

(the .public (only keep? xs)
  (for_any (_ it)
    (-> (Predicate it) (Stack it)
        (Stack it)))
  (.when xs
    {.#Top x xs'}
    (if (keep? x)
      {.#Top x (only keep? xs')}
      (only keep? xs'))

    end
    end))

(the .public (partition satisfies? stack)
  (for_any (_ it)
    (-> (Predicate it) (Stack it)
        [(Stack it) (Stack it)]))
  (.when stack
    {.#Top head tail}
    (let [[in out] (partition satisfies? tail)]
      (if (satisfies? head)
        [{.#Top head in} out]
        [in {.#Top head out}]))

    end
    [end end]))

(the .public partial
  (macro (_ tokens state)
    (.when (reversed tokens)
      {.#Top tail heads}
      {.#Right [state (stack (..mix (function (_ head tail)
                                      (` {.#Top (, head) (, tail)}))
                                    tail
                                    heads))]}

      _
      {.#Left .wrong_syntax})))

(the .public un_even
  (..error "Cannot pair values of an un-even stack."))

(the .public (pairs xs)
  (for_any (_ it)
    (-> (Stack it)
        (Try (Stack [it it]))))
  (.when xs
    (..partial x1 x2 xs')
    (.when (pairs xs')
      {try.#Success tail}
      {try.#Success (partial [x1 x2] tail)}

      failure
      failure)

    (stack)
    {try.#Success (stack)}

    _
    {try.#Failure ..un_even}))

(with_template' [<name> <then> <else>]
  [(the .public (<name> n xs)
     (for_any (_ it)
       (-> Natural (Stack it)
           (Stack it)))
     (if (n.> 0 n)
       (.when xs
         {.#Top x xs'}
         <then>

         end
         end)
       <else>))]
  
  [first {.#Top x (first (-- n) xs')} {.#Empty}]
  [after (after (-- n) xs') xs]
  )

(with_template' [<name> <then> <else>]
  [(the .public (<name> predicate xs)
     (for_any (_ it)
       (-> (Predicate it) (Stack it)
           (Stack it)))
     (.when xs
       {.#Top x xs'}
       (if (predicate x)
         <then>
         <else>)

       end
       end))]

  [while {.#Top x (while predicate xs')} {.#Empty}]
  [until xs (until predicate xs')]
  )

(the .public (split_at n xs)
  (for_any (_ it)
    (-> Natural (Stack it)
        [(Stack it) (Stack it)]))
  (.when n
    0 [{.#Empty} xs]
    _ (.when xs
        {.#Top x xs'}
        (let [[tail rest] (split_at (-- n) xs')]
          [{.#Top x tail} rest])

        end
        [end end])))

(the (split_when' predicate ys xs)
  (for_any (_ it)
    (-> (Predicate it) (Stack it) (Stack it)
        [(Stack it) (Stack it)]))
  (.when xs
    {.#Top x xs'}
    (if (predicate x)
      [ys xs]
      (split_when' predicate {.#Top x ys} xs'))

    end
    [ys xs]))

(the .public (split_when predicate xs)
  (for_any (_ it)
    (-> (Predicate it) (Stack it)
        [(Stack it) (Stack it)]))
  (let [[ys' xs'] (split_when' predicate {.#Empty} xs)]
    [(reversed ys') xs']))

(the .public (sub size stack)
  (for_any (_ it)
    (-> Natural (Stack it)
        (Stack (Stack it))))
  (.when stack
    {.#Empty}
    {.#Empty}

    _
    (let [[pre post] (split_at size stack)]
      {.#Top pre (sub size post)})))

(the .public (repeated n x)
  (for_any (_ it)
    (-> Natural it
        (Stack it)))
  (.when n
    0 {.#Empty}
    _ {.#Top x (repeated (-- n) x)}))

(the (iterations' f x)
  (for_any (_ it)
    (-> (-> it (Maybe it)) it
        (Stack it)))
  (.when (f x)
    {.#Some x'}
    {.#Top x (iterations' f x')}

    {.#None}
    (stack)))

(the .public (iterations f x)
  (for_any (_ it)
    (-> (-> it (Maybe it)) it
        (Stack it)))
  (.when (f x)
    {.#Some x'}
    {.#Top x (iterations' f x')}

    {.#None}
    (stack x)))

(the .public no_one
  (..error "No one value to be found."))

(the .public (one check xs)
  (for_any (_ input output)
    (-> (-> input (Maybe output)) (Stack input)
        (Try output)))
  (.when xs
    {.#Top x xs'}
    (.when (check x)
      {.#None}
      (one check xs')

      {.#Some it}
      {try.#Success it})

    end
    {try.#Failure ..no_one}))

(the .public (all check xs)
  (for_any (_ a b)
    (-> (-> a (Maybe b)) (Stack a) (Stack b)))
  (for .js
       ... TODO: Stop relying on this ASAP.
       (mix (function (_ head tail)
              (.when (check head)
                {.#Some head}
                {.#Top head tail}
                
                {.#None}
                tail))
            {.#Empty}
            (reversed xs))
       (.when xs
         {.#Top x xs'}
         (.when (check x)
           {.#Some output}
           {.#Top output (all check xs')}
           
           {.#None}
           (all check xs'))

         {.#Empty}
         {.#Empty})))

(the .public (example predicate xs)
  (for_any (_ it)
    (-> (Predicate it) (Stack it)
        (Try it)))
  (..one (function (_ value)
           (if (predicate value)
             {.#Some value}
             {.#None}))
         xs))

(the .public (interposed sep xs)
  (for_any (_ it)
    (-> it (Stack it)
        (Stack it)))
  (.when xs
    (stack x)
    xs

    (..partial x xs')
    (..partial x sep (interposed sep xs'))

    end
    end))

(the .public size
  (for_any (_ it)
    (-> (Stack it)
        Nat))
  (mix (function (_ _ acc)
         (++ acc))
       0))

(with_template' [<name> <init> <op>]
  [(the .public (<name> predicate items)
     (for_any (_ it)
       (-> (Predicate it)
           (Predicate (Stack it))))
     (.when items
       {.#Top head tail}
       (<op> (predicate head)
             (<name> predicate tail))

       end
       <init>))]

  [any?   #0 or]
  [every? #1 and]
  )

(the .public no_item
  (..error "The stack lacks a value at that index."))

(the .public (item i xs)
  (for_any (_ it)
    (-> Natural (Stack it)
        (Try it)))
  (.when xs
    {.#Top x xs'}
    (.when i
      0 {try.#Success x}
      _ (item (-- i) xs'))

    end
    {try.#Failure ..no_item}))

(the .public (equivalence Equivalence<a>)
  (for_any (_ it)
    (-> (Equivalence it)
        (Equivalence (Stack it))))
  (implementation
   (the (= xs ys)
     (.when [xs ys]
       [{.#Empty} {.#Empty}]
       true

       [{.#Top x xs'} {.#Top y ys'}]
       (and (by Equivalence<a> = x y)
            (= xs' ys'))

       [_ _]
       false))))

(the .public (hash super)
  (for_any (_ it)
    (-> (Hash it)
        (Hash (Stack it))))
  (implementation
   (the equivalence
     (..equivalence (by super equivalence)))
   
   (the hash
     (by ..mix mix
         (function (_ member hash)
           (n.+ (by super hash member) hash))
         0))))

(the .public monoid
  (for_any (_ it)
    (Monoid (Stack it)))
  (implementation
   (the identity
     {.#Empty})
   (the (composite left right)
     ... TODO: Use the more obvious implementation once "tail recursion modulo cons" is added to the compiler.
     (mix (function (_ head tail)
            {.#Top head tail})
          right
          (reversed left)))))

(use "[0]" ..monoid)

(the .public functor
  (Functor Stack)
  (implementation
   (the (each f it)
     ... TODO: Use the more obvious implementation once "tail recursion modulo cons" is added to the compiler.
     (mix (function (_ head tail)
            {.#Top (f head) tail})
          (stack)
          (reversed it)))))

(use "[0]" ..functor)

(the .public monad
  (Monad Stack)
  (implementation
   (the functor ..functor)

   (the in
     (|>> stack))

   (the conjoint
     (|>> reversed (mix composite identity)))))

(the .public (sorted < xs)
  (for_any (_ it)
    (-> (-> it it Bit) (Stack it)
        (Stack it)))
  (.when xs
    {.#Top x xs'}
    (let [[pre post] (mix (function (_ x' [pre post])
                            (if (< x x')
                              [{.#Top x' pre} post]
                              [pre {.#Top x' post}]))
                          (`` [(is (,, (type_of xs))
                                   (stack))
                               (is (,, (type_of xs))
                                   (stack))])
                          xs')]
      (.all composite
            (sorted < pre)
            (stack x)
            (sorted < post)))

    end
    end))

(the .public (empty? xs)
  (for_any (_ it)
    (Predicate (Stack it)))
  (.when xs
    {.#Empty}
    true
    
    _
    false))

(the .public (member? eq xs x)
  (for_any (_ it)
    (-> (Equivalence it) (Stack it) it
        Bit))
  (.when xs
    {.#Top x' xs'}
    (or (by eq = x x')
        (member? eq xs' x))

    end
    false))

(the .public empty
  (..error "Empty stack."))

(with_template' [<side> <output>]
  [(the .public (<side> it)
     (for_any (_ it)
       (-> (Stack it)
           (Try <output>)))
     (.when it
       {.#Top head tail}
       {try.#Success <side>}

       end
       {try.#Failure ..empty}))]

  [head it]
  [tail (Stack it)]
  )

(the .public (indices size)
  (-> Natural
      (Stack Natural))
  (.when size
    0 (stack)
    _ (|> size -- (enum.range n.enum 0))))

(the (name$ name)
  (-> Text
      Code)
  [["" 0 0] {.#Name "" name}])

(the (natural#as value)
  (-> Natural
      Text)
  (loop (again [input value
                output ""])
    (let [digit (.when (n.% 10 input)
                  0 "0"
                  1 "1"
                  2 "2"
                  3 "3"
                  4 "4"
                  5 "5"
                  6 "6"
                  7 "7"
                  8 "8"
                  9 "9"
                  _ (undefined))
          output' (.text_composite# digit output)
          input' (n./ 10 input)]
      (.when input'
        0 output'
        _ (again input' output')))))

(the .public cannot_zip_0_stacks
  (..error "Cannot zip 0 stacks."))

(the .public zipped
  (macro (_ tokens state)
    (.when tokens
      (stack [_ {.#Natural num_stacks}])
      (if (n.> 0 num_stacks)
        (let [(open "[0]") ..functor
              indices (..indices num_stacks)
              type_vars (is (Stack Code) (each (|>> natural#as name$) indices))
              zipped_type (` (.for_any ((, (name$ "0_")) (,* type_vars))
                               (-> (,* (each (is (-> Code Code) (function (_ var) (` (Stack (, var)))))
                                             type_vars))
                                   (Stack [(,* type_vars)]))))
              vars+stacks (|> indices
                              (each ++)
                              (each (function (_ idx)
                                      (let [base (natural#as idx)]
                                        [(name$ base)
                                         (name$ (.text_composite# base "'"))]))))
              pattern (` [(,* (each (function (_ [v vs]) (` {.#Top (, v) (, vs)}))
                                    vars+stacks))])
              'step (name$ "0step0")
              'blank (name$ "0,0")
              stack_vars (each product.right vars+stacks)
              code (` (is (, zipped_type)
                          (function ((, 'step) (,* stack_vars))
                            (.when [(,* stack_vars)]
                              (, pattern)
                              {.#Top [(,* (each product.left vars+stacks))]
                                     ((, 'step) (,* stack_vars))}

                              (, 'blank)
                              {.#Empty}))))]
          {.#Right [state (stack code)]})
        {.#Left ..cannot_zip_0_stacks})

      _
      {.#Left .wrong_syntax})))

(the .public zipped_2 (zipped 2))
(the .public zipped_3 (zipped 3))

(the .public zipped_with
  (macro (_ tokens state)
    (.when tokens
      (stack [_ {.#Natural num_stacks}])
      (if (n.> 0 num_stacks)
        (let [(open "[0]") ..functor
              indices (..indices num_stacks)
              'return_type (name$ "0return_type0")
              'func (name$ "0func0")
              type_vars (is (Stack Code) (each (|>> natural#as name$) indices))
              zipped_type (` (for_any ((, (name$ "0_")) (,* type_vars) (, 'return_type))
                               (-> (-> (,* type_vars) (, 'return_type))
                                   (,* (each (is (-> Code Code) (function (_ var) (` (Stack (, var)))))
                                             type_vars))
                                   (Stack (, 'return_type)))))
              vars+stacks (|> indices
                              (each ++)
                              (each (function (_ idx)
                                      (let [base (natural#as idx)]
                                        [(name$ base)
                                         (name$ (.text_composite# base "'"))]))))
              pattern (` [(,* (each (function (_ [v vs]) (` {.#Top (, v) (, vs)}))
                                    vars+stacks))])
              'step (name$ "0step0")
              'blank (name$ "0,0")
              stack_vars (each product.right vars+stacks)
              code (` (is (, zipped_type)
                          (function ((, 'step) (, 'func) (,* stack_vars))
                            (.when [(,* stack_vars)]
                              (, pattern)
                              {.#Top ((, 'func) (,* (each product.left vars+stacks)))
                                     ((, 'step) (, 'func) (,* stack_vars))}

                              (, 'blank)
                              {.#Empty}))))]
          {.#Right [state (stack code)]})
        {.#Left ..cannot_zip_0_stacks})

      _
      {.#Left .wrong_syntax})))

(the .public zipped_with_2 (zipped_with 2))
(the .public zipped_with_3 (zipped_with 3))

(the .public (last xs)
  (for_any (_ it)
    (-> (Stack it)
        (Try it)))
  (.when xs
    (stack x)
    {try.#Success x}
    
    (..partial x xs')
    (last xs')

    end
    {try.#Failure ..empty}))

(the .public (inits xs)
  (for_any (_ it)
    (-> (Stack it)
        (Try (Stack it))))
  (.when xs
    (stack x)
    {try.#Success {.#Empty}}
    
    (..partial x xs')
    (.when (inits xs')
      {try.#Success tail}
      {try.#Success {.#Top x tail}}

      {try.#Failure _}
      (undefined))

    end
    {try.#Failure ..empty}
    ))

(the .public together
  (for_any (_ it)
    (-> (Stack (Stack it))
        (Stack it)))
  (by ..monad conjoint))

(the .public (all' monad)
  (for_any (_ ! it)
    (-> (Monad !) (Stack (! it))
        (! (Stack it))))
  (let [(open "/#[0]") monad]
    (function (again xs)
      (.when xs
        {.#Empty}
        (/#in {.#Empty})
        
        {.#Top x xs'}
        (|> x
            (/#each (function (_ _x)
                      (/#each (|>> {.#Top _x}) (again xs'))))
            /#conjoint)))))

(the .public (each' monad f)
  (for_any (_ ! before after)
    (-> (Monad !) (-> before (! after)) (Stack before)
        (! (Stack after))))
  (let [(open "/#[0]") monad]
    (function (again xs)
      (.when xs
        {.#Empty}
        (/#in {.#Empty})
        
        {.#Top x xs'}
        (|> (f x)
            (/#each (function (_ _x)
                      (/#each (|>> {.#Top _x}) (again xs'))))
            /#conjoint)))))

(the .public (only' monad f)
  (for_any (_ ! it)
    (-> (Monad !) (-> it (! Bit)) (Stack it)
        (! (Stack it))))
  (let [(open "/#[0]") monad]
    (function (again xs)
      (.when xs
        {.#Empty}
        (/#in {.#Empty})
        
        {.#Top head xs'}
        (|> (f head)
            (/#each (function (_ verdict)
                      (/#each (function (_ tail)
                                (if verdict
                                  {.#Top head tail}
                                  tail))
                              (again xs'))))
            /#conjoint)))))

(the .public (mix' monad f init xs)
  (for_any (_ ! part whole)
    (-> (Monad !) (-> part whole (! whole)) whole (Stack part)
        (! whole)))
  (.when xs
    {.#Empty}
    (by monad in init)

    {.#Top x xs}
    (do monad
      [init (f x init)]
      (mix' monad f init xs))))

(the .public (with monad)
  (for_any (_ !)
    (-> (Monad !)
        (Monad (for_any (_ it)
                 (! (Stack it))))))
  (implementation
   (the functor
     (functor.composite (its monad.functor monad)
                        ..functor))

   (the in
     (|>> (by ..monad in)
          (by monad in)))
   
   (the (conjoint MlMla)
     (do [! monad]
       [lMla MlMla
        lla (..all' ! lMla)]
       (in (..together lla))))))

(the .public (lifted monad)
  (for_any (_ ! it)
    (-> (Monad !)
        (-> (! it)
            (! (Stack it)))))
  (by monad each (by ..monad in)))

(the .public (enumeration xs)
  (for_any (_ it)
    (-> (Stack it)
        (Stack [Natural it])))
  (loop (again [idx 0
                xs xs])
    (.when xs
      {.#Top x xs'}
      {.#Top [idx x] (again (++ idx) xs')}

      end
      (stack))))

(the .public when
  (template#macro
   (_ <test> <then>)
   [(if <test>
      <then>
      (stack))]))

(the .public (revised item revision it)
  (for_any (_ it)
    (-> Natural (-> it it) (Stack it)
        (Stack it)))
  (.when it
    {.#Top head tail}
    (.when item
      0 {.#Top (revision head) tail}
      _ (revised (-- item) revision it))

    end
    end))
