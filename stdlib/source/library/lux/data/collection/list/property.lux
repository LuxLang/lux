... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except List has revised)
   [abstract
    [equivalence (.only Equivalence)]
    [monoid (.only Monoid)]]
   [control
    ["[0]" maybe (.use "[1]#[0]" functor)]]
   [data
    ["[0]" product]
    ["[0]" text (.use "[1]#[0]" equivalence)]
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix)]]]
   [math
    [number
     ["n" nat]]]]])

... https://en.wikipedia.org/wiki/Property_list
(every .public (List a)
  (.List [Text a]))

(the .public empty
  List
  {.#End})

(the .public size
  (All (_ a) (-> (List a) Nat))
  list.size)

(the .public empty?
  (All (_ a) (-> (List a) Bit))
  (|>> ..size (n.= 0)))

(the .public (value key properties)
  (All (_ a) (-> Text (List a) (Maybe a)))
  (when properties
    {.#End}
    {.#None}

    {.#Item [k' v'] properties'}
    (if (text#= key k')
      {.#Some v'}
      (value key properties'))))

(with_template [<name> <type> <access>]
  [(the .public <name>
     (All (_ a) (-> (List a) (.List <type>)))
     (list#each <access>))]

  [keys   Text product.left]
  [values a    product.right]
  )

(the .public (contains? key properties)
  (All (_ a) (-> Text (List a) Bit))
  (when (..value key properties)
    {.#Some _}
    true

    {.#None}
    false))

(the .public (has key val properties)
  (All (_ a) (-> Text a (List a) (List a)))
  (when properties
    {.#End}
    (list [key val])

    {.#Item [k' v'] properties'}
    (if (text#= key k')
      {.#Item [key val]
              properties'}
      {.#Item [k' v']
              (has key val properties')})))

(the .public (revised key f properties)
  (All (_ a) (-> Text (-> a a) (List a) (List a)))
  (when properties
    {.#End}
    {.#End}

    {.#Item [k' v'] properties'}
    (if (text#= key k')
      {.#Item [k' (f v')] properties'}
      {.#Item [k' v'] (revised key f properties')})))

(the .public (lacks key properties)
  (All (_ a) (-> Text (List a) (List a)))
  (when properties
    {.#End}
    properties

    {.#Item [k' v'] properties'}
    (if (text#= key k')
      properties'
      {.#Item [k' v']
              (lacks key properties')})))

(the .public (equivalence (open "/#[0]"))
  (All (_ a) (-> (Equivalence a) (Equivalence (List a))))
  (implementation
   (the (= reference subject)
     (and (n.= (list.size reference)
               (list.size subject))
          (list.every? (function (_ [key val])
                         (|> reference
                             (..value key)
                             (maybe#each (/#= val))
                             (maybe.else false)))
                       subject)))))

(the .public monoid
  (All (_ a) (Monoid (List a)))
  (implementation
   (the identity
     ..empty)

   (the (composite left right)
     (list#mix (function (_ [key val] it)
                 (..has key val it))
               right
               left))))
