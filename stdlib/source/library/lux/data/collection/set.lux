... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except has list)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    [monoid (.only Monoid)]]
   [control
    [function
     [predicate (.only Predicate)]]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" mix)]]]
   [math
    [number
     ["n" nat]]]
   [meta
    [macro
     ["^" pattern]]]]]
 ["[0]" //
  ["[1]" dictionary (.only Dictionary)]])

(every .public (Set of)
  (Dictionary of Any))

(the .public member_hash
  (for_any (_ of)
    (-> (Set of)
        (Hash of)))
  //.key_hash)

(the .public empty
  (for_any (_ of)
    (-> (Hash of)
        (Set of)))
  //.empty)

(the .public size
  (for_any (_ of)
    (-> (Set of)
        Nat))
  //.size)

(the .public (has elem set)
  (for_any (_ of)
    (-> of (Set of)
        (Set of)))
  (|> set (//.has elem [])))

(the .public lacks
  (for_any (_ of)
    (-> of (Set of)
        (Set of)))
  //.lacks)

(the .public member?
  (for_any (_ of)
    (-> (Set of)
        (Predicate of)))
  //.key?)

(the .public list
  (for_any (_ of)
    (-> (Set of)
        (List of)))
  //.keys)

(the .public union
  (for_any (_ of)
    (-> (Set of) (Set of)
        (Set of)))
  //.composite)

(the .public (difference sub base)
  (for_any (_ of)
    (-> (Set of) (Set of)
        (Set of)))
  (list#mix ..lacks base (..list sub)))

(the .public (intersection filter base)
  (for_any (_ of)
    (-> (Set of) (Set of)
        (Set of)))
  (//.sub (//.keys filter)
          base))

(the .public equivalence
  (for_any (_ of)
    (Equivalence (Set of)))
  (implementation
   (the (= (^.let reference [hash _]) sample)
     (and (n.= (..size reference)
               (..size sample))
          (list.every? (..member? reference)
                       (..list sample))))))

(alias [=]
       ..equivalence)

(the .public hash
  (for_any (_ of)
    (Hash (Set of)))
  (implementation
   (the equivalence ..equivalence)
   
   (the (hash set)
     (|> set
         ..list
         (of (list.hash (..member_hash set)) hash)))))

(the .public (monoid hash)
  (for_any (_ of)
    (-> (Hash of)
        (Monoid (Set of))))
  (implementation
   (the identity (..empty hash))
   (the composite ..union)))

(the .public empty?
  (for_any (_ of)
    (Predicate (Set of)))
  (|>> ..size (n.= 0)))

(the .public (of_list hash elements)
  (for_any (_ of)
    (-> (Hash of) (List of)
        (Set of)))
  (list#mix ..has (..empty hash) elements))

(the .public (sub? super sub)
  (for_any (_ of)
    (-> (Set of)
        (Predicate (Set of))))
  (list.every? (..member? super) (..list sub)))

(the .public (super? sub super)
  (for_any (_ of)
    (-> (Set of)
        (Predicate (Set of))))
  (..sub? super sub))

(the .public predicate
  (for_any (_ of)
    (-> (Set of)
        (Predicate of)))
  ..member?)
