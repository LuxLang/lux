(.module:
  [library
   [lux #*
    [abstract
     [equivalence (#+ Equivalence)]
     [monad (#+ do Monad)]]
    [data
     ["." maybe]
     [collection
      ["." tree #_
       ["#" finger (#+ Tree)]]]]
    [math
     [number
      ["n" nat ("#\." interval)]]]
    [type (#+ :by_example)
     [abstract (#+ abstract: :abstraction :representation)]]]])

(type: #export Priority
  Nat)

(def: #export max Priority n\top)
(def: #export min Priority n\bottom)

(def: builder
  (tree.builder n.maximum))

(def: :@:
  (:by_example [@]
               (tree.Builder @ Priority)
               ..builder
               
               @))

(abstract: #export (Queue a)
  (Maybe (Tree :@: Priority a))

  (def: #export empty
    Queue
    (:abstraction #.None))

  (def: #export (peek queue)
    (All [a] (-> (Queue a) (Maybe a)))
    (do maybe.monad
      [tree (:representation queue)]
      (tree.search (n.= (tree.tag tree))
                   tree)))

  (def: #export (size queue)
    (All [a] (-> (Queue a) Nat))
    (case (:representation queue)
      #.None
      0

      (#.Some tree)
      (loop [node tree]
        (case (tree.root node)
          (0 #0 _)
          1

          (0 #1 [left right])
          (n.+ (recur left) (recur right))))))

  (def: #export (member? equivalence queue member)
    (All [a] (-> (Equivalence a) (Queue a) a Bit))
    (case (:representation queue)
      #.None
      false

      (#.Some tree)
      (loop [node tree]
        (case (tree.root node)
          (0 #0 reference)
          (\ equivalence = reference member)

          (0 #1 [left right])
          (or (recur left)
              (recur right))))))

  (def: #export (pop queue)
    (All [a] (-> (Queue a) (Queue a)))
    (:abstraction
     (do maybe.monad
       [tree (:representation queue)
        #let [highest_priority (tree.tag tree)]]
       (loop [node tree]
         (case (tree.root node)
           (0 #0 reference)
           (if (n.= highest_priority (tree.tag node))
             #.None
             (#.Some node))

           (0 #1 left right)
           (if (n.= highest_priority (tree.tag left))
             (case (recur left)
               #.None
               (#.Some right)

               (#.Some =left)
               (#.Some (\ ..builder branch =left right)))
             (case (recur right)
               #.None
               (#.Some left)

               (#.Some =right)
               (#.Some (\ ..builder branch left =right)))))))))

  (def: #export (push priority value queue)
    (All [a] (-> Priority a (Queue a) (Queue a)))
    (let [addition (\ ..builder leaf priority value)]
      (:abstraction
       (case (:representation queue)
         #.None
         (#.Some addition)

         (#.Some tree)
         (#.Some (\ ..builder branch tree addition))))))
  )

(def: #export empty?
  (All [a] (-> (Queue a) Bit))
  (|>> ..size (n.= 0)))
