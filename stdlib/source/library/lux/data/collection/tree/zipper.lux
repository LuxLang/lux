(.module:
  [library
   [lux #*
    ["@" target]
    [abstract
     [functor (#+ Functor)]
     [comonad (#+ CoMonad)]
     [monad (#+ do)]
     [equivalence (#+ Equivalence)]]
    [data
     ["." product]
     ["." maybe ("#\." monad)]
     [text
      ["%" format (#+ format)]]
     [collection
      ["." list ("#\." functor fold monoid)]]]]]
  ["." // (#+ Tree) ("#\." functor)])

(type: (Family Zipper a)
  {#parent (Zipper a)
   #lefts (List (Tree a))
   #rights (List (Tree a))})

## https://en.wikipedia.org/wiki/Zipper_(data_structure)
(type: .public (Zipper a)
  {#.doc "Tree zippers, for easy navigation and editing of trees."}
  {#family (Maybe (Family Zipper a))
   #node (Tree a)})

(implementation: .public (equivalence super)
  (All [a]
    (-> (Equivalence a)
        (Equivalence (Zipper a))))
  
  (def: (= reference sample)
    (let [== ($_ product.equivalence
                 (maybe.equivalence
                  ($_ product.equivalence
                      =
                      (list.equivalence (//.equivalence super))
                      (list.equivalence (//.equivalence super))))
                 (//.equivalence super))]
      (== reference sample))))

(def: .public (zipper tree)
  (All [a] (-> (Tree a) (Zipper a)))
  {#family #.None
   #node tree})

(def: .public tree
  (All [a] (-> (Zipper a) (Tree a)))
  (get@ #node))

(def: .public value
  (All [a] (-> (Zipper a) a))
  (get@ [#node #//.value]))

(def: .public (set value zipper)
  (All [a] (-> a (Zipper a) (Zipper a)))
  (set@ [#node #//.value] value zipper))

(def: .public (update transform zipper)
  (All [a] (-> (-> a a) (Zipper a) (Zipper a)))
  (update@ [#node #//.value] transform zipper))

(def: children
  (All [a] (-> (Zipper a) (List (Tree a))))
  (get@ [#node #//.children]))

(def: .public leaf?
  (All [a] (-> (Zipper a) Bit))
  (|>> ..children list.empty?))

(def: .public branch?
  (All [a] (-> (Zipper a) Bit))
  (|>> ..leaf? not))

(def: .public (start? zipper)
  (All [a] (-> (Zipper a) Bit))
  (case (get@ #family zipper)
    #.None
    true

    _
    false))

(def: .public (down zipper)
  (All [a] (-> (Zipper a) (Maybe (Zipper a))))
  (case (..children zipper)
    #.End
    #.None

    (#.Item head tail)
    (#.Some {#family (#.Some {#parent (set@ [#node #//.children] (list) zipper)
                              #lefts #.End
                              #rights tail})
             #node head})))

(def: .public (up zipper)
  (All [a] (-> (Zipper a) (Maybe (Zipper a))))
  (do maybe.monad
    [family (get@ #family zipper)]
    (in (let [(^slots [#parent #lefts #rights]) family]
          (for {@.old
                (update@ #node (: (-> (Tree (:parameter 0))
                                      (Tree (:parameter 0)))
                                  (set@ #//.children (list\compose (list.reversed lefts)
                                                                   (#.Item (get@ #node zipper)
                                                                           rights))))
                         parent)}
               (set@ [#node #//.children]
                     (list\compose (list.reversed lefts)
                                   (#.Item (get@ #node zipper)
                                           rights))
                     parent))))))

(template [<one> <all> <side> <op_side>]
  [(def: .public (<one> zipper)
     (All [a] (-> (Zipper a) (Maybe (Zipper a))))
     (case (get@ #family zipper)
       (#.Some family)
       (case (get@ <side> family)
         (#.Item next side')
         (#.Some (for {@.old
                       {#family (#.Some (|> family
                                            (set@ <side> side')
                                            (update@ <op_side> (|>> (#.Item (get@ #node zipper))))))
                        #node next}}
                      (let [move (: (All [a] (-> (List (Tree a)) (Zipper a) (Family Zipper a) (Family Zipper a)))
                                    (function (_ side' zipper)
                                      (|>> (set@ <side> side')
                                           (update@ <op_side> (|>> (#.Item (get@ #node zipper)))))))]
                        {#family (#.Some (move side' zipper family))
                         #node next})))

         #.End
         #.None)

       #.None
       #.None))

   (def: .public (<all> zipper)
     (All [a] (-> (Zipper a) (Maybe (Zipper a))))
     (case (get@ #family zipper)
       #.None
       #.None

       (#.Some family)
       (case (list.reversed (get@ <side> family))
         #.End
         #.None

         (#.Item last prevs)
         (#.Some (for {@.old {#family (#.Some (|> family
                                                  (set@ <side> #.End)
                                                  (update@ <op_side> (|>> (#.Item (get@ #node zipper))
                                                                          (list\compose prevs)))))
                              #node last}}
                      (let [move (: (All [a] (-> (List (Tree a)) (Zipper a) (Family Zipper a) (Family Zipper a)))
                                    (function (_ prevs zipper)
                                      (|>> (set@ <side> #.End)
                                           (update@ <op_side> (|>> (#.Item (get@ #node zipper))
                                                                   (list\compose prevs))))))]
                        {#family (#.Some (move prevs zipper family))
                         #node last}))))))]

  [right rightmost #rights #lefts]
  [left  leftmost  #lefts  #rights]
  )

(def: .public (next zipper)
  (All [a] (-> (Zipper a) (Maybe (Zipper a))))
  (case (..down zipper)
    (#.Some forward)
    (#.Some forward)
    
    #.None
    (loop [@ zipper]
      (case (..right @)
        (#.Some forward)
        (#.Some forward)
        
        #.None
        (do maybe.monad
          [@ (..up @)]
          (recur @))))))

(def: (bottom zipper)
  (All [a] (-> (Zipper a) (Zipper a)))
  (case (..right zipper)
    (#.Some forward)
    (bottom forward)

    #.None
    (case (..down zipper)
      (#.Some forward)
      (bottom forward)

      #.None
      zipper)))

(def: .public (previous zipper)
  (All [a] (-> (Zipper a) (Maybe (Zipper a))))
  (case (..left zipper)
    #.None
    (..up zipper)

    (#.Some backward)
    (#.Some (case (..down backward)
              (#.Some then)
              (..bottom then)

              #.None
              backward))))

(template [<name> <move>]
  [(def: .public (<name> zipper)
     (All [a] (-> (Zipper a) (Maybe (Zipper a))))
     (case (<move> zipper)
       #.None
       #.None

       (#.Some @)
       (loop [@ @]
         (case (<move> @)
           #.None
           (#.Some @)

           (#.Some @)
           (recur @)))))]

  [end ..next]
  [start ..previous]
  )

(def: .public (end? zipper)
  (All [a] (-> (Zipper a) Bit))
  (case (..end zipper)
    #.None
    true

    (#.Some _)
    false))

(def: .public (interpose value zipper)
  (All [a] (-> a (Zipper a) (Zipper a)))
  (update@ [#node #//.children]
           (|>> (//.branch value) list)
           zipper))

(def: .public (adopt value zipper)
  (All [a] (-> a (Zipper a) (Zipper a)))
  (update@ [#node #//.children]
           (|>> (#.Item (//.leaf value)))
           zipper))

(def: .public (remove zipper)
  (All [a] (-> (Zipper a) (Maybe (Zipper a))))
  (do maybe.monad
    [family (get@ #family zipper)]
    (case (get@ #lefts family)
      #.End
      (in (set@ [#node #//.children]
                (get@ #rights family)
                (get@ #parent family)))

      (#.Item next side)
      (in (|> zipper
              (set@ #family (|> family
                                (set@ #lefts side)
                                #.Some))
              (set@ #node next))))))

(template [<name> <side>]
  [(def: .public (<name> value zipper)
     (All [a] (-> a (Zipper a) (Maybe (Zipper a))))
     (case (get@ #family zipper)
       #.None
       #.None

       (#.Some family)
       (#.Some (set@ #family
                     (#.Some (update@ <side> (|>> (#.Item (//.leaf value))) family))
                     zipper))))]

  [insert_left  #lefts]
  [insert_right #rights]
  )

(implementation: .public functor
  (Functor Zipper)
  
  (def: (map f (^slots [#family #node]))
    {#family (maybe\map (function (_ (^slots [#parent #lefts #rights]))
                          {#parent (map f parent)
                           #lefts (list\map (//\map f) lefts)
                           #rights (list\map (//\map f) rights)})
                        family)
     #node (//\map f node)}))

(implementation: .public comonad
  (CoMonad Zipper)
  
  (def: &functor ..functor)

  (def: out (get@ [#node #//.value]))

  (def: (split (^slots [#family #node]))
    (let [tree_splitter (: (All [a] (-> (Tree a) (Tree (Zipper a))))
                           (function (tree_splitter tree)
                             {#//.value (..zipper tree)
                              #//.children (|> tree
                                               (get@ #//.children)
                                               (list\map tree_splitter))}))]
      {#family (maybe\map (function (_ (^slots [#parent #lefts #rights]))
                            {#parent (split parent)
                             #lefts (list\map tree_splitter lefts)
                             #rights (list\map tree_splitter rights)})
                          family)
       #node (tree_splitter node)})))
