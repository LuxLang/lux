... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [functor (.only Functor)]
    [comonad (.only CoMonad)]
    [monad (.only do)]
    [equivalence (.only Equivalence)]]
   [control
    ["[0]" maybe (.use "[1]#[0]" monad)]
    ["[0]" try (.only Try)]]
   [data
    ["[0]" product]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor monoid)]]]
   [macro
    ["[0]" template]]
   [meta
    ["[0]" provenance]]]]
 ["[0]" // (.only Tree) (.use "[1]#[0]" functor)])

(every (Family Zipper of)
  (Record
   [#parent (Zipper of)
    #lefts (Stack (Tree of))
    #rights (Stack (Tree of))]))

... https://en.wikipedia.org/wiki/Zipper_(data_structure)
(every .public (Zipper of)
  (Record
   [#family (Maybe (Family Zipper of))
    #node (Tree of)]))

(the .public (equivalence super)
  (for_any (_ of)
    (-> (Equivalence of)
        (Equivalence (Zipper of))))
  (implementation
   (the (= expected actual)
     (let [== (all product.equivalence
                   (maybe.equivalence
                    (all product.equivalence
                         =
                         (stack.equivalence (//.equivalence super))
                         (stack.equivalence (//.equivalence super))))
                   (//.equivalence super))]
       (== expected actual)))))

(the .public (zipper tree)
  (for_any (_ of)
    (-> (Tree of)
        (Zipper of)))
  [#family {.#None}
   #node tree])

(the .public tree
  (for_any (_ of)
    (-> (Zipper of)
        (Tree of)))
  (its #node))

(the .public value
  (for_any (_ of)
    (-> (Zipper of)
        of))
  (its [#node //.#value]))

(the .public (set value zipper)
  (for_any (_ of)
    (-> of (Zipper of)
        (Zipper of)))
  (has [#node //.#value] value zipper))

(the .public (update transform zipper)
  (for_any (_ of)
    (-> (-> of of) (Zipper of)
        (Zipper of)))
  (revised [#node //.#value] transform zipper))

(the children
  (for_any (_ of)
    (-> (Zipper of)
        (Stack (Tree of))))
  (its [#node //.#children]))

(the .public leaf?
  (for_any (_ of)
    (-> (Zipper of)
        Bit))
  (|>> ..children
       stack.empty?))

(the .public branch?
  (for_any (_ of)
    (-> (Zipper of)
        Bit))
  (|>> ..leaf?
       not))

(the .public (start? zipper)
  (for_any (_ of)
    (-> (Zipper of)
        Bit))
  (when (its #family zipper)
    {.#None}
    true

    _
    false))

(the .public cannot_move
  Error
  (provenance.with (provenance.here)
    "Cannot move."))

(the .public (down zipper)
  (for_any (_ of)
    (-> (Zipper of)
        (Try (Zipper of))))
  (when (..children zipper)
    {.#Top head tail}
    {try.#Success
     [#family {.#Some [#parent (has [#node //.#children] (stack) zipper)
                       #lefts {.#Empty}
                       #rights tail]}
      #node head]}

    {.#Empty}
    {try.#Failure ..cannot_move}))

(the .public (up zipper)
  (for_any (_ of)
    (-> (Zipper of)
        (Try (Zipper of))))
  (<| (try.of_maybe ..cannot_move)
      (do maybe.monad
        [family (its #family zipper)]
        (in (let [(open "_[0]") family]
              (has [#node //.#children]
                   (stack#composite (stack.reversed _#lefts)
                                    {.#Top (its #node zipper)
                                           _#rights})
                   _#parent))))))

(template.with [<one> <all> <side> <op_side>]
  [(the .public (<one> zipper)
     (for_any (_ of)
       (-> (Zipper of)
           (Try (Zipper of))))
     (when (its #family zipper)
       {.#Some family}
       (when (its <side> family)
         {.#Top next side'}
         {try.#Success
          (let [move (is (for_any (_ of) (-> (Stack (Tree of)) (Zipper of) (Family Zipper of) (Family Zipper of)))
                         (function (_ side' zipper)
                           (|>> (has <side> side')
                                (revised <op_side> (|>> {.#Top (its #node zipper)})))))]
            [#family {.#Some (move side' zipper family)}
             #node next])}

         {.#Empty}
         {try.#Failure ..cannot_move})

       {.#None}
       {try.#Failure ..cannot_move}))

   (the .public (<all> zipper)
     (for_any (_ of)
       (-> (Zipper of)
           (Try (Zipper of))))
     (when (its #family zipper)
       {.#Some family}
       (when (stack.reversed (its <side> family))
         {.#Top last prevs}
         {try.#Success
          (let [move (is (for_any (_ of) (-> (Stack (Tree of)) (Zipper of) (Family Zipper of) (Family Zipper of)))
                         (function (_ prevs zipper)
                           (|>> (has <side> {.#Empty})
                                (revised <op_side> (|>> {.#Top (its #node zipper)}
                                                        (stack#composite prevs))))))]
            [#family {.#Some (move prevs zipper family)}
             #node last])}

         {.#Empty}
         {try.#Failure ..cannot_move})

       {.#None}
       {try.#Failure ..cannot_move}))]

  [right rightmost #rights #lefts]
  [left  leftmost  #lefts  #rights]
  )

(the .public (next zipper)
  (for_any (_ of)
    (-> (Zipper of)
        (Try (Zipper of))))
  (all try.or
       (..down zipper)
       (loop (again [@ zipper])
         (when (..right @)
           {try.#Success forward}
           {try.#Success forward}
           
           {try.#Failure _}
           (do try.monad
             [@ (..up @)]
             (again @))))))

(the (minimum zipper)
  (for_any (_ of)
    (-> (Zipper of)
        (Zipper of)))
  (when (..right zipper)
    {try.#Success forward}
    (minimum forward)

    {try.#Failure _}
    (when (..down zipper)
      {try.#Success forward}
      (minimum forward)

      {try.#Failure _}
      zipper)))

(the .public (previous zipper)
  (for_any (_ of)
    (-> (Zipper of)
        (Try (Zipper of))))
  (when (..left zipper)
    {try.#Failure _}
    (..up zipper)

    {try.#Success backward}
    {try.#Success (when (..down backward)
                    {try.#Success then}
                    (..minimum then)

                    {try.#Failure _}
                    backward)}))

(template.with [<name> <move>]
  [(the .public (<name> zipper)
     (for_any (_ of)
       (-> (Zipper of)
           (Try (Zipper of))))
     (do try.monad
       [@ (<move> zipper)]
       (loop (next [@ @])
         (when (<move> @)
           {try.#Failure error}
           (in @)

           {try.#Success @}
           (next @)))))]

  [end ..next]
  [start ..previous]
  )

(the .public (end? zipper)
  (for_any (_ of)
    (-> (Zipper of)
        Bit))
  (when (..end zipper)
    {try.#Failure error}
    true

    {try.#Success _}
    false))

(the .public (interpose value zipper)
  (for_any (_ of)
    (-> of (Zipper of)
        (Zipper of)))
  (revised [#node //.#children]
           (|>> (//.branch value) stack)
           zipper))

(the .public (adopt value zipper)
  (for_any (_ of)
    (-> of (Zipper of)
        (Zipper of)))
  (revised [#node //.#children]
           (|>> {.#Top (//.leaf value)})
           zipper))

(the .public cannot_remove
  Error
  (provenance.with (provenance.here)
    "Cannot remove."))

(the .public (remove zipper)
  (for_any (_ of)
    (-> (Zipper of)
        (Try (Zipper of))))
  (<| (try.of_maybe ..cannot_remove)
      (do maybe.monad
        [family (its #family zipper)]
        (in (when (its #lefts family)
              {.#Empty}
              (has [#node //.#children]
                   (its #rights family)
                   (its #parent family))

              {.#Top next side}
              (|> zipper
                  (has #family (|> family
                                   (has #lefts side)
                                   {.#Some}))
                  (has #node next)))))))

(the .public cannot_insert
  Error
  (provenance.with (provenance.here)
    "Cannot insert."))

(template.with [<name> <side>]
  [(the .public (<name> value zipper)
     (for_any (_ of)
       (-> of (Zipper of)
           (Try (Zipper of))))
     (when (its #family zipper)
       {.#Some family}
       {try.#Success (has #family
                          {.#Some (revised <side> (|>> {.#Top (//.leaf value)}) family)}
                          zipper)}

       {.#None}
       {try.#Failure ..cannot_insert}))]

  [insert_left  #lefts]
  [insert_right #rights]
  )

(the .public functor
  (Functor Zipper)
  (implementation
   (the (each f (open "_[0]"))
     [#family (maybe#each (function (_ (open "_[0]"))
                            [#parent (each f _#parent)
                             #lefts (stack#each (//#each f) _#lefts)
                             #rights (stack#each (//#each f) _#rights)])
                          _#family)
      #node (//#each f _#node)])))

(the .public comonad
  (CoMonad Zipper)
  (implementation
   (the functor
     ..functor)

   (the out
     (its [#node //.#value]))

   (the (disjoint (open "_[0]"))
     (let [tree_splitter (is (for_any (_ of)
                               (-> (Tree of)
                                   (Tree (Zipper of))))
                             (function (tree_splitter tree)
                               [//.#value (..zipper tree)
                                //.#children (|> tree
                                                 (its //.#children)
                                                 (stack#each tree_splitter))]))]
       [#family (maybe#each (function (_ (open "_[0]"))
                              [..#parent (disjoint _#parent)
                               ..#lefts (stack#each tree_splitter _#lefts)
                               ..#rights (stack#each tree_splitter _#rights)])
                            _#family)
        #node (tree_splitter _#node)]))))
