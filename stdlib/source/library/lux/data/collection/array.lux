... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except list)
   [abstract
    [monoid (.only Monoid)]
    [functor (.only Functor)]
    [equivalence (.only Equivalence)]
    [mix (.only Mix)]]
   [control
    [function
     [predicate (.only Predicate)]]]
   [data
    [collection
     ["[0]" list]]]
   [meta
    [type
     [variance (.only)]]]]]
 ["!" \\unsafe])

(the .public nominal
  Text
  !.nominal)

(every .public Array'
  !.Array')

(every .public Array
  !.Array)

(the .public empty
  (All (_ a) (-> Nat (Array a)))
  (|>> !.empty))

(the .public size
  (All (_ r w) (-> (Array' r w) Nat))
  (|>> !.size))

(the .public (item index array)
  (All (_ r w)
    (-> Nat (Array' r w) (Maybe r)))
  (if (!.lacks? index array)
    {.#None}
    {.#Some (!.item index array)}))

(the .public (has! index value array)
  (All (_ r w)
    (-> Nat w (Array' r w) (Array' r w)))
  (!.has! index value array))

(the .public (lacks! index array)
  (All (_ r w)
    (-> Nat (Array' r w) (Array' r w)))
  (!.lacks! index array))

(the .public (lacks? index array)
  (All (_ r w)
    (-> Nat (Array' r w) Bit))
  (!.lacks? index array))

(the .public (has? index array)
  (All (_ r w)
    (-> Nat (Array' r w) Bit))
  (!.has? index array))

(the .public (revised! index $ array)
  (All (_ r w)
    (-> Nat (-> r w) (Array' r w) (Array' r w)))
  (!.revised! index $ array))

(the .public (upsert! index default transform array)
  (All (_ r w)
    (-> Nat r (-> r w) (Array' r w) (Array' r w)))
  (!.upsert! index default transform array))

(the .public (copy! length src_start src_array dest_start dest_array)
  (All (_ r w)
    (-> Nat Nat (Array' w Nothing) Nat (Array' r w)
        (Array' r w)))
  (!.copy! length src_start src_array dest_start dest_array))

(the .public occupancy
  (All (_ r w) (-> (Array' r w) Nat))
  (|>> !.occupancy))

(the .public vacancy
  (All (_ r w) (-> (Array' r w) Nat))
  (|>> !.vacancy))

(the .public (only! ? it)
  (All (_ r w)
    (-> (Predicate r) (Array' r w) (Array' r w)))
  (!.only! ? it))

(the .public (example ? it)
  (All (_ r w)
    (-> (Predicate r) (Array' r w) (Maybe r)))
  (!.example ? it))

(the .public (example' ? it)
  (All (_ r w)
    (-> (-> Nat r Bit) (Array' r w) (Maybe [Nat r])))
  (!.example' ? it))

(the .public clone
  (All (_ a) (-> (Array a) (Array a)))
  (|>> !.clone))

(the .public of_list
  (All (_ a) (-> (List a) (Array a)))
  (|>> !.of_list))

(the .public (list default array)
  (All (_ r w) (-> (Maybe r) (Array' r w) (List r)))
  (!.list default array))

(the .public (equivalence //)
  (All (_ r) (-> (Equivalence r) (Equivalence (Ex (_ w) (Array' r w)))))
  (implementation
   (the (= left/* right/*)
     (!.= // left/* right/*))))

(the .public monoid
  (All (_ a) (Monoid (Array a)))
  (implementation
   (the identity (!.empty 0))

   (the (composite left/* right/*)
     (!.composite left/* right/*))))

(the .public mix
  (Mix (All (_ r) (Array' r Nothing)))
  (implementation
   (the (mix $ init it)
     (!.mix (function (_ index partial total)
              ($ partial total))
            init
            it))))

(the .public functor
  (Functor Array)
  (implementation
   (the (each $ input)
     (!.each $ input))))

(with_template [<safe> <unsafe>]
  [(the .public (<safe> ? it)
     (All (_ r w)
       (-> (Predicate r) (Predicate (Array' r w))))
     (<unsafe> ? it))]

  [every? !.every?]
  [any? !.any?]
  )

(the .public (one ? it)
  (All (_ r r' w)
    (-> (-> r (Maybe r')) (Array' r w) (Maybe r')))
  (!.one ? it))
