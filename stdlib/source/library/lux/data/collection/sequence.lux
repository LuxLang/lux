... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... https://hypirion.com/musings/understanding-persistent-vector-pt-1
... https://hypirion.com/musings/understanding-persistent-vector-pt-2
... https://hypirion.com/musings/understanding-persistent-vector-pt-3
(.require
 [library
  [lux (.except list has revised only all)
   [abstract
    [functor (.only Functor)]
    [apply (.only Apply)]
    [monad (.only Monad do)]
    [equivalence (.only Equivalence)]
    [monoid (.only Monoid)]]
   [algorithm
    [mix (.only Mix)]]
   [control
    ["<>" parser]
    ["[0]" maybe (.use "[1]#[0]" functor)]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" mix functor monoid)]
     ["[0]" array
      ["[1]" \\unsafe (.only Array)]]]]
   [math
    [number
     ["n" nat]
     ["[0]" i64]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\parser (.only Parser)]]
    [macro
     [syntax (.only syntax)]
     ["^" pattern]]]]])

(every (Node a)
  (Variant
   {#Base (Array a)}
   {#Hierarchy (Array (Node a))}))

(every (Base a)
  (Array a))

(every (Hierarchy a)
  (Array (Node a)))

(every Level
  Nat)

(every Index
  Nat)

(the branching_exponent
  Nat
  5)

(the root_level
  Level
  0)

(with_template [<name> <op>]
  [(the <name>
     (-> Level
         Level)
     (<op> branching_exponent))]

  [level_up   n.+]
  [level_down n.-]
  )

(the full_node_size
  Nat
  (i64.left_shifted branching_exponent 1))

(the branch_idx_mask
  Nat
  (-- full_node_size))

(the branch_idx
  (-> Index
      Index)
  (i64.and branch_idx_mask))

(the (empty_hierarchy _)
  (All (_ of)
    (-> Any
        (Hierarchy of)))
  (array.empty ..full_node_size))

(the (tail_off sequence_size)
  (-> Nat
      Nat)
  (if (n.< full_node_size sequence_size)
    0
    (|> (-- sequence_size)
        (i64.right_shifted branching_exponent)
        (i64.left_shifted branching_exponent))))

(the (path level tail)
  (All (_ of)
    (-> Level (Base of)
        (Node of)))
  (if (n.= 0 level)
    {#Base tail}
    (|> (empty_hierarchy [])
        (array.has! 0 (path (level_down level) tail))
        {#Hierarchy})))

(the (tail singleton)
  (All (_ of)
    (-> of
        (Base of)))
  (|> (array.empty 1)
      (array.has! 0 singleton)))

(the (with_tail size level tail parent)
  (All (_ of)
    (-> Nat Level (Base of) (Hierarchy of)
        (Hierarchy of)))
  (let [sub_idx (branch_idx (i64.right_shifted level (-- size)))
        ... If we're currently on a bottom node
        sub_node (if (n.= branching_exponent level)
                   ... Just add the tail to it
                   {#Base tail}
                   ... Otherwise, check whether there's a vacant spot
                   (if (array.lacks? sub_idx parent)
                     ... If so, set the path to the tail
                     (..path (level_down level) tail)
                     (when (array.item sub_idx parent)
                       ... If not, push the tail onto the sub_node.
                       {#Hierarchy sub_node}
                       {#Hierarchy (with_tail size (level_down level) tail sub_node)}

                       _
                       (undefined))))]
    (|> (array.clone parent)
        (array.has! sub_idx sub_node))))

(the (expanded_tail val tail)
  (All (_ of)
    (-> of (Base of)
        (Base of)))
  (let [tail_size (array.size tail)]
    (|> (array.empty (++ tail_size))
        (array.copy! tail_size 0 tail 0)
        (array.has! tail_size val))))

(the (hierarchy#has level idx val hierarchy)
  (All (_ of)
    (-> Level Index of (Hierarchy of)
        (Hierarchy of)))
  (let [sub_idx (branch_idx (i64.right_shifted level idx))]
    (when (array.item sub_idx hierarchy)
      {#Hierarchy sub_node}
      (|> (array.clone hierarchy)
          (array.has! sub_idx {#Hierarchy (hierarchy#has (level_down level) idx val sub_node)}))

      (^.multi {#Base base}
               (n.= 0 (level_down level)))
      (|> (array.clone hierarchy)
          (array.has! sub_idx (|> (array.clone base)
                                  (array.has! (branch_idx idx) val)
                                  {#Base})))

      _
      (undefined))))

(the (without_tail size level hierarchy)
  (All (_ of)
    (-> Nat Level (Hierarchy of)
        (Maybe (Hierarchy of))))
  (let [sub_idx (branch_idx (i64.right_shifted level (n.- 2 size)))]
    (cond (n.= 0 sub_idx)
          {.#None}

          (n.> branching_exponent level)
          (if (array.lacks? sub_idx hierarchy)
            {.#None}
            (maybe#each (function (_ sub)
                          (|> (array.clone hierarchy)
                              (array.has! sub_idx {#Hierarchy sub})))
                        (when (array.item sub_idx hierarchy)
                          {#Hierarchy sub}
                          (without_tail size (level_down level) sub)

                          {#Base _}
                          (undefined))))

          ... Else...
          (|> (array.clone hierarchy)
              (array.lacks! sub_idx)
              {.#Some})
          )))

(the (node#list node)
  (All (_ of)
    (-> (Node of)
        (List of)))
  (when node
    {#Base base}
    (array.list {.#None} base)
    
    {#Hierarchy hierarchy}
    (|> hierarchy
        (array.list {.#None})
        list.reversed
        (list#mix (function (_ sub acc)
                    (list#composite (node#list sub) acc))
                  {.#End}))))

(every .public (Sequence of)
  (Record
   [#level Level
    #size Nat
    #root (Hierarchy of)
    #tail (Base of)]))

(the .public empty
  Sequence
  [#level (level_up root_level)
   #size 0
   #root (empty_hierarchy [])
   #tail (array.empty 0)])

(the .public size
  (All (_ of)
    (-> (Sequence of)
        Nat))
  (its #size))

(the .public (suffix val sequence)
  (All (_ of)
    (-> of (Sequence of)
        (Sequence of)))
  ... Check if there is room in the tail.
  (let [sequence_size (its #size sequence)]
    (if (|> sequence_size (n.- (tail_off sequence_size)) (n.< full_node_size))
      ... If so, append to it.
      (|> sequence
          (.revised #size ++)
          (.revised #tail (..expanded_tail val)))
      ... Otherwise, push tail into the tree
      ... --------------------------------------------------------
      ... Will the root experience an overflow with this addition?
      (|> (if (n.> (i64.left_shifted (its #level sequence) 1)
                   (i64.right_shifted branching_exponent sequence_size))
            ... If so, a brand-new root must be established, that is
            ... 1-level taller.
            (|> sequence
                (.has #root (|> (`` (is (Hierarchy (,, (type_of val)))
                                        (empty_hierarchy [])))
                                (array.has! 0 {#Hierarchy (its #root sequence)})
                                (array.has! 1 (..path (its #level sequence) (its #tail sequence)))))
                (.revised #level level_up))
            ... Otherwise, just push the current tail onto the root.
            (|> sequence
                (.revised #root (..with_tail sequence_size (its #level sequence) (its #tail sequence)))))
          ... Finally, update the size of the sequence and grow a new
          ... tail with the new element as it's sole member.
          (.revised #size ++)
          (.has #tail (..tail val)))
      )))

(exception.the incorrect_sequence_structure)

(exception.the .public (index_out_of_bounds [sequence index])
  (All (_ of)
    (Exception [(Sequence of) Nat]))
  (exception.report
   (.list ["Size" (of n.decimal encoded (its #size sequence))]
          ["Index" (of n.decimal encoded index)])))

(exception.the base_was_not_found)

(the .public (within_bounds? sequence idx)
  (All (_ of)
    (-> (Sequence of) Nat
        Bit))
  (n.< (its #size sequence) idx))

(the (base_for idx sequence)
  (All (_ of)
    (-> Index (Sequence of)
        (Try (Base of))))
  (if (within_bounds? sequence idx)
    (if (n.< (tail_off (its #size sequence)) idx)
      (loop (again [level (its #level sequence)
                    hierarchy (its #root sequence)])
        (let [index (branch_idx (i64.right_shifted level idx))]
          (if (array.lacks? index hierarchy)
            (exception.except ..base_was_not_found [])
            (when [(n.> branching_exponent level)
                   (array.item index hierarchy)]
              [.true {#Hierarchy sub}]
              (again (level_down level) sub)

              [.false {#Base base}]
              {try.#Success base}

              _
              (exception.except ..incorrect_sequence_structure [])))))
      {try.#Success (its #tail sequence)})
    (exception.except ..index_out_of_bounds [sequence idx])))

(the .public (item idx sequence)
  (All (_ of)
    (-> Nat (Sequence of)
        (Try of)))
  (do try.monad
    [base (base_for idx sequence)
     .let [index (branch_idx idx)]]
    (if (array.lacks? index base)
      (exception.except ..incorrect_sequence_structure [])
      {try.#Success (array.item index base)})))

(the .public (has idx val sequence)
  (All (_ of)
    (-> Nat of (Sequence of)
        (Try (Sequence of))))
  (let [sequence_size (its #size sequence)]
    (if (within_bounds? sequence idx)
      {try.#Success (if (n.< (tail_off sequence_size) idx)
                      (.revised #root (hierarchy#has (its #level sequence) idx val)
                                sequence)
                      (.revised #tail (`` (is (-> (Base (,, (type_of val)))
                                                  (Base (,, (type_of val))))
                                              (|>> array.clone (array.has! (branch_idx idx) val))))
                                sequence))}
      (exception.except ..index_out_of_bounds [sequence idx]))))

(the .public (revised idx revision it)
  (All (_ of)
    (-> Nat (-> of of) (Sequence of)
        (Try (Sequence of))))
  (do try.monad
    [val (..item idx it)]
    (..has idx (revision val) it)))

(the .public (prefix sequence)
  (All (_ of)
    (-> (Sequence of)
        (Sequence of)))
  (when (its #size sequence)
    0
    empty

    1
    empty

    sequence_size
    (if (|> sequence_size (n.- (tail_off sequence_size)) (n.> 1))
      (let [old_tail (its #tail sequence)
            new_tail_size (-- (array.size old_tail))]
        (|> sequence
            (.revised #size --)
            (.has #tail (|> (array.empty new_tail_size)
                            (array.copy! new_tail_size 0 old_tail 0)))))
      (maybe.trusted
       (do maybe.monad
         [new_tail (base_for (n.- 2 sequence_size) sequence)
          .let [[level' root'] (let [init_level (its #level sequence)]
                                 (loop (again [level init_level
                                               root (maybe.else (empty_hierarchy [])
                                                                (without_tail sequence_size init_level (its #root sequence)))])
                                   (with_expansions [<else> [level root]]
                                     (if (n.> branching_exponent level)
                                       (if (array.lacks? 1 root)
                                         (when (array.item 0 root)
                                           {#Hierarchy sub_node}
                                           (again (level_down level) sub_node)

                                           ... {#Base _}
                                           ... (undefined)

                                           _
                                           <else>)
                                         <else>)
                                       <else>))))]]
         (in (|> sequence
                 (.revised #size --)
                 (.has #level level')
                 (.has #root root')
                 (.has #tail new_tail))))))
    ))

(the .public (list sequence)
  (All (_ of)
    (-> (Sequence of)
        (List of)))
  (list#composite (node#list {#Hierarchy (its #root sequence)})
                  (node#list {#Base (its #tail sequence)})))

(the .public of_list
  (All (_ of)
    (-> (List of)
        (Sequence of)))
  (list#mix ..suffix ..empty))

(the .public (member? equivalence sequence val)
  (All (_ of)
    (-> (Equivalence of) (Sequence of) of
        Bit))
  (list.member? equivalence (list sequence) val))

(the .public empty?
  (All (_ of)
    (-> (Sequence of)
        Bit))
  (|>> (its #size) (n.= 0)))

(the .public sequence
  (syntax (_ [elems (<>.some <code>.any)])
    (in (.list (` (..of_list (.list (,* elems))))))))

(the (node_equivalence //#=)
  (All (_ of)
    (-> (Equivalence of)
        (Equivalence (Node of))))
  (implementation
   (the (= v1 v2)
     (when [v1 v2]
       [{#Base b1} {#Base b2}]
       (array.= //#= b1 b2)
       
       [{#Hierarchy h1} {#Hierarchy h2}]
       (array.= (node_equivalence //#=) h1 h2)

       _
       false))))

(the .public (equivalence //#=)
  (All (_ of)
    (-> (Equivalence of)
        (Equivalence (Sequence of))))
  (implementation
   (the (= v1 v2)
     (and (n.= (its #size v1) (its #size v2))
          (let [(open "node#[0]") (node_equivalence //#=)]
            (and (node#= {#Base (its #tail v1)}
                         {#Base (its #tail v2)})
                 (node#= {#Hierarchy (its #root v1)}
                         {#Hierarchy (its #root v2)})))))))

(the node_mix
  (Mix Node)
  (implementation
   (the (mix $ init xs)
     (when xs
       {#Base base}
       (array.mix (function (_ _ item output) ($ item output))
                  init
                  base)
       
       {#Hierarchy hierarchy}
       (array.mix (function (_ _ node init') (mix $ init' node))
                  init
                  hierarchy)))))

(the .public mix
  (Mix Sequence)
  (implementation
   (the (mix $ init xs)
     (let [(open "[0]") node_mix]
       (mix $
            (mix $
                 init
                 {#Hierarchy (its #root xs)})
            {#Base (its #tail xs)})))))

(the .public monoid
  (All (_ of)
    (Monoid (Sequence of)))
  (implementation
   (the identity ..empty)
   
   (the (composite xs ys)
     (list#mix suffix xs (..list ys)))))

(the node_functor
  (Functor Node)
  (implementation
   (the (each $ xs)
     (when xs
       {#Base base}
       {#Base (array.each $ base)}
       
       {#Hierarchy hierarchy}
       {#Hierarchy (array.each (each $) hierarchy)}))))

(the .public functor
  (Functor Sequence)
  (implementation
   (the (each $ xs)
     [#level (its #level xs)
      #size (its #size xs)
      #root (let [ ... TODO: This binding was established to get around a compilation error. Fix and inline!
                  $ (of node_functor each $)]
              (|> xs (its #root) (array.each $)))
      #tail (|> xs (its #tail) (array.each $))])))

(the .public apply
  (Apply Sequence)
  (implementation
   (the functor ..functor)

   (the (on fa ff)
     (let [(open "[0]") ..functor
           (open "[0]") ..mix
           (open "[0]") ..monoid
           results (each (function (_ f) (each f fa))
                         ff)]
       (mix composite identity results)))))

(the .public monad
  (Monad Sequence)
  (implementation
   (the functor ..functor)

   (the in
     (|>> sequence))

   (the conjoint
     (let [(open "[0]") ..mix
           (open "[0]") ..monoid]
       (mix (function (_ post pre) (composite pre post)) identity)))))

(the .public reversed
  (All (_ of)
    (-> (Sequence of)
        (Sequence of)))
  (|>> ..list
       list.reversed
       (list#mix suffix ..empty)))

(with_template [<name> <array> <init> <op>]
  [(the .public <name>
     (All (_ of)
       (-> (Predicate of) (Sequence of)
           Bit))
     (let [help (is (All (_ a)
                      (-> (Predicate a) (Node a) Bit))
                    (function (help predicate node)
                      (when node
                        {#Base base}
                        (<array> predicate base)

                        {#Hierarchy hierarchy}
                        (<array> (help predicate) hierarchy))))]
       (function (<name> predicate sequence)
         (let [(open "_[0]") sequence]
           (<op> (help predicate {#Hierarchy _#root})
                 (help predicate {#Base _#tail}))))))]

  [every? array.every? #1 and]
  [any?   array.any?   #0 or]
  )

(the .public (only when items)
  (All (_ of)
    (-> (-> of Bit) (Sequence of)
        (Sequence of)))
  (..mix (function (_ item output)
           (if (when item)
             (..suffix item output)
             output))
         ..empty
         items))

(the (one|node check items)
  (All (_ input output)
    (-> (-> input (Maybe output)) (Node input)
        (Maybe output)))
  (when items
    {#Base items}
    (array.one check items)
    
    {#Hierarchy items}
    (array.one (one|node check) items)))

(exception.the no_one)

(the .public (one check items)
  (All (_ input output)
    (-> (-> input (Maybe output)) (Sequence input)
        (Try output)))
  (.all try.or
        (let [... TODO: This binding was established to get around a compilation error. Fix and inline!
              check (..one|node check)]
          (|> items
              (its #root)
              (array.one check)))
        (|> items
            (its #tail)
            (array.one check))
        (exception.except ..no_one [])
        ))

(the .public (all ? it)
  (All (_ input output)
    (-> (-> input (Maybe output)) (Sequence input)
        (Sequence output)))
  (..mix (function (_ in out)
           (when (? in)
             {.#Some in}
             (suffix in out)
             
             {.#None}
             out))
         (sequence)
         it))
