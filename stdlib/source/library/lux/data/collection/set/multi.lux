... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... https://en.wikipedia.org/wiki/Multiset
(.using
 [library
  [lux (.except has stack)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [control
    ["[0]" try]]
   ["[0]" function]
   [math
    [number
     ["n" natural]]]
   [macro
    ["^" pattern]
    ["[0]" template]]
   [type
    ["[0]" nominal]]]]
 ["[0]" // (.only)
  [//
   ["[0]" stack (.use "[1]#[0]" mix monoid)]
   ["[0]" dictionary (.only Dictionary)]]])

(nominal.every .public (Set of)
  (Dictionary of Natural)

  (the .public empty
    (for_any (_ of)
      (-> (Hash of)
          (Set of)))
    (|>> dictionary.empty
         nominal.abstraction))

  (the .public size
    (for_any (_ of)
      (-> (Set of)
          Natural))
    (|>> nominal.representation
         dictionary.values
         (stack#mix n.+ 0)))

  (the .public (has multiplicity elem set)
    (for_any (_ of)
      (-> Natural of (Set of)
          (Set of)))
    (when multiplicity
      0 set
      _ (|> set
            nominal.representation
            (dictionary.revised' elem 0 (n.+ multiplicity))
            nominal.abstraction)))

  (the .public (lacks multiplicity elem set)
    (for_any (_ of)
      (-> Natural of (Set of)
          (Set of)))
    (when multiplicity
      0 set
      _ (when (dictionary.value elem (nominal.representation set))
          {try.#Success current}
          (nominal.abstraction
           (if (n.> multiplicity current)
             (dictionary.revised elem (n.- multiplicity) (nominal.representation set))
             (dictionary.lacks elem (nominal.representation set))))
          
          failure
          set)))

  (the .public (multiplicity set elem)
    (for_any (_ of)
      (-> (Set of) of
          Natural))
    (|> set
        nominal.representation
        (dictionary.value elem)
        (try.else 0)))

  (the .public stack
    (for_any (_ of)
      (-> (Set of)
          (Stack of)))
    (|>> nominal.representation
         dictionary.entries
         (stack#mix (function (_ [elem multiplicity] output)
                      (stack#composite (stack.repeated multiplicity elem) output))
                    {.#Empty})))

  (template.with [<name> <composite>]
    [(the .public (<name> parameter subject)
       (for_any (_ of)
         (-> (Set of) (Set of)
             (Set of)))
       (nominal.abstraction
        (dictionary.composite_with <composite>
                                   (nominal.representation parameter)
                                   (nominal.representation subject))))]

    [union n.major]
    [sum n.+]
    )

  (the .public (intersection parameter subject)
    (for_any (_ of)
      (-> (Set of) (Set of)
          (Set of)))
    (let [subject (nominal.representation subject)]
      (stack#mix (function (_ [elem multiplicity] output)
                   (..has (n.minor (..multiplicity parameter elem)
                                   multiplicity)
                          elem
                          output))
                 (..empty (dictionary.key_hash subject))
                 (dictionary.entries subject))))

  (the .public (difference parameter subject)
    (for_any (_ of)
      (-> (Set of) (Set of)
          (Set of)))
    (|> parameter
        nominal.representation
        dictionary.entries
        (stack#mix (function (_ [elem multiplicity] output)
                     (..lacks multiplicity elem output))
                   subject)))

  (the .public (sub? reference subject)
    (for_any (_ of)
      (-> (Set of) (Set of)
          Bit))
    (|> subject
        nominal.representation
        dictionary.entries
        (stack.every? (function (_ [elem multiplicity])
                        (|> elem
                            (..multiplicity reference)
                            (n.>= multiplicity))))))

  (the .public (support set)
    (for_any (_ of)
      (-> (Set of)
          (//.Set of)))
    (let [(^.let set [hash _]) (nominal.representation set)]
      (|> set
          dictionary.keys
          (//.of_stack hash))))

  (the .public equivalence
    (for_any (_ of)
      (Equivalence (Set of)))
    (implementation
     (the (= expected actual)
       (let [expected (nominal.representation expected)]
         (and (n.= (dictionary.size expected)
                   (dictionary.size (nominal.representation actual)))
              (|> expected
                  dictionary.entries
                  (stack.every? (function (_ [elem multiplicity])
                                  (|> elem
                                      (..multiplicity actual)
                                      (n.= multiplicity))))))))))

  (the .public hash
    (for_any (_ of)
      (Hash (Set of)))
    (implementation
     (the equivalence ..equivalence)
     
     (the (hash set)
       (let [set (nominal.representation set)
             [hash _] set]
         (stack#mix (function (_ [elem multiplicity] acc)
                      (|> elem (by hash hash) (n.* multiplicity) (n.+ acc)))
                    0
                    (dictionary.entries set))))))
  )

(the .public (member? set elem)
  (for_any (_ of)
    (-> (Set of) of
        Bit))
  (|> elem (..multiplicity set) (n.> 0)))

(the .public empty?
  (for_any (_ of)
    (-> (Set of)
        Bit))
  (|>> ..size (n.= 0)))

(the .public (of_stack hash subject)
  (for_any (_ of)
    (-> (Hash of) (Stack of)
        (Set of)))
  (stack#mix (..has 1) (..empty hash) subject))

(the .public (of_set subject)
  (for_any (_ of)
    (-> (//.Set of)
        (Set of)))
  (..of_stack (//.member_hash subject)
              (//.stack subject)))

(the .public super?
  (for_any (_ of)
    (-> (Set of) (Set of)
        Bit))
  (function.flipped sub?))
