... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except has)
   [abstract
    [equivalence (.only Equivalence)]
    [order (.only Order)]]
   [error (.only error)
    ["[0]" try (.only Try)]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" mix)]
     [dictionary
      ["/" ordered]]]]
   [macro
    ["[0]" template]]
   [type
    ["[0]" nominal]]]])

(nominal.every .public (Set it)
  (/.Dictionary it it)

  (the .public empty
    (for_any (_ it)
      (-> (Order it)
          (Set it)))
    (|>> /.empty
         nominal.of))

  (the .public (member? set elem)
    (for_any (_ it)
      (-> (Set it) it
          Bit))
    (/.key? (nominal.as set) elem))

  (the .public it_is_empty
    (error "Empty."))

  (template.with [<name> <alias>]
    [(the .public <name>
       (for_any (_ it)
         (-> (Set it)
             (Try it)))
       (|>> nominal.as
            <alias>
            (try.as it_is_empty)))]

    [minor /.minor]
    [major /.major]
    )

  (template.with [<type> <name> <alias>]
    [(the .public <name>
       (for_any (_ it)
         (-> (Set it)
             <type>))
       (|>> nominal.as
            <alias>))]

    [Natural size  /.size]
    [Bit empty? /.empty?]
    )

  (the .public (has elem set)
    (for_any (_ it)
      (-> it (Set it)
          (Set it)))
    (|> set
        nominal.as
        (/.has elem elem)
        nominal.of))

  (the .public (lacks elem set)
    (for_any (_ it)
      (-> it (Set it)
          (Set it)))
    (|> set
        nominal.as
        (/.lacks elem)
        nominal.of))

  (the .public as_list
    (for_any (_ it)
      (-> (Set it)
          (List it)))
    (|>> nominal.as
         /.keys))

  (the .public (of_list order list)
    (for_any (_ it)
      (-> (Order it) (List it)
          (Set it)))
    (list#mix has (..empty order) list))

  (the .public (union left right)
    (for_any (_ it)
      (-> (Set it) (Set it)
          (Set it)))
    (list#mix ..has right (..as_list left)))

  (the .public (intersection left right)
    (for_any (_ it)
      (-> (Set it) (Set it)
          (Set it)))
    (|> (..as_list right)
        (list.only (..member? left))
        (..of_list (its /.#order (nominal.as right)))))

  (the .public (difference param subject)
    (for_any (_ it)
      (-> (Set it) (Set it)
          (Set it)))
    (|> (..as_list subject)
        (list.only (|>> (..member? param) not))
        (..of_list (its /.#order (nominal.as subject)))))

  (the .public equivalence
    (for_any (_ it)
      (Equivalence (Set it)))
    (implementation
     (the (= expected actual)
       (by (list.equivalence (by (nominal.as expected) equivalence))
           = (..as_list expected) (..as_list actual)))))
  )

(the .public (sub? super sub)
  (for_any (_ it)
    (-> (Set it) (Set it)
        Bit))
  (|> sub
      ..as_list
      (list.every? (..member? super))))

(the .public (super? sub super)
  (for_any (_ it)
    (-> (Set it) (Set it)
        Bit))
  (sub? super sub))
