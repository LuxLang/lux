... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except has stack)
   [abstract
    [equivalence (.only Equivalence)]
    [order (.only Order)]]
   [error
    ["[0]" try (.only Try)]]
   [data
    [collection
     ["[0]" stack (.use "[1]#[0]" mix)]
     [dictionary
      ["/" ordered]]]]
   [macro
    ["[0]" template]]
   [type
    ["[0]" nominal]]
   [meta
    ["[0]" provenance]]]])

(nominal.every .public (Set it)
  (/.Dictionary it it)

  (the .public empty
    (for_any (_ it)
      (-> (Order it)
          (Set it)))
    (|>> /.empty
         nominal.abstraction))

  (the .public (member? set elem)
    (for_any (_ it)
      (-> (Set it) it
          Bit))
    (/.key? (nominal.reification set) elem))

  (the .public it_is_empty
    Error
    (provenance.with (provenance.here)
      "Empty."))

  (template.with [<name> <alias>]
    [(the .public <name>
       (for_any (_ it)
         (-> (Set it)
             (Try it)))
       (|>> nominal.reification
            <alias>
            (try.as it_is_empty)))]

    [minor /.minor]
    [major /.major]
    )

  (template.with [<type> <name> <alias>]
    [(the .public <name>
       (for_any (_ it)
         (-> (Set it)
             <type>))
       (|>> nominal.reification
            <alias>))]

    [Natural size  /.size]
    [Bit empty? /.empty?]
    )

  (the .public (has elem set)
    (for_any (_ it)
      (-> it (Set it)
          (Set it)))
    (|> set
        nominal.reification
        (/.has elem elem)
        nominal.abstraction))

  (the .public (lacks elem set)
    (for_any (_ it)
      (-> it (Set it)
          (Set it)))
    (|> set
        nominal.reification
        (/.lacks elem)
        nominal.abstraction))

  (the .public stack
    (for_any (_ it)
      (-> (Set it)
          (Stack it)))
    (|>> nominal.reification
         /.keys))

  (the .public (of_stack order stack)
    (for_any (_ it)
      (-> (Order it) (Stack it)
          (Set it)))
    (stack#mix has (..empty order) stack))

  (the .public (union left right)
    (for_any (_ it)
      (-> (Set it) (Set it)
          (Set it)))
    (stack#mix ..has right (..stack left)))

  (the .public (intersection left right)
    (for_any (_ it)
      (-> (Set it) (Set it)
          (Set it)))
    (|> (..stack right)
        (stack.only (..member? left))
        (..of_stack (its /.#order (nominal.reification right)))))

  (the .public (difference param subject)
    (for_any (_ it)
      (-> (Set it) (Set it)
          (Set it)))
    (|> (..stack subject)
        (stack.only (|>> (..member? param) not))
        (..of_stack (its /.#order (nominal.reification subject)))))

  (the .public equivalence
    (for_any (_ it)
      (Equivalence (Set it)))
    (implementation
     (the (= expected actual)
       (by (stack.equivalence (by (nominal.reification expected) equivalence))
           = (..stack expected) (..stack actual)))))
  )

(the .public (sub? super sub)
  (for_any (_ it)
    (-> (Set it) (Set it)
        Bit))
  (|> sub
      ..stack
      (stack.every? (..member? super))))

(the .public (super? sub super)
  (for_any (_ it)
    (-> (Set it) (Set it)
        Bit))
  (sub? super sub))
