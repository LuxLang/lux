(.module:
  [library
   [lux #*
    [abstract
     [equivalence (#+ Equivalence)]
     [functor (#+ Functor)]]
    [data
     [collection
      ["." list ("#\." monoid functor)]]]
    [math
     [number
      ["n" nat]]]]])

(type: #export (Queue a)
  {#.doc (doc "A first-in, first-out sequential data-structure.")}
  {#front (List a)
   #rear (List a)})

(def: #export empty
  Queue
  {#front (list)
   #rear (list)})

(def: #export (of_list entries)
  (All [a] (-> (List a) (Queue a)))
  {#front entries
   #rear (list)})

(def: #export (to_list queue)
  (All [a] (-> (Queue a) (List a)))
  (let [(^slots [#front #rear]) queue]
    (list\compose front (list.reverse rear))))

(def: #export peek
  {#.doc (doc "Yields the first value in the queue, if any.")}
  (All [a] (-> (Queue a) (Maybe a)))
  (|>> (get@ #front) list.head))

(def: #export (size queue)
  (All [a] (-> (Queue a) Nat))
  (let [(^slots [#front #rear]) queue]
    (n.+ (list.size front)
         (list.size rear))))

(def: #export empty?
  (All [a] (-> (Queue a) Bit))
  (|>> (get@ #front) list.empty?))

(def: #export (member? equivalence queue member)
  (All [a] (-> (Equivalence a) (Queue a) a Bit))
  (let [(^slots [#front #rear]) queue]
    (or (list.member? equivalence front member)
        (list.member? equivalence rear member))))

(def: #export (pop queue)
  (All [a] (-> (Queue a) (Queue a)))
  (case (get@ #front queue)
    ## Empty...
    (^ (list))
    queue

    ## Front has dried up...
    (^ (list _))
    (|> queue
        (set@ #front (list.reverse (get@ #rear queue)))
        (set@ #rear (list)))

    ## Consume front!
    (^ (list& _ front'))
    (|> queue
        (set@ #front front'))))

(def: #export (push val queue)
  (All [a] (-> a (Queue a) (Queue a)))
  (case (get@ #front queue)
    #.End
    (set@ #front (list val) queue)

    _
    (update@ #rear (|>> (#.Item val)) queue)))

(implementation: #export (equivalence super)
  (All [a] (-> (Equivalence a) (Equivalence (Queue a))))
  
  (def: (= reference subject)
    (\ (list.equivalence super) =
       (..to_list reference)
       (..to_list subject))))

(implementation: #export functor
  (Functor Queue)
  
  (def: (map f fa)
    {#front (|> fa (get@ #front) (list\map f))
     #rear (|> fa (get@ #rear) (list\map f))}))
