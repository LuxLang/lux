... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except list)
   [abstract
    [equivalence (.only Equivalence)]
    [functor (.only Functor)]]
   [control
    ["[0]" try (.only Try)]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" monoid functor)]]]
   [math
    [number
     ["n" natural]]]
   [meta
    ["[0]" location]]]])

(every .public (Queue of)
  (Record
   [#front (List of)
    #rear (List of)]))

(the .public empty
  Queue
  [#front (.list)
   #rear (.list)])

(the .public (of_list entries)
  (for_any (_ of)
    (-> (List of)
        (Queue of)))
  [#front entries
   #rear (.list)])

(the .public (list queue)
  (for_any (_ of)
    (-> (Queue of)
        (List of)))
  (let [(open "_[0]") queue]
    (list#composite _#front (list.reversed _#rear))))

(the .public it_is_empty
  Error
  (location.with (location.here)
    "Empty."))

(the .public front
  (for_any (_ of)
    (-> (Queue of)
        (Try of)))
  (|>> (its #front)
       list.head
       (try.as ..it_is_empty)))

(the .public (size queue)
  (for_any (_ of)
    (-> (Queue of)
        Natural))
  (let [(open "_[0]") queue]
    (n.+ (list.size _#front)
         (list.size _#rear))))

(the .public empty?
  (for_any (_ of)
    (-> (Queue of)
        Bit))
  (|>> (its #front)
       list.empty?))

(the .public (member? equivalence queue member)
  (for_any (_ of)
    (-> (Equivalence of) (Queue of) of
        Bit))
  (let [(open "_[0]") queue]
    (or (list.member? equivalence _#front member)
        (list.member? equivalence _#rear member))))

(the .public (next queue)
  (for_any (_ of)
    (-> (Queue of)
        (Queue of)))
  (when (its #front queue)
    ... Empty...
    (.list)
    queue

    ... Front has dried up...
    (.list _)
    (|> queue
        (has #front (list.reversed (its #rear queue)))
        (has #rear (.list)))

    ... Consume front!
    (list.partial _ front')
    (|> queue
        (has #front front'))))

(the .public (end val queue)
  (for_any (_ of)
    (-> of (Queue of)
        (Queue of)))
  (when (its #front queue)
    {.#End}
    (has #front (.list val) queue)

    _
    (revised #rear (|>> {.#Item val}) queue)))

(the .public (equivalence super)
  (for_any (_ of)
    (-> (Equivalence of)
        (Equivalence (Queue of))))
  (implementation
   (the (= expected actual)
     (of (list.equivalence super) =
         (..list expected)
         (..list actual)))))

(the .public functor
  (Functor Queue)
  (implementation
   (the (each f fa)
     [#front (|> fa (its #front) (list#each f))
      #rear (|> fa (its #rear) (list#each f))])))
