(.using
 [library
  [lux (.except list)
   [abstract
    [equivalence (.only Equivalence)]
    [functor (.only Functor)]]
   [data
    [collection
     ["[0]" list ("[1]#[0]" monoid functor)]]]
   [math
    [number
     ["n" nat]]]]])

(type: .public (Queue a)
  (Record
   [#front (List a)
    #rear (List a)]))

(def: .public empty
  Queue
  [#front (.list)
   #rear (.list)])

(def: .public (of_list entries)
  (All (_ a) (-> (List a) (Queue a)))
  [#front entries
   #rear (.list)])

(def: .public (list queue)
  (All (_ a) (-> (Queue a) (List a)))
  (let [(open "_[0]") queue]
    (list#composite _#front (list.reversed _#rear))))

(def: .public front
  (All (_ a) (-> (Queue a) (Maybe a)))
  (|>> (the #front) list.head))

(def: .public (size queue)
  (All (_ a) (-> (Queue a) Nat))
  (let [(open "_[0]") queue]
    (n.+ (list.size _#front)
         (list.size _#rear))))

(def: .public empty?
  (All (_ a) (-> (Queue a) Bit))
  (|>> (the #front) list.empty?))

(def: .public (member? equivalence queue member)
  (All (_ a) (-> (Equivalence a) (Queue a) a Bit))
  (let [(open "_[0]") queue]
    (or (list.member? equivalence _#front member)
        (list.member? equivalence _#rear member))))

(def: .public (next queue)
  (All (_ a) (-> (Queue a) (Queue a)))
  (case (the #front queue)
    ... Empty...
    (pattern (.list))
    queue

    ... Front has dried up...
    (pattern (.list _))
    (|> queue
        (has #front (list.reversed (the #rear queue)))
        (has #rear (.list)))

    ... Consume front!
    (pattern (.partial_list _ front'))
    (|> queue
        (has #front front'))))

(def: .public (end val queue)
  (All (_ a) (-> a (Queue a) (Queue a)))
  (case (the #front queue)
    {.#End}
    (has #front (.list val) queue)

    _
    (revised #rear (|>> {.#Item val}) queue)))

(implementation: .public (equivalence super)
  (All (_ a) (-> (Equivalence a) (Equivalence (Queue a))))
  
  (def: (= reference subject)
    (# (list.equivalence super) =
       (..list reference)
       (..list subject))))

(implementation: .public functor
  (Functor Queue)
  
  (def: (each f fa)
    [#front (|> fa (the #front) (list#each f))
     #rear (|> fa (the #rear) (list#each f))]))
