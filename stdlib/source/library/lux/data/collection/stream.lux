... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except pattern only)
   [abstract
    [functor (.only Functor)]
    [comonad (.only CoMonad)]]
   [control
    ["//" continuation (.only Cont)]
    ["<>" parser]
    [function
     ["[0]" predicate]]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" monad)]]]
   [math
    [number
     ["n" nat]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\parser (.only Parser)]]
    [macro (.only with_symbols)
     [syntax (.only syntax)]]]]])

(every .public (Stream a)
  (Cont [a (Stream a)]))

(the .public (iterations step init)
  (All (_ a b)
    (-> (-> a [a b]) a (Stream b)))
  (let [[next x] (step init)]
    (//.pending [x (iterations step next)])))

(the .public (repeated x)
  (All (_ a)
    (-> a (Stream a)))
  (//.pending [x (repeated x)]))

(the .public (cycle [start next])
  (All (_ a)
    (-> [a (List a)] (Stream a)))
  (loop (again [head start
                tail next])
    (//.pending [head (when tail
                        {.#End}
                        (again start next)
                        
                        {.#Item head' tail'}
                        (again head' tail'))])))

(with_template [<name> <return>]
  [(the .public (<name> stream)
     (All (_ a) (-> (Stream a) <return>))
     (let [[head tail] (//.value stream)]
       <name>))]

  [head a]
  [tail (Stream a)]
  )

(the .public (item idx stream)
  (All (_ a) (-> Nat (Stream a) a))
  (let [[head tail] (//.value stream)]
    (when idx
      0 head
      _ (item (-- idx) tail))))

(with_template [<taker> <dropper> <pred_type> <pred_test> <pred_step> <post_test>]
  [(the .public (<taker> pred xs)
     (All (_ a)
       (-> <pred_type> (Stream a) (List a)))
     (let [[x xs'] (//.value xs)]
       (if (<post_test> <pred_test>)
         (list.partial x (<taker> <pred_step> xs'))
         (list))))

   (the .public (<dropper> pred xs)
     (All (_ a)
       (-> <pred_type> (Stream a) (Stream a)))
     (let [[x xs'] (//.value xs)]
       (if (<post_test> <pred_test>)
         (<dropper> <pred_step> xs')
         xs)))]
  
  [while until (-> a Bit) (pred x)     pred       |>]
  [first after Nat        (n.= 0 pred) (-- pred) not]
  )

(with_template [<splitter> <pred_type> <pred_test> <pred_step>]
  [(the .public (<splitter> pred xs)
     (All (_ a)
       (-> <pred_type> (Stream a) [(List a) (Stream a)]))
     (let [[x xs'] (//.value xs)]
       (if <pred_test>
         [(list) xs]
         (let [[tail next] (<splitter> <pred_step> xs')]
           [{.#Item [x tail]} next]))))]
  
  [split_when (-> a Bit) (pred x)     pred]
  [split_at   Nat        (n.= 0 pred) (-- pred)]
  )

(the .public (only predicate stream)
  (All (_ a) (-> (-> a Bit) (Stream a) (Stream a)))
  (let [[head tail] (//.value stream)]
    (if (predicate head)
      (//.pending [head (only predicate tail)])
      (only predicate tail))))

(the .public (partition left? xs)
  (All (_ a) (-> (-> a Bit) (Stream a) [(Stream a) (Stream a)]))
  [(..only left? xs)
   (..only (predicate.complement left?) xs)])

(the .public functor
  (Functor Stream)
  (implementation
   (the (each f fa)
     (let [[head tail] (//.value fa)]
       (//.pending [(f head) (each f tail)])))))

(the .public comonad
  (CoMonad Stream)
  (implementation
   (the functor ..functor)
   
   (the out head)
   
   (the (disjoint wa)
     (let [[head tail] (//.value wa)]
       (//.pending [wa (disjoint tail)])))))

(the .public pattern
  (.pattern
   (syntax (_ [patterns (<code>.form (<>.many <code>.any))
               body <code>.any
               branches (<>.some <code>.any)])
     (with_symbols [g!stream]
       (let [body+ (` (let [(,* (|> patterns
                                    (list#each (function (_ pattern)
                                                 (list (` [(, pattern) (, g!stream)])
                                                       (` (//.value (, g!stream))))))
                                    list#conjoint))]
                        (, body)))]
         (in (list.partial g!stream body+ branches)))))))
