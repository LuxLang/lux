... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except has revised)
   [abstract
    [equivalence (.except)]
    [monad (.only Monad do)]
    ["[0]" order (.only Order)]]
   [control
    ["[0]" maybe]]
   [data
    ["p" product]
    [collection
     ["[0]" list (.use "[1]#[0]" monoid mix)]]]
   [math
    [number
     ["n" nat]]]
   [meta
    [macro
     ["^" pattern]]]]])

(the error_message
  "Invariant violation")

(every Color
  (Variant
   {#Red}
   {#Black}))

(every (Node k v)
  (Record
   [#color Color
    #key k
    #value v
    #left (Maybe (Node k v))
    #right (Maybe (Node k v))]))

(with_template [<create> <color>]
  [(the (<create> key value left right)
     (All (_ k v) (-> k v (Maybe (Node k v)) (Maybe (Node k v)) (Node k v)))
     [#color {<color>}
      #key key
      #value value
      #left left
      #right right])]

  [red   #Red]
  [black #Black]
  )

(every .public (Dictionary k v)
  (Record
   [#order (Order k)
    #root (Maybe (Node k v))]))

(the .public (empty order)
  (All (_ k v) (-> (Order k) (Dictionary k v)))
  [#order order
   #root {.#None}])

... TODO: Doing inneficient access of Order functions due to compiler bug.
... TODO: Must improve it as soon as bug is fixed.
(the .public (value key dict)
  (All (_ k v) (-> k (Dictionary k v) (Maybe v)))
  (let [... (open "_#[0]") (its #order dict)
        ]
    (loop (again [node (its #root dict)])
      (when node
        {.#None}
        {.#None}

        {.#Some node}
        (let [node_key (its #key node)]
          (cond (of dict = node_key key)
                ... (_#= node_key key)
                {.#Some (its #value node)}

                (of dict < node_key key)
                ... (_#< node_key key)
                (again (its #left node))

                ... (_#> (its #key node) key)
                (again (its #right node))))
        ))))

... TODO: Doing inneficient access of Order functions due to compiler bug.
... TODO: Must improve it as soon as bug is fixed.
(the .public (key? dict key)
  (All (_ k v) (-> (Dictionary k v) k Bit))
  (let [... (open "_#[0]") (its #order dict)
        ]
    (loop (again [node (its #root dict)])
      (when node
        {.#None}
        false

        {.#Some node}
        (let [node_key (its #key node)]
          (or (of dict = node_key key)
              ... (_#= node_key key)
              (if (of dict < node_key key)
                ... (_#< node_key key)
                (again (its #left node))
                (again (its #right node)))))))))

(with_template [<name> <side>]
  [(the .public (<name> dict)
     (All (_ k v) (-> (Dictionary k v) (Maybe v)))
     (when (its #root dict)
       {.#None}
       {.#None}

       {.#Some node}
       (loop (again [node node])
         (when (its <side> node)
           {.#None}
           {.#Some (its #value node)}

           {.#Some side}
           (again side)))))]

  [min #left]
  [max #right]
  )

(the .public (size dict)
  (All (_ k v) (-> (Dictionary k v) Nat))
  (loop (again [node (its #root dict)])
    (when node
      {.#None}
      0

      {.#Some node}
      (++ (n.+ (again (its #left node))
               (again (its #right node)))))))

(the .public empty?
  (All (_ k v) (-> (Dictionary k v) Bit))
  (|>> ..size (n.= 0)))

(with_template [<name> <other_color> <self_color> <no_change>]
  [(the (<name> self)
     (All (_ k v) (-> (Node k v) (Node k v)))
     (when (its #color self)
       {<other_color>}
       (.has #color {<self_color>} self)

       {<self_color>}
       <no_change>
       ))]

  [blackened #Red   #Black self]
  [reddened  #Black #Red   (panic! error_message)]
  )

(the (with_left addition center)
  (All (_ k v) (-> (Node k v) (Node k v) (Node k v)))
  (when (its #color center)
    {#Red}
    (red (its #key center)
         (its #value center)
         {.#Some addition}
         (its #right center))
    
    {#Black}
    (with_expansions
      [<default_behavior> (these (black (its #key center)
                                        (its #value center)
                                        {.#Some addition}
                                        (its #right center)))]
      (when (its #color addition)
        {#Red}
        (when (its #left addition)
          (^.multi {.#Some left}
                   [(its #color left) {#Red}])
          (red (its #key addition)
               (its #value addition)
               {.#Some (blackened left)}
               {.#Some (black (its #key center)
                              (its #value center)
                              (its #right addition)
                              (its #right center))})

          _
          (when (its #right addition)
            (^.multi {.#Some right}
                     [(its #color right) {#Red}])
            (red (its #key right)
                 (its #value right)
                 {.#Some (black (its #key addition)
                                (its #value addition)
                                (its #left addition)
                                (its #left right))}
                 {.#Some (black (its #key center)
                                (its #value center)
                                (its #right right)
                                (its #right center))})

            _
            <default_behavior>))
        
        {#Black}
        <default_behavior>))))

(the (with_right addition center)
  (All (_ k v) (-> (Node k v) (Node k v) (Node k v)))
  (when (its #color center)
    {#Red}
    (red (its #key center)
         (its #value center)
         (its #left center)
         {.#Some addition})
    
    {#Black}
    (with_expansions
      [<default_behavior> (these (black (its #key center)
                                        (its #value center)
                                        (its #left center)
                                        {.#Some addition}))]
      (when (its #color addition)
        {#Red}
        (when (its #right addition)
          (^.multi {.#Some right}
                   [(its #color right) {#Red}])
          (red (its #key addition)
               (its #value addition)
               {.#Some (black (its #key center)
                              (its #value center)
                              (its #left center)
                              (its #left addition))}
               {.#Some (blackened right)})

          _
          (when (its #left addition)
            (^.multi {.#Some left}
                     [(its #color left) {#Red}])
            (red (its #key left)
                 (its #value left)
                 {.#Some (black (its #key center)
                                (its #value center)
                                (its #left center)
                                (its #left left))}
                 {.#Some (black (its #key addition)
                                (its #value addition)
                                (its #right left)
                                (its #right addition))})

            _
            <default_behavior>))
        
        {#Black}
        <default_behavior>))))

(the .public (has key value dict)
  (All (_ k v) (-> k v (Dictionary k v) (Dictionary k v)))
  (let [(open "_#[0]") (its #order dict)
        root' (loop (again [?root (its #root dict)])
                (when ?root
                  {.#None}
                  {.#Some (red key value {.#None} {.#None})}

                  {.#Some root}
                  (let [reference (its #key root)]
                    (`` (cond (,, (with_template [<comp> <tag> <add>]
                                    [(<comp> reference key)
                                     (let [side_root (its <tag> root)
                                           outcome (again side_root)]
                                       (if (same? side_root outcome)
                                         ?root
                                         {.#Some (<add> (maybe.trusted outcome)
                                                        root)}))]

                                    [_#<                             #left  ..with_left]
                                    [(order.> (its #order dict)) #right ..with_right]
                                    ))

                              ... (_#= reference key)
                              {.#Some (.has #value value root)}
                              )))
                  ))]
    (.has #root root' dict)))

(the (left_balanced key value ?left ?right)
  (All (_ k v) (-> k v (Maybe (Node k v)) (Maybe (Node k v)) (Node k v)))
  (when ?left
    (^.multi {.#Some left}
             [(its #color left) {#Red}]
             [(its #left left) {.#Some left>>left}]
             [(its #color left>>left) {#Red}])
    (red (its #key left)
         (its #value left)
         {.#Some (blackened left>>left)}
         {.#Some (black key value (its #right left) ?right)})

    (^.multi {.#Some left}
             [(its #color left) {#Red}]
             [(its #right left) {.#Some left>>right}]
             [(its #color left>>right) {#Red}])
    (red (its #key left>>right)
         (its #value left>>right)
         {.#Some (black (its #key left)
                        (its #value left)
                        (its #left left)
                        (its #left left>>right))}
         {.#Some (black key value
                        (its #right left>>right)
                        ?right)})

    _
    (black key value ?left ?right)))

(the (right_balanced key value ?left ?right)
  (All (_ k v) (-> k v (Maybe (Node k v)) (Maybe (Node k v)) (Node k v)))
  (when ?right
    (^.multi {.#Some right}
             [(its #color right) {#Red}]
             [(its #right right) {.#Some right>>right}]
             [(its #color right>>right) {#Red}])
    (red (its #key right)
         (its #value right)
         {.#Some (black key value ?left (its #left right))}
         {.#Some (blackened right>>right)})

    (^.multi {.#Some right}
             [(its #color right) {#Red}]
             [(its #left right) {.#Some right>>left}]
             [(its #color right>>left) {#Red}])
    (red (its #key right>>left)
         (its #value right>>left)
         {.#Some (black key value ?left (its #left right>>left))}
         {.#Some (black (its #key right)
                        (its #value right)
                        (its #right right>>left)
                        (its #right right))})

    _
    (black key value ?left ?right)))

(the (without_left key value ?left ?right)
  (All (_ k v) (-> k v (Maybe (Node k v)) (Maybe (Node k v)) (Node k v)))
  (when ?left
    (^.multi {.#Some left}
             [(its #color left) {#Red}])
    (red key value {.#Some (blackened left)} ?right)

    _
    (when ?right
      (^.multi {.#Some right}
               [(its #color right) {#Black}])
      (right_balanced key value ?left {.#Some (reddened right)})

      (^.multi {.#Some right}
               [(its #color right) {#Red}]
               [(its #left right) {.#Some right>>left}]
               [(its #color right>>left) {#Black}])
      (red (its #key right>>left)
           (its #value right>>left)
           {.#Some (black key value ?left (its #left right>>left))}
           {.#Some (right_balanced (its #key right)
                                   (its #value right)
                                   (its #right right>>left)
                                   (of maybe.functor each reddened (its #right right)))})

      _
      (panic! error_message))
    ))

(the (without_right key value ?left ?right)
  (All (_ k v) (-> k v (Maybe (Node k v)) (Maybe (Node k v)) (Node k v)))
  (when ?right
    (^.multi {.#Some right}
             [(its #color right) {#Red}])
    (red key value ?left {.#Some (blackened right)})

    _
    (when ?left
      (^.multi {.#Some left}
               [(its #color left) {#Black}])
      (left_balanced key value {.#Some (reddened left)} ?right)

      (^.multi {.#Some left}
               [(its #color left) {#Red}]
               [(its #right left) {.#Some left>>right}]
               [(its #color left>>right) {#Black}])
      (red (its #key left>>right)
           (its #value left>>right)
           {.#Some (left_balanced (its #key left)
                                  (its #value left)
                                  (of maybe.functor each reddened (its #left left))
                                  (its #left left>>right))}
           {.#Some (black key value (its #right left>>right) ?right)})

      _
      (panic! error_message)
      )))

(the (prepended ?left ?right)
  (All (_ k v) (-> (Maybe (Node k v)) (Maybe (Node k v)) (Maybe (Node k v))))
  (when [?left ?right]
    [{.#None} _]
    ?right

    [_ {.#None}]
    ?left

    [{.#Some left} {.#Some right}]
    (when [(its #color left) (its #color right)]
      [{#Red} {#Red}]
      (do maybe.monad
        [fused (prepended (its #right left) (its #right right))]
        (when (its #color fused)
          {#Red}
          (in (red (its #key fused)
                   (its #value fused)
                   {.#Some (red (its #key left)
                                (its #value left)
                                (its #left left)
                                (its #left fused))}
                   {.#Some (red (its #key right)
                                (its #value right)
                                (its #right fused)
                                (its #right right))}))

          {#Black}
          (in (red (its #key left)
                   (its #value left)
                   (its #left left)
                   {.#Some (red (its #key right)
                                (its #value right)
                                {.#Some fused}
                                (its #right right))}))))

      [{#Red} {#Black}]
      {.#Some (red (its #key left)
                   (its #value left)
                   (its #left left)
                   (prepended (its #right left)
                              ?right))}
      
      [{#Black} {#Red}]
      {.#Some (red (its #key right)
                   (its #value right)
                   (prepended ?left
                              (its #left right))
                   (its #right right))}

      [{#Black} {#Black}]
      (do maybe.monad
        [fused (prepended (its #right left) (its #left right))]
        (when (its #color fused)
          {#Red}
          (in (red (its #key fused)
                   (its #value fused)
                   {.#Some (black (its #key left)
                                  (its #value left)
                                  (its #left left)
                                  (its #left fused))}
                   {.#Some (black (its #key right)
                                  (its #value right)
                                  (its #right fused)
                                  (its #right right))}))
          
          {#Black}
          (in (without_left (its #key left)
                            (its #value left)
                            (its #left left)
                            {.#Some (black (its #key right)
                                           (its #value right)
                                           {.#Some fused}
                                           (its #right right))}))
          ))
      )

    _
    (undefined)))

(the .public (lacks key dict)
  (All (_ k v) (-> k (Dictionary k v) (Dictionary k v)))
  (let [(open "_#[0]") (its #order dict)
        [?root found?] (loop (again [?root (its #root dict)])
                         (when ?root
                           {.#Some root}
                           (let [root_key (its #key root)
                                 root_val (its #value root)]
                             (if (_#= root_key key)
                               [(prepended (its #left root)
                                           (its #right root))
                                true]
                               (let [go_left? (_#< root_key key)]
                                 (when (again (if go_left?
                                                (its #left root)
                                                (its #right root)))
                                   [{.#None} .false]
                                   [{.#None} false]

                                   [side_outcome _]
                                   (if go_left?
                                     (when (its #left root)
                                       (^.multi {.#Some left}
                                                [(its #color left) {#Black}])
                                       [{.#Some (without_left root_key root_val side_outcome (its #right root))}
                                        false]

                                       _
                                       [{.#Some (red root_key root_val side_outcome (its #right root))}
                                        false])
                                     (when (its #right root)
                                       (^.multi {.#Some right}
                                                [(its #color right) {#Black}])
                                       [{.#Some (without_right root_key root_val (its #left root) side_outcome)}
                                        false]

                                       _
                                       [{.#Some (red root_key root_val (its #left root) side_outcome)}
                                        false])
                                     )))
                               ))

                           {.#None}
                           [{.#None} false]
                           ))]
    (when ?root
      {.#None}
      (if found?
        (.has #root ?root dict)
        dict)

      {.#Some root}
      (.has #root {.#Some (blackened root)} dict)
      )))

(the .public (revised key transform dict)
  (All (_ k v) (-> k (-> v v) (Dictionary k v) (Dictionary k v)))
  (when (..value key dict)
    {.#Some old}
    (..has key (transform old) dict)

    {.#None}
    dict))

(the .public (of_list order list)
  (All (_ k v) (-> (Order k) (List [k v]) (Dictionary k v)))
  (list#mix (function (_ [key value] dict)
              (..has key value dict))
            (empty order)
            list))

(with_template [<name> <type> <output>]
  [(the .public (<name> dict)
     (All (_ k v) (-> (Dictionary k v) (List <type>)))
     (loop (again [node (its #root dict)])
       (when node
         {.#None}
         (list)

         {.#Some node'}
         (all list#composite
              (again (its #left node'))
              (list <output>)
              (again (its #right node'))))))]

  [entries [k v] [(its #key node') (its #value node')]]
  [keys    k     (its #key node')]
  [values  v     (its #value node')]
  )

(the .public (equivalence (open ",#[0]"))
  (All (_ k v) (-> (Equivalence v) (Equivalence (Dictionary k v))))
  (implementation
   (the (= reference sample)
     (let [(open "/#[0]") (its #order reference)]
       (loop (again [entriesR (entries reference)
                     entriesS (entries sample)])
         (when [entriesR entriesS]
           [{.#End} {.#End}]
           true

           [{.#Item [keyR valueR] entriesR'} {.#Item [keyS valueS] entriesS'}]
           (and (/#= keyR keyS)
                (,#= valueR valueS)
                (again entriesR' entriesS'))

           _
           false))))))
