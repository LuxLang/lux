... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [functor (.only Functor)]
    [equivalence (.only Equivalence)]
    [monad (.only do)]
    ["<>" projection]]
   [algorithm
    [mix (.only Mix)]]
   [data
    [collection
     ["[0]" stack (.use "[1]#[0]" monad mix)]]]
   [macro
    ["[0]" syntax]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection (.only Projection)]]]]])

(every .public (Tree a)
  (Record
   [#value a
    #children (Stack (Tree a))]))

(the .public (flat tree)
  (for_any (_ a) (-> (Tree a) (Stack a)))
  (|> tree
      (its #children)
      (stack#each flat)
      stack#conjoint
      {.#Top (its #value tree)}))

(the .public (leaf value)
  (for_any (_ a) (-> a (Tree a)))
  [#value value
   #children (stack)])

(the .public (branch value children)
  (for_any (_ a) (-> a (Stack (Tree a)) (Tree a)))
  [#value value
   #children children])

(every Tree_Code
  (Rec Tree_Code
    [Code (Stack Tree_Code)]))

(the tree^
  (Projection Tree_Code)
  (|> (|>> <>.some
           <code>.variant
           (<>.and <code>.any))
      <>.rec
      <>.some
      <code>.variant
      (<>.else (stack))
      (<>.and <code>.any)))

(the .public tree
  (syntax.macro (_ [root tree^])
    (in (stack (loop (again [[value children] root])
                 (` [#value (, value)
                     #children (stack (,* (stack#each again children)))]))))))

(the .public (equivalence super)
  (for_any (_ a)
    (-> (Equivalence a)
        (Equivalence (Tree a))))
  (implementation
   (the (= tx ty)
     (and (by super = (its #value tx) (its #value ty))
          (by (stack.equivalence (equivalence super)) = (its #children tx) (its #children ty))))))

(the .public functor
  (Functor Tree)
  (implementation
   (the (each f fa)
     [#value (f (its #value fa))
      #children (stack#each (each f)
                            (its #children fa))])))

(the .public mix
  (Mix Tree)
  (implementation
   (the (mix f init tree)
     (stack#mix (function (_ tree' init') (mix f init' tree'))
                (f (its #value tree)
                   init)
                (its #children tree)))))
