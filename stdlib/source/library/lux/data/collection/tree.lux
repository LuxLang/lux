(.require
 [library
  [lux (.except)
   [abstract
    [functor (.only Functor)]
    [equivalence (.only Equivalence)]
    [mix (.only Mix)]
    [monad (.only do)]]
   [control
    ["<>" parser]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" monad mix)]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\parser (.only Parser)]]
    [macro
     [syntax (.only syntax)]]]]])

(type .public (Tree a)
  (Record
   [#value a
    #children (List (Tree a))]))

(def .public (flat tree)
  (All (_ a) (-> (Tree a) (List a)))
  (|> tree
      (the #children)
      (list#each flat)
      list#conjoint
      {.#Item (the #value tree)}))

(def .public (leaf value)
  (All (_ a) (-> a (Tree a)))
  [#value value
   #children (list)])

(def .public (branch value children)
  (All (_ a) (-> a (List (Tree a)) (Tree a)))
  [#value value
   #children children])

(type Tree_Code
  (Rec Tree_Code
    [Code (List Tree_Code)]))

(def tree^
  (Parser Tree_Code)
  (|> (|>> <>.some
           <code>.variant
           (<>.and <code>.any))
      <>.rec
      <>.some
      <code>.variant
      (<>.else (list))
      (<>.and <code>.any)))

(def .public tree
  (syntax (_ [root tree^])
    (in (list (loop (again [[value children] root])
                (` [#value (, value)
                    #children (list (,* (list#each again children)))]))))))

(def .public (equivalence super)
  (All (_ a) (-> (Equivalence a) (Equivalence (Tree a))))
  (implementation
   (def (= tx ty)
     (and (at super = (the #value tx) (the #value ty))
          (at (list.equivalence (equivalence super)) = (the #children tx) (the #children ty))))))

(def .public functor
  (Functor Tree)
  (implementation
   (def (each f fa)
     [#value (f (the #value fa))
      #children (list#each (each f)
                           (the #children fa))])))

(def .public mix
  (Mix Tree)
  (implementation
   (def (mix f init tree)
     (list#mix (function (_ tree' init') (mix f init' tree'))
               (f (the #value tree)
                  init)
               (the #children tree)))))
