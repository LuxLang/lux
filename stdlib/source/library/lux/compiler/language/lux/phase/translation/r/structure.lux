... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [data
    [collection
     ["[0]" list]]]
   [target
    ["_" r]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Expression)]
  ["[1][0]" primitive]
  ["///[1]" ////
   [analysis (.only Variant Tuple)]
   ["[1][0]" synthesis (.only Synthesis)]
   ["//[1]" ///
    ["[1][0]" phase (.use "[1]#[0]" monad)]]]])

(the .public (tuple expression archive elemsS+)
  (Expression (Tuple Synthesis))
  (when elemsS+
    (list)
    (///////phase#pure (//primitive.text /////synthesis.unit))

    (list singletonS)
    (expression archive singletonS)

    _
    (|> elemsS+
        (list.each' ///////phase.monad (expression archive))
        (///////phase#each _.list))))

(the .public (variant expression archive [lefts right? valueS])
  (Expression (Variant Synthesis))
  (let [tag (if right?
                (++ lefts)
                lefts)]
    (///////phase#each (|>> (//runtime.variant tag right?))
                       (expression archive valueS))))
