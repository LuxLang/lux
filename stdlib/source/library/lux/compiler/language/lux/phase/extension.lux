... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except global)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    ["[0]" monad]]
   [control
    ["[0]" maybe]]
   [error
    ["[0]" try]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list (.only)
      ["[0]" property]]
     ["[0]" dictionary (.only Dictionary)]]]
   ["[0]" type (.only sharing)
    ["[0]" check]]
   ["[0]" meta (.only)
    ["[0]" binding]
    ["[0]" name]]
   [compiler
    ["@[0]" type]]]]
 [///
  ["[0]" phase (.only Operation Phase) (.use "[1]#[0]" functor)]
  [///
   [meta
    [archive (.only Archive)]]]])

(every .public (Extension it)
  (Record
   [#name Name
    #parameters (List it)]))

(the .public equivalence
  (for_any (_ it)
    (-> (Equivalence it)
        (Equivalence (Extension it))))
  (|>> list.equivalence
       (product.equivalence name.equivalence)))

(the .public hash
  (for_any (_ it)
    (-> (Hash it)
        (Hash (Extension it))))
  (|>> list.hash
       (product.hash name.hash)))

(every .public (Handler state input output)
  (-> (Phase state input output)
      (Phase state (List input) output)))

(every .public (Bundle state input output)
  (Dictionary Text (Handler state input output)))

(the .public empty
  Bundle
  (dictionary.empty text.hash))

(every .public (Extender state input output)
  (-> Any
      (Handler state input output)))

(exception.the .public (invalid [global expected actual])
  (Exception [Name Type Type])
  (exception.report
   (list ["Definition" (name.as_text global)]
         ["Expected" (type.as_text expected)]
         ["Actual" (type.as_text actual)])))

(exception.the .public (error [it])
  (Exception [Text])
  it)

(the (global name)
  (-> Name
      (Operation @type.Lux @type.Global))
  (monad.let phase.monad
    [name (phase.normal name)
     lux phase.state]
    (loop (again [[normal_module normal_proper] name])
      (when (is (Maybe [Bit @type.Global])
                (monad.let maybe.monad
                  [it (|> lux
                          (its @type.#modules)
                          (property.value normal_module))]
                  (|> it
                      (its @type.#definitions)
                      (property.value normal_proper))))
        {.#Some [exported? it]}
        (when it
          {@type.#Alias de_aliased}
          (again de_aliased)

          _
          (pure it))

        {.#None it}
        (phase.failure (text "Unknown global: " (name.as_text name)))))))

(every Value
  (Variant
   {#Normal Any}
   {#Special Any}))

(the (global_value name)
  (-> Name
      (Operation @type.Lux [@type.Type Value]))
  (monad.let phase.monad
    [global (..global name)]
    (when global
      {@type.#Definition [type value]}
      (pure [type {#Normal value}])

      {@type.#Default [type value]}
      (pure [type {#Special value}])
      
      {@type.#Alias _}
      (undefined))))

(the (global_extension expected_type name)
  (-> Type Name
      (Operation @type.Lux Value))
  (monad.let phase.monad
    [[actual_type value] (global_value name)]
    (if (check.< expected_type (@type.old_type actual_type))
        (pure value)
        (phase.except ..invalid [name expected_type (@type.old_type actual_type)]))))

(the .public (reification extender
                          lux phase archive
                          expected_type global parameters
                          when_valid
                          when_invalid)
  (for_any (_ state input raw_output processed_output)
    (-> (Extender state input raw_output)
        @type.Lux (Phase state input raw_output) Archive
        Type Name (List input)
        (-> raw_output processed_output)
        (-> Text (Maybe (Operation state processed_output)))
        (Operation state processed_output)))
  (when (|> (monad.let [! phase.monad]
              [value (global_extension expected_type global)]
              (pure ((when value
                       {#Normal definition}
                       (extender definition)

                       {#Special default}
                       (sharing [state input raw_output]
                         (is (Extender state input raw_output)
                             extender)
                         (is (Handler state input raw_output)
                             (as_expected default))))
                     phase archive parameters)))
            (phase.value lux))
    {try.#Success operation}
    (phase#each when_valid operation)

    {try.#Failure error}
    (when (when_invalid error)
      {.#Some when_invalid}
      when_invalid

      {.#None}
      (phase.except ..error [error]))))

(exception.the .public (defaults_cannot_be_referenced global)
  (Exception Name)
  (exception.report
   (list ["Default" (name.as_text global)])))
