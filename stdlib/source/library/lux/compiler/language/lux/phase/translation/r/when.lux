... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except when let if)
   [abstract
    ["[0]" monad]]
   [data
    ["[0]" product]
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]:[0]" functor mix)]
     ["[0]" set]]]
   [math
    [number
     [/64
      ["i" integer]]]]
   [macro
    ["[0]" template]]
   [target
    ["_" r (.only SVar)]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Expression)]
  ["[1][0]" reference]
  ["[1][0]" primitive]
  ["/[1]" //
   ["[1][0]" reference]
   ["/[1]" //
    ["[1][0]" synthesis
     ["[1]/[0]" when]]
    ["/[1]" //
     ["[1][0]" synthesis (.only Member Synthesis Path)]
     ["[1][0]" translation]
     ["//[1]" ///
      [reference
       ["[1][0]" variable (.only Register)]]
      ["[1][0]" phase (.use "[1]:[0]" monad)]
      [meta
       [archive (.only Archive)]]]]]]])

(the .public register
  (-> Register SVar)
  (|>> (///reference.local //reference.system) as_expected))

(the .public capture
  (-> Register SVar)
  (|>> (///reference.foreign //reference.system) as_expected))

(the .public (let expression archive [valueS register bodyS])
  (Expression [Synthesis Register Synthesis])
  (monad.let ///////phase.monad
    [valueO (expression archive valueS)
     bodyO (expression archive bodyS)]
    (pure (_.block
           (all _.then
                (_.set! (..register register) valueO)
                bodyO)))))

(the .public (if expression archive [testS thenS elseS])
  (Expression [Synthesis Synthesis Synthesis])
  (monad.let ///////phase.monad
    [testO (expression archive testS)
     thenO (expression archive thenS)
     elseO (expression archive elseS)]
    (pure (_.if testO thenO elseO))))

(the .public (get expression archive [pathP valueS])
  (Expression [(List Member) Synthesis])
  (monad.let ///////phase.monad
    [valueO (expression archive valueS)]
    (pure (list:mix (function (_ side source)
                      (.let [method (`` (.when side
                                          (,, (template.with [<side> <accessor>]
                                                [(<side> lefts)
                                                 (<accessor> (_.int (.integer lefts)))]

                                                [[.:Left  //runtime.tuple:left]
                                                 [.:Right //runtime.tuple:right]]
                                                ))))]
                        (method source)))
                    valueO
                    pathP))))

(the $savepoint (_.var "lux_pm_cursor_savepoint"))
(the $cursor (_.var "lux_pm_cursor"))
(the $temp (_.var "lux_pm_temp"))
(the $alt_error (_.var "alt_error"))

(the top
  _.length)

(the next
  (|>> _.length (_.+ (_.int +1))))

(the (push! value var)
  (-> _.Expression SVar _.Expression)
  (_.set_item! (next var) value var))

(the (pop! var)
  (-> SVar _.Expression)
  (_.set_item! (top var) _.null var))

(the (push_cursor! value)
  (-> _.Expression _.Expression)
  (push! value $cursor))

(the save_cursor!
  _.Expression
  (push! (_.slice (_.float +1.0) (_.length $cursor) $cursor)
         $savepoint))

(the restore_cursor!
  _.Expression
  (_.set! $cursor (_.item (top $savepoint) $savepoint)))

(the peek
  _.Expression
  (|> $cursor (_.item (top $cursor))))

(the pop_cursor!
  _.Expression
  (pop! $cursor))

(the error
  (_.string (template.with_locals [error]
              (template.text [error]))))

(the fail!
  (_.stop ..error))

(the (catch handler)
  (-> _.Expression
      _.Expression)
  (_.function (list $alt_error)
    (_.if (|> $alt_error (_.= ..error))
          handler
          (_.stop $alt_error))))

(the (pattern_matching' expression archive)
  (Expression Path)
  (function (of pathP)
    (`` (.when pathP
          {/////synthesis.:Then bodyS}
          (expression archive bodyS)

          {/////synthesis.:Pop}
          (///////phase:pure ..pop_cursor!)

          {/////synthesis.:Bind register}
          (///////phase:pure (_.set! (..register register) ..peek))

          {/////synthesis.:Bit_Fork when thenP elseP}
          (monad.let [! ///////phase.monad]
            [then! (of thenP)
             else! (.when elseP
                     {.:Some elseP}
                     (of elseP)

                     {.:None}
                     (pure ..fail!))]
            (pure (.if when
                       (_.if ..peek
                             then!
                             else!)
                       (_.if ..peek
                             else!
                             then!))))

          (,, (template.with [<tag> <injection> <=>]
                [{<tag> item}
                 (monad.let [! ///////phase.monad]
                   [clauses (monad.each ! (function (_ [match then])
                                            (monad.let !
                                              [then! (of then)]
                                              (pure [(<=> (|> match <injection>)
                                                          ..peek)
                                                     then!])))
                                        {.:Top item})]
                   (pure (list:mix (function (_ [when then] else)
                                     (_.if when then else))
                                   ..fail!
                                   clauses)))]

                [[/////synthesis.:I64_Fork //primitive.i64 //runtime.i64:=]
                 [/////synthesis.:F64_Fork //primitive.f64 _.=]
                 [/////synthesis.:Text_Fork //primitive.text _.=]]
                ))

          (,, (template.with [<pm> <flag> <prep>]
                [(<pm> idx)
                 (///////phase:pure (all _.then
                                         (_.set! $temp (|> idx <prep> .integer _.int (//runtime.sum:get ..peek (//runtime.flag <flag>))))
                                         (_.if (_.= _.null $temp)
                                               ..fail!
                                               (..push_cursor! $temp))))]

                [[/////synthesis.left_side  false (<|)]
                 [/////synthesis.right_side true  ++]]
                ))

          (/////synthesis.left_member 0)
          (///////phase:pure (_.item (_.int +1) ..peek))

          (,, (template.with [<pm> <getter>]
                [(<pm> lefts)
                 (///////phase:pure (|> ..peek (<getter> (_.int (.integer lefts))) ..push_cursor!))]

                [[/////synthesis.left_member  //runtime.tuple:left]
                 [/////synthesis.right_member //runtime.tuple:right]]
                ))

          (/////synthesis.path/seq leftP rightP)
          (monad.let ///////phase.monad
            [leftO (of leftP)
             rightO (of rightP)]
            (pure (all _.then
                       leftO
                       rightO)))

          (/////synthesis.path/alt leftP rightP)
          (monad.let [! ///////phase.monad]
            [leftO (of leftP)
             rightO (of rightP)]
            (pure (_.try (all _.then
                              ..save_cursor!
                              leftO)
                         {.:None}
                         {.:Some (..catch (all _.then
                                               ..restore_cursor!
                                               rightO))}
                         {.:None})))
          ))))

(the (pattern_matching expression archive pathP)
  (Expression Path)
  (monad.let ///////phase.monad
    [pattern_matching! (pattern_matching' expression archive pathP)]
    (pure (_.try pattern_matching!
                 {.:None}
                 {.:Some (..catch (_.stop (_.string "Invalid expression for pattern-matching.")))}
                 {.:None}))))

(the .public (when expression archive [valueS pathP])
  (Expression [Synthesis Path])
  (monad.let [! ///////phase.monad]
    [valueO (expression archive valueS)]
    (<| (by ! each (|>> (all _.then
                             (_.set! $cursor (_.list (list valueO)))
                             (_.set! $savepoint (_.list (list))))
                        _.block))
        (pattern_matching expression archive pathP))))
