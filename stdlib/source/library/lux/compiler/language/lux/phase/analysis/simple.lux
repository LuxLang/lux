... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except natural integer revolution text)
   [abstract
    [monad (.only do)]]
   [macro
    ["[0]" template]]
   ["[0]" meta]
   [compiler
    ["@[0]" type]]]]
 [////
  ["[0]" phase]
  ["/" analysis (.only Operation)
   ["[1][0]" simple]
   ["[1][0]" type]]])

(template.with [<name> ,type <tag>]
  [(`` (the .public (<name> value)
         (-> (,, (template.name [._] [,type]))
             (Operation /.Term))
         (do phase.monad
           [_ (/type.inference (,, (template.name [@type._] [,type])))
            @ meta.provenance]
           (pure [@ {/.#Simple {<tag> value}}]))))]

  [[bit Bit /simple.#Bit]
   [natural Natural /simple.#Natural]
   [integer Integer /simple.#Integer]
   [revolution Revolution /simple.#Revolution]
   [decimal Decimal /simple.#Decimal]
   [text Text /simple.#Text]]
  )

(the .public unit
  (Operation /.Term)
  (do phase.monad
    [_ (/type.inference .Any)
     @ meta.provenance]
    (pure [@ {/.#Simple {/simple.#Unit}}])))
