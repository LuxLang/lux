... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except natural integer revolution text)
   [abstract
    ["[0]" monad]]
   [macro
    ["[0]" template]]
   ["[0]" meta]
   [compiler
    ["@[0]" type]]]]
 [////
  ["[0]" phase]
  ["/" analysis (.only Operation)
   ["[1][0]" simple]
   ["[1][0]" type]]])

(template.with [<name> ,type <tag>]
  [(`` (the .public (<name> value)
         (-> (,, (template.name [._] [,type]))
             (Operation /.Term))
         (monad.let phase.monad
           [_ (/type.inference (,, (template.name [@type._] [,type])))
            @ /.provenance]
           (pure (<tag> @ value)))))]

  [[bit Bit /.bit]
   [natural Natural /.natural]
   [integer Integer /.integer]
   [revolution Revolution /.revolution]
   [decimal Decimal /.decimal]
   [text Text /.text]])

(the .public unit
  (Operation /.Term)
  (monad.let phase.monad
    [_ (/type.inference .Any)
     @ /.provenance]
    (pure (/.unit @))))
