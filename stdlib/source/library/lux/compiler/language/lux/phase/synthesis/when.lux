... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Pattern)
   [abstract
    [equivalence (.only Equivalence)]
    ["[0]" monad]]
   [control
    ["[0]" pipe]
    ["[0]" maybe (.use "[1]:[0]" functor)]]
   [error
    ["[0]" try]]
   [data
    ["[0]" product]
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]:[0]" functor mix monoid)]
     ["[0]" set (.only Set)]]]
   [logic
    ["[0]" bit]]
   [math
    [number
     ["[0]" /64 (.only)
      ["n" natural]
      ["[0]" decimal]]]]
   [macro
    ["^" pattern]
    ["[0]" expansion]
    ["[0]" template]]]]
 ["[0]" ///
  [//
   ["[0]" phase (.use "[1]:[0]" monad)]
   ["[0]" analysis (.only Match)
    ["[2][0]" simple]
    ["[2][0]" complex]]
   ["/" synthesis (.only Path Operation Phase)
    ["[0]" path]
    ["[1][0]" access (.only)
     ["[2][0]" side]
     ["[2][0]" member (.only Member)]]]
   [///
    ["@[0]" type (.only Register Variable Pattern)]
    ["[1][0]" reference (.only)
     ["[1]/[0]" variable]]
    [meta
     [archive (.only Archive)]]]]])

(the clean_up
  (-> Path
      Path)
  (|>> {path.:And {path.:Pop}}))

(the (path' pattern end? thenC)
  (-> Pattern Bit (Operation Path)
      (Operation Path))
  (when pattern
    {@type.:Simple_Pattern simple}
    (`` (when simple
          {@type.:Simple_Unit}
          thenC

          {@type.:Simple_Bit when}
          (phase:each (function (_ then)
                        {path.:Bit_Fork when then {.:None}})
                      thenC)

          (,, (template.with [<from> <to> <conversion>]
                [{<from> test}
                 (phase:each (function (_ then)
                               {<to> [(<conversion> test) then] (list)})
                             thenC)]

                [[@type.:Simple_Natural path.:I64_Fork .i64]
                 [@type.:Simple_Integer path.:I64_Fork .i64]
                 [@type.:Simple_Revolution path.:I64_Fork .i64]
                 [@type.:Simple_Decimal path.:F64_Fork |>]
                 [@type.:Simple_Text path.:Text_Fork |>]]))))

    {@type.:Bind_Pattern register}
    (<| (by phase.monad each (|>> {path.:And {path.:Bind register}}))
        /.with_new_local
        thenC)

    {@type.:Complex_Pattern {@type.:Complex_Variant [lefts right? value_pattern]}}
    (<| (phase:each (|>> {path.:And {path.:Access {/access.:Side [/side.:lefts lefts
                                                                  /side.:right? right?]}}}))
        (path' value_pattern end?)
        (pipe.if [(pipe.new (not end?) [])]
                 [(phase:each ..clean_up)]
                 ... else
                 [])
        thenC)

    {@type.:Complex_Pattern {@type.:Complex_Tuple tuple}}
    (let [tuple:last (-- (list.size tuple))]
      (list:mix (function (_ [tuple:lefts tuple:member] nextC)
                  (.when tuple:member
                    {@type.:Simple_Pattern {@type.:Simple_Unit}}
                    nextC

                    _
                    (let [right? (n.= tuple:last tuple:lefts)
                          end?' (and end? right?)]
                      (<| (phase:each (|>> {path.:And {path.:Access {/access.:Member [/member.:lefts (if right?
                                                                                                         (-- tuple:lefts)
                                                                                                         tuple:lefts)
                                                                                      /member.:right? right?]}}}))
                          (path' tuple:member end?')
                          (pipe.if [(pipe.new (not end?') [])]
                                   [(phase:each ..clean_up)]
                                   ... else
                                   [])
                          nextC))))
                thenC
                (list.in_reverse (list.enumeration tuple))))

    {@type.:List_Pattern it}
    (let [size (list.size it)
          last (-- size)]
      (|> (list:mix (function (_ [position item] nextC)
                      (.when item
                        {@type.:Simple_Pattern {@type.:Simple_Unit}}
                        nextC

                        _
                        (let [end? (and end?
                                        (n.= last position))]
                          (<| (phase:each (|>> {path.:And {path.:Access {/access.:Item position}}}))
                              (path' item end?)
                              (pipe.if [(pipe.new (not end?) [])]
                                       [(phase:each ..clean_up)]
                                       ... else
                                       [])
                              nextC))))
                    thenC
                    (list.in_reverse (list.enumeration it)))
          (phase:each (|>> {path.:And {path.:List size}}))))
    ))

(the (path archive synthesize pattern bodyA)
  (-> Archive Phase Pattern analysis.Term
      (Operation Path))
  (path' pattern true (phase:each (|>> {path.:Then}) (synthesize archive bodyA))))

(the (weave_branch weave equivalence [new_test new_then] [[old_test old_then] old_tail])
  (for_any (_ it)
    (-> (-> Path Path Path) (Equivalence it) [it Path] (path.Fork it Path)
        (path.Fork it Path)))
  (if (by equivalence = new_test old_test)
      [[old_test (weave new_then old_then)] old_tail]
      [[old_test old_then]
       (when (list.item 0 old_tail)
         {try.:Failure _}
         (list [new_test new_then])
         
         {try.:Success old_item}
         (let [[head tail] (weave_branch weave equivalence [new_test new_then] [old_item (list.after 1 old_tail)])]
           (list:composite (list head) tail)))]))

(the (weave_fork weave equivalence [new_head new_tail] old_fork)
  (for_any (_ it)
    (-> (-> Path Path Path) (Equivalence it) (path.Fork it Path) (path.Fork it Path)
        (path.Fork it Path)))
  (list:mix (..weave_branch weave equivalence)
            old_fork
            (list:composite (list new_head) new_tail)))

(the (weave new old)
  (-> Path Path
      Path)
  (expansion.let [<default> (these {path.:Or old new})]
    (`` (when [new old]
          [_
           {path.:Or old_left old_right}]
          {path.:Or old_left
                    (weave new old_right)}

          [{path.:And preN postN}
           {path.:And preO postO}]
          (when (weave preN preO)
            {path.:Or _}
            <default>

            woven
            {path.:And woven (weave postN postO)})

          [{path.:Pop} {path.:Pop}]
          old

          [{path.:Bit_Fork new_when new_then new_else}
           {path.:Bit_Fork old_when old_then old_else}]
          (if (bit.= new_when old_when)
              {path.:Bit_Fork old_when
                              (weave new_then old_then)
                              (when [new_else old_else]
                                [{.:None} {.:None}]
                                {.:None}

                                (^.or [{.:Some woven_then} {.:None}]
                                      [{.:None} {.:Some woven_then}])
                                {.:Some woven_then}

                                [{.:Some new_else} {.:Some old_else}]
                                {.:Some (weave new_else old_else)})}
              {path.:Bit_Fork old_when
                              (when new_else
                                {.:None}
                                old_then

                                {.:Some new_else}
                                (weave new_else old_then))
                              {.:Some (when old_else
                                        {.:None}
                                        new_then

                                        {.:Some old_else}
                                        (weave new_then old_else))}})

          (,, (template.with [<tag> <equivalence>]
                [[{<tag> new_fork} {<tag> old_fork}]
                 {<tag> (..weave_fork weave <equivalence> new_fork old_fork)}]

                [[path.:I64_Fork /64.equivalence]
                 [path.:F64_Fork decimal.equivalence]
                 [path.:Text_Fork text.equivalence]]))
          
          (,, (template.with [<access> <side> <lefts> <right?>]
                [[{path.:Access {<access> [<lefts> newL <right?> <side>]}}
                  {path.:Access {<access> [<lefts> oldL <right?> <side>]}}]
                 (if (n.= newL oldL)
                     old
                     <default>)]

                [[/access.:Side .false /side.:lefts /side.:right?]
                 [/access.:Side .true /side.:lefts /side.:right?]

                 [/access.:Member .false /member.:lefts /member.:right?]
                 [/access.:Member .true /member.:lefts /member.:right?]]))

          [{path.:Bind newR} {path.:Bind oldR}]
          (if (n.= newR oldR)
              old
              <default>)

          _
          <default>))))

(the .public (synthesize_when synthesize @ archive input [[headP headA] tailPA+])
  (-> Phase Provenance Archive /.Term Match
      (Operation /.Term))
  (monad.let [! phase.monad]
    [headSP (path archive synthesize headP headA)
     tailSP+ (list.each' ! (product.uncurried (path archive synthesize)) tailPA+)]
    (pure (/.when @ [input (list:mix weave headSP tailSP+)]))))

(the !masking
  (template.macro (_ <@> <variable> <output>)
    [[[{@type.:Bind_Pattern <variable>}
       {@type.:Reference <@> (///reference.local <output>)}]
      (list)]]))

(the .public (synthesize_masking synthesize @ archive input @variable @output)
  (-> Phase Provenance Archive /.Term Register Register
      (Operation /.Term))
  (if (n.= @variable @output)
      (phase:pure input)
      (monad.let phase.monad
        [body (/.with_new_local
                (synthesize archive {@type.:Reference @ (///reference.local @output)}))]
        (pure (/.let @ @variable input body)))))

(the .public (synthesize @ synthesize^ head,tail archive inputA)
  (-> Provenance Phase Match
      Phase)
  (monad.let [! phase.monad]
    [inputS (synthesize^ archive inputA)]
    (when head,tail
      (!masking @ @variable @output)
      (..synthesize_masking synthesize^ @ archive inputS @variable @output)
      
      else
      (..synthesize_when synthesize^ @ archive inputS head,tail))))

(the .public (count_pops path)
  (-> Path
      [Natural Path])
  (when path
    {path.:And {path.:Pop} path'}
    (let [[pops post_pops] (count_pops path')]
      [(++ pops) post_pops])

    _
    [0 path]))

(the .public pattern_matching_error
  "Invalid expression for pattern-matching.")

(every .public Storage
  (Record
   [:bindings (Set Register)
    :dependencies (Set Variable)]))

(the empty
  Storage
  [:bindings (set.empty n.hash)
   :dependencies (set.empty ///reference/variable.hash)])

... TODO: Use this to declare all local variables at the beginning of
... script functions.
... That way, it should be possible to do cheap "let" expressions,
... since the variable will exist beforehand, so no closure will need
... to be created for it.
... Apply this trick to JS, Python et al.
(the .public (storage path)
  (-> Path
      Storage)
  (loop (for_path [path path
                   path_storage ..empty])
    (when path
      (^.or {path.:Pop}
            {path.:Access Access}
            {path.:List size})
      path_storage

      {path.:Bind register}
      (revised :bindings (set.has register)
               path_storage)

      {path.:Bit_Fork _ default else}
      (|> (when else
            {.:None}
            path_storage
            
            {.:Some else}
            (for_path else path_storage))
          (for_path default))

      (^.or {path.:I64_Fork [head tail]}
            {path.:F64_Fork [head tail]}
            {path.:Text_Fork [head tail]})
      (|> (list:composite (list head) tail)
          (list:each product.right)
          (list:mix for_path path_storage))

      (^.or {path.:And left right}
            {path.:Or left right})
      (list:mix for_path path_storage (list left right))

      {path.:Then bodyS}
      (loop (for_synthesis [bodyS bodyS
                            synthesis_storage path_storage])
        (when bodyS
          (^.or [@ {/.:Simple _}]
                (/.constant @ _))
          synthesis_storage
          
          (/.variant @ [lefts right? valueS])
          (for_synthesis valueS synthesis_storage)

          (/.tuple @ members)
          (list:mix for_synthesis synthesis_storage members)

          [@ {/.:Reference {@type.:Variable_Reference {@type.:Local register}}}]
          (if (set.member? (its :bindings synthesis_storage) register)
              synthesis_storage
              (revised :dependencies (set.has {@type.:Local register}) synthesis_storage))

          [@ {/.:Reference {@type.:Variable_Reference var}}]
          (revised :dependencies (set.has var) synthesis_storage)

          (/.reification @ [functionS argsS])
          (list:mix for_synthesis (for_synthesis functionS synthesis_storage) argsS)

          (/.abstraction @ [environment arity bodyS])
          (list:mix for_synthesis synthesis_storage environment)

          (/.when @ [inputS pathS])
          (revised :dependencies
                   (set.union (its :dependencies (for_path pathS synthesis_storage)))
                   (for_synthesis inputS synthesis_storage))

          (/.tail_recursion @ [start initsS+ iterationS])
          (revised :dependencies
                   (set.union (|> synthesis_storage
                                  (revised :bindings (set.union (|> initsS+
                                                                    list.enumeration
                                                                    (list:each (|>> product.left (n.+ start)))
                                                                    (set.of_list n.hash))))
                                  (for_synthesis iterationS)
                                  (its :dependencies)))
                   (list:mix for_synthesis synthesis_storage initsS+))

          (/.tail_iteration @ replacementsS+)
          (list:mix for_synthesis synthesis_storage replacementsS+)

          [@ {/.:Extension [extension argsS]}]
          (list:mix for_synthesis synthesis_storage argsS)))
      )))
