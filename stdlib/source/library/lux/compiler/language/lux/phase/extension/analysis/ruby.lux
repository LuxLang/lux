... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   ["[0]" ffi]
   [abstract
    [monad (.only do)]
    ["<>" projection]]
   [data
    [collection
     ["[0]" array]
     ["[0]" dictionary]
     ["[0]" list (.use "[1]#[0]" monoid)
      ["?[1]" \\projection]]]]
   ["[0]" type (.only)
    ["[0]" check]]
   ["[0]" meta (.only)
    ["[0]" code
     ["<[1]>" \\projection]]]
   [target
    ["_" ruby]]]]
 [//
  ["/" lux (.only custom)]
  [///
   ["[0]" extension]
   [//
    ["[0]" phase]
    ["[0]" analysis (.only Operation Phase Handler Bundle)
     ["[1]/[0]" type]]]]])

(the array::new
  (-> Text Handler)
  (custom
   [?list.any
    (function (_ extension phase archive lengthC)
      (<| analysis/type.with_var
          (function (_ [@read :read:]))
          analysis/type.with_var
          (function (_ [@write :write:]))
          (do phase.monad
            [lengthA (<| (analysis/type.expecting Natural)
                         (phase archive lengthC))
             _ (analysis/type.inference (type (array.Array' :read: :write:)))
             @ meta.provenance]
            (in [@ {analysis.#Extension (/.translation extension)
                                        (list lengthA)}]))))]))

(the array::length
  (-> Text Handler)
  (custom
   [?list.any
    (function (_ extension phase archive arrayC)
      (<| analysis/type.with_var
          (function (_ [@read :read:]))
          analysis/type.with_var
          (function (_ [@write :write:]))
          (do phase.monad
            [arrayA (<| (analysis/type.expecting (type (array.Array' :read: :write:)))
                        (phase archive arrayC))
             _ (analysis/type.inference Natural)
             @ meta.provenance]
            (in [@ {analysis.#Extension (/.translation extension)
                                        (list arrayA)}]))))]))

(the array::read
  (-> Text Handler)
  (custom
   [(<>.and ?list.any ?list.any)
    (function (_ extension phase archive [indexC arrayC])
      (<| analysis/type.with_var
          (function (_ [@read :read:]))
          analysis/type.with_var
          (function (_ [@write :write:]))
          (do phase.monad
            [indexA (<| (analysis/type.expecting Natural)
                        (phase archive indexC))
             arrayA (<| (analysis/type.expecting (type (array.Array' :read: :write:)))
                        (phase archive arrayC))
             _ (analysis/type.inference :read:)
             @ meta.provenance]
            (in [@ {analysis.#Extension (/.translation extension)
                                        (list indexA arrayA)}]))))]))

(the array::write
  (-> Text Handler)
  (custom
   [(all <>.and ?list.any ?list.any ?list.any)
    (function (_ extension phase archive [indexC valueC arrayC])
      (<| analysis/type.with_var
          (function (_ [@read :read:]))
          analysis/type.with_var
          (function (_ [@write :write:]))
          (do phase.monad
            [indexA (<| (analysis/type.expecting Natural)
                        (phase archive indexC))
             valueA (<| (analysis/type.expecting :write:)
                        (phase archive valueC))
             arrayA (<| (analysis/type.expecting (type (array.Array' :read: :write:)))
                        (phase archive arrayC))
             _ (analysis/type.inference (type (array.Array' :read: :write:)))
             @ meta.provenance]
            (in [@ {analysis.#Extension (/.translation extension)
                                        (list indexA valueA arrayA)}]))))]))

(the array::delete
  (-> Text Handler)
  (custom
   [(all <>.and ?list.any ?list.any)
    (function (_ extension phase archive [indexC arrayC])
      (<| analysis/type.with_var
          (function (_ [@read :read:]))
          analysis/type.with_var
          (function (_ [@write :write:]))
          (do phase.monad
            [indexA (<| (analysis/type.expecting Natural)
                        (phase archive indexC))
             arrayA (<| (analysis/type.expecting (type (array.Array' :read: :write:)))
                        (phase archive arrayC))
             _ (analysis/type.inference (type (array.Array' :read: :write:)))
             @ meta.provenance]
            (in [@ {analysis.#Extension (/.translation extension)
                                        (list indexA arrayA)}]))))]))

(the with_array_extensions
  (-> Bundle Bundle)
  (|>> (/.with "ruby_array_new#" array::new)
       (/.with "ruby_array_length#" array::length)
       (/.with "ruby_array_read#" array::read)
       (/.with "ruby_array_write#" array::write)
       (/.with "ruby_array_delete#" array::delete)
       ))

(the Nil
  (for .ruby ffi.Nil
       Any))

(the Object
  (for .ruby (type (ffi.Object Any))
       Any))

(the Function
  (for .ruby ffi.Function
       Any))

(the object::get
  (-> Text Handler)
  (custom
   [(all <>.and <code>.text ?list.any)
    (function (_ extension phase archive [fieldC objectC])
      (do phase.monad
        [objectA (<| (analysis/type.expecting ..Object)
                     (phase archive objectC))
         _ (analysis/type.inference .Any)
         @ meta.provenance]
        (in [@ {analysis.#Extension (/.translation extension)
                                    (list (analysis.text @ fieldC)
                                          objectA)}])))]))

(the object::do
  (-> Text Handler)
  (custom
   [(all <>.and <code>.text ?list.any (<code>.tuple (<>.some ?list.any)))
    (function (_ extension phase archive [methodC objectC inputsC])
      (do [! phase.monad]
        [objectA (<| (analysis/type.expecting ..Object)
                     (phase archive objectC))
         inputsA (list.each' ! (|>> (phase archive) (analysis/type.expecting Any))
                             inputsC)
         _ (analysis/type.inference .Any)
         @ meta.provenance]
        (in [@ {analysis.#Extension (/.translation extension)
                                    (list#composite (list (analysis.text @ methodC)
                                                          objectA)
                                                    inputsA)}])))]))

(the with_object_extensions
  (-> Bundle Bundle)
  (|>> (/.with "ruby_object_get#" object::get)
       (/.with "ruby_object_do#" object::do)
       (/.with "ruby_object_nil#" (/.nullary ..Nil))
       (/.with "ruby_object_nil?#" (/.unary Any Bit))
       ))

(the ruby::constant
  (-> Text Handler)
  (custom
   [<code>.text
    (function (_ extension phase archive name)
      (do phase.monad
        [_ (analysis/type.inference Any)
         @ meta.provenance]
        (in [@ {analysis.#Extension (/.translation extension)
                                    (list (analysis.text @ name))}])))]))

(the ruby::apply
  (-> Text Handler)
  (custom
   [(all <>.and ?list.any (<code>.tuple (<>.some ?list.any)))
    (function (_ extension phase archive [abstractionC inputsC])
      (do [! phase.monad]
        [abstractionA (<| (analysis/type.expecting ..Function)
                          (phase archive abstractionC))
         inputsA (list.each' ! (|>> (phase archive) (analysis/type.expecting Any))
                             inputsC)
         _ (analysis/type.inference Any)
         @ meta.provenance]
        (in [@ {analysis.#Extension (/.translation extension)
                                    (list#composite (list abstractionA) inputsA)}])))]))

(the ruby::import
  (-> Text Handler)
  (custom
   [<code>.text
    (function (_ extension phase archive name)
      (do phase.monad
        [_ (analysis/type.inference Bit)
         @ meta.provenance]
        (in [@ {analysis.#Extension (/.translation extension)
                                    (list (analysis.text @ name))}])))]))

(the .public bundle
  Bundle
  (|> extension.empty
      with_array_extensions
      with_object_extensions

      (/.with "ruby_constant#" ruby::constant)
      (/.with "ruby_apply#" ruby::apply)
      (/.with "ruby_import#" ruby::import)
      (/.with "ruby_script_universe#" (/.nullary .Bit))
      ))
