... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Type Label)
   [abstract
    [monad (.only do)]
    ["<>" projection]]
   [control
    ["|" pipe]]
   [error
    ["[0]" try]]
   ["[0]" function]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" monad)
      ["?[1]" \\projection]]
     ["[0]" dictionary]]]
   [math
    [number
     [integer
      ["[0]" /32]]]]
   [macro
    ["[0]" template]]
   [target
    [jvm
     ["_" bytecode (.only Label Bytecode) (.use "[1]#[0]" monad)]
     ["[0]" type (.only Type)
      [category (.only Primitive Class)]
      ["[0]" box]]]]]]
 [/////
  ["//" extension]
  [translation
   [extension (.only Nullary Unary Binary Trinary Variadic
                     nullary unary binary trinary variadic)]
   ["///" jvm
    ["[1][0]" type]
    ["[1][0]" value]
    ["[1][0]" runtime (.only Operation Phase Bundle Handler)]
    ["[1][0]" function
     ["[1]" abstract]]]]
  [//
   ["[0]" phase]
   ["[0]" synthesis
    ["<[1]>" \\projection (.only Projection)]]
   [///
    [meta
     [archive (.only Archive)]]
    ["[0]" extension (.only)
     ["[1]/[0]" lux]]]]])

(the .public (custom [projection handler])
  (for_any (_ it)
    (-> [(Projection it)
         (-> Phase Archive it (Operation (Bytecode Any)))]
        Handler))
  (function (_ phase archive input)
    (when (?list.value input projection)
      {try.#Success input'}
      (handler phase archive input')

      {try.#Failure error}
      (phase.failure error))))

(the $Boolean (type.class (list) box.boolean))
(the $Double (type.class (list) box.double))
(the $Character (type.class (list) box.char))
(the $String (type.class (list) "java.lang.String"))
(the $CharSequence (type.class (list) "java.lang.CharSequence"))
(the $Object (type.class (list) "java.lang.Object"))
(the $PrintStream (type.class (list) "java.io.PrintStream"))
(the $System (type.class (list) "java.lang.System"))
(the $Error (type.class (list) "java.lang.Error"))

(the lux_i64
  (Bytecode Any)
  (all _.composite
       _.i2l
       (///value.boxed type.long)))

(the lux_natural
  ..lux_i64)

(the jvm_int
  (Bytecode Any)
  (all _.composite
       (///value.primitive type.long)
       _.l2i))

(the .public (predicate bytecode)
  (-> (-> Label (Bytecode Any))
      (Bytecode Any))
  (do _.monad
    [@then _.new_label
     @end _.new_label]
    (all _.composite
         (bytecode @then)
         (_.getstatic $Boolean "FALSE" $Boolean)
         (_.goto @end)
         (_.set_label @then)
         (_.getstatic $Boolean "TRUE" $Boolean)
         (_.set_label @end)
         )))

... TODO: Get rid of this ASAP
(the lux::syntax_char_case!
  (..custom [(all <>.and
                  ?list.any
                  ?list.any
                  (<>.some (<synthesis>.tuple (all <>.and
                                                   (<synthesis>.tuple (<>.many <synthesis>.i64))
                                                   ?list.any))))
             (function (_ phase archive [inputS elseS conditionalsS])
               (do [! phase.monad]
                 [@end ///runtime.forge_label
                  inputG (phase archive inputS)
                  elseG (phase archive elseS)
                  conditionalsG+ (is (Operation (List [(List [/32.Number Label])
                                                       (Bytecode Any)]))
                                     (list.each' ! (function (_ [chars branch])
                                                     (do !
                                                       [branchG (phase archive branch)
                                                        @branch ///runtime.forge_label]
                                                       (in [(list#each (function (_ char)
                                                                         [(/32.of (.integer char)) @branch])
                                                                       chars)
                                                            (all _.composite
                                                                 (_.set_label @branch)
                                                                 branchG
                                                                 (_.when_continuous (_.goto @end)))])))
                                                 conditionalsS))
                  .let [table (|> conditionalsG+
                                  (list#each product.left)
                                  list#conjoint)
                        conditionalsG (|> conditionalsG+
                                          (list#each product.right)
                                          (list.complete _.monad))]]
                 (in (do _.monad
                       [@else _.new_label]
                       (all _.composite
                            inputG (///value.primitive type.long) _.l2i
                            (_.lookupswitch @else table)
                            conditionalsG
                            (_.set_label @else)
                            elseG
                            (<| (_.when_acknowledged @end)
                                (_.set_label @end))
                            )))))]))

(the (lux::is [referenceG sampleG])
  (Binary (Bytecode Any))
  (all _.composite
       referenceG
       sampleG
       (..predicate _.if_acmpeq)))

(the (lux::try riskyG)
  (Unary (Bytecode Any))
  (all _.composite
       riskyG
       (_.checkcast ///function.class)
       ///runtime.try))

(the with_basic_extensions
  (Change Bundle)
  (|>> (dictionary.has "when_char#|translation" ..lux::syntax_char_case!)
       (dictionary.has "is?#|translation" (binary ..lux::is))
       (dictionary.has "try#|translation" (unary ..lux::try))))

(template.with [<name> <op>]
  [(the (<name> [maskG inputG])
     (Binary (Bytecode Any))
     (all _.composite
          inputG (///value.primitive type.long)
          maskG (///value.primitive type.long)
          <op> (///value.boxed type.long)))]

  [i64::and _.land]
  [i64::or  _.lor]
  [i64::xor _.lxor]
  )

(template.with [<name> <op>]
  [(the (<name> [shiftG inputG])
     (Binary (Bytecode Any))
     (all _.composite
          inputG (///value.primitive type.long)
          shiftG ..jvm_int
          <op> (///value.boxed type.long)))]

  [i64::left_shifted  _.lshl]
  [i64::right_shifted _.lushr]
  )

(template.with [<name> <type> <op>]
  [(the (<name> [paramG subjectG])
     (Binary (Bytecode Any))
     (all _.composite
          subjectG (///value.primitive <type>)
          paramG (///value.primitive <type>)
          <op> (///value.boxed <type>)))]

  [i64::+ type.long   _.ladd]
  [i64::- type.long   _.lsub]
  [i64::* type.long   _.lmul]
  [i64::/ type.long   _.ldiv]
  [i64::% type.long   _.lrem]
  
  [f64::+ type.double _.dadd]
  [f64::- type.double _.dsub]
  [f64::* type.double _.dmul]
  [f64::/ type.double _.ddiv]
  [f64::% type.double _.drem]
  )

(template.with [<eq> <lt> <type> <cmp>]
  [(template.with [<name> <reference>]
     [(the (<name> [paramG subjectG])
        (Binary (Bytecode Any))
        (all _.composite
             subjectG (///value.primitive <type>)
             paramG (///value.primitive <type>)
             <cmp>
             <reference>
             (..predicate _.if_icmpeq)))]
     
     [<eq> _.iconst_0]
     [<lt> _.iconst_m1])]

  [i64::= i64::< type.long   _.lcmp]
  [f64::= f64::< type.double _.dcmpg]
  )

(the (::toString class from)
  (-> (Type Class) (Type Primitive)
      (Bytecode Any))
  (_.invokestatic class "toString" (type.method [(list) (list from) ..$String (list)])))

(template.with [<name> <prepare> <transform>]
  [(the (<name> inputG)
     (Unary (Bytecode Any))
     (all _.composite
          inputG
          <prepare>
          <transform>))]

  [i64::f64
   (///value.primitive type.long)
   (all _.composite
        _.l2d
        (///value.boxed type.double))]

  [i64::char
   (///value.primitive type.long)
   (all _.composite
        _.l2i
        _.i2c
        (..::toString ..$Character type.char))]

  [f64::i64
   (///value.primitive type.double)
   (all _.composite
        _.d2l
        (///value.boxed type.long))]
  )

(the with_i64_extensions
  (Change Bundle)
  (|>> (dictionary.has "i64_and#|translation" (binary ..i64::and))
       (dictionary.has "i64_or#|translation" (binary ..i64::or))
       (dictionary.has "i64_xor#|translation" (binary ..i64::xor))
       (dictionary.has "i64_left#|translation" (binary ..i64::left_shifted))
       (dictionary.has "i64_right#|translation" (binary ..i64::right_shifted))

       (dictionary.has "i64_=#|translation" (binary ..i64::=))
       (dictionary.has "i64_+#|translation" (binary ..i64::+))
       (dictionary.has "i64_-#|translation" (binary ..i64::-))))

(the with_integer_extensions
  (Change Bundle)
  (|>> (dictionary.has "int_<#|translation" (binary ..i64::<))
       (dictionary.has "int_*#|translation" (binary ..i64::*))
       (dictionary.has "int_/#|translation" (binary ..i64::/))
       (dictionary.has "int_%#|translation" (binary ..i64::%))
       
       (dictionary.has "int_f64#|translation" (unary ..i64::f64))
       (dictionary.has "int_char#|translation" (unary ..i64::char))))

(the with_decimal_extensions
  (Change Bundle)
  (|>> (dictionary.has "f64_+#|translation" (binary ..f64::+))
       (dictionary.has "f64_-#|translation" (binary ..f64::-))
       (dictionary.has "f64_*#|translation" (binary ..f64::*))
       (dictionary.has "f64_/#|translation" (binary ..f64::/))
       (dictionary.has "f64_%#|translation" (binary ..f64::%))
       (dictionary.has "f64_=#|translation" (binary ..f64::=))
       (dictionary.has "f64_<#|translation" (binary ..f64::<))
       (dictionary.has "f64_int#|translation" (unary ..f64::i64))))

(the $String::length
  (_.invokevirtual ..$String "length" (type.method [(list) (list) type.int (list)])))

(the (text::size inputG)
  (Unary (Bytecode Any))
  (all _.composite
       inputG
       (_.checkcast $String)
       $String::length
       ..lux_i64))

(the no_op
  (Bytecode Any)
  (_#in []))

(template.with [<name> <pre_subject> <pre_param> <op> <post>]
  [(the (<name> [paramG subjectG])
     (Binary (Bytecode Any))
     (all _.composite
          subjectG <pre_subject>
          paramG <pre_param>
          <op> <post>))]

  [text::= ..no_op ..no_op
   (_.invokevirtual ..$Object "equals" (type.method [(list) (list ..$Object) type.boolean (list)]))
   (///value.boxed type.boolean)]
  [text::< (_.checkcast $String) (_.checkcast $String)
   (_.invokevirtual ..$String "compareTo" (type.method [(list) (list ..$String) type.int (list)]))
   (..predicate _.iflt)]
  [text::char (_.checkcast $String) ..jvm_int
   (_.invokevirtual ..$String "charAt" (type.method [(list) (list type.int) type.char (list)]))
   ..lux_natural]
  )

(the text::composite
  (Variadic (Bytecode Any))
  (let [$StringBuilder (type.class (list) "java.lang.StringBuilder")
        add_part! (is (-> (Bytecode Any)
                          (Bytecode Any))
                      (function (_ it)
                        (all _.composite
                             it
                             (_.checkcast $String)
                             )))
        update_size! (is (Bytecode Any)
                         (all _.composite
                              _.dup
                              $String::length
                              _.dup2_x1
                              _.pop2
                              _.iadd
                              ))
        new_StringBuilder (is (Bytecode Any)
                              (all _.composite
                                   (_.new $StringBuilder)
                                   _.dup_x1
                                   _.swap
                                   (_.invokespecial $StringBuilder "<init>" (type.method [(list) (list type.int) type.void (list)]))
                                   ))
        compose_part! (is (Bytecode Any)
                          (all _.composite
                               _.swap
                               (_.invokevirtual $StringBuilder "append" (type.method [(list) (list ..$String) $StringBuilder (list)]))
                               ))]
    (|>> list.of_stack
         (|.when
           (list)
           (_.string "")

           (list single)
           single

           (list left right)
           (all _.composite
                left (_.checkcast $String)
                right (_.checkcast $String)
                (_.invokevirtual ..$String "concat" (type.method [(list) (list ..$String) ..$String (list)])))

           parts
           (do [! _.monad]
             [_ (_.int (/32.of +0))
              _ (list.each' ! (is (-> (Bytecode Any)
                                      (Bytecode Any))
                                  (function (_ it)
                                    (all _.composite
                                         (add_part! it)
                                         update_size!
                                         )))
                            (list.reversed parts))
              _ new_StringBuilder
              _ (list.each' ! (function.constant compose_part!)
                            parts)]
             (_.invokevirtual $StringBuilder "toString" (type.method [(list) (list) ..$String (list)])))
           ))))

(the (text::clip [offset! length! subject!])
  (Trinary (Bytecode Any))
  (all _.composite
       subject! (_.checkcast $String)
       offset! ..jvm_int
       _.dup
       length! ..jvm_int
       _.iadd
       (_.invokevirtual ..$String "substring" (type.method [(list) (list type.int type.int) ..$String (list)]))))

(the index_method (type.method [(list) (list ..$String type.int) type.int (list)]))
(the (text::index [startG partG textG])
  (Trinary (Bytecode Any))
  (do _.monad
    [@not_found _.new_label
     @end _.new_label]
    (all _.composite
         textG (_.checkcast $String)
         partG (_.checkcast $String)
         startG ..jvm_int
         (_.invokevirtual ..$String "indexOf" index_method)
         _.dup
         _.iconst_m1
         (_.if_icmpeq @not_found)
         ..lux_natural
         ///runtime.some_injection
         (_.goto @end)
         (_.set_label @not_found)
         _.pop
         ///runtime.none_injection
         (_.set_label @end))))

(the with_text_extensions
  (Change Bundle)
  (|>> (dictionary.has "text_=#|translation" (binary ..text::=))
       (dictionary.has "text_<#|translation" (binary ..text::<))
       (dictionary.has "text_composite#|translation" (variadic ..text::composite))
       (dictionary.has "text_index#|translation" (trinary ..text::index))
       (dictionary.has "text_size#|translation" (unary ..text::size))
       (dictionary.has "text_char#|translation" (binary ..text::char))
       (dictionary.has "text_clip#|translation" (trinary ..text::clip))))

(the string_method (type.method [(list) (list ..$String) type.void (list)]))
(the (io::log messageG)
  (Unary (Bytecode Any))
  (all _.composite
       (_.getstatic ..$System "out" ..$PrintStream)
       messageG
       (_.checkcast $String)
       (_.invokevirtual ..$PrintStream "println" ..string_method)
       ///runtime.unit))

(the (io::error messageG)
  (Unary (Bytecode Any))
  (all _.composite
       (_.new ..$Error)
       _.dup
       messageG
       (_.checkcast $String)
       (_.invokespecial ..$Error "<init>" ..string_method)
       _.athrow))

(the with_io_extensions
  (Change Bundle)
  (|>> (dictionary.has "log!#|translation" (unary ..io::log))
       (dictionary.has "error#|translation" (unary ..io::error))))

(the (lux::list elements)
  (Variadic (Bytecode Any))
  (let [elements (list.of_stack elements)]
    (all _.composite
         (_.int (/32.of (.integer (list.size elements))))
         (_.anewarray $Object)
         (|> elements
             list.enumeration
             (list#each (function (_ [where what])
                          (all _.composite
                               _.dup
                               (_.int (/32.of (.integer where)))
                               what
                               _.aastore)))
             (list.complete _.monad)))))

(the (lux::list_size it)
  (Unary (Bytecode Any))
  (all _.composite
       it
       (_.checkcast ///type.list)
       _.arraylength
       ..lux_natural))

(the (lux::list_item [address it])
  (Binary (Bytecode Any))
  (all _.composite
       it (_.checkcast ///type.list)
       address ..jvm_int
       _.aaload))

(the system::arraycopy
  (Bytecode Any)
  (_.invokestatic ..$System "arraycopy"
                  (type.method [(list)
                                (list ///type.value type.int
                                      ///type.value type.int
                                      type.int)
                                type.void
                                (list)])))

(the (lux::list_composite [prefix suffix])
  (Binary (Bytecode Any))
  (let [swap2
        (all _.composite
             ... a b c d
             _.dup2_x2 _.pop2
             ... c d a b
             )

        swap_2,1
        (all _.composite
             ... a b c
             _.dup2_x1
             _.pop2
             ... b c a
             )

        swap_1,2
        (all _.composite
             ... a b c
             _.dup_x2
             _.pop
             ... c a b
             )

        as_list (function (_ it)
                  (all _.composite
                       it
                       (_.checkcast ///type.list)
                       ))

        with_size
        (all _.composite
             ... it
             _.dup
             _.arraylength
             ... it (size it)
             )
        
        empty_composite
        (all _.composite
             ... (size prefix) (size suffix)
             _.iadd (_.anewarray $Object)
             ... composite
             )

        beginning
        (_.int (/32.of +0))

        copy_of_prefix
        (all _.composite
             ... P (# P) P+S
             _.dup2_x1
             ... {(# P) P+S} P (# P) P+S
             _.swap beginning _.dup_x2 _.swap
             ... {(# P) P+S} P 0 P+S 0 (# P)
             system::arraycopy
             ... {(# P) P+S}
             )

        copy_of_suffix
        (all _.composite
             ... S (# S) (# P) P+S
             swap_1,2
             ... S P+S (# S) (# P)
             swap2 _.swap _.dup2_x2 _.pop
             ... {P+S} S (# S) (# P) P+S
             beginning _.swap swap2 _.swap
             ... {P+S} S 0 P+S (# P) (# S)
             system::arraycopy
             ... {P+S}
             )]
    (all _.composite
         (as_list suffix) with_size
         ... [S (# S)]
         _.dup
         (as_list prefix) with_size
         swap_2,1
         ... [S (# S)] [P (# P)] (# S)
         _.swap _.dup_x1 empty_composite
         ... ... [S (# S)] [P (# P)] P+S
         copy_of_prefix
         ... S (# S) (# P) P+S
         copy_of_suffix
         ... P+S
         )))

(the with_list_extensions
  (Change Bundle)
  (|>> (dictionary.has (extension.translation extension/lux.list)
                       (variadic ..lux::list))
       (dictionary.has (extension.translation extension/lux.list_size)
                       (unary ..lux::list_size))
       (dictionary.has (extension.translation extension/lux.list_item)
                       (binary ..lux::list_item))
       (dictionary.has (extension.translation extension/lux.list_composite)
                       (binary ..lux::list_composite))
       ))

(the (array it)
  (Unary (Bytecode Any))
  (all _.composite
       it
       ..jvm_int
       (_.anewarray ///type.value)))

(the array_size
  ..lux::list_size)

(the array_its
  ..lux::list_item)

(the (array_has? [address it])
  (Binary (Bytecode Any))
  (all _.composite
       (array_its [address it])
       (..predicate _.ifnonnull)))

(the (array_has [address value it])
  (Trinary (Bytecode Any))
  (all _.composite
       it
       (_.checkcast ///type.list)
       _.dup
       address ..jvm_int
       value
       _.aastore))

(the (array_lacks [address it])
  (Binary (Bytecode Any))
  (array_has [address _.aconst_null it]))

(the with_array_extensions
  (Change Bundle)
  (|>> (dictionary.has (extension.translation extension/lux.array)
                       (unary ..array))
       (dictionary.has (extension.translation extension/lux.array_size)
                       (unary ..array_size))
       (dictionary.has (extension.translation extension/lux.array_its)
                       (binary ..array_its))
       (dictionary.has (extension.translation extension/lux.array_has?)
                       (binary ..array_has?))
       (dictionary.has (extension.translation extension/lux.array_has)
                       (trinary ..array_has))
       (dictionary.has (extension.translation extension/lux.array_lacks)
                       (binary ..array_lacks))
       ))

(the .public bundle
  Bundle
  (|> //.empty
      with_basic_extensions
      with_io_extensions
      with_text_extensions
      with_i64_extensions
      with_integer_extensions
      with_decimal_extensions
      with_list_extensions
      with_array_extensions
      ))
