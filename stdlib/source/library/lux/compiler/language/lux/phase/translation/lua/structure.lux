... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Tuple Variant
                tuple variant)
   [abstract
    [monad (.only do)]]
   [target
    ["_" lua]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Expression)]
  ["[1][0]" primitive]
  ["///[1]" ////
   ["[0]" phase (.use "[1]#[0]" monad)]
   ["[0]" synthesis]
   [analysis
    [complex (.only Variant Tuple)]]]])

(the .public (tuple phase archive elemsS+)
  (Expression (Tuple synthesis.Term))
  (when elemsS+
    {.#Empty}
    (phase#in (//primitive.text synthesis.unit))

    {.#Top singletonS {.#Empty}}
    (phase archive singletonS)

    _
    (|> elemsS+
        (stack.each' phase.monad (phase archive))
        (phase#each _.array))))

(the .public (variant phase archive [lefts right? valueS])
  (Expression (Variant synthesis.Term))
  (phase#each (//runtime.variant lefts right?)
              (phase archive valueS)))
