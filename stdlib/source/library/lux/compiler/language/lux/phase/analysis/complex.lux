... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Tag
                variant tuple record)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" maybe]
    ["[0]" state]]
   [error
    ["[0]" try]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" bit]
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list]
     ["[0]" stack (.use "[1]#[0]" monad)]
     ["[0]" dictionary (.only Dictionary)]]]
   [math
    [number
     ["n" natural]]]
   [macro
    ["[0]" template]]
   ["[0]" type (.only)
    ["[0]" check]]
   ["[0]" meta (.only)
    ["[0]" module]
    ["[0]" name]
    ["[0]" code]
    ["[0]" label]
    ["[0]" binding (.only)
     ["[0]" local]]]]]
 ["[0]" //
  ["[1][0]" simple]
  [///
   ["[0]" phase (.use "[1]#[0]" monad)]
   ["/" analysis (.only Operation Phase)
    ["[1][0]" complex (.only Tag)]
    ["[1][0]" type]
    ["[1][0]" inference]]
   [///
    [meta
     [archive (.only Archive)]]]]])

(exception.the .public (not_a_quantified_type type)
  (Exception Type)
  (exception.report
   (list ["Type" (type.as_text type)])))

(template.with [<name>]
  [(exception.the .public (<name> [module type members])
     (Exception [Text Type (Stack Code)])
     (exception.report
      (list ["Type" (type.as_text type)]
            ["Expression" (code.relative module (` [(,* members)]))])))]

  [invalid_tuple_type]
  [cannot_analyse_tuple]
  )

(template.with [<name>]
  [(exception.the .public (<name> [module type lefts right? code])
     (Exception [Text Type Natural Bit Code])
     (exception.report
      (list ["Type" (type.as_text type)]
            ["Lefts" (%.natural lefts)]
            ["Right?" (bit.as_text right?)]
            ["Expression" (code.relative module code)])))]

  [invalid_variant_type]
  [cannot_analyse_sum]
  [cannot_infer_sum]
  )

(exception.the .public (cannot_analyse_variant [module type tag code])
  (Exception [Text Type Name Code])
  (exception.report
   (list ["Type" (type.as_text type)]
         ["Tag" (name.as_text tag)]
         ["Expression" (code.relative module code)])))

(exception.the .public (cannot_order_record [module @ code])
  (Exception [Text Code (Stack [Name Code])])
  (exception.report
   (list ["@" (code.as_text @)]
         ["Expression" (|> code
                           (stack#each (function (_ [slot value])
                                         (stack (code.name slot) value)))
                           stack#conjoint
                           code.tuple
                           (code.relative module))])))

(the (case lefts right? cases)
  (-> Natural Bit (Stack Type)
      Type)
  (let [tag (/complex.tag right? lefts)
        expected_size (++ tag)
        actual_size (stack.size cases)]
    (if (and right?
             (n.< actual_size expected_size))
      (type.variant (stack.after tag cases))
      (|> cases
          (stack.item tag)
          (try.else .Nothing)))))

(the .public (sum analyse lefts right? archive)
  (-> Phase Natural Bit Phase)
  (function (again valueC)
    (do [! phase.monad]
      [expectedT meta.expected_type
       expectedT' (/type.check (check.clean (stack) expectedT))
       @ meta.provenance
       [here _] module.current]
      (/.with_exception ..cannot_analyse_sum [here expectedT' lefts right? valueC]
        (when expectedT
          {.#Sum _}
          (|> (analyse archive valueC)
              (by ! each (|>> [lefts right?] (/.variant @)))
              (/type.expecting (..case lefts right? (type.flat_variant expectedT))))

          {.#Named name unnamedT}
          (<| (/type.expecting unnamedT)
              (again valueC))

          {.#Variable id}
          (do !
            [?expectedT' (phase.try (/type.check (check.type id)))]
            (when ?expectedT'
              {try.#Success expectedT'}
              (<| (/type.expecting expectedT')
                  (again valueC))

              ... Cannot do inference when the tag is numeric.
              ... This is because there is no way of knowing how many
              ... cases the inferred sum type would have.
              _
              (/.except ..cannot_infer_sum [here expectedT lefts right? valueC])))

          {.#Universal _}
          (do !
            [[@instance :instance:] (/type.check check.existential)]
            (<| (/type.expecting (maybe.trusted (type.applied (stack :instance:) expectedT)))
                (again valueC)))
          {.#Existential _}
          (<| /type.with_var
              (function (_ [@instance :instance:]))
              (/type.expecting (maybe.trusted (type.applied (stack :instance:) expectedT)))
              (again valueC))
          
          {.#Reification inputT funT}
          (when funT
            {.#Variable funT_id}
            (do !
              [?funT' (phase.try (/type.check (check.type funT_id)))]
              (when ?funT'
                {try.#Success funT'}
                (<| (/type.expecting {.#Reification inputT funT'})
                    (again valueC))

                _
                (/.except ..invalid_variant_type [here expectedT lefts right? valueC])))

            _
            (when (type.applied (stack inputT) funT)
              {.#Some outputT}
              (<| (/type.expecting outputT)
                  (again valueC))

              {.#None}
              (/.except ..not_a_quantified_type [funT])))
          
          _
          (/.except ..invalid_variant_type [here expectedT lefts right? valueC]))))))

(the .public (variant analyse tag archive valueC)
  (-> Phase Name
      Phase)
  (do [! phase.monad]
    [tag (binding.normal tag)
     [lefts,right? variantT] (label.tag tag)
     [lefts right?] (when lefts,right?
                      {.#Some [lefts right? family]}
                      (in [lefts right?])

                      {.#None}
                      (in [0 false]))
     expectedT meta.expected_type]
    (when expectedT
      {.#Variable _}
      (do !
        [inferenceT (/inference.variant lefts right? variantT)
         [inferredT valueA+] (/inference.general archive analyse inferenceT (stack valueC))
         @ meta.provenance]
        (in (/.variant @ [lefts right? (|> valueA+ stack.head try.trusted)])))

      _
      (do !
        [[here _] module.current]
        (/.with_exception ..cannot_analyse_variant [here expectedT tag valueC]
          (..sum analyse lefts right? archive valueC))))))

(the (typed_product analyse expectedT archive members)
  (-> Phase Type Archive (Stack Code)
      (Operation /.Term))
  (<| (do [! phase.monad]
        [@ meta.provenance])
      (by ! each (|>> list.of_stack (/.tuple @)))
      (is (Operation (Stack /.Term)))
      (loop (again [membersT+ (type.flat_tuple expectedT)
                    membersC+ members])
        (when [membersT+ membersC+]
          [{.#Top memberT {.#Empty}} {.#Top memberC {.#Empty}}]
          (<| (by ! each (|>> stack))
              (/type.expecting memberT)
              (analyse archive memberC))
          
          [{.#Top memberT {.#Empty}} _]
          (<| (/type.expecting memberT)
              (by ! each (|>> stack) (analyse archive (code.tuple membersC+))))
          
          [_ {.#Top memberC {.#Empty}}]
          (<| (/type.expecting (type.tuple membersT+))
              (by ! each (|>> stack) (analyse archive memberC)))
          
          [{.#Top memberT membersT+'} {.#Top memberC membersC+'}]
          (do !
            [memberA (<| (/type.expecting memberT)
                         (analyse archive memberC))
             memberA+ (again membersT+' membersC+')]
            (in {.#Top memberA memberA+}))
          
          _
          (do !
            [[here _] module.current]
            (/.except ..cannot_analyse_tuple [here expectedT members]))))))

(the .public (product analyse archive membersC)
  (-> Phase Archive (Stack Code)
      (Operation /.Term))
  (do [! phase.monad]
    [expectedT meta.expected_type
     [here _] module.current]
    (/.with_exception ..cannot_analyse_tuple [here expectedT membersC]
      (when expectedT
        {.#Product _}
        (..typed_product analyse expectedT archive membersC)

        {.#Named name unnamedT}
        (<| (/type.expecting unnamedT)
            (product analyse archive membersC))

        {.#Variable id}
        (do !
          [?expectedT' (phase.try (/type.check (check.type id)))]
          (when ?expectedT'
            {try.#Success expectedT'}
            (<| (/type.expecting expectedT')
                (product analyse archive membersC))

            _
            ... Must infer...
            (do !
              [membersTA (stack.each' ! (|>> (analyse archive) /type.inferring) membersC)
               _ (/type.check (check.check expectedT
                                           (type.tuple (stack#each product.left membersTA))))
               @ meta.provenance]
              (in (/.tuple @ (list.of_stack (stack#each product.right membersTA)))))))

        {.#Universal _}
        (do !
          [[@instance :instance:] (/type.check check.existential)]
          (<| (/type.expecting (maybe.trusted (type.applied (stack :instance:) expectedT)))
              (product analyse archive membersC)))

        {.#Existential _}
        (<| /type.with_var
            (function (_ [@instance :instance:]))
            (/type.expecting (maybe.trusted (type.applied (stack :instance:) expectedT)))
            (product analyse archive membersC))

        {.#Reification inputT funT}
        (when funT
          {.#Variable funT_id}
          (do !
            [?funT' (phase.try (/type.check (check.type funT_id)))]
            (when ?funT'
              {try.#Success funT'}
              (<| (/type.expecting {.#Reification inputT funT'})
                  (product analyse archive membersC))

              _
              (/.except ..invalid_tuple_type [here expectedT membersC])))

          _
          (when (type.applied (stack inputT) funT)
            {.#Some outputT}
            (<| (/type.expecting outputT)
                (product analyse archive membersC))

            {.#None}
            (/.except ..not_a_quantified_type [funT])))
        
        _
        (/.except ..invalid_tuple_type [here expectedT membersC])
        ))))

... There cannot be any ambiguity or improper syntax when analysing
... records, so they must be normalized for further analysis.
... Normalization just means that all the tags get resolved to their
... canonical form (with their corresponding module identified).
(the .public (normal pattern_matching? record)
  (-> Bit (Stack Code)
      (Operation (Maybe (Stack [Name Code]))))
  (loop (again [input record
                output (is (Stack [Name Code])
                           {.#Empty})])
    (when input
      (stack.partial [_ {.#Name ["" slotH]}] valueH tail)
      (if pattern_matching?
        (phase#in {.#None})
        (do phase.monad
          [slotH (binding.normal ["" slotH])]
          (again tail {.#Top [slotH valueH] output})))
      
      (stack.partial [_ {.#Name slotH}] valueH tail)
      (do phase.monad
        [slotH (binding.normal slotH)]
        (again tail {.#Top [slotH valueH] output}))

      {.#Empty}
      (phase#in {.#Some output})

      _
      (phase#in {.#None}))))

(the (local_binding? name)
  (-> Text
      (Meta Bit))
  (by meta.monad each
      (stack.any? (stack.any? (|>> product.left (text.= name))))
      local.all))

(the (slot it)
  (-> Name
      (Meta Label))
  (do meta.monad
    [it (binding.normal it)]
    (label.slot it)))

(the (slot_type [[_ it] _])
  (-> [Label Code]
      Type)
  it)

(the (same_record? it)
  (-> (Stack [Label Code])
      Bit)
  (when it
    (stack.partial head tail)
    (let [expected (slot_type head)]
      (stack.every? (|>> slot_type (type.= expected)) tail))
    
    (stack)
    false))

(the (complete_record? it)
  (-> (Stack [Label Code])
      Bit)
  (loop (again [expected_lefts 0
                remaining it])
    (when remaining
      {.#Top [[{.#Some [lefts .true family]} type] value] {.#Empty}}
      (n.= (-- expected_lefts) lefts)

      {.#Top [[{.#None} type] value] {.#Empty}}
      (n.= 0 expected_lefts)

      {.#Top [[{.#Some [lefts .false family]} type] value] tail}
      (and (n.= expected_lefts lefts)
           (again (++ expected_lefts) tail))

      _
      false)))

(the sorted_record
  (-> (Stack [Label Code])
      (Stack [Label Code]))
  (stack.sorted (function (_ left right)
                  (when [left right]
                    [[[{.#Some [leftsL right?L familyL]} typeL] valueL]
                     [[{.#Some [leftsR right?R familyR]} typeR] valueR]]
                    (if (n.= leftsL leftsR)
                      (not right?R)
                      (n.< leftsL leftsR))

                    _
                    false))))

... Lux already possesses the means to analyse tuples, so
... re-implementing the same functionality for records makes no sense.
... Records, thus, get transformed into tuples by ordering the elements.
(the (order' head_k original_record)
  (-> Name (Stack [Name Code])
      (Operation (Maybe [Natural (Stack Code) Type])))
  (do [! phase.monad]
    [record (<| meta.try
                (stack.each' ! (function (_ [slot value])
                                 (do !
                                   [slot (..slot slot)]
                                   (in [slot value])))
                             original_record))]
    (when record
      {try.#Success record}
      (do !
        [.let [record (sorted_record record)]
         [here _] module.current
         _ (phase.assertion ..cannot_order_record [here (` same_record?) original_record]
                            (same_record? record))
         _ (phase.assertion ..cannot_order_record [here (` complete_record?) original_record]
                            (complete_record? record))]
        (in (<| try.maybe
                (do try.monad
                  [[[_ :record:] _] (stack.head record)]
                  (in [(stack.size record)
                       (stack#each product.right record)
                       :record:])))))

      {try.#Failure error}
      (in {.#None}))))

(the .public (order pattern_matching? record)
  (-> Bit (Stack [Name Code])
      (Operation (Maybe [Natural (Stack Code) Type])))
  (when record
    ... empty_record = empty_tuple = unit/any = []
    {.#Empty}
    (phase#in {.#Some [0 (stack) .Any]})

    {.#Top [head_k head_v] _}
    (when head_k
      ["" head_k']
      (if pattern_matching?
        (phase#in {.#None})
        (do phase.monad
          [local_binding? (..local_binding? head_k')]
          (if local_binding?
            (in {.#None})
            (order' head_k record))))
      
      _
      (order' head_k record))))

(the .public (record analyse archive members)
  (-> Phase Archive (Stack Code)
      (Operation /.Term))
  (when members
    (stack)
    //simple.unit

    (stack singletonC)
    (analyse archive singletonC)

    (stack [_ {.#Name pseudo_slot}] singletonC)
    (do [! phase.monad]
      [head_k (binding.normal pseudo_slot)
       slot (meta.try (label.slot head_k))]
      (when slot
        {try.#Success [lefts,right? recordT]}
        (when lefts,right?
          {.#None}
          (analyse archive singletonC)
          
          _
          (..product analyse archive members))

        _
        (..product analyse archive members)))

    _
    (do [! phase.monad]
      [?members (..normal false members)]
      (when ?members
        {.#None}
        (..product analyse archive members)

        {.#Some slots}
        (do !
          [record_size,membersC,recordT (..order false slots)]
          (when record_size,membersC,recordT
            {.#None}
            (..product analyse archive members)

            {.#Some [record_size membersC recordT]}
            (do !
              [expectedT meta.expected_type]
              (when expectedT
                {.#Variable _}
                (do !
                  [inferenceT (/inference.record record_size recordT)
                   [inferredT membersA] (/inference.general archive analyse inferenceT membersC)
                   @ meta.provenance]
                  (in (/.tuple @ (list.of_stack membersA))))

                _
                (..product analyse archive membersC)))))))))
