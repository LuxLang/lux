... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix)]
     ["[0]" set]]]
   [math
    [number
     [/64
      ["n" natural]]]]
   [target
    ["_" python (.only SVar)]]]]
 ["[0]" //
  [runtime (.only Operation Phase Expression Phase! Statement)]
  ["[1][0]" when]
  ["/[1]" //
   ["[1][0]" reference]
   ["/[1]" //
    [synthesis
     ["[0]" when]]
    ["/[1]" //
     ["[0]" phase]
     ["[0]" synthesis]
     ["[1][0]" translation]
     ["//[1]" ///
      [meta
       ["[0]" cache
        [dependency
         ["[1]" artifact]]]]
      [reference
       [register (.only Register)]
       ["[1][0]" variable]]]]]]])

(the (setup offset bindings body)
  (-> Register (List _.Expression) _.Statement _.Statement)
  (let [variables (|> bindings
                      list.enumeration
                      (list#each (|>> product.left (n.+ offset) //when.register)))]
    (all _.then
         (_.set variables (_.multi bindings))
         body)))

(the .public (set_tail_recursion body!)
  (-> _.Statement _.Statement)
  (_.while (_.bool true)
           body!
           {.#None}))

(the .public (tail_recursion! statement expression archive [start initsS+ bodyS])
  (Statement (synthesis.Tail_Recursion synthesis.Term))
  (when initsS+
    ... function/false/non-independent loop
    (list)
    (statement expression archive bodyS)

    ... true loop
    non_empty
    (monad.let [! phase.monad]
      [initsO+ (list.each' ! (expression archive) initsS+)
       body! (/////translation.with_anchor start
               (statement expression archive bodyS))]
      (pure (<| (..setup start initsO+)
                ..set_tail_recursion
                body!)))))

(the .public (tail_recursion statement expression archive [start initsS+ bodyS])
  (-> Phase!
      (Expression (synthesis.Tail_Recursion synthesis.Term)))
  (when initsS+
    ... function/false/non-independent loop
    (list)
    (expression archive bodyS)

    ... true loop
    non_empty
    (monad.let [! phase.monad]
      [dependencies (cache.dependencies archive bodyS)
       initsO+ (list.each' ! (expression archive) initsS+)
       [[loop_module loop_artifact] body!] (/////translation.with_new_context archive dependencies
                                             (/////translation.with_anchor start
                                               (statement expression archive bodyS)))
       .let [@loop (_.var (///reference.artifact [loop_module loop_artifact]))
             locals (|> initsS+
                        list.enumeration
                        (list#each (|>> product.left (n.+ start) //when.register)))
             actual_loop (<| (_.def @loop locals)
                             ..set_tail_recursion
                             body!)
             [declaration instantiation] (is [_.Statement _.Expression]
                                             (when (|> (synthesis.path/then bodyS)
                                                       //when.dependencies
                                                       (set.of_list _.hash)
                                                       (set.difference (set.of_list _.hash locals))
                                                       set.as_list)
                                               (list)
                                               [actual_loop
                                                @loop]

                                               foreigns
                                               [(_.def @loop foreigns
                                                       (all _.then
                                                            actual_loop
                                                            (_.return @loop)
                                                            ))
                                                (_.of foreigns @loop)]))]
       _ (/////translation.execute! declaration)
       _ (/////translation.save! loop_artifact {.#None} declaration)]
      (pure (_.of initsO+ instantiation)))))

(the .public (tail_iteration! statement expression archive it)
  (Statement (List synthesis.Term))
  (monad.let [! phase.monad]
    [offset /////translation.anchor
     it (list.each' ! (expression archive) it)]
    (pure (..setup offset it
                   _.continue))))
