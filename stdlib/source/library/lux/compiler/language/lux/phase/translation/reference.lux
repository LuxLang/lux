... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except local)
   [data
    [text
     ["%" \\injection]]]
   [macro
    ["[0]" expansion]
    ["[0]" template]]]]
 [////
  ["[0]" phase (.use "[1]#[0]" monad)]
  ["[0]" translation]
  [///
   ["[0]" reference (.only Reference)
    [register (.only Register)]
    ["[0]" variable (.only Variable)]]
   [meta
    [archive (.only Archive)
     ["[0]" unit]]]]])

(the .public (artifact [module artifact])
  (-> unit.ID Text)
  (text "u" (%.natural (.universe#))
        "m" (%.natural module)
        "a" (%.natural artifact)))

(every .public (System expression)
  (Interface
   (is (-> Text
           expression)
       constant')
   (is (-> Text
           expression)
       variable')))

(the .public (constant system archive name)
  (for_any (_ anchor expression declaration)
    (-> (System expression) Archive Name
        (translation.Operation anchor expression declaration expression)))
  (phase#each (|>> ..artifact (by system constant'))
              (translation.remember archive name)))

(template.with [<sigil> <name>]
  [(the .public (<name> system)
     (for_any (_ expression)
       (-> (System expression)
           (-> Register expression)))
     (|>> %.natural (text <sigil>) (by system variable')))]

  ["f" foreign]
  ["l" local]
  )

(the .public (variable system variable)
  (for_any (_ expression)
    (-> (System expression)
        Variable expression))
  (when variable
    {variable.#Local register}
    (..local system register)
    
    {variable.#Foreign register}
    (..foreign system register)))

(the .public (reference system archive reference)
  (for_any (_ anchor expression declaration)
    (-> (System expression) Archive Reference
        (translation.Operation anchor expression declaration expression)))
  (when reference
    {reference.#Constant value}
    (..constant system archive value)
    
    {reference.#Variable value}
    (phase#in (..variable system value))))
