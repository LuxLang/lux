... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except local)
   [data
    [text
     ["%" \\injection]]]
   [macro
    ["[0]" expansion]
    ["[0]" template]]
   [math
    [number
     [/64
      ["n" natural]]]]]]
 [////
  ["[0]" phase (.use "[1]#[0]" monad)]
  ["[0]" translation]
  [///
   [meta
    [archive (.only Archive)
     ["[0]" unit]]]
   ["@[0]" type (.only Register Variable Reference)]]])

(the .public (artifact [module artifact])
  (-> unit.ID Text)
  (text "u" (%.natural (n.% 10 (++ (.universe#))))
        "m" (%.natural module)
        "a" (%.natural artifact)))

(every .public (System expression)
  (Interface
   (is (-> Text
           expression)
       constant')
   (is (-> Text
           expression)
       variable')))

(the .public (constant system archive name)
  (for_any (_ anchor expression declaration)
    (-> (System expression) Archive Name
        (translation.Operation anchor expression declaration expression)))
  (phase#each (|>> ..artifact (by system constant'))
              (translation.remember archive name)))

(template.with [<sigil> <name>]
  [(the .public (<name> system)
     (for_any (_ expression)
       (-> (System expression)
           (-> Register expression)))
     (|>> %.natural (text <sigil>) (by system variable')))]

  [["f" foreign]
   ["l" local]]
  )

(the .public (variable system variable)
  (for_any (_ expression)
    (-> (System expression)
        Variable expression))
  (when variable
    {@type.#Local register}
    (..local system register)
    
    {@type.#Foreign register}
    (..foreign system register)))

(the .public (reference system archive reference)
  (for_any (_ anchor expression declaration)
    (-> (System expression) Archive Reference
        (translation.Operation anchor expression declaration expression)))
  (when reference
    {@type.#Constant_Reference value}
    (..constant system archive value)
    
    {@type.#Variable_Reference value}
    (phase#pure (..variable system value))))
