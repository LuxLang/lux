... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Global
                function)
   [abstract
    ["[0]" monad]]
   [control
    pipe]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]:[0]" functor mix)]]]
   [target
    ["_" php (.only Var Global Argument Label)]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Phase! Expression)]
  ["[1][0]" reference]
  ["[1][0]" when]
  ["/[1]" //
   ["[1][0]" reference]
   ["//[1]" ///
    [analysis (.only Variant Tuple Abstraction Application Analysis)]
    [synthesis (.only Synthesis)]
    ["[1][0]" translation (.only Context)]
    ["//[1]" ///
     [arity (.only Arity)]
     ["[1][0]" phase (.use "[1]:[0]" monad)]
     [reference
      [variable (.only Register Variable)]]]]]])

(the .public (reification expression archive [functionS argsS+])
  (Expression (Application Synthesis))
  (monad.let [! ///////phase.monad]
    [functionG (expression archive functionS)
     argsG+ (list.each' ! (expression archive) argsS+)]
    (pure (_.of' argsG+ functionG))))

(the capture
  (-> Register Var)
  (|>> (///reference.foreign //reference.system) as_expected))

(the input
  (|>> ++ //when.register))

(the (@scope function_name)
  (-> Context Label)
  (_.label (text (///reference.artifact function_name) "_scope")))

(the (with_closure inits @selfG @selfL body!)
  (-> (List _.Expression) Global Var _.Statement
      [_.Statement _.Expression])
  (when inits
    {.:Empty}
    [(all _.then
          (_.set! @selfL (_.closure (list (_.reference @selfL)) (list) body!))
          (_.set! @selfG @selfL))
     @selfG]

    _
    (let [@inits (|> (list.enumeration inits)
                     (list:each (|>> product.left ..capture)))]
      [(_.set! @selfG (_.closure (list) (list:each _.parameter @inits)
                                 (all _.then
                                      (_.set! @selfL (_.closure (list.partial (_.reference @selfL) (list:each _.reference @inits))
                                                                (list)
                                                                body!))
                                      (_.return @selfL))))
       (_.of inits @selfG)])))

(the .public (function statement expression archive [environment arity bodyS])
  (-> Phase!
      (Expression (Abstraction Synthesis)))
  (monad.let [! ///////phase.monad]
    [[function_name body!] (/////translation.with_new_context archive
                             (monad.let !
                               [@scope (by ! each ..@scope
                                           (/////translation.context archive))]
                               (/////translation.with_anchor [1 @scope]
                                 (statement expression archive bodyS))))
     closureG+ (list.each' ! (expression archive) environment)
     .let [@curried (_.var "curried")
           arityG (|> arity .integer _.int)
           @num_args (_.var "num_args")
           @scope (..@scope function_name)
           @selfG (_.global (///reference.artifact function_name))
           @selfL (_.var (///reference.artifact function_name))
           initialize_self! (_.set! (//when.register 0) @selfL)
           initialize! (list:mix (.function (_ post pre!)
                                   (all _.then
                                        pre!
                                        (_.set! (..input post) (_.item (|> post .integer _.int) @curried))))
                                 initialize_self!
                                 (list.domain arity))]
     .let [[definition instantiation] (..with_closure closureG+ @selfG @selfL
                                        (all _.then
                                             (_.set! @num_args (_.func_num_args/0 []))
                                             (_.set! @curried (_.func_get_args/0 []))
                                             (_.cond (list [(|> @num_args (_.=== arityG))
                                                            (all _.then
                                                                 initialize!
                                                                 (_.set_label @scope)
                                                                 body!)]
                                                           [(|> @num_args (_.> arityG))
                                                            (let [arity_inputs (_.array_slice/3 [@curried (_.int +0) arityG])
                                                                  extra_inputs (_.array_slice/2 [@curried arityG])
                                                                  next (_.call_user_func_array/2 [@selfL arity_inputs])]
                                                              (_.return (_.call_user_func_array/2 [next extra_inputs])))])
                                                     ... (|> @num_args (_.< arityG))
                                                     (let [@missing (_.var "missing")]
                                                       (_.return (<| (_.closure (list (_.reference @selfL) (_.reference @curried)) (list))
                                                                     (all _.then
                                                                          (_.set! @missing (_.func_get_args/0 []))
                                                                          (_.return (_.call_user_func_array/2 [@selfL (_.array_merge/+ @curried (list @missing))])))))))
                                             ))]
     _ (/////translation.execute! definition)
     _ (/////translation.save! (product.right function_name) definition)]
    (pure instantiation)))
