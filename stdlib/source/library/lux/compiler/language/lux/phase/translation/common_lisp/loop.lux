... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Scope)
   [abstract
    ["[0]" monad (.only do)]]
   [data
    ["[0]" product]
    [text
     ["%" \\injection]]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor)]]]
   [math
    [number
     ["n" natural]]]
   [target
    ["_" common_lisp]]]]
 ["[0]" //
  [runtime (.only Operation Phase Expression)]
  ["[1][0]" when]
  ["/[1]" //
   ["[1][0]" reference]
   ["/[1]" //
    [synthesis
     ["[0]" when]]
    ["/[1]" //
     ["[0]"synthesis (.only Scope Synthesis)]
     ["[1][0]" translation]
     ["//[1]" ///
      ["[1][0]" phase]
      [meta
       [archive (.only Archive)]]
      [reference
       [variable (.only Register)]]]]]]])

(the .public (scope expression archive [start initsS+ bodyS])
  (Expression (Scope Synthesis))
  (when initsS+
    ... function/false/non-independent loop
    {.#Empty}
    (expression archive bodyS)

    ... true loop
    _
    (do [! ///////phase.monad]
      [@scope (by ! each (|>> %.natural (text "loop_scope") _.tag) /////translation.next)
       @output (by ! each (|>> %.natural (text "loop_output") _.var) /////translation.next)
       initsG+ (monad.each ! (expression archive) initsS+)
       bodyG (/////translation.with_anchor [@scope start]
               (expression archive bodyS))]
      ... TODO: There is a bug in the way the variables are updated. Do a _.multiple_value_setq instead.
      (in (_.let (|> initsG+
                     stack.enumeration
                     (stack#each (function (_ [idx init])
                                   [(|> idx (n.+ start) //when.register)
                                    init]))
                     (stack.partial [@output _.nil]))
            (stack (_.tagbody (stack @scope
                                     (_.setq @output bodyG)))
                   @output))))))

(the .public (again expression archive argsS+)
  (Expression (Stack Synthesis))
  (do [! ///////phase.monad]
    [[tag offset] /////translation.anchor
     argsO+ (monad.each ! (expression archive) argsS+)
     .let [bindings (|> argsO+
                        stack.enumeration
                        (stack#each (|>> product.left (n.+ offset) //when.register))
                        _.args)]]
    (in (_.progn (stack (_.multiple_value_setq bindings (_.values/* argsO+))
                        (_.go tag))))))
