... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except with)
   [abstract
    ["?" projection]
    ["[0]" monad]]
   ["[0]" function]
   [data
    [collection
     ["[0]" list (.only)
      ["?[1]" \\projection]]
     ["[0]" dictionary]]]
   [macro
    ["[0]" template]]
   [target
    ["_" ruby (.only Var Expression)]]]]
 ["[0]" //
  [common (.only custom with)]
  [////
   ["[1]" extension]
   [translation
    [extension (.only Nullary Unary Binary Trinary
                      nullary unary binary trinary)]
    [ruby
     ["[0]" runtime (.only Operation Phase Handler Bundle
                           with_vars)]]]
   [//
    ["[0]" phase]
    ["[0]" synthesis
     ["?[1]" \\projection]]
    [///
     ["[0]" extension (.only)
      ["[1]/[0]" ruby]]]]]])

(the object:get
  Handler
  (custom
   [(all ?.and ?synthesis.text ?list.any)
    (function (_ phase archive [fieldS objectS])
      (monad.let phase.monad
        [objectG (phase archive objectS)]
        (pure (_.its fieldS objectG))))]))

(the object:do
  Handler
  (custom
   [(all ?.and
         ?synthesis.text
         ?list.any
         (?.some ?list.any))
    (function (_ phase archive [methodS objectS inputsS])
      (monad.let [! phase.monad]
        [objectG (phase archive objectS)
         inputsG (list.each' ! (phase archive) inputsS)]
        (pure (_.do methodS inputsG {.:None} objectG))))]))

(template.with [<!> <?> <unit>]
  [(the <!> (Nullary Expression) (function.constant <unit>))
   (the <?> (Unary Expression) (_.= <unit>))]

  [[object:nil object:nil? _.nil]]
  )

(the with_object
  (-> Bundle Bundle)
  (|>> (with extension/ruby.object_get object:get)
       (with extension/ruby.object_do object:do)
       (with extension/ruby.object_nil (nullary object:nil))
       (with extension/ruby.object_nil? (unary object:nil?))
       ))

(the ruby:constant
  (custom
   [?synthesis.text
    (function (_ phase archive name)
      (by phase.monad pure (is _.CVar (_.manual name))))]))

(the ruby:of
  (custom
   [(all ?.and
         ?list.any
         (?.some ?list.any))
    (function (_ phase archive [abstractionS inputsS])
      (monad.let [! phase.monad]
        [abstractionG (phase archive abstractionS)
         inputsG (list.each' ! (phase archive) inputsS)]
        (pure (_.of inputsG {.:None} abstractionG))))]))

(the ruby:import
  (custom
   [?synthesis.text
    (function (_ phase archive module)
      (by phase.monad pure
          (_.require/1 (_.string module))))]))

(the .public bundle
  Bundle
  (|> //.empty
      with_object

      (with extension/ruby.constant ruby:constant)
      (with extension/ruby.of ruby:of)
      (with extension/ruby.import ruby:import)
      ))
