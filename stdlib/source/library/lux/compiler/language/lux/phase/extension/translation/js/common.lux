... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except list)
   [abstract
    ["<>" projection]
    ["[0]" monad]]
   [error
    ["[0]" try]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix)
      ["?[1]" \\projection]]
     ["[0]" dictionary]]]
   [macro
    ["[0]" template]]
   [target
    ["_" js (.only Literal)]]
   [compiler
    [meta
     [archive (.only Archive)]]
    ["[0]" extension (.only)
     ["[1]/[0]" lux]]]]]
 [/////
  ["//" extension]
  [translation
   [extension (.only Nullary Unary Binary Trinary Variadic
                     nullary unary binary trinary variadic)]
   ["//" js
    ["[0]" runtime (.only Operation Phase Phase! Handler Bundle)]
    ["[0]" when]
    ["[0]" loop]
    ["[0]" function]]]
  [//
   ["[0]" phase (.use "[1]#[0]" monad)]
   ["[0]" synthesis (.only)
    ["?[1]" \\projection (.only Projection)]]]])

(the .public (custom [projection handler])
  (for_any (_ s)
    (-> [(Projection s)
         (-> Phase Archive s (Operation _.Expression))]
        Handler))
  (function (_ phase archive input)
    (when (?list.value input projection)
      {try.#Success input'}
      (handler phase archive input')

      {try.#Failure error}
      (phase.failure error))))

... [Procedures]
... [[Bits]]
(template.with [<name> <op>]
  [(the (<name> [paramG subjectG])
     (Binary _.Expression)
     (<op> subjectG (runtime.i64::number paramG)))]

  [[i64::<< runtime.i64::<<]
   [i64::>> runtime.i64::>>]])

... [[Numbers]]
(the (i64::char it)
  (Unary _.Expression)
  (_.of (.list (runtime.i64::number it))
        (_.var "String.fromCharCode")))

... [[Text]]
(the (text//composite it)
  (Variadic _.Expression)
  (when it
    (.list)
    (_.string "")

    (.list single)
    single

    (.list left right)
    (|> left (_.+ right))

    rights
    (|> (text//composite (.list))
        (_.do "concat" rights))))

(the (text//clip [startG endG subjectG])
  (Trinary _.Expression)
  (runtime.text//clip startG endG subjectG))

(the (text//index [startG partG subjectG])
  (Trinary _.Expression)
  (runtime.text//index startG partG subjectG))

... [[IO]]
(the (io//log messageG)
  (Unary _.Expression)
  (all _.,
       (runtime.io//log messageG)
       runtime.unit))

(the .public (statement expression archive synthesis)
  Phase!
  (`` (when synthesis
        ... TODO: Get rid of this ASAP
        [@ {synthesis.#Extension [.prelude "when_char#|translation"] parameters}]
        (monad.let phase.monad
          [body (expression archive synthesis)]
          (pure (as (-> _.Expression _.Statement)
                    body)))

        (,, (template.with [<tag>]
              [(<tag> @ value)
               (phase#each _.return
                           (expression archive synthesis))]

              [[synthesis.bit]
               [synthesis.i64]
               [synthesis.f64]
               [synthesis.text]
               [synthesis.variant]
               [synthesis.tuple]
               [synthesis.reification]]
              ))

        (,, (template.with [<tag>]
              [[@ {<tag> value}]
               (phase#each _.return
                           (expression archive synthesis))]

              [[synthesis.#Reference]
               [synthesis.#Extension]]
              ))

        (synthesis.let @ register input body)
        (when.let! statement expression archive [register input body])

        (synthesis.exec @ before after)
        (when.exec! statement expression archive [before after])

        (synthesis.when @ it)
        (<| (,, (template.with [,synthesis]
                  [(when (,synthesis it)
                     {.#Some it}
                     (phase#each _.return
                                 (expression archive synthesis))

                     else)]

                  [[synthesis.its]]
                  ))
            (,, (template.with [,synthesis ,translation]
                  [(when (,synthesis it)
                     {.#Some it}
                     (,translation statement expression archive it)

                     else)]

                  [[synthesis.if when.if!]
                   [synthesis.when_i64 when.when_i64!]
                   [synthesis.when_f64 when.when_f64!]
                   [synthesis.when_text when.when_text!]
                   [synthesis.multi_let when.multi_let!]]
                  ))
            (when.when! statement expression archive it))

        (synthesis.tail_recursion @ tail_recursion)
        (loop.tail_recursion! statement expression archive tail_recursion)

        (synthesis.tail_iteration @ updates)
        (loop.tail_iteration! statement expression archive updates)

        (synthesis.abstraction @ abstraction)
        (phase#each _.return (function.function statement expression archive abstraction))
        )))

... TODO: Get rid of this ASAP
(the lux::syntax_char_case!
  (..custom [(all <>.and
                  ?list.any
                  ?list.any
                  (<>.some (?synthesis.tuple (all <>.and
                                                  (?synthesis.tuple (<>.many ?synthesis.i64))
                                                  ?list.any))))
             (function (_ phase archive [input else conditionals])
               (monad.let [! phase.monad]
                 [inputG (phase archive input)
                  else! (..statement phase archive else)
                  conditionals! (is (Operation (List [(List Literal)
                                                      _.Statement]))
                                    (list.each' ! (function (_ [chars branch])
                                                    (monad.let !
                                                      [branch! (..statement phase archive branch)]
                                                      (pure [(list#each (|>> .integer _.int)
                                                                        chars)
                                                             branch!])))
                                                conditionals))]
                 ... (pure (|> (_.switch (_.its runtime.i64_low_field inputG)
                 ...                   conditionals!
                 ...                   {.#Some (_.return else!)})
                 ...         (_.closure (list))
                 ...         (_.of (list))))
                 (pure (<| (as (-> _.Statement
                                   _.Expression))
                           (_.switch (_.its runtime.i64_low_field inputG)
                                     conditionals!
                                     {.#Some else!})))))]))

... [Bundles]
(the with_basic
  (-> Bundle Bundle)
  (|>> (dictionary.has "when_char#|translation" lux::syntax_char_case!)
       (dictionary.has "is?#|translation" (binary (product.uncurried _.=)))
       (dictionary.has "try#|translation" (unary runtime.lux//try))))

(the with_i64
  (-> Bundle Bundle)
  (|>> (dictionary.has "i64_and#|translation" (binary (product.uncurried runtime.i64::and)))
       (dictionary.has "i64_or#|translation" (binary (product.uncurried runtime.i64::or)))
       (dictionary.has "i64_xor#|translation" (binary (product.uncurried runtime.i64::xor)))
       (dictionary.has "i64_left#|translation" (binary i64::<<))
       (dictionary.has "i64_right#|translation" (binary i64::>>))
       (dictionary.has "i64_=#|translation" (binary (product.uncurried runtime.i64::=)))
       (dictionary.has "i64_+#|translation" (binary (product.uncurried runtime.i64::+)))
       (dictionary.has "i64_-#|translation" (binary (product.uncurried runtime.i64::-)))
       ))

(the with_integer
  (-> Bundle Bundle)
  (|>> (dictionary.has "int_<#|translation" (binary (product.uncurried runtime.i64::<)))
       
       (dictionary.has "int_x#|translation" (binary (product.uncurried runtime.i64::x)))
       (dictionary.has "int_/#|translation" (binary (product.uncurried runtime.i64::/)))
       (dictionary.has "int_%#|translation" (binary (product.uncurried runtime.i64::%)))
       
       (dictionary.has "int_f64#|translation" (unary runtime.i64::number))
       (dictionary.has "int_char#|translation" (unary i64::char))
       ))

(the with_f64
  (-> Bundle Bundle)
  (|>> (dictionary.has "f64_+#|translation" (binary (product.uncurried _.+)))
       (dictionary.has "f64_-#|translation" (binary (product.uncurried _.-)))
       (dictionary.has "f64_x#|translation" (binary (product.uncurried _.*)))
       (dictionary.has "f64_/#|translation" (binary (product.uncurried _./)))
       (dictionary.has "f64_%#|translation" (binary (product.uncurried _.%)))

       (dictionary.has "f64_=#|translation" (binary (product.uncurried _.=)))
       (dictionary.has "f64_<#|translation" (binary (product.uncurried _.<)))
       
       (dictionary.has "f64_int#|translation" (unary runtime.i64::of_number))
       ))

(the with_text
  (-> Bundle Bundle)
  (|>> (dictionary.has "text_=#|translation" (binary (product.uncurried _.=)))
       (dictionary.has "text_<#|translation" (binary (product.uncurried _.<)))
       
       (dictionary.has "text_composite#|translation" (variadic text//composite))
       (dictionary.has "text_index#|translation" (trinary text//index))
       (dictionary.has "text_size#|translation" (unary (|>> (_.its "length") runtime.i64::of_number)))
       (dictionary.has "text_char#|translation" (binary (product.uncurried runtime.text//char)))
       (dictionary.has "text_clip#|translation" (trinary text//clip))
       ))

(the with_io
  (-> Bundle Bundle)
  (|>> (dictionary.has "log!#|translation" (unary io//log))
       (dictionary.has "error#|translation" (unary runtime.io//error))))

(the list
  (Variadic _.Expression)
  _.array)

(the list_size
  (Unary _.Expression)
  (|>> (_.its "length")
       runtime.i64::of_number))

(the (list_item [address it])
  (Binary _.Expression)
  (_.at (_.its runtime.i64_low_field address)
        it))

(the (list_composite [left right])
  (Binary _.Expression)
  (for .js
       (|> left
           (_.do "concat" (.list right)))
       
       ... else
       (let [as_array (is (Change _.Expression)
                          (function (as_array it)
                            (_.do "slice" (.list) it)))
             as_array (is (Change _.Expression)
                          (for .js
                               (|>>)
                               
                               ... else
                               as_array))]
         (_.of (.list (_.array (.list)) (as_array left) (as_array right))
               (let [it (_.var "it")
                     left (_.var "left")
                     right (_.var "right")
                     item (_.var "item")]
                 (_.closure (.list it left right)
                            (all _.then
                                 (_.for item (_.int +0)
                                        (_.< (_.its "length" left)
                                             item)
                                        (_.++ item)
                                        (_.; (_.do "push" (.list (_.at item left)) it)))
                                 (_.for item (_.int +0)
                                        (_.< (_.its "length" right)
                                             item)
                                        (_.++ item)
                                        (_.; (_.do "push" (.list (_.at item right)) it)))
                                 (_.return it)
                                 )))))))

(the with_list
  (Change Bundle)
  (|>> (dictionary.has (extension.translation extension/lux.list)
                       (variadic ..list))
       (dictionary.has (extension.translation extension/lux.list_size)
                       (unary ..list_size))
       (dictionary.has (extension.translation extension/lux.list_item)
                       (binary ..list_item))
       (dictionary.has (extension.translation extension/lux.list_composite)
                       (binary ..list_composite))
       ))

(the array
  (Unary _.Expression)
  (|>> (_.its runtime.i64_low_field)
       .list
       (_.new (_.var "Array"))))

(the array_size
  (Unary _.Expression)
  ..list_size)

(the array_its
  (Binary _.Expression)
  ..list_item)

(the (array_has [address value it])
  (Trinary _.Expression)
  (runtime.array//write address value it))

(the (array_lacks [address it])
  (Binary _.Expression)
  (runtime.array//delete address it))

(the (array_lacks? [address it])
  (Binary _.Expression)
  (|> (array_its [address it])
      (_.= _.undefined)))

(the with_array
  (Change Bundle)
  (|>> (dictionary.has (extension.translation extension/lux.array)
                       (unary ..array))
       (dictionary.has (extension.translation extension/lux.array_size)
                       (unary ..array_size))
       (dictionary.has (extension.translation extension/lux.array_its)
                       (binary ..array_its))
       (dictionary.has (extension.translation extension/lux.array_has)
                       (trinary ..array_has))
       (dictionary.has (extension.translation extension/lux.array_lacks)
                       (binary ..array_lacks))
       (dictionary.has (extension.translation extension/lux.array_lacks?)
                       (binary ..array_lacks?))
       ))

(the .public bundle
  Bundle
  (|> //.empty
      with_basic
      with_i64
      with_integer
      with_f64
      with_text
      with_io
      with_list
      with_array
      ))
