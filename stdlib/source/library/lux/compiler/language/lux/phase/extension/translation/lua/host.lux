... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["<>" projection (.use "[1]#[0]" functor)]]
   ["[0]" function]
   [data
    [collection
     ["[0]" dictionary]
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [macro
    ["[0]" template]]
   [target
    ["_" lua (.only Var Expression)]]]]
 ["[0]" //
  ["[1][0]" common (.only custom)]
  ["///[1]" ////
   ["[0]" extension]
   [translation
    [extension (.only Nullary Unary Binary Trinary
                      nullary unary binary trinary)]
    ["[0]" reference]
    ["//" lua
     ["[1][0]" runtime (.only Operation Phase Handler Bundle
                              with_vars)]]]
   ["/[1]" //
    ["[0]" phase]
    ["[0]" translation]
    [synthesis
     ["<s>" \\projection]]]]])

(the array::new
  (Unary Expression)
  (|>> ["n"] list _.table))

(the array::length
  (Unary Expression)
  (_.its "n"))

(the (array::read [indexG arrayG])
  (Binary Expression)
  (_.item (_.+ (_.int +1) indexG) arrayG))

(the (array::write [indexG valueG arrayG])
  (Trinary Expression)
  (//runtime.array//write indexG valueG arrayG))

(the (array::delete [indexG arrayG])
  (Binary Expression)
  (//runtime.array//write indexG _.nil arrayG))

(the with_array_extensions
  (-> Bundle Bundle)
  (|>> (dictionary.has "lua_array_new#|translation" (unary array::new))
       (dictionary.has "lua_array_length#|translation" (unary array::length))
       (dictionary.has "lua_array_read#|translation" (binary array::read))
       (dictionary.has "lua_array_write#|translation" (trinary array::write))
       (dictionary.has "lua_array_delete#|translation" (binary array::delete))
       ))

(the object::get
  Handler
  (custom
   [(all <>.and <s>.text <s>.any)
    (function (_ phase archive [fieldS objectS])
      (do phase.monad
        [objectG (phase archive objectS)]
        (in (_.its fieldS objectG))))]))

(the object::do
  Handler
  (custom
   [(all <>.and <s>.text <s>.any (<>.some <s>.any))
    (function (_ phase archive [methodS objectS inputsS])
      (do [! phase.monad]
        [objectG (phase archive objectS)
         inputsG (list.each' ! (phase archive) inputsS)]
        (in (_.do methodS inputsG objectG))))]))

(template.with [<!> <?> <unit>]
  [(the <!> (Nullary Expression) (function.constant <unit>))
   (the <?> (Unary Expression) (_.= <unit>))]

  [object::nil object::nil? _.nil]
  )

(the with_object_extensions
  (-> Bundle Bundle)
  (|>> (dictionary.has "lua_object_get#|translation" object::get)
       (dictionary.has "lua_object_do#|translation" object::do)
       (dictionary.has "lua_object_nil#|translation" (nullary object::nil))
       (dictionary.has "lua_object_nil?#|translation" (unary object::nil?))
       ))

(the $input
  (_.var "input"))

(the utf8::encode
  (custom
   [<s>.any
    (function (_ phase archive inputS)
      (do [! phase.monad]
        [inputG (phase archive inputS)]
        (in (<| (_.apply (list inputG))
                (_.closure (list $input))
                (_.return (_.apply (list (_.apply (list $input (_.int +1) (_.length $input))
                                                  (_.var "string.byte")))
                                   (_.var "table.pack")))))))]))

(the utf8::decode
  (custom
   [<s>.any
    (function (_ phase archive inputS)
      (do [! phase.monad]
        [inputG (phase archive inputS)]
        (in (_.apply (list (_.apply (list inputG)
                                    (_.var "table.unpack")))
                     (_.var "string.char")))))]))

(the with_utf8_extensions
  (-> Bundle Bundle)
  (|>> (dictionary.has "lua_utf8_injection#|translation" utf8::encode)
       (dictionary.has "lua_utf8_projection#|translation" utf8::decode)
       ))

(the lua::constant
  (custom
   [<s>.text
    (function (_ phase archive name)
      (by phase.monad in (_.var name)))]))

(the lua::apply
  (custom
   [(all <>.and <s>.any (<>.some <s>.any))
    (function (_ phase archive [abstractionS inputsS])
      (do [! phase.monad]
        [abstractionG (phase archive abstractionS)
         inputsG (list.each' ! (phase archive) inputsS)]
        (in (_.apply inputsG abstractionG))))]))

(the lua::power
  (custom
   [(all <>.and <s>.any <s>.any)
    (function (_ phase archive [powerS baseS])
      (do [! phase.monad]
        [powerG (phase archive powerS)
         baseG (phase archive baseS)]
        (in (_.^ powerG baseG))))]))

(the lua::import
  (custom
   [<s>.text
    (function (_ phase archive module)
      (by phase.monad in
          (_.require/1 (_.string module))))]))

(the lua::function
  (custom
   [(all <>.and <s>.i64 <s>.any)
    (function (_ phase archive [arity abstractionS])
      (do [! phase.monad]
        [abstractionG (phase archive abstractionS)
         .let [variable (is (-> Text (Operation Var))
                            (|>> translation.name
                                 (by ! each _.var)))]
         'inputs (list.each' ! (function (_ _)
                                 (variable "input"))
                             (list.repeated (.natural arity) []))]
        (in (<| (_.closure 'inputs)
                _.return
                (when (.natural arity)
                  0 (_.apply (list //runtime.unit) abstractionG)
                  1 (_.apply 'inputs abstractionG)
                  _ (_.apply (list (_.array 'inputs)) abstractionG))))))]))

(the .public bundle
  Bundle
  (|> extension.empty
      with_array_extensions
      with_object_extensions
      with_utf8_extensions

      (dictionary.has "lua_constant#|translation" lua::constant)
      (dictionary.has "lua_apply#|translation" lua::apply)
      (dictionary.has "lua_power#|translation" lua::power)
      (dictionary.has "lua_import#|translation" lua::import)
      (dictionary.has "lua_function#|translation" lua::function)
      (dictionary.has "lua_script_universe#|translation" (nullary (function.constant (_.boolean reference.universe))))
      ))
