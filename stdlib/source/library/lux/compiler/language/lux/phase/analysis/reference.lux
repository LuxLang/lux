... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   ["[0]" meta (.only)
    ["[0]" code]
    ["[0]" module]]]]
 [////
  ["[0]" phase]
  ["/" analysis (.only Operation Phase Extender)
   ["[0]" scope]]
  [///
   [meta
    [archive (.only Archive)]]]])

(the .public (reference extender analysis archive quoted_module it)
  (-> Extender Phase Archive Text Name
      (Operation /.Term))
  (when it
    ["" proper]
    (monad.let [! phase.monad]
      [?var (scope.variable proper)]
      (when ?var
        {.:Some _}
        (/.delegated extender analysis archive (name .local#)
                     (list (code.name it)))

        {.:None}
        (monad.let !
          [this_module phase.name_of_current_module]
          (/.delegated extender analysis archive (name .global#)
                       (list (code.text quoted_module)
                             (code.name [this_module proper]))))))

    _
    (/.delegated extender analysis archive (name .global#)
                 (list (code.text quoted_module)
                       (code.name it)))))
