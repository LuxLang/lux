... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Variant Tuple
                variant tuple)
   [abstract
    [monad (.only do)]]
   [data
    [collection
     ["[0]" list]]]
   [target
    ["_" js]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Expression)]
  ["[1][0]" primitive]
  ["///[1]" ////
   ["[0]" phase (.use "[1]#[0]" monad)]
   ["[0]" synthesis]
   [analysis
    [complex (.only Variant Tuple)]]]])

(the .public (tuple translate archive elemsS+)
  (Expression (Tuple synthesis.Term))
  (when elemsS+
    (list)
    (phase#pure //runtime.unit)

    (list singletonS)
    (translate archive singletonS)

    _
    (|> elemsS+
        (list.each' phase.monad (translate archive))
        (phase#each _.array))))

(the .public (variant translate archive [lefts right? valueS])
  (Expression (Variant synthesis.Term))
  (phase#each (//runtime.variant (_.i32 (.integer lefts))
                                 (//runtime.flag right?))
              (translate archive valueS)))
