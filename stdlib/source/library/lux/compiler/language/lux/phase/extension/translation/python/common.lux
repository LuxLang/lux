... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except list with)
   [abstract
    ["<>" projection]
    ["[0]" monad]]
   [error
    ["[0]" try]]
   ["[0]" function]
   [data
    ["[0]" product]
    [collection
     ["[0]" dictionary]
     ["[0]" list (.use "[1]:[0]" functor mix)
      ["?[1]" \\projection]]]]
   [macro
    ["[0]" template]]
   [target
    ["_" python]]
   [compiler
    [meta
     [archive (.only Archive)]]
    ["[0]" extension (.only)
     ["[1]/[0]" lux]]]
   [meta
    ["[0]" static]]]]
 [/////
  ["//" extension]
  [translation
   ["[0]" reference]
   [extension (.only Nullary Unary Binary Trinary Variadic
                     nullary unary binary trinary variadic)]
   ["//" python
    ["[1][0]" runtime (.only Operation Phase Phase! Handler Bundle)]
    ["[1][0]" primitive]
    ["[1][0]" structure]
    ["[1][0]" reference]
    ["[1][0]" function]
    ["[1][0]" when]
    ["[1][0]" loop]]]
  [//
   [analysis (.only)]
   ["[0]" phase (.use "[1]:[0]" monad)]
   ["[0]" translation]
   ["[0]" synthesis (.only)
    ["?[1]" \\projection (.only Projection)]]]])

(the .public (custom [projection handler])
  (for_any (_ s)
    (-> [(Projection s)
         (-> Phase Archive s (Operation _.Expression))]
        Handler))
  (function (_ phase archive input)
    (when (?list.value input projection)
      {try.:Success input'}
      (handler phase archive input')

      {try.:Failure error}
      (phase.failure error))))

(the .public (statement expression archive synthesis)
  Phase!
  (`` (when synthesis
        ... TODO: Get rid of this ASAP
        [@ {synthesis.:Extension [.prelude (,, (static.text (extension.synthesis extension/lux.when_char)))] parameters}]
        (monad.let phase.monad
          [body (expression archive synthesis)]
          (pure (as (-> _.Expression _.Statement) body)))
        
        (,, (template.with [<tag>]
              [(<tag> @ value)
               (phase:each _.return (expression archive synthesis))]

              [[synthesis.bit]
               [synthesis.i64]
               [synthesis.f64]
               [synthesis.text]
               [synthesis.variant]
               [synthesis.tuple]
               [synthesis.reification]]
              ))

        (,, (template.with [<tag>]
              [[@ {<tag> value}]
               (phase:each _.return (expression archive synthesis))]

              [[synthesis.:Reference]
               [synthesis.:Extension]]
              ))

        (synthesis.let @ register input body)
        (//when.let! statement expression archive [register input body])

        (synthesis.exec @ before after)
        (//when.exec! statement expression archive [before after])

        (synthesis.when @ it)
        (<| (,, (template.with [,synthesis]
                  [(when (,synthesis it)
                     {.:Some it}
                     (phase:each _.return (expression archive synthesis))

                     else)]

                  [[synthesis.its]]
                  ))
            (,, (template.with [,synthesis ,translation]
                  [(when (,synthesis it)
                     {.:Some it}
                     (,translation statement expression archive it)

                     else)]

                  [[synthesis.variant_if //when.variant_if!]
                   [synthesis.if //when.if!]
                   [synthesis.when_i64 //when.when_i64!]
                   [synthesis.when_f64 //when.when_f64!]
                   [synthesis.when_text //when.when_text!]
                   [synthesis.multi_let //when.multi_let!]]
                  ))
            (//when.when! false statement expression archive it))

        (,, (template.with [<tag> <translator>]
              [(<tag> @ value)
               (<translator> statement expression archive value)]

              [[synthesis.tail_recursion //loop.tail_recursion!]
               [synthesis.tail_iteration //loop.tail_iteration!]]
              ))

        (synthesis.abstraction @ abstraction)
        (phase:each _.return (//function.function statement expression archive abstraction))
        )))

... TODO: Get rid of this ASAP
(the lux:syntax_char_case!
  (..custom [(all <>.and
                  ?list.any
                  ?list.any
                  (<>.some (?synthesis.tuple (all <>.and
                                                  (?synthesis.tuple (<>.many ?synthesis.i64))
                                                  ?list.any))))
             (function (_ phase archive [input else conditionals])
               (monad.let [! phase.monad]
                 [inputG (phase archive input)
                  else! (..statement phase archive else)
                  @input (by ! each _.var (translation.name "input"))
                  conditionals! (is (Operation (List [_.Expression
                                                      _.Statement]))
                                    (list.each' ! (function (_ [chars branch])
                                                    (monad.let !
                                                      [branch! (..statement phase archive branch)]
                                                      (pure [(|> chars
                                                                 (list:each (|>> .integer _.int (_.= @input)))
                                                                 (list:mix (function (_ clause total)
                                                                             (if (identical? _.none total)
                                                                                 clause
                                                                                 (_.or clause total)))
                                                                           _.none))
                                                             branch!])))
                                                conditionals))
                  ... .let [dependencies (//case.dependencies (list:mix (function (_ right left)
                  ...                                                      (synthesis.path/seq left right))
                  ...                                                    (synthesis.path/then input)
                  ...                                                    {.:Top (synthesis.path/then else)
                  ...                                                     (list:each (|>> product.right
                  ...                                                                     synthesis.path/then)
                  ...                                                                conditionals)}))
                  ...       @closure (_.var (reference.artifact artifact_id))
                  ...       closure (_.def @closure dependencies
                  ...                      (all _.then
                  ...                          (_.set (.list @input) inputG)
                  ...                          (list:mix (function (_ [test then!] else!)
                  ...                                       (_.if test then! else!))
                  ...                                     else!
                  ...                                     conditionals!)))]
                  ... _ (translation.execute! closure)
                  ... _ (translation.save! (product.right artifact_id) {.:None} closure)
                  ]
                 ... (pure (_.of @closure dependencies))
                 (pure (<| (as (-> _.Statement _.Expression))
                           (all _.then
                                (_.set (.list @input) inputG)
                                (list:mix (function (_ [test then!] else!)
                                            (_.if test then! else!))
                                          else!
                                          conditionals!))))))]))

(the .public (with name anonymous)
  (-> extension.Name Handler
      (Change Bundle))
  (dictionary.has (extension.synthesis name) anonymous))

(the with_basic
  (Change Bundle)
  (|>> (with extension/lux.when_char lux:syntax_char_case!)
       (with extension/lux.is? (binary (function.uncurried _.is)))
       (with extension/lux.try (unary //runtime.lux:try))))

(the (capped operation parameter subject)
  (-> (-> _.Expression _.Expression _.Expression)
      (-> _.Expression _.Expression _.Expression))
  (//runtime.i64:64 (operation parameter subject)))

(the with_i64
  (Change Bundle)
  (|>> (with extension/lux.i64_and (binary (function.uncurried //runtime.i64:and)))
       (with extension/lux.i64_or (binary (function.uncurried //runtime.i64:or)))
       (with extension/lux.i64_xor (binary (function.uncurried //runtime.i64:xor)))
       (with extension/lux.i64_left (binary (function.uncurried //runtime.i64:<<)))
       (with extension/lux.i64_right (binary (function.uncurried //runtime.i64:>>)))

       (with extension/lux.i64_= (binary (function.uncurried _.=)))
       (with extension/lux.i64_+ (binary (function.uncurried (..capped _.+))))
       (with extension/lux.i64_- (binary (function.uncurried (..capped _.-))))
       ))

(the with_integer
  (Change Bundle)
  (|>> (with extension/lux.int_< (binary (function.uncurried _.<)))
       (with extension/lux.int_x (binary (function.uncurried (..capped _.*))))
       (with extension/lux.int_/ (binary (function.uncurried //runtime.i64:/)))
       (with extension/lux.int_% (binary (function.uncurried //runtime.i64:remainder)))
       
       (with extension/lux.int_f64 (unary _.float/1))
       (with extension/lux.int_char (unary //runtime.i64:char))
       ))

(the with_decimal
  (Change Bundle)
  (|>> (with extension/lux.f64_+ (binary (function.uncurried _.+)))
       (with extension/lux.f64_- (binary (function.uncurried _.-)))
       (with extension/lux.f64_x (binary (function.uncurried _.*)))
       (with extension/lux.f64_/ (binary (function.uncurried //runtime.f64:/)))
       (with extension/lux.f64_% (binary (function (_ [parameter subject])
                                           (|> (_.__import__/1 (_.unicode "math"))
                                               (_.do "fmod" (.list subject parameter))))))
       (with extension/lux.f64_= (binary (function.uncurried _.=)))
       (with extension/lux.f64_< (binary (function.uncurried _.<)))
       (with extension/lux.f64_int (unary _.int/1))))

(the (text:clip [paramO extraO subjectO])
  (Trinary _.Expression)
  (//runtime.text:clip paramO extraO subjectO))

(the (text:index [startO partO textO])
  (Trinary _.Expression)
  (//runtime.text:index startO partO textO))

(the (text:composite it)
  (Variadic _.Expression)
  (when it
    (.list)
    (_.string "")

    (.list single)
    single

    (.list left right)
    (|> left (_.+ right))

    parts
    (|> (text:composite (.list))
        (_.do "join" (.list (_.list parts))))))

(the with_text
  (Change Bundle)
  (|>> (with extension/lux.text_= (binary (function.uncurried _.=)))
       (with extension/lux.text_< (binary (function.uncurried _.<)))
       (with extension/lux.text_composite (variadic ..text:composite))
       (with extension/lux.text_index (trinary ..text:index))
       (with extension/lux.text_size (unary _.len/1))
       (with extension/lux.text_char (binary (function.uncurried //runtime.text:char)))
       (with extension/lux.text_clip (trinary ..text:clip))
       ))

(the with_io
  (Change Bundle)
  (|>> (with extension/lux.log! (unary //runtime.io:log!))
       (with extension/lux.error (unary //runtime.io:throw!))
       ))

(the list
  (Variadic _.Expression)
  _.list)

(the list_size
  (Unary _.Expression)
  (|>> _.len/1
       //runtime.i64:64))

(the (list_item [address it])
  (Binary _.Expression)
  (_.item address it))

(the (list_composite [left right])
  (Binary _.Expression)
  (|> (_.list/1 left)
      (_.+ (_.list/1 right))))

(the with_list
  (Change Bundle)
  (|>> (with extension/lux.list (variadic ..list))
       (with extension/lux.list_size (unary ..list_size))
       (with extension/lux.list_item (binary ..list_item))
       (with extension/lux.list_composite (binary ..list_composite))
       ))

(the (array size)
  (Unary _.Expression)
  (|> (.list _.none)
      _.list
      (_.* size)))

(the array_size
  (Unary _.Expression)
  ..list_size)

(the array_its
  (Binary _.Expression)
  ..list_item)

(the (array_has [address value it])
  (Trinary _.Expression)
  (//runtime.array:write address value it))

(the (array_lacks [address it])
  (Binary _.Expression)
  (array_has [address _.none it]))

(the (array_lacks? [address it])
  (Binary _.Expression)
  (|> (array_its [address it])
      (_.= _.none)))

(the with_array
  (Change Bundle)
  (|>> (with extension/lux.array (unary ..array))
       (with extension/lux.array_size (unary ..array_size))
       (with extension/lux.array_its (binary ..array_its))
       (with extension/lux.array_has (trinary ..array_has))
       (with extension/lux.array_lacks (binary ..array_lacks))
       (with extension/lux.array_lacks? (binary ..array_lacks?))
       ))

(the .public bundle
  Bundle
  (|> //.empty
      with_basic
      with_i64
      with_integer
      with_decimal
      with_text
      with_io
      with_list
      with_array
      ))
