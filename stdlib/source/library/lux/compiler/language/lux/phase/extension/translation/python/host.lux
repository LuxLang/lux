... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["<>" projection]
    ["[0]" monad (.only do)]]
   ["[0]" function]
   [data
    [collection
     ["[0]" list]
     ["[0]" dictionary]
     ["[0]" stack]]]
   [macro
    ["[0]" template]]
   [target
    ["_" python (.only Expression SVar)]]]]
 [//
  [common (.only custom)]
  [////
   ["[0]" extension]
   [translation
    [extension (.only Nullary Unary Binary Trinary
                      nullary unary binary trinary)]
    ["//" python
     ["[1][0]" runtime (.only Operation Phase Handler Bundle
                              with_vars)]]]
   [//
    ["[0]" phase]
    ["[0]" translation]
    [synthesis
     ["<s>" \\projection]]]]])

(the (array::new size)
  (Unary Expression)
  (|> (list _.none)
      _.list
      (_.* size)))

(the array::length
  (Unary Expression)
  (|>> _.len/1 //runtime.i64::64))

(the (array::read [indexG arrayG])
  (Binary Expression)
  (_.item indexG arrayG))

(the (array::write [indexG valueG arrayG])
  (Trinary Expression)
  (//runtime.array::write indexG valueG arrayG))

(the (array::delete [indexG arrayG])
  (Binary Expression)
  (//runtime.array::write indexG _.none arrayG))

(the with_array_extensions
  (-> Bundle Bundle)
  (|>> (dictionary.has "python_array_new#|translation" (unary array::new))
       (dictionary.has "python_array_length#|translation" (unary array::length))
       (dictionary.has "python_array_read#|translation" (binary array::read))
       (dictionary.has "python_array_write#|translation" (trinary array::write))
       (dictionary.has "python_array_delete#|translation" (binary array::delete))
       ))

(the object::get
  Handler
  (custom
   [(all <>.and <s>.text <s>.any)
    (function (_ phase archive [fieldS objectS])
      (do phase.monad
        [objectG (phase archive objectS)]
        (in (_.its fieldS objectG))))]))

(the object::do
  Handler
  (custom
   [(all <>.and <s>.text <s>.any (<>.some <s>.any))
    (function (_ phase archive [methodS objectS inputsS])
      (do [! phase.monad]
        [objectG (phase archive objectS)
         inputsG (monad.each ! (phase archive) inputsS)]
        (in (_.do methodS (list.of_stack inputsG) objectG))))]))

(template.with [<!> <?> <unit>]
  [(the <!> (Nullary Expression) (function.constant <unit>))
   (the <?> (Unary Expression) (_.= <unit>))]

  [object::none object::none? _.none]
  )

(the with_object_extensions
  (-> Bundle Bundle)
  (|>> (dictionary.has "python_object_get#|translation" object::get)
       (dictionary.has "python_object_do#|translation" object::do)
       (dictionary.has "python_object_none#|translation" (nullary object::none))
       (dictionary.has "python_object_none?#|translation" (unary object::none?))
       ))

(the python::constant
  (custom
   [<s>.text
    (function (_ phase archive name)
      (do phase.monad
        []
        (in (_.var name))))]))

(the python::import
  (custom
   [<s>.text
    (function (_ phase archive module)
      (do phase.monad
        []
        (in (_.apply (list (_.string module)
                           _.none
                           _.none
                           (_.list (list (_.string ""))))
                     (_.var "__import__")))))]))

(the python::apply
  (custom
   [(all <>.and <s>.any (<>.some <s>.any))
    (function (_ phase archive [abstractionS inputsS])
      (do [! phase.monad]
        [abstractionG (phase archive abstractionS)
         inputsG (monad.each ! (phase archive) inputsS)]
        (in (_.apply (list.of_stack inputsG) abstractionG))))]))

(the python::function
  (custom
   [(all <>.and <s>.i64 <s>.any)
    (function (_ phase archive [arity abstractionS])
      (do [! phase.monad]
        [abstractionG (phase archive abstractionS)
         .let [variable (is (-> Text (Operation SVar))
                            (|>> translation.name
                                 (by ! each _.var)))]
         'inputs (monad.each ! (function (_ _) (variable "input"))
                             (stack.repeated (.natural arity) []))]
        (in (_.lambda (list.of_stack 'inputs)
                      (when (.natural arity)
                        0 (_.apply (list //runtime.unit) abstractionG)
                        1 (_.apply (list.of_stack 'inputs) abstractionG)
                        _ (_.apply (list (_.list (list.of_stack 'inputs))) abstractionG))))))]))

(the python::exec
  (custom
   [(all <>.and <s>.any <s>.any)
    (function (_ phase archive [codeS globalsS])
      (do [! phase.monad]
        [codeG (phase archive codeS)
         globalsG (phase archive globalsS)]
        (in (//runtime.lux::exec codeG globalsG))))]))

(the .public bundle
  Bundle
  (|> extension.empty
      with_array_extensions
      with_object_extensions

      (dictionary.has "python_constant#|translation" python::constant)
      (dictionary.has "python_import#|translation" python::import)
      (dictionary.has "python_apply#|translation" python::apply)
      (dictionary.has "python_function#|translation" python::function)
      (dictionary.has "python_exec#|translation" python::exec)
      ))
