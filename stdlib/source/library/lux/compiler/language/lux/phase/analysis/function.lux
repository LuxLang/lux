... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except function)
   [abstract
    ["[0]" monad]]
   [control
    ["[0]" maybe]]
   [error
    ["[0]" try]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" product]
    ["[0]" text (.only \n)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" monoid monad)]]]
   [math
    [number
     [/64
      ["n" natural]]]]
   ["[0]" type (.only)
    ["[0]" check]]
   ["[0]" meta (.only)
    ["[0]" code]
    ["[0]" module]]
   [compiler
    ["@[0]" type]]
   [aspect
    ["[0]" view]]]]
 [////
  ["[0]" phase (.use "[1]#[0]" functor)]
  ["/" analysis (.only Operation Phase)
   ["[1][0]" type]
   ["[1][0]" inference]
   ["[1][0]" scope]]
  [///
   [reference (.only)
    [variable (.only)]]]])

(exception.the .public (cannot_analyse [module expected function argument body])
  (Exception [Text Type Text Text @type.Code])
  (exception.report
   (list ["Type" (type.as_text expected)]
         ["Function" function]
         ["Argument" argument]
         ["Body" (code.relative module body)])))

(exception.the .public (cannot_reify [module :function: functionC arguments])
  (Exception [Text Type @type.Code (Stack @type.Code)])
  (exception.report
   (list ["Function type" (type.as_text :function:)]
         ["Function" (code.relative module functionC)]
         ["Arguments" (|> arguments
                          (view.of list.stack)
                          list.enumeration
                          (list#each (.function (_ [idx argC])
                                       (text (%.natural idx) " " (code.relative module argC))))
                          (text.interposed \n))])))

(the .public (function analyse function_name arg_name archive body)
  (-> Phase Text Text
      Phase)
  (monad.let [! phase.monad]
    [functionT meta.expected_type
     here module.current_name]
    (loop (again [expectedT functionT])
      (/.with_exception ..cannot_analyse [here expectedT function_name arg_name body]
        (when expectedT
          (type.Function :input: :output:)
          (<| (monad.let !
                [@ meta.provenance])
              (by ! each (.function (_ [scope bodyA])
                           [@ {/.#Abstraction (list#each (|>> (/.variable @))
                                                         (/scope.environment scope))
                                              bodyA}]))
              /scope.with
              ... Functions have access not only to their argument, but
              ... also to themselves, through a local variable.
              (/scope.with_local [function_name functionT])
              (/scope.with_local [arg_name :input:])
              (/type.with :output:)
              (analyse archive body))
          
          {.#Named name :anonymous:}
          (again :anonymous:)

          {.#Reification argT funT}
          (when (type.reified (list argT) funT)
            {.#Some value}
            (again value)

            {.#None}
            (/.failure (exception.error ..cannot_analyse [here expectedT function_name arg_name body])))

          {.#Quantification .universal _}
          (monad.let !
            [[@instance :instance:] (/type.check check.existential)]
            (again (maybe.trusted (type.reified (list :instance:) expectedT))))

          {.#Quantification .existential _}
          (<| /type.with_var
              (.function (_ [@instance :instance:]))
              (again (maybe.trusted (type.reified (list :instance:) expectedT))))
          
          {.#Variable id}
          (monad.let !
            [?expectedT' (phase.try (/type.check (check.type id)))]
            (when ?expectedT'
              {try.#Success expectedT'}
              (again expectedT')

              ... Inference
              _
              (<| /type.with_var
                  (.function (_ [@input :input:]))
                  /type.with_var
                  (.function (_ [@output :output:]))
                  (monad.let !
                    [functionA (again (type.Function :input: :output:))])
                  /type.check
                  (monad.let check.monad
                    [:output: (check.identity (list) @output)
                     ?:input: (check.try (check.identity (list @output) @input))
                     ? (check.linked? @input @output)
                     .let [not_quantified (type.Function :input: (if ? :input: :output:))]
                     _ (<| (check.check expectedT)
                           (when ?:input:
                             {try.#Success :input:}
                             not_quantified

                             {try.#Failure _}
                             (|> not_quantified
                                 (/inference.quantified @input 0)
                                 {.#Quantification .universal (stack)})))]
                    (pure functionA)))))

          _
          (/.failure "")
          )))))

(the .public (reification analyse argsC+ :function: functionA archive functionC)
  (-> Phase (Stack @type.Code) Type /.Term
      Phase)
  (monad.let phase.monad
    [here module.current_name]
    (|> (/inference.general archive analyse :function: argsC+)
        (phase#each (|>> product.right (view.of list.stack) [functionA] /.reified))
        (/.with_exception ..cannot_reify [here :function: functionC argsC+]))))
