... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [data
    ["[0]" product]
    ["[0]" bit]
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    [number
     [/64
      ["[0]" natural]]]]]])

(every .public Side
  (Record
   [#lefts Natural
    #right? Bit]))

(the .public (as_text it)
  (text.Injection Side)
  (text "{" (%.natural (its #lefts it)) " " (bit.as_text (its #right? it)) "}"))

(the .public hash
  (Hash Side)
  (all product.hash
       natural.hash
       bit.hash
       ))

(the .public equivalence
  (Equivalence Side)
  (by ..hash equivalence))

(alias [=]
       ..equivalence)
