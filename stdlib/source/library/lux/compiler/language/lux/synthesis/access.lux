... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equality (.only Equality)]
    [hash (.only Hash)]]
   [data
    ["[0]" sum]
    [text (.only Injection)
     ["%" \\injection]]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["[0]" natural]]]]]]
 ["[0]" /
  ["[1][0]" side (.only Side)]
  ["[1][0]" member (.only Member)]
  ["[1][0]" item (.only Item)]])

(every .public Access
  (Variant
   {:Side Side}
   {:Member Member}
   {:Item Item}))

(the .public (as_text it)
  (Injection Access)
  (when it
    {:Side it}
    (/side.as_text it)
    
    {:Member it}
    (/member.as_text it)

    {:Item it}
    (/item.as_text it)))

(the .public hash
  (Hash Access)
  (all sum.hash
       /side.hash
       /member.hash
       /item.hash
       ))

(the .public equality
  (Equality Access)
  (by ..hash equality))

(alias [=]
       ..equality)

(the .public random
  (Random Access)
  (all random.or
       /side.random
       /member.random
       /item.random
       ))
