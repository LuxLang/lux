... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Tag)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]:[0]" functor)]]]
   [logic
    ["[0]" bit]]
   [math
    [number
     [/64
      ["n" natural]]]]
   [compiler
    ["@[0]" type]]]])

(every .public Tag
  Natural)

(the .public (tag right? lefts)
  (-> Bit Natural
      Tag)
  (if right?
      (++ lefts)
      lefts))

(the .public (lefts right? tag)
  (-> Bit Tag
      Natural)
  (if right?
      (-- tag)
      tag))

(the .public (choice multiplicity pick)
  (-> Natural Tag
      [Natural Bit])
  (let [right? (n.= (-- multiplicity) pick)]
    [(..lefts right? pick)
     right?]))

(the .public (equivalence (open "/:[0]"))
  (for_any (_ it)
    (-> (Equivalence it)
        (Equivalence (@type.Complex it))))
  (implementation
   (the (= expected actual)
     (when [expected actual]
       [{@type.#Complex_Variant [expected_lefts expected_right? expected_value]}
        {@type.#Complex_Variant [actual_lefts actual_right? actual_value]}]
       (and (n.= expected_lefts actual_lefts)
            (bit.= expected_right? actual_right?)
            (/:= expected_value actual_value))
       
       [{@type.#Complex_Tuple expected} {@type.#Complex_Tuple actual}]
       (by (list.equivalence /:=) = expected actual)

       _
       false))))

(the .public (hash super)
  (for_any (_ it)
    (-> (Hash it)
        (Hash (@type.Complex it))))
  (implementation
   (the equivalence
     (..equivalence (by super equivalence)))
   
   (the (hash value)
     (when value
       {@type.#Complex_Variant [lefts right? value]}
       (all n.x 2
            (by n.hash hash lefts)
            (by bit.hash hash right?)
            (by super hash value))
       
       {@type.#Complex_Tuple members}
       (all n.x 3
            (by (list.hash super) hash members))
       ))))

(the .public (as_text %it it)
  (for_any (_ it)
    (-> (text.Injection it)
        (text.Injection (@type.Complex it))))
  (when it
    {@type.#Complex_Variant [lefts right? it]}
    (text "{"
          (%.natural lefts)
          " " (bit.as_text right?)
          " " (%it it)
          "}")
    
    {@type.#Complex_Tuple it}
    (|> it
        (list:each %it)
        (text.interposed " ")
        (text.enclosed ["[" "]"]))))
