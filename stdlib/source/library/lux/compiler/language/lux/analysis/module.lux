... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except with alias all)
   [abstract
    ["[0]" monad]]
   [control
    ["[0]" pipe]
    ["[0]" maybe]]
   [error
    ["[0]" try]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" mix functor monoid)
      ["[0]" property]]]]
   [macro
    ["[0]" template]]
   [meta
    ["[0]" module]
    ["[0]" name]
    ["[0]" label]]
   [compiler
    ["@[0]" type]]
   ["[0]" type]]]
 ["/" // (.only Operation)
  [//
   ["[0]" phase (.use "[1]#[0]" functor)]]])

(exception.the .public (not_known [module])
  (Exception [module.Name])
  (exception.report
   (list ["Module" module])))

(exception.the .public no_current)

(exception.the .public (cannot_define_more_than_once [name already_existing])
  (Exception [Name @type.Global])
  (exception.report
   (list ["Definition" (name.as_text name)]
         ["Original" (when already_existing
                       {@type.#Alias it}
                       (text "alias of " (name.as_text it))
                       
                       {@type.#Definition [type expression value]}
                       (text "definition : " (type.as_text type)))])))

(exception.the .public (can_only_change_state_of_active_module [module state])
  (Exception [module.Name Module_State])
  (exception.report
   (list ["Module" module]
         ["Desired state" (when state
                            {.#Active}   "Active"
                            {.#Compiled} "Compiled"
                            {.#Cached}   "Cached")])))

(the .public (empty hash)
  (-> Natural
      @type.Module)
  [.#module_hash hash
   .#module_aliases (list)
   .#definitions (list)
   .#imports (list)
   .#module_state {.#Active}])

(the .public (import module)
  (-> module.Name
      (Operation Any))
  (monad.let phase.monad
    [self_name phase.name_of_current_module]
    (function (_ state)
      {try.#Success [(revised @type.#modules
                              (property.revised self_name (revised @type.#imports (function (_ current)
                                                                                    (if (list.any? (text.= module)
                                                                                                   current)
                                                                                        current
                                                                                        (list#composite current (list module))))))
                              state)
                     []]})))

(the .public (by_alias it origin)
  (-> Text module.Name
      (Operation [module.Name @type.Module]))
  (monad.let phase.monad
    [modules phase.every_module
     name (phase.of_try
           (monad.let try.monad
             [origin (property.value origin modules)]
             (property.value it (its @type.#module_aliases origin))))
     module (phase.by_name name)]
    (pure [name module])))

(exception.the .public (cannot_re-use_alias [alias for previous origin])
  (Exception [Text module.Name module.Name module.Name])
  (exception.report
   (list ["Alias" (%.text alias)]
         ["For" (%.text for)]
         ["Previous" (%.text previous)]
         ["In module" (%.text origin)])))

(the .public (alias it module)
  (-> Text module.Name
      (Operation Any))
  (monad.let phase.monad
    [current phase.name_of_current_module
     previous (phase.try (by_alias it current))]
    (when previous
      {try.#Success [previous _]}
      (/.except ..cannot_re-use_alias [it module previous current])
      
      {try.#Failure _}
      (function (_ state)
        {try.#Success [(revised @type.#modules
                                (property.revised current (revised @type.#module_aliases (property.has it module)))
                                state)
                       []]}))))

(the .public (exists? module)
  (-> module.Name
      (Operation Bit))
  (function (_ state)
    (|> state
        (its @type.#modules)
        (property.value module)
        (pipe.when
          {try.#Success _}
          true
          
          failure
          false)
        [state]
        {try.#Success})))

(the .public (override_definition [module proper] exported?,definition)
  (-> Name [Bit @type.Global]
      (Operation Any))
  (function (_ state)
    {try.#Success [(revised @type.#modules
                            (property.revised module
                                              (revised @type.#definitions
                                                       (property.has proper exported?,definition)))
                            state)
                   []]}))

(the .public (define name exported?,definition)
  (-> Text [Bit @type.Global]
      (Operation Any))
  (monad.let phase.monad
    [[self_name self] phase.current_module]
    (when (property.value name (its @type.#definitions self))
      {try.#Success [_ already_existing]}
      (/.except ..cannot_define_more_than_once [[self_name name] already_existing])

      failure
      (..override_definition [self_name name] exported?,definition))))

(the .public (create hash name)
  (-> Natural module.Name
      (Operation Any))
  (function (_ state)
    {try.#Success [(revised @type.#modules
                            (property.has name (..empty hash))
                            state)
                   []]}))

(the .public (with hash name action)
  (for_any (_ it)
    (-> Natural module.Name (Operation it)
        (Operation [@type.Module it])))
  (monad.let phase.monad
    [_ (..create hash name)
     output (/.with_current_module name
              action)
     module (phase.by_name name)]
    (pure [module output])))

(template.with [<setter> <asker> <tag>]
  [(the .public (<setter> module_name)
     (-> module.Name
         (Operation Any))
     (monad.let phase.monad
       [module (phase.by_name module_name)]
       (let [active? (when (its @type.#module_state module)
                       {.#Active}
                       true
                       
                       _
                       false)]
         (if active?
             (function (_ state)
               {try.#Success [(|> state
                                  (revised @type.#modules
                                           (property.has module_name (has @type.#module_state {<tag>} module))))
                              []]})
             (/.except ..can_only_change_state_of_active_module [module_name {<tag>}])))))
   
   (the .public (<asker> module_name)
     (-> module.Name
         (Operation Bit))
     (function (_ state)
       (when (|> state
                 (its @type.#modules)
                 (property.value module_name))
         {try.#Success module}
         {try.#Success [state
                        (when (its @type.#module_state module)
                          {<tag>}
                          true
                          
                          _
                          false)]}

         failure
         ((/.except ..not_known module_name)
          state))))]

  [[set_active   active?   .#Active]
   [set_compiled compiled? .#Compiled]
   [set_cached   cached?   .#Cached]])
