... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Variant Pattern
                #Bit #Natural #Integer #Revolution #Decimal #Text #Variant)
   [abstract
    [equivalence (.only Equivalence)]
    ["[0]" monad (.only do)]]
   [control
    ["[0]" maybe (.use "[1]#[0]" monoid monad)]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" bit]
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor mix)]
     ["[0]" dictionary (.only Dictionary)]
     ["[0]" set (.only Set)]]]
   [math
    [number
     ["n" natural (.use "[1]#[0]" interval)]
     ["i" integer]
     ["r" revolution]
     ["d" decimal]]]
   [macro
    ["^" pattern]
    ["[0]" template]
    ["[0]" expansion]]]]
 ["[0]" //
  ["[1][0]" simple]
  ["[1][0]" complex]
  ["[1][0]" pattern (.only Pattern)]])

... The coverage of a pattern-matching expression summarizes how well
... all the possible values of an input are being covered by the
... different patterns involved.
... Ideally, the pattern-matching has "exhaustive" coverage, which just
... means that every possible value can be matched by at least 1
... pattern.
... Every other coverage is considered partial, and it would be valued
... as insuficient (since it could lead to runtime errors due to values
... not being handled by any pattern).
(template.let [(Variant' @)
               [[(Maybe Natural) (Dictionary Natural @)]]]
  (these (every .public Coverage
           (Rec @
             (.Variant
              {#Exhaustive}
              {#Bit Bit}
              {#Natural (Set Natural)}
              {#Integer (Set Integer)}
              {#Revolution (Set Revolution)}
              {#Decimal (Set Decimal)}
              {#Text (Set Text)}
              {#Variant (Variant' @)}
              {#Seq @ @}
              {#Alt @ @})))

         (every .public Variant
           (Variant' Coverage))))

(the .public (minimum [maximum cases])
  (-> Variant
      Natural)
  (maybe.else (|> cases
                  dictionary.keys
                  (stack#mix n.major 0)
                  ++)
              maximum))

(the .public (maximum [maximum cases])
  (-> Variant
      Natural)
  (maybe.else n#maximum maximum))

(the (alternatives coverage)
  (-> Coverage (Stack Coverage))
  (when coverage
    {#Alt left right}
    (stack.partial left (alternatives right))

    _
    (stack coverage)))

(the .public equivalence
  (Equivalence Coverage)
  (implementation
   (the (= expected actual)
     (`` (when [expected actual]
           [{#Exhaustive} {#Exhaustive}]
           true

           [{#Bit sideR} {#Bit sideS}]
           (bit.= sideR sideS)

           (,, (template.with [<tag>]
                 [[{<tag> partialR} {<tag> partialS}]
                  (set.= partialR partialS)]

                 [#Natural]
                 [#Integer]
                 [#Revolution]
                 [#Decimal]
                 [#Text]))

           [{#Variant allR casesR} {#Variant allS casesS}]
           (and (by (maybe.equivalence n.equivalence) = allR allS)
                (by (dictionary.equivalence =) = casesR casesS))
           
           [{#Seq leftR rightR} {#Seq leftS rightS}]
           (and (= leftR leftS)
                (= rightR rightS))

           [{#Alt _} {#Alt _}]
           (let [flatR (alternatives expected)
                 flatS (alternatives actual)]
             (and (n.= (stack.size flatR) (stack.size flatS))
                  (stack.every? (function (_ [coverageR coverageS])
                                  (= coverageR coverageS))
                                (stack.zipped_2 flatR flatS))))

           _
           false)))))

(alias [=]
       ..equivalence)

(the .public (as_text value)
  (text.Injection Coverage)
  (`` (when value
        {#Bit it}
        (bit.as_text it)

        (,, (template.with [<tag> <injection>]
              [{<tag> it}
               (|> it
                   set.stack
                   (stack#each <injection>)
                   (text.interposed " ")
                   (text.enclosed ["[" "]"]))]

              [#Natural %.natural]
              [#Integer %.integer]
              [#Revolution %.revolution]
              [#Decimal %.decimal]
              [#Text %.text]))
        
        {#Variant ?max_cases cases}
        (|> cases
            dictionary.entries
            (stack#each (function (_ [tag it])
                          (text (%.natural tag) " " (as_text it))))
            (text.interposed " ")
            (text (maybe.else "?" (maybe#each %.natural ?max_cases)) " ")
            (text.enclosed ["{" "}"]))

        {#Seq left right}
        (text "(& " (as_text left) " " (as_text right) ")")
        
        {#Alt left right}
        (text "(| " (as_text left) " " (as_text right) ")")

        {#Exhaustive}
        "*")))

(exception.the .public (invalid_tuple size)
  (Exception Natural)
  (exception.report
   (stack ["Expected size" ">= 2"]
          ["Actual size" (%.natural size)])))

(the .public (coverage pattern)
  (-> Pattern (Try Coverage))
  (`` (when pattern
        (^.or {//pattern.#Simple {//simple.#Unit}}
              {//pattern.#Bind _})
        {try.#Success {#Exhaustive}}

        ... Simple patterns (other than unit/[]) always have partial coverage because there
        ... are too many possibilities as far as values go.
        (,, (template.with [<from> <to> <hash>]
              [{//pattern.#Simple {<from> it}}
               {try.#Success {<to> (set.of_stack <hash> (stack it))}}]

              [//simple.#Natural #Natural n.hash]
              [//simple.#Integer #Integer i.hash]
              [//simple.#Revolution #Revolution r.hash]
              [//simple.#Decimal #Decimal d.hash]
              [//simple.#Text #Text text.hash]))

        ... Bits are the exception, since there is only "#1" and
        ... "#0", which means it is possible for bit
        ... pattern-matching to become exhaustive if complementary parts meet.
        {//pattern.#Simple {//simple.#Bit value}}
        {try.#Success {#Bit value}}

        ... Tuple patterns can be exhaustive if there is exhaustiveness for all of
        ... their sub-patterns.
        {//pattern.#Complex {//complex.#Tuple membersP+}}
        (when (stack.reversed membersP+)
          (^.or (stack)
                (stack _))
          (exception.except ..invalid_tuple [(stack.size membersP+)])
          
          {.#Top lastP prevsP+}
          (do [! try.monad]
            [lastC (coverage lastP)]
            (monad.mix !
                       (function (_ leftP rightC)
                         (do !
                           [leftC (coverage leftP)]
                           (when rightC
                             {#Exhaustive}
                             (in leftC)

                             _
                             (in {#Seq leftC rightC}))))
                       lastC prevsP+)))

        ... Variant patterns can be shown to be exhaustive if all the possible
        ... cases are handled exhaustively.
        {//pattern.#Complex {//complex.#Variant [lefts right? value]}}
        (do try.monad
          [value_coverage (coverage value)
           .let [idx (if right?
                       (++ lefts)
                       lefts)]]
          (in {#Variant (if right?
                          {.#Some (++ idx)}
                          {.#None})
                        (|> (dictionary.empty n.hash)
                            (dictionary.has idx value_coverage))})))))

(the (xor left right)
  (-> Bit Bit Bit)
  (or (and left (not right))
      (and (not left) right)))

... The coverage checker not only verifies that pattern-matching is
... exhaustive, but also that there are no redundant patterns.
... Redundant patterns will never be executed, since there will
... always be a pattern prior to them that would match the input.
... Because of that, the presence of redundant patterns is assumed to
... be a bug, likely due to programmer carelessness.
(exception.the .public (redundancy [so_far addition])
  (Exception [Coverage Coverage])
  (exception.report
   (stack ["Coverage so-far" (as_text so_far)]
          ["Additional coverage" (as_text addition)])))

(exception.the .public (variant_mismatch [expected mismatched])
  (Exception [Natural Natural])
  (exception.report
   (stack ["Expected cases" (%.natural expected)]
          ["Mismatched cases" (%.natural mismatched)])))

(the .public (exhaustive? coverage)
  (-> Coverage Bit)
  (when coverage
    {#Exhaustive}
    true

    _
    false))

... After determining the coverage of each individual pattern, it is
... necessary to merge them all to figure out if the entire
... pattern-matching expression is exhaustive and whether it contains
... redundant patterns.
(the .public (composite addition so_far)
  (-> Coverage Coverage (Try Coverage))
  (expansion.let [<redundancy> (exception.except ..redundancy [so_far addition])
                  <alternatively> {try.#Success {#Alt addition so_far}}
                  <else> (if (= so_far addition)
                           ... The addition cannot possibly improve the coverage.
                           <redundancy>
                           ... There are now 2 alternative paths.
                           <alternatively>)]
    (`` (when [addition so_far]
          ... 2 bit coverages are exhaustive if they complement one another.
          [{#Bit sideA} {#Bit sideSF}]
          (if (xor sideA sideSF)
            {try.#Success {#Exhaustive}}
            <redundancy>)

          (,, (template.with [<tag>]
                [[{<tag> partialA} {<tag> partialSF}]
                 (if (set.empty? (set.intersection partialA partialSF))
                   {try.#Success {<tag> (set.union partialA partialSF)}}
                   <redundancy>)]

                [#Natural]
                [#Integer]
                [#Revolution]
                [#Decimal]
                [#Text]))

          [{#Variant addition'} {#Variant so_far'}]
          (let [[allA casesA] addition'
                [allSF casesSF] so_far'
                addition_cases (..maximum addition')
                so_far_cases (..maximum so_far')]
            (if (template.let [(known_cases? it)
                               [(n.< n#maximum it)]]
                  (and (known_cases? so_far_cases)
                       (if (known_cases? addition_cases)
                         (not (n.= so_far_cases addition_cases))
                         (n.> so_far_cases (..minimum addition')))))
              (exception.except ..variant_mismatch [so_far_cases addition_cases])

              (by (dictionary.equivalence ..equivalence) = casesSF casesA)
              <redundancy>

              ... else
              (do [! try.monad]
                [casesM (monad.mix !
                                   (function (_ [tagA coverageA] casesSF')
                                     (when (dictionary.value tagA casesSF')
                                       {try.#Success coverageSF}
                                       (do !
                                         [coverageM (composite coverageA coverageSF)]
                                         (in (dictionary.has tagA coverageM casesSF')))

                                       {try.#Failure _}
                                       (in (dictionary.has tagA coverageA casesSF'))))
                                   casesSF
                                   (dictionary.entries casesA))]
                (in (if (and (n.= (n.minor addition_cases so_far_cases)
                                  (dictionary.size casesM))
                             (stack.every? ..exhaustive? (dictionary.values casesM)))
                      {#Exhaustive}
                      {#Variant (maybe#composite allA allSF) casesM})))))

          [{#Seq leftA rightA} {#Seq leftSF rightSF}]
          (when [(= leftSF leftA) (= rightSF rightA)]
            ... Same prefix
            [.true .false]
            (do try.monad
              [rightM (composite rightA rightSF)]
              (in (if (..exhaustive? rightM)
                    ... If all that follows is exhaustive, then it can be safely dropped
                    ... (since only the "left" part would influence whether the
                    ... composite coverage is exhaustive or not).
                    leftSF
                    {#Seq leftSF rightM})))

            ... Same suffix
            [.false .true]
            (do try.monad
              [leftM (composite leftA leftSF)]
              (in {#Seq leftM rightA}))

            ... The 2 sequences cannot possibly be merged.
            [.false .false]
            <alternatively>

            ... There is nothing the addition adds to the coverage.
            [.true .true]
            <redundancy>)

          ... The addition cannot possibly improve the coverage.
          [_ {#Exhaustive}]
          <redundancy>

          ... The addition completes the coverage.
          [{#Exhaustive} _]
          {try.#Success {#Exhaustive}}
          
          ... When merging a new coverage against one based on Alt, it may be
          ... that one of the many coverages in the Alt is complementary to
          ... the new one, so effort must be made to fuse carefully, to match
          ... the right coverages together.
          ... If one of the Alt sub-coverages matches the new one, the cycle
          ... must be repeated, in case the resulting coverage can now match
          ... other ones in the original Alt.
          ... This process must be repeated until no further productive
          ... merges can be done.
          [_ {#Alt leftS rightS}]
          (do [! try.monad]
            [.let [fuse_once (is (-> Coverage (Stack Coverage)
                                     (Try [(Maybe Coverage)
                                           (Stack Coverage)]))
                                 (function (_ coverageA possibilitiesSF)
                                   (loop (again [altsSF possibilitiesSF])
                                     (when altsSF
                                       {.#Empty}
                                       (in [{.#None} (stack coverageA)])
                                       
                                       {.#Top altSF altsSF'}
                                       (do !
                                         [altMSF (composite coverageA altSF)]
                                         (when altMSF
                                           {#Alt _}
                                           (do !
                                             [[success altsSF+] (again altsSF')]
                                             (in [success {.#Top altSF altsSF+}]))

                                           _
                                           (in [{.#Some altMSF} altsSF'])))))))]]
            (loop (again [addition addition
                          possibilitiesSF (alternatives so_far)])
              (do !
                [[addition' possibilitiesSF'] (fuse_once addition possibilitiesSF)]
                (when addition'
                  {.#Some addition'}
                  (again addition' possibilitiesSF')
                  
                  {.#None}
                  (when (stack.reversed possibilitiesSF')
                    {.#Top last prevs}
                    (in (stack#mix (function (_ left right) {#Alt left right})
                                   last
                                   prevs))

                    {.#Empty}
                    (undefined))))))

          ... The left part will always match, so the addition is redundant.
          [{#Seq left right} single]
          (if (= left single)
            <redundancy>
            <else>)

          ... The right part is not necessary, since it can always match the left.
          [single {#Seq left right}]
          (if (= left single)
            {try.#Success single}
            <else>)

          _
          <else>))))
