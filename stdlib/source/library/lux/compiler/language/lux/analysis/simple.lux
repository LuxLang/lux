... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except #Bit #Natural #Integer #Revolution #Decimal #Text)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" text (.only Injection)
     ["%" \\injection]]]
   [logic
    ["[0]" bit]]
   [math
    [number
     [/64
      ["n" natural]
      ["i" integer]
      ["r" revolution]
      ["d" decimal]]]]
   [macro
    ["[0]" template]]]])

(every .public Simple
  (Variant
   {#Unit}
   {#Bit Bit}
   {#Natural Natural}
   {#Integer Integer}
   {#Revolution Revolution}
   {#Decimal Decimal}
   {#Text Text}))

(the .public equivalence
  (Equivalence Simple)
  (implementation
   (the (= expected actual)
     (`` (when [expected actual]
           [{#Unit} {#Unit}]
           true
           
           (,, (template.with [<tag> <=>]
                 [[{<tag> expected} {<tag> actual}]
                  (<=> expected actual)]

                 [[#Bit bit.=]
                  [#Natural n.=]
                  [#Integer i.=]
                  [#Revolution r.=]
                  [#Decimal d.=]
                  [#Text text.=]]
                 ))
           
           _
           false)))))

(alias [=]
       ..equivalence)

(the .public (as_text it)
  (Injection Simple)
  (`` (when it
        {#Unit}
        "[]"

        (,, (template.with [<tag> <injection>]
              [{<tag> value}
               (<injection> value)]

              [[#Bit bit.as_text]
               [#Natural %.natural]
               [#Integer %.integer]
               [#Revolution %.revolution]
               [#Decimal %.decimal]
               [#Text %.text]]
              )))))
