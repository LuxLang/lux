... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" text (.only Injection)
     ["%" \\injection]]]
   [logic
    ["[0]" bit]]
   [math
    [number
     [/64
      ["n" natural]
      ["i" integer]
      ["r" revolution]
      ["d" decimal]]]]
   [macro
    ["[0]" template]]
   [compiler
    ["@[0]" type (.only Simple)]]]])

(the .public equivalence
  (Equivalence Simple)
  (implementation
   (the (= expected actual)
     (`` (when [expected actual]
           [{@type.:Simple_Unit} {@type.:Simple_Unit}]
           true
           
           (,, (template.with [<tag> <=>]
                 [[{<tag> expected} {<tag> actual}]
                  (<=> expected actual)]

                 [[@type.:Simple_Bit bit.=]
                  [@type.:Simple_Natural n.=]
                  [@type.:Simple_Integer i.=]
                  [@type.:Simple_Revolution r.=]
                  [@type.:Simple_Decimal d.=]
                  [@type.:Simple_Text text.=]]))
           
           _
           false)))))

(alias [=]
       ..equivalence)

(the .public (as_text it)
  (Injection Simple)
  (`` (when it
        {@type.:Simple_Unit}
        "[]"

        (,, (template.with [<tag> <injection>]
              [{<tag> value}
               (<injection> value)]

              [[@type.:Simple_Bit bit.as_text]
               [@type.:Simple_Natural %.natural]
               [@type.:Simple_Integer %.integer]
               [@type.:Simple_Revolution %.revolution]
               [@type.:Simple_Decimal %.decimal]
               [@type.:Simple_Text %.text]])))))
