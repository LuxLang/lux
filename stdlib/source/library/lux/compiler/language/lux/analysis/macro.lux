... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [error
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    [text
     ["%" \\injection]]
    [collection
     ["[0]" list]]]
   ["[0]" meta (.only)
    ["[0]" module]
    ["[0]" name]
    ["[0]" code]]]]
 [///
  ["[0]" phase]])

(exception.the .public (expansion_failed [module macro inputs error])
  (Exception [Text Name (List Code) Text])
  (exception.report
   (list ["Macro" (name.as_text macro)]
         ["Inputs" (exception.listing (code.relative module) inputs)]
         ["Error" error])))

(exception.the .public (must_have_single_expansion [module macro inputs outputs])
  (Exception [Text Name (List Code) (List Code)])
  (exception.report
   (list ["Macro" (name.as_text macro)]
         ["Inputs" (exception.listing (code.relative module) inputs)]
         ["Outputs" (exception.listing (code.relative module) outputs)])))

(every .public Expander
  (-> Macro (Stack Code) Lux
      (Try (Try [Lux (Stack Code)]))))

(the .public (expansion expander name macro inputs)
  (-> Expander Name Macro (Stack Code)
      (Meta (Stack Code)))
  (function (_ state)
    (do try.monad
      [output (expander macro inputs state)]
      (when output
        {try.#Failure error}
        ((is (Meta (Stack Code))
             (do meta.monad
               [[here _] module.current]
               (meta.failure (exception.error ..expansion_failed [here name (list.of_stack inputs) error]))))
         state)

        _
        output))))

(the .public (single_expansion expander name macro inputs)
  (-> Expander Name Macro (Stack Code)
      (Meta Code))
  (do [! meta.monad]
    [expansion (..expansion expander name macro inputs)]
    (when expansion
      (stack single)
      (in single)

      _
      (do !
        [[here _] module.current]
        (meta.failure (exception.error ..must_have_single_expansion [here name (list.of_stack inputs) (list.of_stack expansion)]))))))
