... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only Monad do)]]
   [control
    ["[0]" pipe]]
   [error
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    [binary (.only Binary)]
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor)]
     ["[0]" dictionary (.only Dictionary)]]]
   [macro
    ["[0]" expansion]]
   [world
    ["[0]" file]]]]
 ["[0]" // (.only)
  [//
   [archive
    ["[0]" module]]]])

(exception.the .public (cannot_enable [archive @module error])
  (Exception [file.Path module.ID Text])
  (exception.report
   (list ["Archive" archive]
         ["Module ID" (%.natural @module)]
         ["Error" error])))

(the .public (path fs @module)
  (for_any (_ !)
    (-> (file.System !) module.ID
        file.Path))
  (let [/ (by fs delimiter)]
    (text //.path / (%.natural @module))))

(the .public (enabled? fs @module)
  (for_any (_ !)
    (-> (file.System !) module.ID
        (! Bit)))
  (by fs directory? (..path fs @module)))

(the .public (enable! ! fs @module)
  (for_any (_ !)
    (-> (Monad !) (file.System !) module.ID
        (! (Try Any))))
  (do !
    [.let [path (..path fs @module)]
     module_exists? (by fs directory? path)]
    (if module_exists?
      (in {try.#Success []})
      (expansion.let [<failure> (exception.except ..cannot_enable [//.path
                                                                   @module
                                                                   error])]
        (do !
          [? (//.enable! ! fs)]
          (when ?
            {try.#Failure error}
            (in <failure>)
            
            success
            (|> path
                (by fs make_directory)
                (by ! each (|>> (pipe.when
                                  {try.#Failure error}
                                  <failure>

                                  success
                                  success))))))))))

(the file
  file.Path
  "descriptor")

(the .public (descriptor fs @module)
  (for_any (_ !)
    (-> (file.System !) module.ID
        file.Path))
  (let [/ (by fs delimiter)]
    (text (..path fs @module) / ..file)))

(the .public (cache! fs @module content)
  (for_any (_ !)
    (-> (file.System !) module.ID Binary
        (! (Try Any))))
  (by fs write (..descriptor fs @module) content))

(the .public (cache fs @module)
  (for_any (_ !)
    (-> (file.System !) module.ID
        (! (Try Binary))))
  (by fs read (..descriptor fs @module)))

(the .public (artifacts ! fs @module)
  (for_any (_ !)
    (-> (Monad !) (file.System !) module.ID
        (! (Try (Dictionary Text Binary)))))
  (do [! (try.with !)]
    [files (by fs directory_files (..path fs @module))
     pairs (|> files
               (stack#each (function (_ file)
                             [(file.name fs file) file]))
               (stack.only (|>> product.left (text.= ..file) not))
               (stack.each' ! (function (_ [name path])
                                (|> path
                                    (by fs read)
                                    (by ! each (|>> [name]))))))]
    (in (dictionary.of_stack text.hash pairs))))
