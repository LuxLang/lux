... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only Monad)]]
   [error
    ["[0]" try (.only Try)]]
   [world
    ["[0]" file]]
   [function
    [predicate (.only Predicate)]]
   [data
    ["[0]" text]]]])

(the .public path
  file.Path
  "cache")

(the .public (path? fs it)
  (for_any (_ !)
    (-> (file.System !)
        (Predicate file.Path)))
  (let [prefix (text ..path (by fs delimiter))]
    (or (text.starts_with? prefix it)
        (text.= path it))))

(the .public (enabled? fs)
  (for_any (_ !)
    (-> (file.System !)
        (! Bit)))
  (by fs directory? ..path))

(the .public (enable! ! fs)
  (for_any (_ !)
    (-> (Monad !) (file.System !)
        (! (Try Any))))
  (monad.let !
    [? (..enabled? fs)]
    (if ?
        (pure {try.#Success []})
        (file.make_directories ! fs ..path))))
