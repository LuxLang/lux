... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Module)
   [abstract
    [equality (.only Equality)]
    ["<>" projection]]
   [data
    ["[0]" product]
    ["[0]" text]
    ["[0]" binary
     ["[0]" \\injection (.only Injection)]
     ["<[1]>" \\projection (.only Projection)]]
    [collection
     ["[0]" set (.only Set)]]]
   [math
    [number
     [/64
      ["[0]" natural]]]]
   [macro
    ["[0]" template]]
   [world
    [file (.only Path)]]]])

(every .public Module
  Text)

(the .public runtime
  Module
  "")

(every .public Descriptor
  (Record
   [:name Module
    :file Path
    :hash Natural
    :state Module_State
    :references (Set Module)]))

(the module_state_equality
  (Equality Module_State)
  (implementation
   (the (= left right)
     (`` (when [left right]
           (,, (template.with [<tag>]
                 [[{<tag>} {<tag>}]
                  true]

                 [[.:Active]
                  [.:Compiled]
                  [.:Cached]]))

           _
           false)))))

(the .public equality
  (Equality Descriptor)
  (all product.equality
       text.equality
       text.equality
       natural.equality
       ..module_state_equality
       set.equality
       ))

(alias [=]
       ..equality)

(the .public as_binary
  (Injection Descriptor)
  (all \\injection.and
       \\injection.text
       \\injection.text
       \\injection.natural
       \\injection.any
       (\\injection.set \\injection.text)
       ))

(the .public of_binary
  (Projection Descriptor)
  (all <>.and
       <binary>.text
       <binary>.text
       <binary>.natural
       (by <>.monad pure {.:Cached})
       (<binary>.set text.hash <binary>.text)
       ))
