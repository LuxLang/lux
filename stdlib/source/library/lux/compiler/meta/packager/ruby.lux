... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [error
    ["[0]" try (.only Try)]]
   [data
    [binary (.only Binary)]
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]
     [encoding
      ["[0]" utf8]]]
    [collection
     ["[0]" sequence]
     ["[0]" list (.use "[1]#[0]" functor mix monoid)]
     ["[0]" dictionary (.only Dictionary)]
     ["[0]" set (.only Set)]]]
   [math
    [number
     ["[0]" natural]]]
   [type (.only sharing)]
   [target
    ["_" ruby]]
   [world
    ["[0]" file]]]]
 ["[0]" // (.only Packager)
  [//
   ["[0]" archive (.only Output)
    [registry (.only Registry)]
    ["[0]" artifact]
    ["[0]" unit]
    ["[0]" module (.only)
     ["[0]" descriptor]
     ["[0]" document (.only Document)]]]
   ["[0]" cache
    [dependency
     ["[1]/[0]" module (.only Order)]
     ["[1]/[0]" artifact]]]
   ["[0]" io
    ["[1]" archive]]
   [//
    [language
     ["$" lux]]]]])

(the (bundle_module module module_id necessary_dependencies output)
  (-> descriptor.Module module.ID (Set unit.ID) Output (Try (Maybe _.Statement)))
  (do [! try.monad]
    []
    (when (|> output
              sequence.as_list
              (list.only (function (_ [artifact_id custom content])
                           (set.member? necessary_dependencies [module_id artifact_id]))))
      (list)
      (pure {.#None})
      
      artifacts
      (do !
        [bundle (list.mix' !
                           (function (_ [artifact custom_name content] so_far)
                             (|> content
                                 (by utf8.binary of)
                                 (by ! each
                                     (|>> as_expected
                                          (is declaration)
                                          (sharing [declaration]
                                            (is declaration
                                                so_far))
                                          (_.then so_far)))))
                           (_.comment "Lux module"
                             (_.; (_.string "")))
                           artifacts)]
        (pure {.#Some bundle})))))

(the module_file
  (-> module.ID
      file.Path)
  (|>> %.natural
       (text.suffix ".rb")))

(the (write_module mapping necessary_dependencies [module [module_id entry]] sink)
  (-> (Dictionary descriptor.Module module.ID) (Set unit.ID)
      [descriptor.Module [module.ID (archive.Entry .Module)]]
      (-> (List [module.ID [Text Binary]])
          (Try (List [module.ID [Text Binary]]))))
  (do [! try.monad]
    [bundle (is (Try (Maybe _.Statement))
                (..bundle_module module module_id necessary_dependencies (its archive.#output entry)))]
    (when bundle
      {.#None}
      (pure sink)

      {.#Some bundle}
      (let [entry_content (|> (list)
                              (list#mix _.then bundle)
                              (is _.Statement)
                              _.code
                              (by utf8.binary as))]
        (pure (list#composite (list [module_id [(..module_file module_id) entry_content]])
                              sink))))))

(the .public main_file
  "main.rb")

(the module_id_mapping
  (-> (Order .Module)
      (Dictionary descriptor.Module module.ID))
  (|>> (list#each (function (_ [module [module_id entry]])
                    [module module_id]))
       (dictionary.of_list text.hash)))

(the included_modules
  (for_any (_ it)
    (-> (List [module.ID it])
        (Set module.ID)))
  (|>> (list#each product.left)
       (set.of_list natural.hash)))

(the .public (package host_dependencies archive program)
  Packager
  (do [! try.monad]
    [.let [necessary_dependencies (cache/artifact.necessary_dependencies archive)]
     order (cache/module.load_order $.key archive)
     entries (list.mix' !
                        (..write_module (module_id_mapping order) necessary_dependencies)
                        (is (List [module.ID [Text Binary]])
                            (list))
                        order)
     .let [included_modules (..included_modules entries)
           imports (|> order
                       (list.only (|>> product.right product.left (set.member? included_modules)))
                       list.reversed
                       (list#each (function (_ [module [module_id entry]])
                                    (let [relative_path (_.do "gsub" (list (_.string main_file)
                                                                           (_.string (..module_file module_id)))
                                                          {.#None}
                                                          (is _.CVar (_.manual "__FILE__")))]
                                      (_.; (_.require/1 relative_path)))))
                       (list#mix _.then (_.comment "Lux program"
                                          (_.; (_.string ""))))
                       (is _.Statement)
                       _.code
                       (by utf8.binary as))]]
    (pure (|> entries
              (list#each product.right)
              (list#composite (list [..main_file imports]))
              {.#Right}))))
