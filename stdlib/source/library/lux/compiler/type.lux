... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Module Name
                Bit
                Text text
                Decimal
                I64 i64
                Natural natural
                Integer integer
                Revolution revolution
                Label Tag Slot
                List list
                Macro macro
                Array

                function
                Type
                #Nominal #Parameter #Variable #Opaque #Quantification #Reification #Named

                Provenance
                Code
                #Bit #Natural #Integer #Revolution #Decimal #Text #Name #Form #Variant #Tuple

                Maybe
                Info
                #target #version #mode #configuration
                Definition Default Alias
                Global
                #Definition #Alias #Default
                Module
                #module_hash #module_aliases #definitions #imports #module_state
                Bindings
                #counter #mappings
                Scope
                #locals #captured
                Type_Context
                #ex_counter #var_counter #var_bindings
                Lux
                #info #source #provenance
                #current_module #modules #scopes
                #type_context #expected #seed
                #eval

                Meta)
   [control
    ["[0]" maybe (.use "[1]#[0]" functor)]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)
      ["[0]" property]]]]
   [meta
    ["[0]" name]]
   ["[0]" function]
   [math
    [number
     [/64
      ["n" natural]]]]]])

(the prefix
  name.delimiter)

(every .public Identity
  (Record
   [#alias .Text
    #name .Text]))

(the expansion#let (.in_module# .prelude .expansion#let))
(the with_template (.in_module# .prelude .with_template))
(the template#macro (.in_module# .prelude .template#macro))

(with_template [,name ,pre_processing]
  [(the ,name
     (.macro (_ it state)
       (when it
         (.list {.#Name @ ["" it]})
         {.#Right [state (.list {.#Text @ (|> it ,pre_processing)})]}

         _
         {.#Left .wrong_syntax})))]

  [[alias_of_type |>]
   [name_of_type (.text ..prefix)]]
  )

(expansion#let [,each_simple (these [bit Bit .co_variant .co_variant]
                                    [decimal Decimal .co_variant .co_variant]
                                    [text Text .co_variant .co_variant]
                                    [tag Tag .co_variant .co_variant]
                                    [slot Slot .co_variant .co_variant]
                                    [macro Macro .co_variant .co_variant]
                                    [eval Eval .co_variant .co_variant])
                ,each_complex (these [i64 I64 .co_variant .co_variant]
                                     [list List .co_variant .co_variant])
                ,each_integer (these [natural Natural .co_variant .co_variant]
                                     [integer Integer .co_variant .co_variant]
                                     [revolution Revolution .co_variant .co_variant])
                ,each_2 (these [sum Sum .co_variant .co_variant]
                               [product Product .co_variant .co_variant]
                               [function Function .contra_variant .co_variant]
                               [array Array .contra_variant .co_variant])
                ,each (these ,each_simple
                             ,each_complex
                             ,each_integer
                             ,each_2)
                ,each_name (with_template [,name ,type ,left_polarity ,right_polarity]
                             [,name]
                             
                             [,each]
                             )]
               (these (with_template [,name ,type ,left_polarity ,right_polarity]
                        [(the .public ,name
                           Identity
                           [#alias (..alias_of_type ,name)
                            #name (..name_of_type ,name)])]

                        [,each]
                        )
                      (with_template [,name ,type ,left_polarity ,right_polarity]
                        [(the .public ,type
                           .Type
                           {.#Nominal (its #name ,name) (.list)})]

                        [,each_simple]
                        )
                      (with_template [,name ,type ,left_polarity ,right_polarity]
                        [(the .public ,type
                           .Type
                           (for_any (_ it)
                             {.#Nominal (its #name ,name) (.list [.co_variant it])}))]

                        [,each_complex]
                        )
                      (with_template [,name ,type ,left_polarity ,right_polarity]
                        [(the .public ,type
                           .Type
                           {.#Nominal (its #name ..i64)
                                      (.list [.co_variant {.#Nominal (its #name ,name) (.list)}])})]

                        [,each_integer]
                        )
                      (with_template [,name ,type ,left_polarity ,right_polarity]
                        [(`` (the .public ,type
                               (<| (template#macro (_ ,left ,right))
                                   [{.#Nominal (,, (name_of_type ,name))
                                               (.list [(,, (static ,left_polarity)) ,left]
                                                      [(,, (static ,right_polarity)) ,right])}])))]

                        [,each_2]
                        )
                      (the .public each_name
                        (property.List .Text)
                        (.list ,each_name))
                      ))

(the .public Name_Of_Module
  ..Text)

(the .public Name
  (And ..Name_Of_Module ..Text))

(`` (the List'
      (<| (template#macro (_ ,it))
          [{.#Nominal (its #name ..list) (.list [(,, (static .co_variant)) ,it])}])))

(every .public (Type _)
  (Variant
   {#Nominal ..Text (..List' [.Polarity (Type _)])}
   {#Parameter .Parameter ..Natural}
   {#Variable ..Natural}
   {#Opaque ..Natural}
   {#Quantification .Quantification (.Stack [(Type _) (Type _)]) (Type _)}
   {#Reification (Type _) (Type _)}
   {#Named ..Name (Type _)}))

(the .public Character
  .Type
  ..Natural)

(the .public Provenance
  .Type
  (And ..Name_Of_Module ..Natural ..Natural))

(the Code'
  (<| (template#macro (_ ,it))
      [(And ..Provenance ,it)]))

(every .public (Code _)
  (Variant
   {#Bit (Code' ..Bit)}
   {#Natural (Code' ..Natural)}
   {#Integer (Code' ..Integer)}
   {#Revolution (Code' ..Revolution)}
   {#Decimal (Code' ..Decimal)}
   {#Text (Code' ..Text)}
   {#Name (Code' ..Name)}
   {#Form (Code' (List' (Code _)))}
   {#Variant (Code' (List' (Code _)))}
   {#Tuple (Code' (List' (Code _)))}))

(the Maybe
  (<| (template#macro (_ ,it))
      [(.Or .Any ,it)]))

(every .public Label
  [(..Maybe [..Natural ..Bit (..List' ..Name)])
   ..Type])

(the Try
  (<| (template#macro (_ ,it))
      [(.Or ..Text ,it)]))

(the Property_List
  (<| (template#macro (_ ,it))
      [(..List' (And ..Text ,it))]))

(every .public Info
  (Record
   [#target ..Text
    #version ..Text
    #mode .Mode
    #configuration (..Property_List ..Text)]))

(the .public Definition
  (And ..Type .Any))

(the .public Default
  Definition)

(the .public Alias
  ..Name)

(every .public Global
  (Variant
   {#Definition Definition}
   {#Alias Alias}
   {#Default Default}))

(every .public Module
  (Record
   [#module_hash ..Natural
    #module_aliases (..Property_List ..Text)
    #definitions (..Property_List [..Bit ..Global])
    #imports (..List' ..Text)
    #module_state .Module_State]))

(every .public (Bindings it)
  (Record
   [#counter ..Natural
    #mappings (..Property_List it)]))

(every .public Scope
  (Record
   [#locals (..Bindings [..Type ..Natural])
    #captured (..Bindings [..Type .Ref])]))

(every .public Type_Context
  (Record
   [#ex_counter ..Natural
    #var_counter ..Natural
    #var_bindings (..List' [..Natural (..Maybe ..Type)])]))

(every .public Lux
  (Record
   [#info ..Info
    #source .Source
    #provenance ..Provenance
    #current_module (Maybe ..Name_Of_Module)
    #modules (..Property_List ..Module)
    #scopes (.Stack ..Scope)
    #type_context ..Type_Context
    #expected (Maybe ..Type)
    #seed ..Natural
    #eval ..Eval]))

(the Meta
  (<| (template#macro (_ ,it))
      [(-> ..Lux
           (Try (.And ..Lux ,it)))]))

(the .public Eval'
  (-> ..Type ..Code
      (Meta Any)))

(the code#bit (.in_module# .prelude .code#bit))
(the code#natural (.in_module# .prelude .code#natural))
(the code#text (.in_module# .prelude .code#text))
(the code#tuple (.in_module# .prelude .code#tuple))
(the code#variant (.in_module# .prelude .code#variant))

(the empty
  ..Code
  (code#variant (.list (code#bit 0b))))

(the (top head tail)
  (-> ..Code ..Code
      ..Code)
  (code#variant (.list (code#bit 1b)
                       head
                       tail)))

(every (Injection it)
  (-> it
      ..Code))

(the (code#stack code it)
  (for_any (_ it)
    (-> (Injection it)
        (Injection (Stack it))))
  (when it
    {.#Empty}
    empty

    {.#Top head tail}
    (top (code head) (code#stack code tail))))

(the (code#argument code [polarity it])
  (for_any (_ it)
    (-> (Injection it)
        (Injection [Bit it])))
  (code#tuple (.list (code#bit polarity)
                     (code it))))

(the ('list code it)
  (for_any (_ it)
    (-> (Injection it)
        (Injection (List it))))
  (` (.list# (,* (list#each code it)))))

(the (nominal code [name parameters])
  (-> (Injection .Type)
      (Injection [Text (List [.Polarity .Type])]))
  (code#variant (.list (code#natural 0)
                       (code#bit 0b)
                       (code#tuple (.list (code#text name)
                                          ('list (code#argument code) parameters))))))

(the (code it)
  (Injection .Type)
  (`` (when it
        {.#Nominal it}
        (nominal code it)

        {.#Parameter parameter it}
        (code#variant (.list (code#natural 1)
                             (code#bit 0b)
                             (code#bit parameter)
                             (code#natural it)))

        (,, (with_template [,lefts ,tag]
              [{,tag it}
               (code#variant (.list (code#natural ,lefts)
                                    (code#bit 0b)
                                    (code#natural it)))]

              [[2 .#Variable]
               [3 .#Opaque]]
              ))

        {.#Quantification quantification closure body}
        (code#variant (.list (code#natural 4)
                             (code#bit 0b)
                             (code#tuple (.list (code#bit quantification)
                                                (code#stack (.function (_ [abstraction argument])
                                                              (` [(, (code abstraction))
                                                                  (, (code argument))]))
                                                            closure)
                                                (code body)))))
        
        {.#Reification left right}
        (code#variant (.list (code#natural 5)
                             (code#bit 0b)
                             (code#tuple (.list (code left)
                                                (code right)))))

        {.#Named [module proper] anonymous}
        (code#variant (.list (code#natural 5)
                             (code#bit 1b)
                             (code#tuple (.list (code#tuple (.list (code#text module)
                                                                   (code#text proper)))
                                                (code anonymous)))))
        )))

(the .public each_type
  (property.List ..Code)
  (|> (.list ["label" ..Label]
             ["name_of_module" ..Name_Of_Module]
             ["name" ..Name]
             ["provenance" ..Provenance]
             ["code" ..Code]
             ["type" ..Type]
             ["info" ..Info]
             ["definition" ..Definition]
             ["default" ..Default]
             ["alias" ..Alias]
             ["global" ..Global]
             ["module" ..Module]
             ["bindings" ..Bindings]
             ["scope" ..Scope]
             ["type_context" ..Type_Context]
             ["lux" ..Lux]
             ["eval" ..Eval'])
      (is (property.List .Type))
      (list#each (.function (_ [name type])
                   [name (..code type)]))))
