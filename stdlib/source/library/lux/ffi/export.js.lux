(.require
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["<>" parser]]
   [data
    [text
     ["%" \\format]]
    [collection
     ["[0]" list (.use "[1]#[0]" monad mix)]
     ["[0]" set]]]
   ["[0]" meta (.only)
    [extension (.only declaration)]
    ["[0]" code (.only)
     ["<[1]>" \\parser]]
    [macro
     [syntax (.only syntax)]
     ["[0]" expansion]]
    [target
     ["/" js]]
    [compiler
     ["[0]" phase]
     [meta
      [cache
       ["[0]" dependency
        ["[1]" artifact]]]]
     [language
      [lux
       ["[0]" translation]
       ["[0]" declaration]
       [analysis
        ["[0]" type]]]]]]]])

(def definition
  (-> Code (Meta [Text Code]))
  (|>> (list)
       (<code>.result (<| <code>.form
                          (<>.after (<code>.this_symbol (symbol .def#)))
                          (<>.before <code>.any)
                          (all <>.and
                               <code>.local
                               <code>.any)))
       meta.of_try))

(def .public export_one
  (declaration (_ phase archive [name <code>.text
                                 term <code>.any])
    (do [! phase.monad]
      [next declaration.analysis
       [_ term] (<| declaration.of_analysis
                    type.inferring
                    (next archive term))

       next declaration.synthesis
       term (declaration.of_synthesis
             (next archive term))

       dependencies (declaration.of_translation
                     (dependency.dependencies archive term))

       next declaration.translation
       [interim_artifacts term] (declaration.of_translation
                                 (translation.with_interim_artifacts archive
                                   (next archive term)))

       _ (declaration.of_translation
          (do !
            [@self (translation.learn_custom name (list#mix set.has dependencies interim_artifacts))
             .let [$module (/.var "module")
                   $exports (/.the "exports" $module)
                   definition (/.define (/.var name) term)
                   export (/.when (/.not (/.= (/.string "undefined") (/.type_of $module)))
                            (/.statement (/.set (/.the name $exports) (/.var name))))
                   code (all /.then
                             definition
                             export)]
             _ (translation.execute! definition)
             _ (translation.save! @self {.#None} code)]
            (translation.log! (%.format "Export " (%.text name)))))]
      (in declaration.no_requirements))))

(def .public export
  (syntax (_ [exports (<>.many <code>.any)])
    (let [! meta.monad]
      (|> exports
          (monad.each ! expansion.complete)
          (at ! each (|>> list#conjoint
                          (monad.each ! ..definition)))
          (at ! conjoint)
          (at ! each (list#each (function (_ [name term])
                                  (` (..export_one (, (code.text name)) (, term))))))))))
