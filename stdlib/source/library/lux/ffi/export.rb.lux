... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except global)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["<>" projection]]
   [data
    ["[0]" text
     ["%" \\injection]
     ["<[1]>" \\projection (.only Projection)]]
    [collection
     ["[0]" list (.use "[1]#[0]" monad mix)]
     ["[0]" set]]]
   ["[0]" meta (.only)
    [extension (.only declaration)]
    ["[0]" type]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro
     ["[0]" syntax]
     ["[0]" expansion]]
    [compiler
     [target
      ["/" ruby]]
     [meta
      [cache
       ["[0]" dependency
        ["[1]" artifact]]]]
     [language
      [lux
       ["[0]" phase]
       ["[0]" translation]
       ["[0]" declaration]
       ["[0]" analysis
        ["[1]" type]]]]]]]])

(the upper! (<text>.one_of! "ABCDEFGHIJKLMNOPQRSTUVWXYZ"))
(the lower! (<text>.one_of! "abcdefghijklmnopqrstuvwxyz"))
(the decimal! (<text>.one_of! "0123456789"))
(the sigil! (<text>.one_of! "_"))

(the tail!
  (all <>.either
       ..upper!
       ..lower!
       ..decimal!
       ..sigil!
       ))

(with_template [<name> <head>]
  [(the <name>
     (Projection Text)
     (<| <text>.slice
         (<text>.and! <head>)
         (<text>.some! ..tail!)))]

  [method ..lower!]
  [global (<text>.one_of! "$")]
  [constant ..upper!]
  )

(every Name
  (Variant
   {#Method Text}
   {#Global Text}))

(the name
  (Projection Name)
  (<>.or ..method
         (<>.either ..global
                    ..constant)))

(the definition
  (-> Code (Meta [Name Code]))
  (|>> (list)
       (<code>.value (<| <code>.form
                         (<>.after (<code>.this_symbol (symbol .def#)))
                         (<>.before <code>.any)
                         (all <>.and
                              (<text>.then ..name <code>.local)
                              <code>.any)))
       meta.of_try))

(the .public export_one
  (declaration (_ phase archive [global? <code>.bit
                                 name <code>.text
                                 term <code>.any])
    (do [! phase.monad]
      [next declaration.analysis
       [type term] (<| declaration.of_analysis
                       analysis.inferring
                       (next archive term))

       lux (declaration.of_analysis meta.compiler_state)
       next declaration.synthesis
       term (declaration.of_synthesis
             (next lux archive term))

       dependencies (declaration.of_translation
                     (dependency.dependencies archive term))

       next declaration.translation
       [interim_artifacts term] (declaration.of_translation
                                 (translation.with_interim_artifacts archive
                                   (next lux archive term)))

       _ (declaration.of_translation
          (do !
            [@self (translation.learn_custom name (list#mix set.has dependencies interim_artifacts))
             .let [[:input:/* :output:] (type.flat_function type)
                   code (if global?
                          (/.set (list (/.manual name)) term)
                          (when :input:/*
                            {.#End}
                            (/.function (/.manual name) (list)
                              (/.return term))

                            _
                            (/.statement (/.apply (list (/.string name) term) {.#None}
                                                  (/.manual "define_method")))))]
             _ (translation.execute! code)
             _ (translation.save! @self {.#None} code)]
            (translation.log! (%.message "Export " (%.text name)))))]
      (in declaration.no_requirements))))

(the .public export
  (syntax.macro (_ [exports (<>.many <code>.any)])
    (let [! meta.monad]
      (|> exports
          (monad.each ! expansion.complete)
          (of ! each (|>> list#conjoint
                          (monad.each ! ..definition)))
          (of ! conjoint)
          (of ! each (list#each (function (_ [name term])
                                  (` (export_one (,* (when name
                                                       {#Method name}
                                                       (list (code.bit false) (code.text name))
                                                       
                                                       {#Global name}
                                                       (list (code.bit true) (code.text name))))
                                                 (, term))))))))))
