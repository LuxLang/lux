(.module:
  [library
   [lux (#- int char)
    [abstract
     [equivalence (#+ Equivalence)]
     [hash (#+ Hash)]]
    [data
     ["." text ("#\." hash)
      ["%" format (#+ format)]]
     [collection
      ["." list ("#\." functor)]]]
    [type
     abstract]]]
  ["." // #_
   [category (#+ Void Value Return Method Primitive Object Class Array Var Parameter Declaration)]
   ["#." descriptor]
   ["/#" // #_
    [encoding
     ["#." name (#+ External)]]]])

(abstract: #export (Signature category)
  Text

  (def: #export signature
    (-> (Signature Any) Text)
    (|>> :representation))

  (template [<category> <name> <descriptor>]
    [(def: #export <name>
       (Signature <category>)
       (:abstraction (//descriptor.descriptor <descriptor>)))]

    [Void void //descriptor.void]
    [Primitive boolean //descriptor.boolean]
    [Primitive byte //descriptor.byte]
    [Primitive short //descriptor.short]
    [Primitive int //descriptor.int]
    [Primitive long //descriptor.long]
    [Primitive float //descriptor.float]
    [Primitive double //descriptor.double]
    [Primitive char //descriptor.char]
    )

  (def: #export array
    (-> (Signature Value) (Signature Array))
    (|>> :representation
         (format //descriptor.array_prefix)
         :abstraction))

  (def: #export wildcard
    (Signature Parameter)
    (:abstraction "*"))

  (def: #export var_prefix "T")
  
  (def: #export var
    (-> Text (Signature Var))
    (|>> (text.enclose [..var_prefix //descriptor.class_suffix])
         :abstraction))

  (def: #export var_name
    (-> (Signature Var) Text)
    (|>> :representation
         (text.replace_all ..var_prefix "")
         (text.replace_all //descriptor.class_suffix "")))

  (def: #export lower_prefix "-")
  (def: #export upper_prefix "+")
  
  (template [<name> <prefix>]
    [(def: #export <name>
       (-> (Signature Class) (Signature Parameter))
       (|>> :representation (format <prefix>) :abstraction))]

    [lower ..lower_prefix]
    [upper ..upper_prefix]
    )

  (def: #export parameters_start "<")
  (def: #export parameters_end ">")

  (def: #export (class name parameters)
    (-> External (List (Signature Parameter)) (Signature Class))
    (:abstraction
     (format //descriptor.class_prefix
             (|> name ///name.internal ///name.read)
             (case parameters
               #.End
               ""

               _
               (format ..parameters_start
                       (|> parameters
                           (list\map ..signature)
                           (text.join_with ""))
                       ..parameters_end))
             //descriptor.class_suffix)))

  (def: #export (declaration name variables)
    (-> External (List (Signature Var)) (Signature Declaration))
    (:transmutation (..class name variables)))

  (def: #export as_class
    (-> (Signature Declaration) (Signature Class))
    (|>> :transmutation))

  (def: #export arguments_start "(")
  (def: #export arguments_end ")")

  (def: #export exception_prefix "^")

  (def: class_bound
    (|> (..class "java.lang.Object" (list))
        ..signature
        (format ":")))

  (def: #export (method [type_variables inputs output exceptions])
    (-> [(List (Signature Var))
         (List (Signature Value))
         (Signature Return)
         (List (Signature Class))]
        (Signature Method))
    (:abstraction
     (format (case type_variables
               #.End
               ""
               _
               (|> type_variables
                   (list\map (|>> ..var_name
                                  (text.suffix ..class_bound)))
                   (text.join_with "")
                   (text.enclose [..parameters_start
                                  ..parameters_end])))
             (|> inputs
                 (list\map ..signature)
                 (text.join_with "")
                 (text.enclose [..arguments_start
                                ..arguments_end]))
             (:representation output)
             (|> exceptions
                 (list\map (|>> :representation (format ..exception_prefix)))
                 (text.join_with "")))))

  (implementation: #export equivalence
    (All [category] (Equivalence (Signature category)))
    
    (def: (= parameter subject)
      (text\= (:representation parameter)
              (:representation subject))))

  (implementation: #export hash
    (All [category] (Hash (Signature category)))

    (def: &equivalence ..equivalence)
    (def: hash (|>> :representation text\hash)))
  )
