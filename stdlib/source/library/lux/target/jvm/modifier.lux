... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["<>" projection]
    ["[0]" equivalence (.only Equivalence)]
    ["[0]" monoid (.only Monoid)]]
   [error
    ["[0]" try]]
   [data
    ["[0]" binary
     ["[1]F" \\injection (.only Injection)]]]
   [math
    ["[0]" number (.only hex)
     ["[0]" i64]]]
   [macro (.only with_names)
    ["[0]" syntax]
    ["[0]" template]]
   [type
    ["[0]" nominal]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]]]
 ["[0]" //
  [encoding
   ["[1][0]" unsigned]]])

(nominal.every .public (Modifier it)
  //unsigned.U2

  (the .public code
    (-> (Modifier Any)
        //unsigned.U2)
    (|>> nominal.reification))

  (the .public equivalence
    (for_any (_ it)
      (Equivalence (Modifier it)))
    (implementation
     (the (= expected actual)
       (by //unsigned.equivalence =
           (nominal.reification expected)
           (nominal.reification actual)))))

  (alias [=]
         ..equivalence)

  (the !abstraction
    (template.macro (_ value)
      [(|> value
           //unsigned.u2
           try.trusted
           nominal.abstraction)]))

  (the !representation
    (template.macro (_ value)
      [(|> value
           nominal.reification
           //unsigned.value)]))

  (the .public (has? sub super)
    (for_any (_ it)
      (-> (Modifier it) (Modifier it)
          Bit))
    (let [sub (!representation sub)]
      (|> (!representation super)
          (i64.and sub)
          (by i64.equivalence = sub))))

  (the .public monoid
    (for_any (_ it)
      (Monoid (Modifier it)))
    (implementation
     (the identity
       (!abstraction
        (hex "0000")))
     
     (the (composite left right)
       (!abstraction
        (i64.or (!representation left)
                (!representation right))))))

  (the .public empty
    Modifier
    (by ..monoid identity))

  (the .public as_binary
    (for_any (_ it)
      (Injection (Modifier it)))
    (|>> nominal.reification
         //unsigned.as_binary/2))
  )

(the .public modifiers
  (syntax.macro (_ [itT <code>.any
                    options (<>.many <code>.any)])
    (with_names ['modifier 'code]
      (in (stack (` (template.with [(, 'code) (, 'modifier)]
                      [(the (,' .public) (, 'modifier)
                         (..Modifier (, itT))
                         (|> (number.hex (, 'code))
                             //unsigned.u2
                             try.trusted
                             as_expected))]
                      
                      (,* options))))))))
