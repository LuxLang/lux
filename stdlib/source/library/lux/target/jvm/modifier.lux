(.module:
  [library
   [lux #*
    [abstract
     ["." equivalence (#+ Equivalence)]
     ["." monoid (#+ Monoid)]]
    [control
     ["." try]
     ["<>" parser
      ["<c>" code]]]
    [data
     [format
      [".F" binary (#+ Writer)]]]
    [macro (#+ with_gensyms)
     [syntax (#+ syntax:)]
     ["." code]]
    [math
     ["." number (#+ hex)
      ["." i64]]]
    [type
     abstract]]]
  ["." // #_
   [encoding
    ["#." unsigned]]])

(abstract: .public (Modifier of)
  {}
  
  //unsigned.U2

  (def: .public code
    (-> (Modifier Any) //unsigned.U2)
    (|>> :representation))

  (implementation: .public equivalence
    (All [of] (Equivalence (Modifier of)))
    
    (def: (= reference sample)
      (\ //unsigned.equivalence =
         (:representation reference)
         (:representation sample))))

  (template: (!wrap value)
    [(|> value
         //unsigned.u2
         try.assumed
         :abstraction)])

  (template: (!unwrap value)
    [(|> value
         :representation
         //unsigned.value)])

  (def: .public (has? sub super)
    (All [of] (-> (Modifier of) (Modifier of) Bit))
    (let [sub (!unwrap sub)]
      (|> (!unwrap super)
          (i64.and sub)
          (\ i64.equivalence = sub))))

  (implementation: .public monoid
    (All [of] (Monoid (Modifier of)))

    (def: identity
      (!wrap (hex "0000")))
    
    (def: (compose left right)
      (!wrap (i64.or (!unwrap left) (!unwrap right)))))

  (def: .public empty
    Modifier
    (\ ..monoid identity))

  (def: .public writer
    (All [of] (Writer (Modifier of)))
    (|>> :representation //unsigned.writer/2))

  (def: modifier
    (-> Nat Modifier)
    (|>> !wrap))
  )

(syntax: .public (modifiers: ofT {options (<>.many <c>.any)})
  (with_gensyms [g!modifier g!code]
    (in (list (` (template [(~ g!code) (~ g!modifier)]
                   [(def: (~' .public) (~ g!modifier)
                      (..Modifier (~ ofT))
                      ((~! ..modifier) ((~! number.hex) (~ g!code))))]
                   
                   (~+ options)))))))
