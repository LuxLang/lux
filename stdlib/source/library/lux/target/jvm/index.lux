... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" equivalence (.only Equivalence)]]
   [data
    [binary
     [\\injection (.only Injection)]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]
    [number
     ["n" natural]]]
   [type
    ["[0]" nominal]]]]
 ["[0]" //
  [encoding
   ["[1]" unsigned (.only U2)]]])

(the .public length
  //.bytes/2)

(nominal.every .public (Index it)
  U2

  (the .public index
    (for_any (_ it)
      (-> U2
          (Index it)))
    (|>> nominal.abstraction))

  (the .public value
    (-> (Index Any)
        U2)
    (|>> nominal.reification))

  (the .public equivalence
    (for_any (_ it)
      (Equivalence (Index it)))
    (by equivalence.functor each
        ..value
        //.equivalence))

  (alias [=]
         ..equivalence)

  (the .public as_binary
    (for_any (_ it)
      (Injection (Index it)))
    (|>> nominal.reification
         //.as_binary/2))
  )

(the .public random
  (for_any (_ it)
    (Random (Index it)))
  (let [maximum (//.value //.maximum/2)]
    (|> random.natural
        (random#each (n.% (++ maximum)))
        (random.one //.u2)
        (random#each ..index))))
