(.module:
  [library
   [lux #*
    [data
     ["." text
      ["%" format (#+ format)]]]
    [type
     abstract]]])

(def: .public internal_separator "/")
(def: .public external_separator ".")

(type: .public External Text)

(abstract: .public Internal
  {}
  
  Text

  (def: .public internal
    (-> External Internal)
    (|>> (text.replace_all ..external_separator
                           ..internal_separator)
         :abstraction))

  (def: .public read
    (-> Internal Text)
    (|>> :representation))

  (def: .public external
    (-> Internal External)
    (|>> :representation
         (text.replace_all ..internal_separator
                           ..external_separator))))

(def: .public safe
  (-> Text External)
  (|>> ..internal ..external))

(def: .public (qualify package class)
  (-> Text External External)
  (format (..safe package) ..external_separator class))
