(.module:
  [library
   [lux #*
    [data
     ["." text
      ["%" format (#+ format)]]]
    [type
     abstract]]])

(def: #export internal_separator "/")
(def: #export external_separator ".")

(type: #export External Text)

(abstract: #export Internal
  Text

  (def: #export internal
    (-> External Internal)
    (|>> (text.replace_all ..external_separator
                           ..internal_separator)
         :abstraction))

  (def: #export read
    (-> Internal Text)
    (|>> :representation))

  (def: #export external
    (-> Internal External)
    (|>> :representation
         (text.replace_all ..internal_separator
                           ..external_separator))))

(def: #export safe
  (-> Text External)
  (|>> ..internal ..external))

(def: #export (qualify package class)
  (-> Text External External)
  (format (..safe package) ..external_separator class))
