... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Name name)
   [abstract
    ["[0]" equivalence (.only Equivalence)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]]
   [type
    ["[0]" nominal]]]]
 [//
  ["[0]" external]
  [///
   [type
    ["[0]" box]]]])

... https://en.wikipedia.org/wiki/Delimiter
(the .public delimiter "/")

(nominal.every .public Name
  Text

  (the .public name
    (-> external.Name
        Name)
    (|>> (text.replaced external.delimiter
                        ..delimiter)
         nominal.abstraction))

  (the .public value
    (-> Name
        Text)
    (|>> nominal.reification))

  (the .public equivalence
    (Equivalence Name)
    (by equivalence.functor each ..value text.equivalence))

  (alias [=]
         ..equivalence)

  (the .public external
    (-> Name
        external.Name)
    (|>> nominal.reification
         (text.replaced ..delimiter
                        external.delimiter))))

(the .public safe
  (-> Text
      external.Name)
  (|>> ..name
       ..external))

(the .public random
  (Random Name)
  (random#each ..name box.random))
