... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... https://docs.oracle.com/javase/specs/jvms/se7/html/jvms-4.html#jvms-4.7.12
(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" product]
    ["[0]" binary
     ["![1]" \\injection (.only Injection) (.use "[1]#[0]" monoid)]]
    [collection
     ["[0]" sequence (.only Sequence)]]]
   [math
    [number
     ["n" natural]
     ["[0]" /16
      ["[1]" natural]]]]]])

(every .public Entry
  (Record
   [#start_program_counter /16.Number
    #line_number /16.Number]))

(the entry_length
  Natural
  (all n.+
       ... u2 start_pc;
       /16.size
       ... u2 line_number;
       /16.size
       ))

(the entry_equivalence
  (Equivalence Entry)
  (all product.equivalence
       /16.equivalence
       /16.equivalence
       ))

(the (entry_as_binary it)
  (Injection Entry)
  (all !binary#composite
       (/16.as_binary (its #start_program_counter it))
       (/16.as_binary (its #line_number it))
       ))

(every .public Line_Number_Table
  (Sequence Entry))

(the .public empty
  Line_Number_Table
  sequence.empty)

(the .public (length it)
  (-> Line_Number_Table
      Natural)
  (all n.+
       ... u2 line_number_table_length;
       /16.size
       ... line_number_table[line_number_table_length];
       (n.x entry_length (sequence.size it))
       ))

(the .public equivalence
  (Equivalence Line_Number_Table)
  (sequence.equivalence entry_equivalence))

(alias [=]
       ..equivalence)

(the .public as_binary
  (Injection Line_Number_Table)
  (!binary.sequence_16 entry_as_binary))
