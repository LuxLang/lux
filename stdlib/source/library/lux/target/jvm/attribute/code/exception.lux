... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" product]
    [binary
     ["[0]" \\injection (.only Injection)]]]
   [math
    [number
     ["n" natural]
     [natural
      ["[0]" /16]]]]]]
 ["[0]" //
  ["//[1]" ///
   ["[1][0]" index (.only Index)]
   [constant
    [class (.only Class)]]
   [bytecode
    ["[1][0]" address (.only Address)]]]])

(every .public Exception
  (Record
   [#start Address
    #end Address
    #handler Address
    #catch (Index Class)]))

(the .public equivalence
  (Equivalence Exception)
  (all product.equivalence
       ////address.equivalence
       ////address.equivalence
       ////address.equivalence
       ////index.equivalence
       ))

(alias [=]
       ..equivalence)

... https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-4.html#jvms-4.7.3
(the .public length
  Natural
  (all n.+
       ... u2 start_pc;
       ////address.length
       ... u2 end_pc;
       ////address.length
       ... u2 handler_pc;
       ////address.length
       ... u2 catch_type;
       ////index.length
       ))

(the .public as_binary
  (Injection Exception)
  (all \\injection.and
       ////address.as_binary
       ////address.as_binary
       ////address.as_binary
       ////index.as_binary
       ))
