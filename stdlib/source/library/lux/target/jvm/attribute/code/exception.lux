(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" product]
    ["[0]" format
     ["[1]" binary (.only Writer)]]]
   [math
    [number
     ["n" nat]]]]]
 ["[0]" //
  ["//[1]" ///
   [constant (.only Class)]
   ["[1][0]" index (.only Index)]
   [bytecode
    ["[1][0]" address (.only Address)]]
   [encoding
    ["[1][0]" unsigned (.only U2)]]]])

(type: .public Exception
  (Record
   [#start Address
    #end Address
    #handler Address
    #catch (Index Class)]))

(def: .public equivalence
  (Equivalence Exception)
  (all product.equivalence
       ////address.equivalence
       ////address.equivalence
       ////address.equivalence
       ////index.equivalence
       ))

... https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-4.html#jvms-4.7.3
(def: .public length
  Nat
  (all n.+
       ... u2 start_pc;
       ////unsigned.bytes/2
       ... u2 end_pc;
       ////unsigned.bytes/2
       ... u2 handler_pc;
       ////unsigned.bytes/2
       ... u2 catch_type;
       ////unsigned.bytes/2
       ))

(def: .public writer
  (Writer Exception)
  (all format.and
       ////address.writer
       ////address.writer
       ////address.writer
       ////index.writer
       ))
