... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Type Declaration Parameter Array)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [control
    ["[0]" maybe]]
   [data
    ["[0]" text (.only Injection)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    [number
     ["n" natural]]]
   [macro
    ["[0]" template]]
   [type
    ["[0]" nominal]]]]
 [//
  [encoding
   [name
    ["[0]" external]
    ["[0]" internal]]]]
 ["[0]" /
  ["[0]" category]
  ["[1][0]" signature (.only Signature)]
  ["[1][0]" descriptor (.only Descriptor)]
  ["[1][0]" reflection (.only Reflection)]])

(nominal.every .public (Type category)
  [(Signature category)
   (Descriptor category)
   (Reflection category)]

  (template.with [,name]
    [(`` (every .public ,name
           (Type (,, (template.name [category._] [,name])))))]

    [Void]
    [Primitive]
    
    [Array]
    [Class]
    [Object]

    [Var]
    [Parameter]

    [Value]
    [Return]

    [Declaration]
    [Method]
    )

  (every .public Argument
    [Text ..Value])

  (every .public (Typed it)
    (Record
     [#type ..Value
      #term it]))

  (every .public Constraint
    (Record
     [#name Text
      #super_class ..Class
      #super_interfaces (List ..Class)]))
  
  (template.with [<name> <style>]
    [(the .public (<name> type)
       (for_any (_ category)
         (-> (Type category)
             (<style> category)))
       (let [[signature descriptor reflection] (nominal.as type)]
         <name>))]

    [signature Signature]
    [descriptor Descriptor]
    )

  (the .public (reflection type)
    (for_any (_ category)
      (-> (Type (category.Value category))
          (Reflection (category.Value category))))
    (let [[signature descriptor reflection] (nominal.as type)]
      reflection))

  (template.with [<category> <name> <signature> <descriptor> <reflection>]
    [(the .public <name>
       <category>
       (nominal.of
        [<signature>
         <descriptor>
         <reflection>]))]

    [..Void void /signature.void /descriptor.void /reflection.void]
    [..Primitive boolean /signature.boolean /descriptor.boolean /reflection.boolean]
    [..Primitive byte /signature.byte /descriptor.byte /reflection.byte]
    [..Primitive short /signature.short /descriptor.short /reflection.short]
    [..Primitive int /signature.int /descriptor.int /reflection.int]
    [..Primitive long /signature.long /descriptor.long /reflection.long]
    [..Primitive float /signature.float /descriptor.float /reflection.float]
    [..Primitive double /signature.double /descriptor.double /reflection.double]
    [..Primitive char /signature.char /descriptor.char /reflection.char]
    )

  (the .public (array type)
    (-> ..Value
        ..Array)
    (nominal.of
     [(/signature.array (..signature type))
      (/descriptor.array (..descriptor type))
      (/reflection.array (..reflection type))]))

  (the .public (class parameters name)
    (-> (List ..Parameter) external.Name
        ..Class)
    (nominal.of
     [(/signature.class (list#each ..signature parameters) name)
      (/descriptor.class name)
      (/reflection.class name)]))

  (the .public (declaration variables name)
    (-> (List ..Var) external.Name
        ..Declaration)
    (nominal.of
     [(/signature.declaration (list#each ..signature variables) name)
      (/descriptor.declaration name)
      (/reflection.declaration name)]))

  (the .public (as_class type)
    (-> ..Declaration
        ..Class)
    (nominal.of
     (let [[signature descriptor reflection] (nominal.as type)]
       [(/signature.as_class signature)
        (/descriptor.as_class descriptor)
        (/reflection.as_class reflection)])))

  (the .public wildcard
    ..Parameter
    (nominal.of
     [/signature.wildcard
      /descriptor.wildcard
      /reflection.wildcard]))

  (the .public (var name)
    (-> Text
        ..Var)
    (nominal.of
     [(/signature.var name)
      /descriptor.var
      /reflection.var]))

  (the .public (lower bound)
    (-> ..Parameter
        ..Parameter)
    (nominal.of
     (let [[signature descriptor reflection] (nominal.as bound)]
       [(/signature.lower signature)
        (/descriptor.lower descriptor)
        (/reflection.lower reflection)])))

  (the .public (upper bound)
    (-> ..Parameter
        ..Parameter)
    (nominal.of
     (let [[signature descriptor reflection] (nominal.as bound)]
       [(/signature.upper signature)
        (/descriptor.upper descriptor)
        (/reflection.upper reflection)])))

  (the .public (method [type_variables inputs output exceptions])
    (-> [(List ..Var)
         (List ..Value)
         ..Return
         (List ..Class)]
        ..Method)
    (nominal.of
     [(/signature.method [(list#each ..signature type_variables)
                          (list#each ..signature inputs)
                          (..signature output)
                          (list#each ..signature exceptions)])
      (/descriptor.method [(list#each ..descriptor inputs)
                           (..descriptor output)])
      (as_expected ..void)]))

  (the .public equivalence
    (for_any (_ category)
      (Equivalence (Type category)))
    (implementation
     (the (= parameter subject)
       (by /signature.equivalence =
           (..signature parameter)
           (..signature subject)))))

  (alias [=]
         ..equivalence)

  (the .public hash
    (for_any (_ category)
      (Hash (Type category)))
    (implementation
     (the equivalence ..equivalence)
     (the hash (|>> ..signature (by /signature.hash hash)))))

  (the .public (primitive? type)
    (-> ..Value
        (Either ..Object
                ..Primitive))
    (if (`` (or (,, (template.with [<type>]
                      [(by ..equivalence = (is ..Value <type>) type)]
                      
                      [..boolean]
                      [..byte]
                      [..short]
                      [..int]
                      [..long]
                      [..float]
                      [..double]
                      [..char]))))
        (|> type (as (-> ..Value ..Primitive)) {.#Right})
        (|> type (as (-> ..Value ..Object)) {.#Left})))

  (the .public (void? type)
    (-> ..Return
        (Either ..Value
                ..Void))
    (if (`` (or (,, (template.with [<type>]
                      [(by ..equivalence = (is ..Return <type>) type)]
                      
                      [..void]))))
        (|> type (as (-> ..Return ..Void)) {.#Right})
        (|> type (as (-> ..Return ..Value)) {.#Left})))
  )

(the .public (class? type)
  (-> ..Value
      (Maybe external.Name))
  (let [repr (|> type ..descriptor /descriptor.descriptor)]
    (if (and (text.starts_with? /descriptor.class_prefix repr)
             (text.ends_with? /descriptor.class_suffix repr))
        (let [prefix_size (text.size /descriptor.class_prefix)
              suffix_size (text.size /descriptor.class_suffix)
              name_size (|> (text.size repr)
                            (n.- prefix_size)
                            (n.- suffix_size))]
          (|> repr
              (text.clip prefix_size name_size)
              (by maybe.monad each (|>> internal.name internal.external))))
        {.#None})))

(the .public as_text
  (for_any (_ it)
    (Injection (Type it)))
  (|>> ..signature
       /signature.signature))
