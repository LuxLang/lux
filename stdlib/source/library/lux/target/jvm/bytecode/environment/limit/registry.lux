(.module:
  [library
   [lux (#- Type for static)
    [abstract
     ["." equivalence (#+ Equivalence)]]
    [control
     ["." try (#+ Try) ("#\." functor)]]
    [data
     [format
      [binary (#+ Writer)]]
     [collection
      ["." list ("#\." functor fold)]]]
    [math
     [number
      ["n" nat]]]
    [type
     abstract]]]
  ["." ///// #_
   [encoding
    ["#." unsigned (#+ U1 U2)]]
   ["#." type (#+ Type)
    [category (#+ Method)]
    ["#/." parser]]])

(type: .public Register U1)

(def: normal 1)
(def: wide 2)

(abstract: .public Registry
  {}
  
  U2

  (def: .public registry
    (-> U2 Registry)
    (|>> :abstraction))

  (def: (minimal type)
    (-> (Type Method) Nat)
    (let [[type_variables inputs output exceptions] (/////type/parser.method type)]
      (|> inputs
          (list\map (function (_ input)
                      (if (or (is? /////type.long input)
                              (is? /////type.double input))
                        ..wide
                        ..normal)))
          (list\fold n.+ 0))))

  (template [<start> <name>]
    [(def: .public <name>
       (-> (Type Method) (Try Registry))
       (|>> ..minimal
            (n.+ <start>)
            /////unsigned.u2
            (try\map ..registry)))]

    [0 static]
    [1 virtual]
    )

  (def: .public equivalence
    (Equivalence Registry)
    (\ equivalence.functor map
       (|>> :representation)
       /////unsigned.equivalence))

  (def: .public writer
    (Writer Registry)
    (|>> :representation /////unsigned.writer/2))

  (def: .public (has needed)
    (-> Registry Registry Registry)
    (|>> :representation
         (/////unsigned.max/2 (:representation needed))
         :abstraction))

  (template [<name> <extra>]
    [(def: .public <name>
       (-> Register Registry)
       (let [extra (|> <extra> /////unsigned.u2 try.assumed)]
         (|>> /////unsigned.lift/2
              (/////unsigned.+/2 extra)
              try.assumed
              :abstraction)))]

    [for ..normal]
    [for_wide ..wide]
    )
  )

(def: .public length
  /////unsigned.bytes/2)
