... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Type for)
   [abstract
    [equivalence (.only Equivalence)]]
   [error
    ["[0]" try]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number
     ["[0]" /16
      ["[1]" natural]]]]
   [macro
    ["[0]" template]]]]
 [/////
  ["[0]" type (.only Type)]])

(every .public Datum
  /16.Number)

(template.with [,value ,name]
  [(the .public ,name
     Datum
     (/16.of ,value))]

  [[1 small]
   [2 big]])

(alias [boolean byte short int
        char
        float
        object]
       ..small)

(alias [long double]
       ..big)

(the .public equivalence
  (Equivalence Datum)
  /16.equivalence)

(alias [=]
       ..equivalence)

(the .public random
  (Random Datum)
  (all random.either
       (random#pure ..small)
       (random#pure ..big)
       ))

(the .public (for it)
  (for_any (_ it)
    (-> (Type it)
        Datum))
  (if (or (identical? (is (Type Any) it) (is (Type Any) type.long))
          (identical? (is (Type Any) it) (is (Type Any) type.double)))
      ..big
      ..small))
