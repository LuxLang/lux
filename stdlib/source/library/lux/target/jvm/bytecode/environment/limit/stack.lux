... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Stack)
   [abstract
    ["[0]" equivalence (.only Equivalence)]]
   [control
    ["[0]" maybe]]
   [error
    ["[0]" try (.only Try)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    ["[0]" binary
     ["[1]" \\injection]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number
     [natural
      ["[0]" /16]]]]
   [macro
    ["[0]" template]]
   [type
    ["[0]" nominal]]]]
 [//
  [datum (.only Datum)]])

(the Reification /16.Number)

(nominal.every .public Stack
  Reification

  (template.with [<datums> <name>]
    [(the .public <name>
       Stack
       (|> <datums>
           /16.of
           nominal.abstraction))]

    [0 empty]
    [1 catch]
    )

  (the .public equivalence
    (Equivalence Stack)
    (by equivalence.functor each
        (|>> nominal.reification)
        /16.equivalence))

  (alias [=]
         ..equivalence)

  (the .public as_binary
    (binary.Injection Stack)
    (|>> nominal.reification
         /16.as_binary))

  (template.with [<op> <name>]
    [(the .public (<name> amount)
       (-> Datum Stack
           Stack)
       (|>> nominal.reification
            (<op> amount)
            nominal.abstraction))]

    [/16.+ push]
    [/16.- pop]
    )

  (the .public (major left right)
    (-> Stack Stack
        Stack)
    (nominal.abstraction
     (/16.major (nominal.reification left)
                (nominal.reification right))))

  (the .public as_text
    (text.Injection Stack)
    (|>> nominal.reification
         /16.as
         %.natural))
  )

(the .public length
  /16.size)

(the .public random
  (Random Stack)
  (all random.either
       (random#in ..empty)
       (random#in ..catch)
       ))
