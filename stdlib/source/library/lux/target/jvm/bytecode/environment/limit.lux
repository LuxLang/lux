... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Type Stack
                static)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" product]
    [binary
     ["[0]" \\injection (.only Injection) (.use "[1]#[0]" monoid)]]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [macro
    ["[0]" template]]]]
 ["[0]" /
  ["[1][0]" stack (.only Stack)]
  ["[1][0]" registry (.only Registry)]
  [////
   [type (.only Type)
    [category (.only Method)]]]])

(every .public Limit
  (Record
   [#stack Stack
    #registry Registry]))

(the .public random
  (Random Limit)
  (all random.and
       /stack.random
       /registry.random
       ))

(template.with [<name> <registry>]
  [(the .public (<name> type)
     (-> (Type Method)
         Limit)
     [#stack /stack.empty
      #registry (<registry> type)])]

  [[static /registry.static]
   [virtual /registry.virtual]])

(the .public length
  (all n.+
       ... u2 max_stack;
       /stack.length
       ... u2 max_locals;
       /registry.length
       ))

(the .public equivalence
  (Equivalence Limit)
  (all product.equivalence
       /stack.equivalence
       /registry.equivalence
       ))

(alias [=]
       ..equivalence)

(the .public (as_binary limit)
  (Injection Limit)
  (all \\injection#composite
       (/stack.as_binary (its #stack limit))
       (/registry.as_binary (its #registry limit))
       ))
