... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equality (.only Equality)]]
   [data
    ["[0]" product]
    ["[0]" binary
     ["[1]" \\injection (.only Injection)]]]
   [math
    ["[0]" random (.only Random)]]]]
 ["[0]" //
  [class (.only Class)]
  [name_and_type (.only Name_And_Type)]
  [//
   ["[0]" index (.only Index)]]])

(every .public (Reference it)
  (Record
   [:class (Index Class)
    :name_and_type (Index (Name_And_Type it))]))

(the .public equality
  (Equality (Reference Any))
  (all product.equality
       index.equality
       index.equality
       ))

(alias [=]
       ..equality)

(the .public as_binary
  (Injection (Reference Any))
  (all binary.and
       index.as_binary
       index.as_binary
       ))

(the .public random
  (for_any (_ it)
    (Random (Reference it)))
  (all random.and
       index.random
       index.random
       ))
