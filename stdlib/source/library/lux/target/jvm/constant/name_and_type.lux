... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" product]
    ["[0]" binary
     ["[1]" \\injection (.only Injection)]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]]]]
 [//
  [utf8 (.only UTF8)]
  [//
   ["[0]" index (.only Index)]
   [type
    [descriptor (.only Descriptor)]]]])

(every .public (Name_And_Type it)
  (Record
   [#name (Index UTF8)
    #descriptor (Index (Descriptor it))]))

(the .public equivalence
  (Equivalence (Name_And_Type Any))
  (all product.equivalence
       index.equivalence
       index.equivalence
       ))

(alias [=]
       ..equivalence)

(the .public as_binary
  (Injection (Name_And_Type Any))
  (all binary.and
       index.as_binary
       index.as_binary
       ))

(the .public random
  (Random (Name_And_Type Any))
  (all random.and
       index.random
       index.random
       ))
