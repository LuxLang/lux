... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Tag
                integer)
   [abstract
    [equivalence (.only Equivalence)]
    ["[0]" hash (.only Hash)]]
   [error
    ["[0]" try]]
   [data
    [binary
     [\\injection (.only Injection)]]]
   [math
    [number
     ["[0]" /08
      ["[1]" natural]]]]
   [macro
    ["[0]" template]]
   [type
    ["[0]" nominal]]]])

(nominal.every .public Tag
  /08.Number

  (the .public hash
    (Hash Tag)
    (by hash.functor each (|>> nominal.as)
        /08.hash))

  (the .public equivalence
    (Equivalence Tag)
    (implementation
     (the (= expected actual)
       (/08.= (nominal.as expected)
              (nominal.as actual)))))

  (alias [=]
         ..equivalence)

  (template.with [<code> <name>]
    [(the .public <name>
       Tag
       (|> <code>
           /08.of
           nominal.of))]

    [[01 utf8]
     [03 integer]
     [04 float]
     [05 long]
     [06 double]
     [07 class]
     [08 string]
     [09 field]
     [10 method]
     [11 interface_method]
     [12 name_and_type]
     [15 method_handle]
     [16 method_type]
     [18 invoke_dynamic]])

  (the .public as_binary
    (Injection Tag)
    (|>> nominal.as
         /08.as_binary))
  )
