... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" equivalence (.only Equivalence)]]
   [data
    [binary
     [\\injection (.only Injection)]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]]
   [type
    ["[0]" nominal]]]]
 [//
  [utf8 (.only UTF8)]
  [//
   ["[0]" index (.only Index)]]])

(the Reification
  (type (Index UTF8)))

(nominal.every .public Class
  ..Reification

  (the .public index
    (-> Class
        ..Reification)
    (|>> nominal.reification))
  
  (the .public class
    (-> ..Reification
        Class)
    (|>> nominal.abstraction))

  (the .public equivalence
    (Equivalence Class)
    (by equivalence.functor each
        ..index
        index.equivalence))

  (alias [=]
         ..equivalence)

  (the .public as_binary
    (Injection Class)
    (|>> nominal.reification
         index.as_binary))
  )

(the .public random
  (Random Class)
  (|> index.random
      (is (Random ..Reification))
      (random#each class)))
