... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except char int function)
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor)]]]
   [macro
    ["[0]" template]]]]
 ["/" //])

(template.with [<name>]
  [(the .public <name>
     /.Type
     (/.type (/.global [(stack) (template.text [<name>])] (stack))))]

  [void]
  [bool]
  [char]
  [short]
  [int]
  [long]
  [float]
  [double]
  )

(the .public copy
  (-> /.Type
      /.Type)
  (|>> /.code
       (text.suffix "&")
       /.manual))

(alias [reference]
       ..copy)

(the .public move
  (-> /.Type
      /.Type)
  (|>> /.code
       (text.suffix "&&")
       /.manual))

(the .public address
  (-> /.Type
      /.Type)
  (|>> /.code
       (text.suffix "*")
       /.manual))

(alias [pointer array]
       ..address)

(the .public constant
  (-> /.Type
      /.Type)
  (|>> /.code
       (text.prefix "const ")
       /.manual))

(template.with [<ns> <name>/*]
  [(`` (template.with [<lux> <c++>]
         [(the .public <lux>
            /.Type
            (/.type (/.global [(stack <ns>) <c++>] (stack))))]

         (,, (template.spliced <name>/*))))]

  ... https://en.cppreference.com/w/cpp/string/basic_string
  [/.standard
   [[string "string"]
    [wide_string "wstring"]
    [u08_string "u8string"]
    [u16_string "u16string"]
    [u32_string "u32string"]]]
  )

(template.with [<lux> <c++>]
  [(the .public <lux>
     /.Type
     (/.type (/.global [(stack) <c++>] (stack))))]

  [char_08 "char8_t"]
  [char_16 "char16_t"]
  [char_32 "char32_t"]

  [int_08 "int8_t"]
  [int_16 "int16_t"]
  [int_32 "int32_t"]
  [int_64 "int64_t"]

  [int_fast_08 "int_fast8_t"]
  [int_fast_16 "int_fast16_t"]
  [int_fast_32 "int_fast32_t"]
  [int_fast_64 "int_fast64_t"]

  [int_least_08 "int_least8_t"]
  [int_least_16 "int_least16_t"]
  [int_least_32 "int_least32_t"]
  [int_least_64 "int_least64_t"]

  [int_max "intmax_t"]
  [int_ptr "intptr_t"]
  
  [uint_08 "uint8_t"]
  [uint_16 "uint16_t"]
  [uint_32 "uint32_t"]
  [uint_64 "uint64_t"]

  [uint_fast_08 "uint_fast8_t"]
  [uint_fast_16 "uint_fast16_t"]
  [uint_fast_32 "uint_fast32_t"]
  [uint_fast_64 "uint_fast64_t"]

  [uint_least_08 "uint_least8_t"]
  [uint_least_16 "uint_least16_t"]
  [uint_least_32 "uint_least32_t"]
  [uint_least_64 "uint_least64_t"]

  [uint_max "uintmax_t"]
  [uint_ptr "uintptr_t"]
  )

(the .public shared_ptr
  (-> /.Type
      /.Type)
  (|>> stack
       (/.global [(stack /.standard) "shared_ptr"])
       /.type))

(the .public (function arguments return)
  (-> (Stack /.Parameter) /.Parameter
      /.Type)
  (|> arguments
      (stack#each /.code)
      (text.interposed ",")
      (text.enclosed ["(" ")"])
      (text (/.code return))
      /.manual))

(the .public (lambda arguments return)
  (-> (Stack /.Parameter) /.Parameter
      /.Type)
  (/.type (/.global [(stack /.standard) "function"]
                    (stack (..function arguments return)))))

(the .public initializer_list
  (-> /.Parameter
      /.Type)
  (|>> stack
       (/.global [(stack /.standard) "initializer_list"])
       /.type))
