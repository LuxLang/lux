(.using
 [library
  [lux {"-" Location Code not or and list if int comment exec try the is}
   ["@" target]
   ["[0]" ffi]
   [abstract
    [equivalence {"+" Equivalence}]
    [hash {"+" Hash}]
    ["[0]" enum]]
   [control
    ["[0]" pipe]
    [parser
     ["<[0]>" code]]]
   [data
    ["[0]" text
     ["%" format {"+" format}]]
    [collection
     ["[0]" list ("[1]#[0]" functor mix)]]]
   [macro
    [syntax {"+" syntax:}]
    ["[0]" template]
    ["[0]" code]]
   [math
    [number
     ["n" nat]
     ["f" frac]]]
   [type
    [primitive "*"]]]])

(def: input_separator
  ", ")

(def: expression
  (-> Text Text)
  (text.enclosed ["(" ")"]))

(for @.old (these (ffi.import: java/lang/CharSequence
                    "[1]::[0]")
                  
                  (ffi.import: java/lang/String
                    "[1]::[0]"
                    (replace [java/lang/CharSequence java/lang/CharSequence] java/lang/String)))
     (these))

... Added the carriage return for better Windows compatibility.
(def: \n+
  Text
  (format text.carriage_return text.new_line))

(def: nested
  (-> Text Text)
  (.let [nested_new_line (format text.new_line text.tab)]
    (for @.old (|>> (format \n+)
                    (as java/lang/String)
                    (java/lang/String::replace (as java/lang/CharSequence text.new_line)
                                               (as java/lang/CharSequence nested_new_line)))
         (|>> (format \n+)
              (text.replaced text.new_line nested_new_line)))))

(primitive: .public (Code brand)
  Text

  (implementation: .public equivalence
    (All (_ brand) (Equivalence (Code brand)))

    (def: (= reference subject)
      (# text.equivalence = (representation reference) (representation subject))))

  (implementation: .public hash
    (All (_ brand) (Hash (Code brand)))

    (def: equivalence ..equivalence)
    (def: hash (|>> representation (# text.hash hash))))

  (def: .public manual
    (-> Text Code)
    (|>> abstraction))

  (def: .public code
    (-> (Code Any) Text)
    (|>> representation))

  (template [<type> <super>]
    [(with_expansions [<brand> (template.symbol [<type> "'"])]
       (`` (primitive: (<brand> brand) Any))
       (`` (type: .public (<type> brand)
             (<super> (<brand> brand)))))]
    
    [Expression Code]
    [Computation Expression]
    [Location Computation]
    [Var Location]
    [Statement Code]
    )

  (template [<type> <super>]
    [(with_expansions [<brand> (template.symbol [<type> "'"])]
       (`` (primitive: <brand> Any))
       (`` (type: .public <type> (<super> <brand>))))]

    [Literal Computation]
    [Access Location]
    [Loop Statement]
    )

  (template [<var> <brand>]
    [(primitive: .public <brand> Any)

     (type: .public <var> (Var <brand>))]

    [SVar Single]
    [PVar Poly]
    [KVar Keyword]
    )

  (def: .public var
    (-> Text SVar)
    (|>> abstraction))

  (template [<name> <brand> <prefix>]
    [(def: .public <name>
       (-> SVar (Var <brand>))
       (|>> representation (format <prefix>) abstraction))]

    [poly    Poly    "*"]
    [keyword Keyword "**"]
    )

  (def: .public none
    Literal
    (abstraction "None"))

  (def: .public bool
    (-> Bit Literal)
    (|>> (pipe.case
           #0 "False"
           #1 "True")
         abstraction))

  (def: .public int
    (-> Int Literal)
    (|>> %.int abstraction))

  (def: .public (long value)
    (-> Int Literal)
    (abstraction (format (%.int value) "L")))

  (def: .public float
    (-> Frac Literal)
    (`` (|>> (pipe.cond (~~ (template [<test> <python>]
                              [[<test>]
                               [(pipe.new (format "float(" text.double_quote <python> text.double_quote ")") [])]]

                              [(f.= f.positive_infinity) "inf"]
                              [(f.= f.negative_infinity) "-inf"]
                              [f.not_a_number? "nan"]
                              ))
                        
                        ... else
                        [%.frac])
             abstraction)))

  (def: safe
    (-> Text Text)
    (`` (|>> (~~ (template [<find> <replace>]
                   [(text.replaced <find> <replace>)]

                   ["\" "\\"]
                   [text.tab "\t"]
                   [text.vertical_tab "\v"]
                   [text.null "\0"]
                   [text.back_space "\b"]
                   [text.form_feed "\f"]
                   [text.new_line "\n"]
                   [text.carriage_return "\r"]
                   [text.double_quote (format "\" text.double_quote)]
                   ))
             )))

  (def: .public string
    (-> Text Literal)
    (|>> ..safe
         (text.enclosed [text.double_quote text.double_quote])
         abstraction))

  (def: .public unicode
    (-> Text Literal)
    (|>> ..string
         representation
         (format "u")
         abstraction))

  (def: (composite_literal left_delimiter right_delimiter entry_serializer)
    (All (_ a)
      (-> Text Text (-> a Text)
          (-> (List a) Literal)))
    (function (_ entries)
      (<| abstraction
          ... ..expression
          (format left_delimiter
                  (|> entries
                      (list#each (|>> entry_serializer (text.suffix ..input_separator)))
                      text.together)
                  right_delimiter))))

  (template [<name> <pre> <post>]
    [(def: .public <name>
       (-> (List (Expression Any)) Literal)
       (composite_literal <pre> <post> ..code))]

    [tuple "(" ")"]
    [list  "[" "]"]
    )

  (def: .public (slice from to list)
    (-> (Expression Any) (Expression Any) (Expression Any) Access)
    (<| abstraction
        ... ..expression
        (format (representation list) "[" (representation from) ":" (representation to) "]")))

  (def: .public (slice_from from list)
    (-> (Expression Any) (Expression Any) Access)
    (<| abstraction
        ... ..expression
        (format (representation list) "[" (representation from) ":]")))

  (def: .public dict
    (-> (List [(Expression Any) (Expression Any)]) (Computation Any))
    (composite_literal "{" "}" (.function (_ [k v]) (format (representation k) " : " (representation v)))))

  (def: .public (apply args func)
    (-> (List (Expression Any)) (Expression Any) (Computation Any))
    (<| abstraction
        ... ..expression
        (format (representation func)
                "("
                (|> args (list#each ..code) (text.interposed ..input_separator))
                ")")))

  (template [<name> <brand> <prefix>]
    [(def: .public <name>
       (-> (Expression Any) (Expression Any))
       (|>> representation
            (format <prefix>)
            abstraction))]

    [splat_poly    Poly    "*"]
    [splat_keyword Keyword "**"]
    )

  (def: .public (the name object)
    (-> Text (Expression Any) Access)
    (abstraction (format (representation object) "." name)))

  (def: .public (do method args object)
    (-> Text (List (Expression Any)) (Expression Any) (Computation Any))
    (|> object
        (..the method)
        (..apply args)))

  (def: .public (item idx array)
    (-> (Expression Any) (Expression Any) Access)
    (abstraction (format (representation array) "[" (representation idx) "]")))

  (def: .public (? test then else)
    (-> (Expression Any) (Expression Any) (Expression Any) (Computation Any))
    (<| abstraction
        ..expression
        (format (representation then) " if " (representation test) " else " (representation else))))

  (template [<name> <op>]
    [(def: .public (<name> param subject)
       (-> (Expression Any) (Expression Any) (Computation Any))
       (<| abstraction
           ..expression
           (format (representation subject) " " <op> " " (representation param))))]

    [is      "is"]
    [=       "=="]
    [<       "<"]
    [<=      "<="]
    [>       ">"]
    [>=      ">="]
    [+       "+"]
    [-       "-"]
    [*       "*"]
    [/       "/"]
    [//      "//"]
    [%       "%"]
    [**      "**"]
    [bit_or  "|"]
    [bit_and "&"]
    [bit_xor "^"]
    [bit_shl "<<"]
    [bit_shr ">>"]

    [or      "or"]
    [and     "and"]

    [in?     "in"]
    )

  (template [<name> <unary>]
    [(def: .public (<name> subject)
       (-> (Expression Any) (Computation Any))
       (<| abstraction
           ... ..expression
           (format <unary> " " (representation subject))))]

    [not "not"]
    [opposite "-"]
    )

  (def: .public (lambda arguments body)
    (-> (List (Var Any)) (Expression Any) (Computation Any))
    (<| abstraction
        ..expression
        (format "lambda " (|> arguments (list#each ..code) (text.interposed ..input_separator))
                ": " (representation body))))

  (def: .public (set vars value)
    (-> (List (Location Any)) (Expression Any) (Statement Any))
    (abstraction
     (format (|> vars (list#each ..code) (text.interposed ..input_separator))
             " = "
             (representation value))))

  (def: .public multi
    (-> (List (Expression Any)) (Expression Any))
    (|>> (list#each ..code)
         (text.interposed ..input_separator)
         abstraction))

  (def: .public (delete where)
    (-> (Location Any) (Statement Any))
    (abstraction (format "del " (representation where))))

  (def: .public (if test then! else!)
    (-> (Expression Any) (Statement Any) (Statement Any) (Statement Any))
    (abstraction
     (format "if " (representation test) ":"
             (..nested (representation then!))
             \n+ "else:"
             (..nested (representation else!)))))

  (def: .public (when test then!)
    (-> (Expression Any) (Statement Any) (Statement Any))
    (abstraction
     (format "if " (representation test) ":"
             (..nested (representation then!)))))

  (def: .public (then pre! post!)
    (-> (Statement Any) (Statement Any) (Statement Any))
    (abstraction
     (format (representation pre!)
             \n+
             (representation post!))))

  (template [<keyword> <0>]
    [(def: .public <0>
       (Statement Any)
       (abstraction <keyword>))]

    ["break"    break]
    ["continue" continue]
    )

  (def: .public (while test body! else!)
    (-> (Expression Any) (Statement Any) (Maybe (Statement Any)) Loop)
    (abstraction
     (format "while " (representation test) ":"
             (..nested (representation body!))
             (case else!
               {.#Some else!}
               (format \n+ "else:"
                       (..nested (representation else!)))
               
               {.#None}
               ""))))

  (def: .public (for_in var inputs body!)
    (-> SVar (Expression Any) (Statement Any) Loop)
    (abstraction
     (format "for " (representation var) " in " (representation inputs) ":"
             (..nested (representation body!)))))

  (def: .public statement
    (-> (Expression Any) (Statement Any))
    (|>> transmutation))

  (def: .public pass
    (Statement Any)
    (abstraction "pass"))

  (type: .public Except
    (Record
     [#classes (List Text)
      #exception SVar
      #handler (Statement Any)]))
  
  (def: .public (try body! excepts)
    (-> (Statement Any) (List Except) (Statement Any))
    (abstraction
     (format "try:"
             (..nested (representation body!))
             (|> excepts
                 (list#each (function (_ [classes exception catch!])
                              (format \n+ "except (" (text.interposed ..input_separator classes)
                                      ") as " (representation exception) ":"
                                      (..nested (representation catch!)))))
                 text.together))))

  (template [<name> <keyword> <pre>]
    [(def: .public (<name> value)
       (-> (Expression Any) (Statement Any))
       (abstraction
        (format <keyword> (<pre> (representation value)))))]

    [raise  "raise "  |>]
    [return "return " |>]
    [print  "print"   ..expression]
    )

  (def: .public (exec globals code)
    (-> (Maybe (Expression Any)) (Expression Any) (Statement Any))
    (let [extra (case globals
                  {.#Some globals}
                  (.list globals)

                  {.#None}
                  (.list))]
      (abstraction
       (format "exec" (representation (..tuple (partial_list code extra)))))))

  (def: .public (def name args body)
    (-> SVar (List (Ex (_ k) (Var k))) (Statement Any) (Statement Any))
    (abstraction
     (format "def " (representation name)
             "("
             (|> args (list#each ..code) (text.interposed ..input_separator))
             "):"
             (..nested (representation body)))))

  (def: .public (import module_name)
    (-> Text (Statement Any))
    (abstraction (format "import " module_name)))

  (def: .public (comment commentary on)
    (All (_ brand) (-> Text (Code brand) (Code brand)))
    (abstraction (format "# "  (text.replaced text.\n "\n" commentary) \n+
                         (representation on))))
  )

(syntax: (arity_inputs [arity <code>.nat])
  (in (case arity
        0 (.list)
        _ (|> (-- arity)
              (enum.range n.enum 0)
              (list#each (|>> %.nat code.local))))))

(syntax: (arity_types [arity <code>.nat])
  (in (list.repeated arity (` (Expression Any)))))

(template [<arity> <function>+]
  [(with_expansions [<inputs> (arity_inputs <arity>)
                     <definitions> (template.spliced <function>+)]
     (template [<function>]
       [(`` (def: .public ((~~ (template.symbol [<function> "/" <arity>])) <inputs>)
              (-> (~~ (arity_types <arity>)) (Computation Any))
              (..apply (.list <inputs>) (..var <function>))))]

       <definitions>))]

  [0
   [["locals"]
    ["globals"]]]

  [1
   [["str"]
    ["ord"]
    ["float"]
    ["int"]
    ["len"]
    ["chr"]
    ["unichr"]
    ["unicode"]
    ["repr"]
    ["__import__"]
    ["Exception"]]]

  [2
   []]

  [3
   []]
  )
