... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Alias Integer)
   [abstract
    [monad (.only do)]]
   [control
    ["<>" projection (.use "[1]#[0]" monad)]
    ["[0]" io]
    ["[0]" maybe]]
   [data
    ["[0]" product]
    [text
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   ["[0]" meta (.only)
    [type
     ["[0]" nominal]]
    ["[0]" code (.only)
     ["<[1]>" \\projection (.only Projection)]]
    [macro (.only with_names)
     ["[0]" syntax]
     ["[0]" template]
     ["[0]" expansion]]]]])

(nominal.every .public (Object brand) Any)

(template.with [<name>]
  [(expansion.let [<brand> (template.name [<name> "'"])]
     (nominal.every .public <brand> Any)
     (every .public <name>
       (..Object <brand>)))]

  [Nil]
  [Function]
  )

(template.with [<name> <type>]
  [(every .public <name>
     <type>)]

  [Boolean Bit]
  [Integer .Integer]
  [Float   Decimal]
  [String  Text]
  )

(every Nilable
  [Bit Code])

(the nilable
  (Projection Nilable)
  (let [token (' "?")]
    (<| (<>.and (<>.parses? (<code>.this token)))
        (<>.after (<>.not (<code>.this token)))
        <code>.any)))

(every Alias
  Text)

(the alias
  (Projection Alias)
  (<>.after (<code>.this (' "as")) <code>.local))

(every Field
  [Bit Text (Maybe Alias) Nilable])

(the static!
  (Projection Any)
  (<code>.this (' "static")))

(the field
  (Projection Field)
  (<code>.form (all <>.and
                    (<>.parses? ..static!)
                    <code>.local
                    (<>.maybe ..alias)
                    ..nilable)))

(the constant
  (Projection Field)
  (<code>.form (all <>.and
                    (<>#in true)
                    <code>.local
                    (<>.maybe ..alias)
                    ..nilable)))

(every Common_Method
  (Record
   [#name Text
    #alias (Maybe Alias)
    #inputs (List Nilable)
    #io? Bit
    #try? Bit
    #output Nilable]))

(the common_method
  (Projection Common_Method)
  (all <>.and
       <code>.local
       (<>.maybe ..alias)
       (<code>.tuple (<>.some ..nilable))
       (<>.parses? (<code>.this (' "io")))
       (<>.parses? (<code>.this (' "try")))
       ..nilable))

(the input_variables
  (-> (List Nilable) (List [Bit Code]))
  (|>> list.enumeration
       (list#each (function (_ [idx [nilable? type]])
                    [nilable? (|> idx %.natural code.local)]))))

(the (nilable_type [nilable? type])
  (-> Nilable Code)
  (if nilable?
    (` (.Maybe (, type)))
    type))

(the (with_nil 'temp [nilable? input])
  (-> Code [Bit Code] Code)
  (if nilable?
    (` (when (, input)
         {.#Some (, 'temp)}
         (, 'temp)

         {.#None}
         ("scheme object nil")))
    input))

(the (without_nil 'temp [nilable? outputT] output)
  (-> Code Nilable Code Code)
  (if nilable?
    (` (let [(, 'temp) (, output)]
         (if ("scheme object nil?" (, 'temp))
           {.#None}
           {.#Some (, 'temp)})))
    (` (let [(, 'temp) (, output)]
         (if (not ("scheme object nil?" (, 'temp)))
           (, 'temp)
           (.halt! "Nil is an invalid value!"))))))

(every Import
  (Variant
   {#Function Common_Method}
   {#Constant Field}))

(the import
  (Projection Import)
  (all <>.or
       (<code>.form ..common_method)
       ..constant
       ))

(the (with_io with? without)
  (-> Bit Code Code)
  (if with?
    (` (io.io (, without)))
    without))

(the (io_type io? rawT)
  (-> Bit Code Code)
  (if io?
    (` (io.IO (, rawT)))
    rawT))

(the (with_try with? without_try)
  (-> Bit Code Code)
  (if with?
    (` (..try (, without_try)))
    without_try))

(the (try_type try? rawT)
  (-> Bit Code Code)
  (if try?
    (` (.Either .Text (, rawT)))
    rawT))

(the (make_function 'method 'temp source inputsT io? try? outputT)
  (-> Code Code Code (List Nilable) Bit Bit Nilable Code)
  (let ['inputs (input_variables inputsT)]
    (` (the ((, 'method)
             [(,* (list#each product.right 'inputs))])
         (-> [(,* (list#each nilable_type inputsT))]
             (, (|> (nilable_type outputT)
                    (try_type try?)
                    (io_type io?))))
         (as_expected
          (, (<| (with_io io?)
                 (with_try try?)
                 (without_nil 'temp outputT)
                 (` ("scheme apply"
                     (as ..Function (, source))
                     (,* (list#each (with_nil 'temp) 'inputs)))))))))))

(the .public import
  (syntax.macro (_ [import ..import])
    (with_names ['temp]
      (when import
        {#Function [name alias inputsT io? try? outputT]}
        (let [imported (` ("scheme constant" (, (code.text name))))]
          (in (list (..make_function (code.local (maybe.else name alias))
                                     'temp
                                     imported
                                     inputsT
                                     io?
                                     try?
                                     outputT))))

        {#Constant [_ name alias fieldT]}
        (let [imported (` ("scheme constant" (, (code.text name))))
              'name (code.local (maybe.else name alias))]
          (in (list (` (the (, 'name)
                         (syntax.macro ((, 'name) [])
                           (by meta.monad (,' in)
                               (list (` (.as (, (nilable_type fieldT)) (, imported)))))))))))
        ))))
