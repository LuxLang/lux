... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... https://en.wikipedia.org/wiki/Physics
(.using
 [library
  [lux (.except)
   [math
    [number
     [/64
      ["/" decimal]]]
    [geometry
     ["[0]" circle]]]
   [macro
    ["[0]" template]]
   ["[0]" function (.only)
    [predicate (.only Predicate)]]
   [abstract
    [equivalence (.only Equivalence)]]]]
 ["[0]" /1])

... https://en.wikipedia.org/wiki/Scalar_(physics)
(every .public Scalar
  /1.Dimension)

(every .public (Constant dimension)
  dimension)

(every .public (Variable dimension)
  (-> /1.Time
      (Constant dimension)))

... https://en.wikipedia.org/wiki/Velocity
(every .public Velocity
  Scalar)

(the .public (average_velocity interval displacement)
  (-> /1.Interval /1.Displacement
      Velocity)
  (/./ (/1.value interval)
       (/1.value displacement)))

... https://en.wikipedia.org/wiki/Acceleration
(every .public Acceleration
  Scalar)

(the .public (average_acceleration interval difference_in_velocity)
  (-> /1.Interval (/1.Difference Velocity)
      Acceleration)
  (/./ (/1.value interval)
       (/1.value difference_in_velocity)))

(the .public (velocity acceleration initial)
  (-> Acceleration Velocity
      (Variable Velocity))
  (|>> (/.x acceleration)
       (/.+ initial)))

(the (mean left right)
  (-> Scalar Scalar
      Scalar)
  (/./ +2.0
       (/.+ left right)))

(the .public (position acceleration initial_velocity initial)
  (-> Acceleration Velocity /1.Position
      (Variable /1.Position))
  (function (_ time)
    (let [velocity (mean initial_velocity
                         (..velocity acceleration initial_velocity time))]
      (|> time
          (/.x velocity)
          (/.+ initial)))))

... https://en.wikipedia.org/wiki/Euclidean_vector
(every .public Vector
  (Record
   [#forward/backward /1.Dimension
    #left/right /1.Dimension]))

(the .public equivalence
  (Equivalence Vector)
  (implementation
   (the (= reference it)
     (and (/.= (its #forward/backward reference)
               (its #forward/backward it))
          (/.= (its #left/right reference)
               (its #left/right it))))))

(alias [=]
       ..equivalence)

(the .public origin
  Vector
  [#forward/backward /1.origin
   #left/right /1.origin])

(template.with [,vector ,scalar]
  [(the .public (,vector origin it)
     (-> Vector
         (.Change Vector))
     [#forward/backward (,scalar (its #forward/backward origin)
                                 (its #forward/backward it))
      #left/right (,scalar (its #left/right origin)
                           (its #left/right it))])]

  [[+ /.+]
   [- /.-]])

(the .public opposite
  (.Change Vector)
  (|>> (revised #forward/backward /.opposite)
       (revised #left/right /.opposite)))

(template.with [,vector ,scalar]
  [(the .public (,vector scale)
     (-> Scalar
         (.Change Vector))
     (|>> (revised #forward/backward (,scalar scale))
          (revised #left/right (,scalar scale))))]

  [[x /.x]
   [/ /./]])

(every .public Orientation
  Scalar)

(the (square it)
  (.Change /1.Dimension)
  (/.x it it))

... https://en.wikipedia.org/wiki/Magnitude_(mathematics)
(the .public (magnitude it)
  (-> Vector
      Scalar)
  (/.root_2 (/.+ (square (its #forward/backward it))
                 (square (its #left/right it)))))

... https://en.wikipedia.org/wiki/Orientation_(geometry)
(the .public (orientation it)
  (-> Vector
      Orientation)
  (circle.arc_tangent_2 (its #forward/backward it)
                        (its #left/right it)))

(the .public (polar magnitude orientation)
  (-> Scalar Orientation
      Vector)
  (|> [#forward/backward (circle.co_sine orientation)
       #left/right (circle.sine orientation)]
      (x magnitude)))

(the .public (approximately? margin_of_error standard value)
  (-> Scalar Vector
      (Predicate Vector))
  (|> value
      (- standard)
      magnitude
      (/.<= margin_of_error)))

(the .public (distance from to)
  (-> Vector Vector
      Scalar)
  (magnitude (- from to)))

(every .public (Difference it)
  (.Change it))

(the .public value
  (-> (Difference Vector)
      Vector)
  (function.of ..origin))

... https://en.wikipedia.org/wiki/Displacement_(geometry)
(every .public Displacement
  (Difference Vector))

(the .public (difference initial final)
  (-> Vector Vector
      Displacement)
  (+ [#forward/backward (/.- (its #forward/backward initial)
                             (its #forward/backward final))
      #left/right (/.- (its #left/right initial)
                       (its #left/right final))]))

(the .public displacement
  difference)

(the .public (average_velocity' interval displacement)
  (-> /1.Interval Displacement
      Vector)
  (/ (/1.value interval)
     (..value displacement)))

(the .public (average_acceleration' interval difference_in_velocity)
  (-> /1.Interval (/1.Difference Vector)
      Vector)
  (/ (/1.value interval)
     (..value difference_in_velocity)))

(the .public (velocity' acceleration initial)
  (-> Vector Vector
      (Variable Vector))
  (function (_ time)
    (|> acceleration
        (x time)
        (+ initial))))

... https://en.wikipedia.org/wiki/Center_of_mass#Barycentric_coordinates
(the (center_of_mass left right)
  (-> Vector Vector
      Vector)
  (/ +2.0
     (+ left right)))

(the .public (position' acceleration initial_velocity initial)
  (-> Vector Vector Vector
      (Variable Vector))
  (function (_ time)
    (let [velocity (center_of_mass initial_velocity
                                   (..velocity' acceleration initial_velocity time))]
      (|> velocity
          (x time)
          (+ initial)))))

(every .public Radius
  Scalar)

... https://en.wikipedia.org/wiki/Centripetal_force
(every .public Centripetal_Acceleration
  Scalar)

(the .public (centripetal_acceleration radius velocity)
  (-> Radius Vector
      Centripetal_Acceleration)
  (/./ radius
       (square (magnitude velocity))))

... https://en.wikipedia.org/wiki/Frequency#Period_versus_frequency
(every .public Period
  Scalar)

(the .public (period radius velocity)
  (-> Radius Vector
      Period)
  (|> (magnitude velocity)
      (/./ radius)
      (/.x circle.tau)))

... https://en.wikipedia.org/wiki/Angular_frequency
(every .public Angular_Speed
  Scalar)

(the .public (angular_speed period)
  (-> Period
      Angular_Speed)
  (/./ period
       circle.tau))

(the .public (speed radius angular_speed)
  (-> Radius Angular_Speed
      Scalar)
  (/.x radius angular_speed))

... https://en.wikipedia.org/wiki/Acceleration#Circular_motion
(every .public Radial_Acceleration
  Scalar)

(the .public (radial_acceleration radius velocity)
  (-> Radius Vector
      Radial_Acceleration)
  (/.opposite (centripetal_acceleration radius velocity)))
