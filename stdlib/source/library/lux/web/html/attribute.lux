... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Scope Source
                for list loop open pattern type)
   [data
    ["[0]" text (.only)
     [char (.only Char)]
     ["%" \\format]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [meta
    [macro
     ["[0]" template]]
    [type
     ["[0]" nominal]]]
   [world
    [net (.only URL)
     ["[0]" mime (.only MIME)]]
    [time
     [instant (.only Instant)]]
    [locale
     ["[0]" language (.only Language)]]]]]
 [/
  [capitalization (.only Capitalization)]
  [completion (.only Completion)]
  [decoding (.only Decoding)]
  [text_direction (.only Text_Direction)]
  [drag (.only Drag)]
  [enter (.only Enter)]
  [input_mode (.only Input_Mode)]
  [pre_load (.only Pre_Load)]
  [referrer_policy (.only Referrer_Policy)]
  [relationship (.only Relationship)]
  [role (.only Role)]
  [sandbox (.only Sandbox)]
  [shape (.only Shape)]
  ["[0]" source (.only Source)]
  [type
   ["[0]" button]
   ["[0]" input]
   ["[0]" numbering]
   ["[0]" script]]
  [///
   ["[0]" permission]
   [css
    ["[0]" id (.only ID)]
    ["[0]" class (.only Class)]
    ["[0]" query (.only Query)]
    ["[0]" value (.only Value Length)]
    ["[0]" style (.only Style)]]]])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes#attribute_list
(every .public (Format of)
  (-> of
      (Maybe Text)))

(every .public (Name of)
  (Record
   [#label Text
    #format (Format of)]))

(every .public (Attribute of)
  (Record
   [#name (Name of)
    #value of]))

(every .public Attributes
  (List (Ex (_ of)
          (Attribute of))))

(the .public empty
  Attributes
  (.list))

(with_template [<name>]
  [(the .public <name>
     MIME
     (mime.mime (template.text [<name> "/*"])))]

  [audio]
  [application]
  [video]
  [image]
  [text]
  [font]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/accept
(the .public accept
  (Name (List MIME))
  [#label "accept"
   #format (|>> (list#each mime.name)
                (text.interposed ", ")
                {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/accesskey
(the .public access_key
  (Name Char)
  [#label "accesskey"
   #format (|>> text.of_char
                {.#Some})])

(with_template [<name>]
  [(the .public <name>
     (Name URL)
     [#label (|> (template.text [<name>])
                 (text.replaced "form_" "form")
                 (text.replaced "source" "src"))
      #format (|>> {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/form#action
  [action]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/blockquote
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/ins
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/q
  [cite]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#data
  [data]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  [form_action]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/area#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/base#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/link#attributes
  [href]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#poster
  [poster]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/embed#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/source#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/track#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
  [source]
  )

(alias [hyper_link]
       ..href)

(the Allowances
  (.type (Maybe (List permission.Allowance))))

(the Permission
  (.type [permission.Directive Allowances]))

(the (allowances it)
  (-> Allowances
      Text)
  (when it
    {.#None}
    "*"
    
    {.#Some it}
    (when it
      (.list)
      "'none'"

      _
      (text.interposed " " it))))

(the (permission [directive allowances])
  (-> Permission
      Text)
  (%.format directive " " (..allowances allowances)))

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#allow
(the .public allow
  (Name (List Permission))
  [#label "allow"
   ... https://developer.mozilla.org/en-US/docs/Web/HTTP/Permissions_Policy
   #format (|>> (list#each permission)
                (text.interposed "; ")
                {.#Some})])

(with_template [<name>]
  [(the .public <name>
     (Name Text)
     [#label (|> (template.text [<name>])
                 (text.replaced "direction_" "dir")
                 (text.replaced "_property" "prop"))
      #format (|>> {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#alt
  [alt]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meta#content
  [content]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  [direction_name]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/itemprop
  [item_property]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/optgroup#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/option#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/track#attributes
  [label]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/form#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/fieldset#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/output#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/select#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/textarea#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/map#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meta#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/param#attributes
  [name]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/textarea#attributes
  [placeholder]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/title
  [title]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/data#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/li#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meter#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/option#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/progress#attributes
  [value]
  )

(the presential_bit
  (Format Bit)
  (function (_ it)
    (if it
      {.#Some ""}
      {.#None})))

(with_template [<name>]
  [(the .public <name>
     (Name Bit)
     [#label (|> (template.text [<name>])
                 (text.replaced "_" ""))
      #format ..presential_bit])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script#async
  [async]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video
  [auto_play]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#checked
  [checked]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
  [controls]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/track#default
  [default]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script#defer
  [defer]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/disabled
  [disabled]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/area#attributes
  [download]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  [form_no_validate]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#ismap
  [is_map]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
  [loop]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/multiple
  [multiple]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
  [muted]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/form#novalidate
  [no_validate]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/details#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/dialog#attributes
  [open]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#playsinline
  [plays_inline]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/readonly
  [read_only]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/required
  [required]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/ol#reversed
  [reversed]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/option#selected
  [selected]
  )

(the verbose_bit
  (Format Bit)
  (function (_ it)
    {.#Some (if it
              "true"
              "false")}))

(with_template [<name>]
  [(the .public <name>
     (Name Bit)
     [#label (|> (template.text [<name>])
                 (text.replaced "_" ""))
      #format ..verbose_bit])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/spellcheck
  [spell_check]
  )

(the succinct_bit
  (Format Bit)
  (function (_ it)
    {.#Some (if it
              "yes"
              "no")}))

(with_template [<name>]
  [(the .public <name>
     (Name Bit)
     [#label (|> (template.text [<name>])
                 (text.replaced "_" ""))
      #format ..succinct_bit])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/translate
  [translate]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/autocapitalize
(the .public auto_capitalize
  (Name Capitalization)
  [#label "autocapitalize"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/autocomplete
(the .public auto_complete
  (Name Completion)
  [#label "autocomplete"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/capture
(every .public Capture
  Text)

(with_template [<name>]
  [(the .public <name>
     Capture
     (template.text [<name>]))]

  [user]
  [environment]
  )

(the .public capture
  (Name Capture)
  [#label "capture"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/class
(the .public class
  (Name (List Class))
  [#label "class"
   #format (|>> (list#each class.class)
                (text.interposed " ")
                {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/td#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/th#attributes
(the .public minimum_column_span
  Nat
  1)

(the .public maximum_column_span
  Nat
  1,000)

(with_template [<name>]
  [(the .public <name>
     (Name Nat)
     [#label (|> (template.text [<name>])
                 (text.replaced "umn" "")
                 (text.replaced "_" ""))
      #format (|>> %.nat
                   {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/textarea#cols
  [columns]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/td#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/th#attributes
  [column_span]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/maxlength
  [max_length]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/minlength
  [min_length]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/textarea#rows
  [rows]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/td#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/th#attributes
  [row_span]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/size
  [size]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/col#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/colgroup#attributes
  [span]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/step
  [step]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/canvas#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/embed#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
  [height]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/canvas#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/embed#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
  [width]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/contenteditable
(every .public Editable
  Text)

(with_template [<name> <value>]
  [(the .public <name>
     Editable
     <value>)]

  [is_editable "true"]
  [not_editable "false"]
  [only_plain_text "plaintext-only"]
  )

(the .public content_editable
  (Name Editable)
  [#label "contenteditable"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/area#coords
(the .public coordinates
  (Name (List Nat))
  [#label "coords"
   #format (|>> (list#each %.nat)
                (text.interposed ",")
                {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/crossorigin
(every .public Cross_Origin
  Text)

(with_template [<name>]
  [(the .public <name>
     Cross_Origin
     (|> (template.text [<name>])
         (text.replaced "_" "-")))]

  [anonymous]
  [use_credentials]
  )

(the .public cross_origin
  (Name Cross_Origin)
  [#label "crossorigin"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/del#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/ins#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/time#attributes
(the .public date_time
  (Name Instant)
  [#label "datetime"
   #format (|>> %.instant
                {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#decoding
(the .public decoding
  (Name Decoding)
  [#label "decoding"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/dir
(the .public text_direction
  (Name Text_Direction)
  [#label "dir"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/draggable
(the .public draggable
  (Name Drag)
  [#label "draggable"
   #format (|>> {.#Some})])

(with_template [<name>]
  [(the .public <name>
     (Name MIME)
     [#label (|> (template.text [<name>])
                 (text.replaced "encoding" "enc")
                 (text.replaced "_" ""))
      #format (|>> mime.name
                   {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/form#enctype
  [encoding_type]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  [form_encoding_type]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/embed#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/source#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/link#attributes
  [type]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/enterkeyhint
(the .public enter_key_hint
  (Name Enter)
  [#label "enterkeyhint"
   #format (|>> {.#Some})])

(with_template [<name>]
  [(the .public <name>
     (Name ID)
     [#label (template.text [<name>])
      #format (|>> id.id
                   {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/for
  [for]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/fieldset#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/output#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/select#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/textarea#attributes
  [form]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/id
  [id]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#list
  [list]
  )

(every .public Target
  Text)

(with_template [<name>]
  [(the .public <name>
     Target
     (template.text ["_" <name>]))]

  [self]
  [blank]
  [parent]
  [top]
  )

(with_template [<name>]
  [(the .public <name>
     (Name Target)
     [#label (|> (template.text [<name>])
                 (text.replaced "_" ""))
      #format (|>> {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  [form_target]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/area#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/base#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/form#attributes
  [target]
  )

(every .public Hidden
  Text)

(with_template [<name>]
  [(the .public <name>
     Hidden
     (|> (template.text [<name>])
         (text.replaced "_" "-")
         (text.replaced "forever" "hidden")))]

  [forever]
  [until_found]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/hidden
(the .public hidden
  (Name Hidden)
  [#label "hidden"
   #format (|>> {.#Some})])

(with_template [<name>]
  [(the .public <name>
     (Name Frac)
     [#label (template.text [<name>])
      #format (|>> %.frac
                   {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meter#high
  [high]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meter#low
  [low]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/max
  [max]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/min
  [min]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meter#optimum
  [optimum]
  )

(with_template [<name>]
  [(the .public <name>
     (Name Language)
     [#label (|> (template.text [<name>])
                 (text.replaced "language" "lang")
                 (text.replaced "source" "src")
                 (text.replaced "_" "-"))
      #format (|>> language.name
                   {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/link#attributes
  [href_language]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/lang
  [language]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/track#srclang
  [source_language]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meta#http-equiv
(every .public Pragma
  Text)

(with_template [<name>]
  [(the .public <name>
     Pragma
     (|> (template.text [<name>])
         (text.replaced "_" "-")))]

  [content_security_policy]
  [content_type]
  [default_style]
  [x_ua_compatible]
  [refresh]
  )

(the .public http_equivalent
  (Name Pragma)
  [#label "http-equiv"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/Security/Subresource_Integrity
(nominal.every Hash
  Text

  (with_template [<name>]
    [(the .public <name>
       (-> Text
           Hash)
       (let [prefix (|> (template.text [<name>])
                        (text.replaced "_" "-"))]
         (|>> (%.format prefix "-")
              nominal.abstraction)))]
    
    [sha_256]
    [sha_384]
    [sha_512]
    )

  (the .public integrity
    (Name Hash)
    [#label "integrity"
     #format (|>> nominal.representation
                  {.#Some})])
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/inputmode
(the .public input_mode
  (Name Input_Mode)
  [#label "inputmode"
   #format (|>> {.#Some})])

(every .public Track_Kind
  Text)

(with_template [<name>]
  [(the .public <name>
     Track_Kind
     (template.text [<name>]))]

  [subtitles]
  [captions]
  [descriptions]
  [chapters]
  [metadata]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/track#kind
(the .public track_kind
  (Name Track_Kind)
  [#label "kind"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#attributes
(every .public Loading
  Text)

(with_template [<name>]
  [(the .public <name>
     Loading
     (template.text [<name>]))]

  [eager]
  [lazy]
  )

(the .public loading
  (Name Loading)
  [#label "loading"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/link#attributes
(the .public media
  (Name Query)
  [#label "media"
   #format (|>> query.query
                {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/form#method
(every .public Method
  Text)

(with_template [<name>]
  [(the .public <name>
     Method
     (template.text [<name>]))]

  [post]
  [get]
  [dialog]
  )

(the .public method
  (Name Method)
  [#label "method"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/pattern
(every .public Regular_Expression
  Text)

(the .public pattern
  (Name Regular_Expression)
  [#label "pattern"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#ping
(the .public ping
  (Name (List URL))
  [#label "ping"
   #format (|>> (text.interposed " ")
                {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
(the .public pre_load
  (Name Pre_Load)
  [#label "preload"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/area#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/link#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script#attributes
(the .public referrer_policy
  (Name Referrer_Policy)
  [#label "referrerpolicy"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/rel
(the .public relationship
  (Name Relationship)
  [#label "rel"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/Accessibility/ARIA/Roles
(the .public role
  (Name Role)
  [#label "role"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#sandbox
(the .public sandbox
  (Name (List Sandbox))
  [#label "sandbox"
   #format (|>> (text.interposed " ")
                {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/th#scope
(every .public Scope
  Text)

(with_template [<name>]
  [(the .public <name>
     Scope
     (|> (template.text [<name>])
         (text.replaced "column" "col")
         (text.replaced "_" "")))]

  [row]
  [column]
  [row_group]
  [column_group]
  )

(the .public scope
  (Name Scope)
  [#label "scope"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/area#attributes
(the .public shape
  (Name Shape)
  [#label "shape"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/source#attributes
(the .public sizes
  (Name [(Value Length) (List [query.Feature (Value Length)])])
  [#label "sizes"
   #format (function (_ [default conditionals])
             {.#Some (%.format (|> conditionals
                                   (list#each (function (_ [feature length])
                                                (%.format (query.feature feature)
                                                          " " (value.value length)
                                                          ", ")))
                                   (text.interposed ""))
                               (value.value default))})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/source#attributes
(the .public source_set
  (Name (List Source))
  [#label "srcset"
   #format (function (_ it)
             (when it
               (.list)
               {.#None}

               _
               {.#Some (|> it
                           (list#each source.source)
                           (text.interposed ", "))}))])

(with_template [<name>]
  [(the .public <name>
     (Name Int)
     [#label (|> (template.text [<name>])
                 (text.replaced "_" ""))
      #format (|>> %.int
                   {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/ol#start
  [start]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/tabindex
  [tab_index]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/style
(the .public style
  (Name Style)
  [#label "style"
   #format (|>> style.inline
                {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
(the .public button_type
  (Name button.Type)
  [#label "type"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#input_types
(the .public input_type
  (Name input.Type)
  [#label "type"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/ol#attributes
(the .public numbering_type
  (Name numbering.Type)
  [#label "type"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script#attributes
(the .public script_type
  (Name script.Type)
  [#label "type"
   #format (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#attributes
(the .public use_map
  (Name Text)
  [#label "usemap"
   #format (|>> (%.format "#")
                {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/textarea#wrap
(every .public Wrap
  Text)

(with_template [<name>]
  [(the .public <name>
     Wrap
     (template.text [<name>]))]

  [hard]
  [soft]
  [off]
  )

(the .public wrap
  (Name Wrap)
  [#label "wrap"
   #format (|>> {.#Some})])
