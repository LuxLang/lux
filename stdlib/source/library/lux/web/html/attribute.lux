... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Scope Source Target
                text for loop open pattern type list
                Name name)
   [data
    ["[0]" text (.only)
     [character (.only Character)]
     ["%" \\injection]]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor)]]]
   [macro
    ["[0]" template]]
   [type
    ["[0]" nominal]]
   [time
    ["[0]" instant (.only Instant)]]
   [world
    [net (.only URL)
     ["[0]" mime (.only MIME)]]
    [locale
     ["[0]" language (.only Language)]]]]]
 [/
  [capitalization (.only Capitalization)]
  [completion (.only Completion)]
  [decoding (.only Decoding)]
  [text_direction (.only Text_Direction)]
  [drag (.only Drag)]
  [enter (.only Enter)]
  [input_mode (.only Input_Mode)]
  [pre_load (.only Pre_Load)]
  [referrer_policy (.only Referrer_Policy)]
  [relationship (.only Relationship)]
  [role (.only Role)]
  [sandbox (.only Sandbox)]
  [shape (.only Shape)]
  [target (.only Target)]
  [pragma (.only Pragma)]
  ["[0]" source (.only Source)]
  [type
   ["[0]" button]
   ["[0]" input]
   ["[0]" numbering]
   ["[0]" script]]
  [///
   ["[0]" permission]
   [css
    ["[0]" id (.only ID)]
    ["[0]" class (.only Class)]
    ["[0]" query (.only Query)]
    ["[0]" value (.only Value Length)]
    ["[0]" style (.only Style)]]]])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes#attribute_list
(every .public (Injection of)
  (-> of
      (Maybe Text)))

(every .public (Name of)
  (Record
   [#label Text
    #as (Injection of)]))

(every .public (Attribute of)
  (Record
   [#name (Name of)
    #value of]))

(every .public Attributes
  (Stack (for_some (_ of)
           (Attribute of))))

(the .public empty
  Attributes
  (.stack))

(template.with [<name>]
  [(the .public <name>
     MIME
     (mime.mime (template.text [<name> "/*"])))]

  [audio]
  [application]
  [video]
  [image]
  [text]
  [font]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/accept
(the .public accept
  (Name (Stack MIME))
  [#label "accept"
   #as (|>> (stack#each mime.name)
            (text.interposed ", ")
            {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/accesskey
(the .public access_key
  (Name Character)
  [#label "accesskey"
   #as (|>> text.of_character
            {.#Some})])

(template.with [<name>]
  [(the .public <name>
     (Name URL)
     [#label (|> (template.text [<name>])
                 (text.replaced "form_" "form")
                 (text.replaced "source" "src"))
      #as (|>> {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/form#action
  [action]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/blockquote
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/ins
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/q
  [cite]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#data
  [data]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  [form_action]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/area#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/base#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/link#attributes
  [href]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#poster
  [poster]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/embed#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/source#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/track#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
  [source]
  )

(alias [hyper_link]
       ..href)

(the Allowances
  (.type (Maybe (Stack permission.Allowance))))

(the Permission
  (.type [permission.Directive Allowances]))

(the (allowances it)
  (-> Allowances
      Text)
  (when it
    {.#None}
    "*"
    
    {.#Some it}
    (when it
      (.stack)
      "'none'"

      _
      (text.interposed " " it))))

(the (permission [directive allowances])
  (-> Permission
      Text)
  (.text directive " " (..allowances allowances)))

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#allow
(the .public allow
  (Name (Stack Permission))
  [#label "allow"
   ... https://developer.mozilla.org/en-US/docs/Web/HTTP/Permissions_Policy
   #as (|>> (stack#each permission)
            (text.interposed "; ")
            {.#Some})])

(template.with [<name>]
  [(the .public <name>
     (Name Text)
     [#label (|> (template.text [<name>])
                 (text.replaced "direction_" "dir")
                 (text.replaced "_property" "prop"))
      #as (|>> {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#alt
  [alt]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meta#content
  [content]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  [direction_name]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/itemprop
  [item_property]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/optgroup#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/option#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/track#attributes
  [label]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/form#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/fieldset#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/output#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/select#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/textarea#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/map#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meta#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/param#attributes
  [name]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/textarea#attributes
  [placeholder]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/title
  [title]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/data#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/li#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meter#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/option#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/progress#attributes
  [value]
  )

(the presential_bit
  (Injection Bit)
  (function (_ it)
    (if it
      {.#Some ""}
      {.#None})))

(template.with [<name>]
  [(the .public <name>
     (Name Bit)
     [#label (|> (template.text [<name>])
                 (text.replaced "_" ""))
      #as ..presential_bit])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script#async
  [async]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video
  [auto_play]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#checked
  [checked]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
  [controls]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/track#default
  [default]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script#defer
  [defer]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/disabled
  [disabled]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/area#attributes
  [download]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  [form_no_validate]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#ismap
  [is_map]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
  [loop]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/multiple
  [multiple]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
  [muted]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/form#novalidate
  [no_validate]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/details#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/dialog#attributes
  [open]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#playsinline
  [plays_inline]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/readonly
  [read_only]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/required
  [required]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/ol#reversed
  [reversed]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/option#selected
  [selected]
  )

(the verbose_bit
  (Injection Bit)
  (function (_ it)
    {.#Some (if it
              "true"
              "false")}))

(template.with [<name>]
  [(the .public <name>
     (Name Bit)
     [#label (|> (template.text [<name>])
                 (text.replaced "_" ""))
      #as ..verbose_bit])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/spellcheck
  [spell_check]
  )

(the succinct_bit
  (Injection Bit)
  (function (_ it)
    {.#Some (if it
              "yes"
              "no")}))

(template.with [<name>]
  [(the .public <name>
     (Name Bit)
     [#label (|> (template.text [<name>])
                 (text.replaced "_" ""))
      #as ..succinct_bit])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/translate
  [translate]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/autocapitalize
(the .public auto_capitalize
  (Name Capitalization)
  [#label "autocapitalize"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/autocomplete
(the .public auto_complete
  (Name Completion)
  [#label "autocomplete"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/capture
(every .public Capture
  Text)

(template.with [<name>]
  [(the .public <name>
     Capture
     (template.text [<name>]))]

  [user]
  [environment]
  )

(the .public capture
  (Name Capture)
  [#label "capture"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/class
(the .public class
  (Name (Stack Class))
  [#label "class"
   #as (|>> (stack#each class.class)
            (text.interposed " ")
            {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/td#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/th#attributes
(the .public minimum_column_span
  Natural
  1)

(the .public maximum_column_span
  Natural
  1,000)

(template.with [<name>]
  [(the .public <name>
     (Name Natural)
     [#label (|> (template.text [<name>])
                 (text.replaced "umn" "")
                 (text.replaced "_" ""))
      #as (|>> %.natural
               {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/textarea#cols
  [columns]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/td#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/th#attributes
  [column_span]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/maxlength
  [max_length]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/minlength
  [min_length]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/textarea#rows
  [rows]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/td#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/th#attributes
  [row_span]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/size
  [size]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/col#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/colgroup#attributes
  [span]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/step
  [step]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/canvas#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/embed#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
  [height]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/canvas#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/embed#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
  [width]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/contenteditable
(every .public Editable
  Text)

(template.with [<name> <value>]
  [(the .public <name>
     Editable
     <value>)]

  [is_editable "true"]
  [not_editable "false"]
  [only_plain_text "plaintext-only"]
  )

(the .public content_editable
  (Name Editable)
  [#label "contenteditable"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/area#coords
(the .public coordinates
  (Name (Stack Natural))
  [#label "coords"
   #as (|>> (stack#each %.natural)
            (text.interposed ",")
            {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/crossorigin
(every .public Cross_Origin
  Text)

(template.with [<name>]
  [(the .public <name>
     Cross_Origin
     (|> (template.text [<name>])
         (text.replaced "_" "-")))]

  [anonymous]
  [use_credentials]
  )

(the .public cross_origin
  (Name Cross_Origin)
  [#label "crossorigin"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/del#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/ins#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/time#attributes
(the .public date_time
  (Name Instant)
  [#label "datetime"
   #as (|>> instant.as_text
            {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#decoding
(the .public decoding
  (Name Decoding)
  [#label "decoding"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/dir
(the .public text_direction
  (Name Text_Direction)
  [#label "dir"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/draggable
(the .public draggable
  (Name Drag)
  [#label "draggable"
   #as (|>> {.#Some})])

(template.with [<name>]
  [(the .public <name>
     (Name MIME)
     [#label (|> (template.text [<name>])
                 (text.replaced "encoding" "enc")
                 (text.replaced "_" ""))
      #as (|>> mime.name
               {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/form#enctype
  [encoding_type]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  [form_encoding_type]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/embed#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/source#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/link#attributes
  [type]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/enterkeyhint
(the .public enter_key_hint
  (Name Enter)
  [#label "enterkeyhint"
   #as (|>> {.#Some})])

(template.with [<name>]
  [(the .public <name>
     (Name ID)
     [#label (template.text [<name>])
      #as (|>> id.id
               {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/for
  [for]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/fieldset#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/output#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/select#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/textarea#attributes
  [form]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/id
  [id]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#list
  [list]
  )

(template.with [<name>]
  [(the .public <name>
     (Name Target)
     [#label (|> (template.text [<name>])
                 (text.replaced "_" ""))
      #as (|>> {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#attributes
  [form_target]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/area#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/base#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/form#attributes
  [target]
  )

(every .public Hidden
  Text)

(template.with [<name>]
  [(the .public <name>
     Hidden
     (|> (template.text [<name>])
         (text.replaced "_" "-")
         (text.replaced "forever" "hidden")))]

  [forever]
  [until_found]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/hidden
(the .public hidden
  (Name Hidden)
  [#label "hidden"
   #as (|>> {.#Some})])

(template.with [<name>]
  [(the .public <name>
     (Name Decimal)
     [#label (template.text [<name>])
      #as (|>> %.decimal
               {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meter#high
  [high]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meter#low
  [low]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/max
  [max]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/min
  [min]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meter#optimum
  [optimum]
  )

(template.with [<name>]
  [(the .public <name>
     (Name Language)
     [#label (|> (template.text [<name>])
                 (text.replaced "language" "lang")
                 (text.replaced "source" "src")
                 (text.replaced "_" "-"))
      #as (|>> language.name
               {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#attributes
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/link#attributes
  [href_language]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/lang
  [language]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/track#srclang
  [source_language]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/meta#http-equiv
(the .public http_equivalent
  (Name Pragma)
  [#label "http-equiv"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/Security/Subresource_Integrity
(nominal.every Hash
  Text

  (template.with [<name>]
    [(the .public <name>
       (-> Text
           Hash)
       (let [prefix (|> (template.text [<name>])
                        (text.replaced "_" "-"))]
         (|>> (.text prefix "-")
              nominal.abstraction)))]
    
    [sha_256]
    [sha_384]
    [sha_512]
    )

  (the .public integrity
    (Name Hash)
    [#label "integrity"
     #as (|>> nominal.reification
              {.#Some})])
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/inputmode
(the .public input_mode
  (Name Input_Mode)
  [#label "inputmode"
   #as (|>> {.#Some})])

(every .public Track_Kind
  Text)

(template.with [<name>]
  [(the .public <name>
     Track_Kind
     (template.text [<name>]))]

  [subtitles]
  [captions]
  [descriptions]
  [chapters]
  [metadata]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/track#kind
(the .public track_kind
  (Name Track_Kind)
  [#label "kind"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#attributes
(every .public Loading
  Text)

(template.with [<name>]
  [(the .public <name>
     Loading
     (template.text [<name>]))]

  [eager]
  [lazy]
  )

(the .public loading
  (Name Loading)
  [#label "loading"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/link#attributes
(the .public media
  (Name Query)
  [#label "media"
   #as (|>> query.query
            {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/form#method
(every .public Method
  Text)

(template.with [<name>]
  [(the .public <name>
     Method
     (template.text [<name>]))]

  [post]
  [get]
  [dialog]
  )

(the .public method
  (Name Method)
  [#label "method"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/pattern
(every .public Regular_Expression
  Text)

(the .public pattern
  (Name Regular_Expression)
  [#label "pattern"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#ping
(the .public ping
  (Name (Stack URL))
  [#label "ping"
   #as (|>> (text.interposed " ")
            {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/audio#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/video#attributes
(the .public pre_load
  (Name Pre_Load)
  [#label "preload"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/a#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/area#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/link#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script#attributes
(the .public referrer_policy
  (Name Referrer_Policy)
  [#label "referrerpolicy"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Attributes/rel
(the .public relationship
  (Name Relationship)
  [#label "rel"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/Accessibility/ARIA/Roles
(the .public role
  (Name Role)
  [#label "role"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/iframe#sandbox
(the .public sandbox
  (Name (Stack Sandbox))
  [#label "sandbox"
   #as (|>> (text.interposed " ")
            {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/th#scope
(every .public Scope
  Text)

(template.with [<name>]
  [(the .public <name>
     Scope
     (|> (template.text [<name>])
         (text.replaced "column" "col")
         (text.replaced "_" "")))]

  [row]
  [column]
  [row_group]
  [column_group]
  )

(the .public scope
  (Name Scope)
  [#label "scope"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/area#attributes
(the .public shape
  (Name Shape)
  [#label "shape"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/source#attributes
(the .public sizes
  (Name [(Value Length) (Stack [query.Feature (Value Length)])])
  [#label "sizes"
   #as (function (_ [default conditionals])
         {.#Some (.text (|> conditionals
                            (stack#each (function (_ [feature length])
                                          (.text (query.feature feature)
                                                 " " (value.value length)
                                                 ", ")))
                            (text.interposed ""))
                        (value.value default))})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/source#attributes
(the .public source_set
  (Name (Stack Source))
  [#label "srcset"
   #as (function (_ it)
         (when it
           (.stack)
           {.#None}

           _
           {.#Some (|> it
                       (stack#each source.source)
                       (text.interposed ", "))}))])

(template.with [<name>]
  [(the .public <name>
     (Name Integer)
     [#label (|> (template.text [<name>])
                 (text.replaced "_" ""))
      #as (|>> %.integer
               {.#Some})])]

  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/ol#start
  [start]
  ... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/tabindex
  [tab_index]
  )

... https://developer.mozilla.org/en-US/docs/Web/HTML/Global_attributes/style
(the .public style
  (Name Style)
  [#label "style"
   #as (|>> style.inline
            {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/button#attributes
(the .public button_type
  (Name button.Type)
  [#label "type"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input#input_types
(the .public input_type
  (Name input.Type)
  [#label "type"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/ol#attributes
(the .public numbering_type
  (Name numbering.Type)
  [#label "type"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/script#attributes
(the .public script_type
  (Name script.Type)
  [#label "type"
   #as (|>> {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/img#attributes
... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/object#attributes
(the .public use_map
  (Name Text)
  [#label "usemap"
   #as (|>> (.text "#")
            {.#Some})])

... https://developer.mozilla.org/en-US/docs/Web/HTML/Element/textarea#wrap
(every .public Wrap
  Text)

(template.with [<name>]
  [(the .public <name>
     Wrap
     (template.text [<name>]))]

  [hard]
  [soft]
  [off]
  )

(the .public wrap
  (Name Wrap)
  [#label "wrap"
   #as (|>> {.#Some})])
