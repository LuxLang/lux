... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [data
    ["[0]" text (.use "[1]#[0]" hash)
     ["%" \\injection]]]
   [macro
    ["[0]" syntax]]
   [type
    ["[0]" nominal]]
   ["[0]" meta (.only)
    ["[0]" module]
    ["[0]" code]]]])

(nominal.every .public Class
  Text

  (the .public class
    (-> Class Text)
    (|>> nominal.reification))

  (the .public custom
    (-> Text Class)
    (|>> nominal.abstraction))

  (the .public generic
    (syntax.macro (_ [])
      (do meta.monad
        [[module _] module.current
         class meta.seed]
        (pure (list (` (..custom (, (code.text (text "c" (%.natural_16 class)
                                                     "_" (%.natural_16 (text#hash module))))))))))))
  )
