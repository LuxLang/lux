... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [data
    ["[0]" text (.use "[1]#[0]" hash)
     ["%" \\injection]]]
   ["[0]" meta (.only)
    ["[0]" code]
    [macro
     ["[0]" syntax]]
    [type
     ["[0]" nominal]]]]])

(nominal.every .public ID
  Text

  (the .public id
    (-> ID Text)
    (|>> nominal.representation))

  (the .public custom
    (-> Text ID)
    (|>> nominal.abstraction))

  (the .public generic
    (syntax.macro (_ [])
      (do meta.monad
        [module meta.current_module_name
         id meta.seed]
        (in (list (` (..custom (, (code.text (%.message "i" (%.nat_16 id)
                                                        "_" (%.nat_16 (text#hash module))))))))))))
  )
