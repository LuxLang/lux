... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except with)
   [data
    [text
     ["%" \\format (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" mix)]]]
   [meta
    [type
     ["[0]" nominal (.except def)]]]]]
 ["[0]" //
  ["[1][0]" value (.only Value)]
  ["[1][0]" property (.only Property)]])

(nominal.def .public Style
  Text

  (def .public empty
    Style
    (abstraction ""))

  (def .public (with [property value])
    (All (_ brand)
      (-> [(Property brand) (Value brand)]
          (-> Style Style)))
    (|>> representation
         (format (//property.name property) ": " (//value.value value) ";")
         abstraction))

  (def .public inline
    (-> Style Text)
    (|>> representation))

  (def .public (style config)
    (-> (List (Ex (_ brand) [(Property brand) (Value brand)]))
        Style)
    (list#mix ..with ..empty config))
  )
