... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except and or not all only except)
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [macro
    ["[0]" syntax]
    ["[0]" template]]
   [type
    ["[0]" nominal]]
   [meta
    ["[0]" code (.only)
     ["s" \\projection]]]]]
 ["[0]" //
  ["[1][0]" value (.only Value Length Count Resolution Ratio
                         Orientation Scan Boolean Update
                         Block_Overflow Inline_Overflow
                         Display_Mode Color_Gamut Inverted_Colors
                         Pointer Hover
                         Light Scripting Motion Color_Scheme)]])

(the text_name
  (syntax.macro (_ [name s.text])
    (in (list (code.local (text.replaced "-" "_" name))))))

(nominal.every .public Media
  Text

  (the .public media
    (-> Media Text)
    (|>> nominal.reification))

  (template.with [<media>]
    [(`` (the .public (,, (text_name <media>))
           Media
           (nominal.abstraction <media>)))]

    ["all"]
    ["print"]
    ["screen"]
    ["speech"]
    ))

(nominal.every .public Feature
  Text

  (the .public feature
    (-> Feature Text)
    (|>> nominal.reification))

  (template.with [<feature> <brand>]
    [(`` (the .public ((,, (text_name <feature>)) input)
           (-> (Value <brand>) Feature)
           (nominal.abstraction
            (text "(" <feature> ": " (//value.value input) ")"))))]

    ["min-color" Count]
    ["color" Count]
    ["max-color" Count]

    ["min-color-index" Count]
    ["color-index" Count]
    ["max-color-index" Count]

    ["min-monochrome" Count]
    ["monochrome" Count]
    ["max-monochrome" Count]

    ["min-height" Length]
    ["height" Length]
    ["max-height" Length]

    ["min-width" Length]
    ["width" Length]
    ["max-width" Length]

    ["min-resolution" Resolution]
    ["resolution" Resolution]
    ["max-resolution" Resolution]

    ["aspect-ratio" Ratio]
    ["max-aspect-ratio" Ratio]
    ["min-aspect-ratio" Ratio]

    ["display-mode" Display_Mode]
    ["color-gamut" Color_Gamut]
    ["grid" Boolean]
    ["orientation" Orientation]
    ["overflow-block" Block_Overflow]
    ["overflow-inline" Inline_Overflow]
    ["scan" Scan]
    ["update" Update]
    ["inverted-colors" Inverted_Colors]
    ["pointer" Pointer]
    ["any-pointer" Pointer]
    ["hover" Hover]
    ["any-hover" Hover]
    ["light-level" Light]
    ["scripting" Scripting]
    ["prefers-reduced-motion" Motion]
    ["prefers-color-scheme" Color_Scheme]
    )
  )

(nominal.every .public Query
  Text

  (the .public query
    (-> Query Text)
    (|>> nominal.reification))

  (template.with [<name> <operator>]
    [(the .public <name>
       (-> Media Query)
       (|>> ..media
            (text <operator>)
            nominal.abstraction))]

    [except "not "]
    [only "only "]
    )

  (the .public not
    (-> Feature Query)
    (|>> ..feature
         (text "not ")
         nominal.abstraction))

  (template.with [<name> <operator>]
    [(the .public (<name> left right)
       (-> Query Query Query)
       (nominal.abstraction
        (text (nominal.reification left)
              <operator>
              (nominal.reification right))))]

    [and " and "]
    [or " or "]
    )
  )
