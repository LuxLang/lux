... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)]])

(the with_template (.in_module# .prelude .with_template))
(the template#macro (.in_module# .prelude .template#macro))

(every .public (Co it)
  (-> Any
      it))

(every .public (Contra it)
  (-> it
      Any))

(every .public (In it)
  (-> it
      it))

(every .public (Mutable r w)
  (Nominal "#Mutable" [[1b (-> w r)]]))

(with_template [<name> <type>]
  [(the .public <name>
     (template#macro
      (<name> it)
      [((.is (.for_any (_ r w) <type>)
             (.|>> .as_expected))
        it)]))]

  [[read (.-> (..Mutable r w) r)]
   [write (.-> w (..Mutable r w))]])

(every .public (Read_Only a)
  (Mutable a Nothing))

(every .public (Write_Only a)
  (Mutable Any a))

(with_template [<name> <type>]
  [(the .public <name>
     (template#macro
      (<name> it)
      [((.is (.for_any (_ r w) <type>)
             (.|>>))
        it)]))]

  [[read_only (.-> (..Mutable r w) (..Read_Only r))]
   [write_only (.-> (..Mutable r w) (..Write_Only w))]])
