... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except try except with type existential)
   [abstract
    [equivalence (.only Equivalence)]
    [functor (.only Functor)]
    ["[0]" monad (.only Monad)]]
   [control
    ["[0]" maybe]
    ["[0]" state]]
   [error
    ["[0]" try (.only Try) (.use "[1]#[0]" functor)]
    ["[0]" exception (.only Exception)]]
   [function
    [predicate (.only Predicate)]
    ["[0]" mixin (.only Mixin)]]
   [data
    ["[0]" product]
    ["[0]" text (.use "[1]#[0]" monoid)]
    [collection
     ["[0]" list (.use "[1]#[0]" monoid)]
     ["[0]" stack (.use "[1]#[0]" mix)]
     ["[0]" set (.only Set)]]]
   [logic
    ["[0]" bit]]
   [math
    [number
     ["[0]" /64 (.only)
      ["n" natural (.use "[1]#[0]" base_10)]]]]
   [macro
    ["^" pattern]
    ["[0]" template]]
   [aspect
    ["[0]" view]]]]
 ["[0]" // (.only)
  ["[0]" environment (.only Environment)]])

(the !n#=
  (template.macro (_ ,super ,sub)
    [(.i64_=# (.is .Natural ,super)
              (.is .Natural ,sub))]))

(exception.the .public (invalid_reification [abstraction parameters])
  (Exception [Type (List Type)])
  (exception.report
   (list ["Abstraction" (//.absolute_text abstraction)]
         ["Parameter" (exception.listing //.absolute_text parameters)])))

(exception.the .public (does_not_subsume [super sub])
  (Exception [Type Type])
  (exception.report
   (list ["Super type" (//.absolute_text super)]
         ["Sub type" (//.absolute_text sub)])))

(every .public Check
  (state.With Type_Context Try))

(the .public monad
  (Monad Check)
  (state.with try.monad))

(the .public functor
  (Functor Check)
  (by ..monad functor))

(use "/#[0]" ..monad)

... https://en.wikipedia.org/wiki/Hypothesis
(every Hypothesis
  (Record
   [#super Type
    #sub Type]))

... https://en.wikipedia.org/wiki/Subsumption
(every (Subsumption it)
  (Mixin [(List Hypothesis) it it]
         (Check (List Hypothesis))))

(every (Checker it)
  (-> [(List Hypothesis) it it]
      (Check (List Hypothesis))))

(the .public (value context check)
  (for_any (_ it)
    (-> Type_Context (Check it)
        (Try it)))
  (|> context
      check
      (try#each product.right)))

(the .public (failure message)
  (-> Text
      Check)
  (function (_ context)
    {try.#Failure message}))

(the .public (assertion message test)
  (-> Text Bit
      (Check Any))
  (function (_ context)
    (if test
        {try.#Success [context []]}
        {try.#Failure message})))

(the .public (except exception message)
  (for_any (_ ex it)
    (-> (Exception ex) ex
        (Check it)))
  (..failure (exception.error exception message)))

(the .public existential
  (Check [Natural Type])
  (function (_ context)
    (let [id (its .#ex_counter context)]
      {try.#Success [(revised .#ex_counter ++ context)
                     [id {.#Opaque id}]]})))

(the (query it)
  (for_any (_ it)
    (-> (environment.Query it)
        (Check it)))
  (function (_ context)
    (|> context
        (its .#var_bindings)
        it
        (try#each (|>> [context])))))

(the binding
  (-> //.Variable
      (Check environment.Binding))
  (|>> environment.binding
       ..query))

(the .public type
  (-> //.Variable
      (Check Type))
  (|>> environment.type
       ..query))

(the .public bound?
  (-> //.Variable
      (Check Bit))
  (|>> environment.bound?
       ..query))

(the (change it)
  (-> environment.Change
      (Check Any))
  (function (_ context)
    (|> context
        (its .#var_bindings)
        it
        (try#each (function (_ environment)
                    [(has .#var_bindings environment context)
                     []])))))

(the (new variable type)
  (-> //.Variable Type
      (Check Any))
  (|> (environment.new variable type)
      ..change))

(the (changed type variable)
  (-> //.Variable environment.Binding
      (Check Any))
  (|> (environment.changed type variable)
      ..change))

(the .public var
  (Check [//.Variable Type])
  (function (_ context)
    (let [id (its .#var_counter context)]
      {try.#Success [(|> context
                         (revised .#var_counter ++)
                         (revised .#var_bindings (function (_ it)
                                                   (list#composite it (list [id (environment.un_bound)])))))
                     [id {.#Variable id}]]})))

(the (of parameters abstraction)
  (-> (List Type) Type
      (Check Type))
  (when abstraction
    {.#Variable func_id}
    (monad.let ..monad
      [?abstraction' (..binding func_id)]
      (when ?abstraction'
        {.#Some abstraction'}
        (of parameters abstraction')

        _
        (except ..invalid_reification [abstraction parameters])))

    {.#Reification parameter' abstraction'}
    (of (list#composite (list parameter') parameters) abstraction')

    _
    (when (//.reified parameters abstraction)
      {.#Some output}
      (/#pure output)

      _
      (except ..invalid_reification [abstraction parameters]))))

(the ring
  (-> //.Variable
      (Check environment.Ring))
  (|>> environment.ring
       ..query))

(the .public (linked? @0 @1)
  (-> //.Variable //.Variable
      (Check Bit))
  (/#each (function (_ it)
            (stack.member? n.equivalence it @1))
          (..ring @0)))

(exception.the .public (cannot_identify var)
  (Exception //.Variable)
  (exception.report
   (list ["Variable" (n#as var)])))

(the .public (identity aliases @)
  (-> (List //.Variable) //.Variable
      (Check Type))
  (monad.let [! ..monad]
    [:bound: (..binding @)]
    (when :bound:
      (environment.bound :bound:)
      (pure :bound:)

      (environment.un_bound)
      (monad.let !
        [existing (..ring @)]
        (if (n.< 2 (stack.size existing))
            (..except ..cannot_identify [@])
            (monad.let !
              [.let [forbidden (|> aliases
                                   (set.of_list n.hash)
                                   (set.has @))
                     permitted (|> (view.of list.stack existing)
                                   (set.of_list n.hash)
                                   (set.difference forbidden))]]
              (when (list.item 0 (set.as_list permitted))
                {try.#Success identity}
                (pure {.#Variable identity})
                
                {try.#Failure _}
                (..except ..cannot_identify [@]))))))))

(the (erase! @)
  (-> //.Variable
      (Check Any))
  (..change (|>> (environment.lacks @)
                 {try.#Success})))

(the .public (forget! @)
  (-> //.Variable
      (Check Any))
  (monad.let [! ..monad]
    [ring (|> (environment.ring @)
              ..query)]
    (when ring
      (stack)
      (pure [])
      
      (stack @me)
      (erase! @me)
      
      (stack @other @me)
      (monad.let !
        [_ (..changed @other (environment.un_bound))]
        (erase! @me))
      
      (stack.partial @prev _)
      (when (stack.reversed ring)
        (stack.partial @me @next _)
        (monad.let !
          [_ (..changed @prev (environment.bound {.#Variable @next}))
           _ (..changed @next (environment.bound {.#Variable @prev}))]
          (erase! @me))

        _
        (undefined)))))

(the .public (try it)
  (for_any (_ it)
    (-> (Check it)
        (Check (Try it))))
  (function (_ context)
    {try.#Success (when (it context)
                    {try.#Success [context' output]}
                    [context' {try.#Success output}]

                    {try.#Failure error}
                    [context {try.#Failure error}])}))

(the .public fresh_context
  Type_Context
  [.#var_counter 0
   .#ex_counter 0
   .#var_bindings environment.empty])

(the (either left right)
  (for_any (_ it)
    (-> (Check it) (Check it)
        (Check it)))
  (function (_ context)
    (when (left context)
      {try.#Failure _}
      (right context)

      output
      output)))

(the hypothesis_equivalence
  (Equivalence Hypothesis)
  (all product.equivalence
       //.equivalence
       //.equivalence
       ))

(the (hypothesized? expected hypotheses)
  (-> Hypothesis (List Hypothesis)
      Bit)
  (list.any? (by ..hypothesis_equivalence = expected) hypotheses))

... TODO: "if_can_bind" can be optimized...
(the (if_can_bind id type then else)
  (for_any (_ it)
    (-> //.Variable Type (Check it) (-> Type (Check it))
        (Check it)))
  (all either
       (monad.let ..monad
         [_ (..new id type)]
         then)
       (monad.let [! ..monad]
         [ring (..ring id)
          _ (..assertion "" (n.> 1 (stack.size ring)))
          _ (stack.each' ! (function (_ variable)
                             (..changed variable (environment.bound type)))
                         ring)]
         then)
       (monad.let ..monad
         [?bound (..binding id)]
         (else (maybe.else {.#Variable id} ?bound)))))

(the (two left right)
  (-> //.Variable //.Variable
      (Check Any))
  (|> (environment.two left right)
      ..change))

(the (three interpose to from)
  (-> //.Variable //.Variable //.Variable
      (Check Any))
  (|> (environment.three interpose to from)
      ..change))

... TODO: Can be optimized...
(the (variable_on_variable complete [hypotheses idE idA])
  (-> (Checker Type)
      (Checker //.Variable))
  (if (!n#= idE idA)
      (/#pure hypotheses)
      (monad.let [! ..monad]
        [ebound (..query (environment.its idE))
         abound (..query (environment.its idA))]
        (when [ebound abound]
          ... Link the 2 variables circularly
          [(environment.un_bound) (environment.un_bound)]
          (monad.let !
            [_ (..two idE idA)]
            (pure hypotheses))

          ... Interpose new variable between 2 existing links
          [(environment.bound etype) (environment.un_bound)]
          (when etype
            {.#Variable targetE}
            (monad.let !
              [_ (..three idA targetE idE)]
              (pure hypotheses))

            _
            (complete [hypotheses etype {.#Variable idA}]))

          ... Interpose new variable between 2 existing links
          [(environment.un_bound) (environment.bound atype)]
          (when atype
            {.#Variable targetA}
            (monad.let !
              [_ (..three idE targetA idA)]
              (pure hypotheses))

            _
            (complete [hypotheses {.#Variable idE} atype]))

          [(environment.bound etype) (environment.bound atype)]
          (`` (when [etype atype]
                [{.#Variable targetE} {.#Variable targetA}]
                (monad.let !
                  [ringE (..ring idE)
                   ringA (..ring idA)]
                  (if (by set.equivalence =
                          (set.of_list n.hash (view.of list.stack ringE))
                          (set.of_list n.hash (view.of list.stack ringA)))
                      (pure hypotheses)
                      ... Fuse 2 rings
                      (monad.let !
                        [_ (stack.mix' ! (function (_ interpose to)
                                           (monad.let !
                                             [_ (..three interpose to idE)]
                                             (pure interpose)))
                                       targetE
                                       ringA)]
                        (pure hypotheses))))

                (,, (template.with [<pattern> <id> <type>]
                      [<pattern>
                       (monad.let !
                         [ring (..ring <id>)
                          _ (stack.each' ! (function (_ variable)
                                             (..changed variable (environment.bound <type>)))
                                         ring)]
                         (pure hypotheses))]

                      [[[{.#Variable _} _] idE atype]
                       [[_ {.#Variable _}] idA etype]]
                      ))
                
                _
                (complete [hypotheses etype atype])))))))

(the silent_failure!
  Check
  (..failure ""))

... TODO: Can be optimized...
(the (reification_on_reification complete [hypotheses super sub])
  (-> (Checker Type)
      (Checker [Type Type]))
  (let [[super_parameter super_abstraction] super
        [sub_parameter sub_abstraction] sub]
    (when [super_abstraction sub_abstraction]
      [{.#Quantification .universal _ _} {.#Opaque _}]
      (monad.let ..monad
        [super' (..of (list super_parameter) super_abstraction)]
        (complete [hypotheses super' {.#Reification sub}]))

      [{.#Opaque _} {.#Quantification .universal _ _}]
      (monad.let ..monad
        [sub' (..of (list sub_parameter) sub_abstraction)]
        (complete [hypotheses {.#Reification super} sub']))

      [{.#Reification [super_parameter' super_abstraction']} {.#Opaque _}]
      (monad.let ..monad
        [super_abstraction'' (..of (list super_parameter') super_abstraction')]
        (complete [hypotheses {.#Reification [super_parameter super_abstraction'']} {.#Reification sub}]))

      [{.#Opaque _} {.#Reification [sub_parameter' sub_abstraction']}]
      (monad.let ..monad
        [sub_abstraction'' (..of (list sub_parameter') sub_abstraction')]
        (complete [hypotheses {.#Reification super} {.#Reification [sub_parameter sub_abstraction'']}]))

      (^.or [{.#Opaque _} _] [_ {.#Opaque _}])
      (monad.let ..monad
        [hypotheses (complete [hypotheses super_abstraction sub_abstraction])]
        (complete [hypotheses super_parameter sub_parameter]))

      [{.#Variable id} _]
      (all ..either
           (monad.let ..monad
             [super_abstraction' (..type id)]
             (complete [hypotheses {.#Reification super_parameter super_abstraction'} {.#Reification sub}]))
           (when sub_abstraction
             {.#Quantification .universal _ _}
             (monad.let ..monad
               [sub' (..of (list sub_parameter) sub_abstraction)]
               (complete [hypotheses {.#Reification super} sub']))

             _
             (monad.let ..monad
               [hypotheses (complete [hypotheses super_abstraction sub_abstraction])
                super' (..of (list super_parameter) sub_abstraction)
                sub' (..of (list sub_parameter) sub_abstraction)]
               (complete [hypotheses super' sub']))))

      [_ {.#Variable id}]
      (all ..either
           (monad.let ..monad
             [sub_abstraction' (..type id)]
             (complete [hypotheses {.#Reification super} {.#Reification sub_parameter sub_abstraction'}]))
           (monad.let ..monad
             [hypotheses (complete [hypotheses super_abstraction sub_abstraction])
              super' (..of (list super_parameter) super_abstraction)
              sub' (..of (list sub_parameter) super_abstraction)]
             (complete [hypotheses super' sub'])))

      _
      ..silent_failure!)))

(the when_variable
  (template.macro (_ hypotheses complete)
    [[{.#Variable idE} {.#Variable idA}]
     (variable_on_variable complete [hypotheses idE idA])
     
     [{.#Variable id} _]
     (if_can_bind id sub
                  (/#pure hypotheses)
                  (function (_ bound)
                    (complete [hypotheses bound sub])))
     
     [_ {.#Variable id}]
     (if_can_bind id super
                  (/#pure hypotheses)
                  (function (_ bound)
                    (complete [hypotheses super bound])))]))

(the !text#=
  (template.macro (_ super sub)
    [(.text_=# super sub)]))

(the when_nominal
  (template.macro (_ hypotheses complete)
    [[{.#Nominal e_name e_params} {.#Nominal a_name a_params}]
     (if (and (!text#= e_name a_name)
              (n.= (list.size e_params) (list.size a_params)))
         (list.mix' ..monad
                    (function (_ [[expected_polarity e_head]
                                  [actual_polarity a_head]]
                                 hypotheses)
                      (if (bit.= expected_polarity actual_polarity)
                          (when expected_polarity
                            .co_variant
                            (complete [hypotheses e_head a_head])

                            .contra_variant
                            (complete [hypotheses a_head e_head]))
                          ..silent_failure!))
                    hypotheses
                    (list.zipped_2 e_params a_params))
         ..silent_failure!)]))

(the when_opaque
  (template.macro (_ hypotheses complete)
    [[{.#Opaque e!id} {.#Opaque a!id}]
     (if (!n#= e!id a!id)
         (/#pure hypotheses)
         ..silent_failure!)]))

(the when_named
  (template.macro (_ hypotheses complete)
    [[{.#Named _ ?etype} sub]
     (complete [hypotheses ?etype sub])

     [super {.#Named _ ?atype}]
     (complete [hypotheses super ?atype])]))

(the limit (/64.<< 7 1))

(the not_too_many?
  (Predicate (List Hypothesis))
  (|>> list.size
       (n.< ..limit)))

(the (hypothesis_as_text it)
  (text.Injection Hypothesis)
  (text (//.as_text (its #super it))
        " = "
        (//.as_text (its #sub it))))

(exception.the .public (too_many_hypotheses [it])
  (Exception [(List Hypothesis)])
  (exception.report
   (list ["Limit" (n#as limit)]
         ["Hypotheses" (exception.listing ..hypothesis_as_text it)])))

(the (super_reification complete hypotheses super_parameter super_abstraction sub)
  (-> (Checker Type)
      (List Hypothesis) Type Type Type
      (Check (List Hypothesis)))
  (let [new_hypothesis [#super {.#Reification super_parameter super_abstraction}
                        #sub sub]]
    (if (hypothesized? new_hypothesis hypotheses)
        (/#pure hypotheses)

        (not_too_many? hypotheses)
        (monad.let ..monad
          [super' (..of (list super_parameter) super_abstraction)]
          (complete [(list#composite (list new_hypothesis) hypotheses) super' sub]))

        ... else
        (..except ..too_many_hypotheses [hypotheses]))))

(the (opaque_reification complete hypotheses
                         [super_quantification super_parameters]
                         [sub_quantification sub_parameters])
  (-> (Checker Type)
      (List Hypothesis)
      [Type (List Type)] [Type (List Type)]
      (Check (List Hypothesis)))
  (monad.let [! ..monad]
    [hypotheses (complete [hypotheses super_quantification sub_quantification])]
    (if (n.= (list.size super_parameters)
             (list.size sub_parameters))
        (list.mix' !
                   (function (_ [super sub] hypotheses)
                     (complete [hypotheses super sub]))
                   hypotheses
                   (list.reversed (list.zipped_2 super_parameters sub_parameters)))
        ..silent_failure!)))

(`` (the when_reification
      (template.macro (_ hypotheses complete)
        [[{.#Reification super_parameter super_abstraction}
          {.#Reification sub_parameter sub_abstraction}]
         (let [super_application (//.flat_reification super)
               sub_application (//.flat_reification sub)]
           (when [(product.left super_application) (product.left sub_application)]
             (^.or [{.#Opaque _} {.#Opaque _}]
                   [{.#Variable _} {.#Opaque _}]
                   [{.#Opaque _} {.#Variable _}])
             (opaque_reification complete hypotheses super_application sub_application)

             _
             (when [super_abstraction sub_abstraction]
               (,, (template.with [<fE> <fA>]
                     [[<fE> <fA>]
                      (reification_on_reification complete
                                                  [hypotheses
                                                   [super_parameter <fE>]
                                                   [sub_parameter <fA>]])]

                     [[fE {.#Opaque ex}]
                      [{.#Opaque exE} fA]
                      
                      [fE {.#Variable idA}]
                      [{.#Variable idE} fA]]
                     ))

               _
               (super_reification complete hypotheses super_parameter super_abstraction sub))))
         
         [{.#Reification super_parameter super_abstraction} sub]
         (super_reification complete hypotheses super_parameter super_abstraction sub)

         [super {.#Reification A F}]
         (monad.let ..monad
           [sub' (..of (list A) F)]
           (complete [hypotheses super sub']))])))

(`` (the when_quantification
      (template.macro (_ hypotheses complete)
        [... TODO: Refactor-away as cold-code
         (,, (template.with [<tag> <instancer>]
               [[{.#Quantification <tag> _} _]
                (monad.let ..monad
                  [[_ paramT] <instancer>
                   super' (..of (list paramT) super)]
                  (complete [hypotheses super' sub]))]

               [[.universal ..existential]
                [.existential ..var]]
               ))

         ... TODO: Refactor-away as cold-code
         (,, (template.with [<tag> <instancer>]
               [[_ {.#Quantification <tag> _}]
                (monad.let ..monad
                  [[_ paramT] <instancer>
                   sub' (..of (list paramT) sub)]
                  (complete [hypotheses super sub']))]

               [[.universal ..var]
                [.existential ..existential]]
               ))])))

(the subsumption
  (Checker Type)
  (function (complete it)
    (let [[hypotheses super sub] it]
      (if (for .php
               ... TODO: Remove this once JPHP is gone.
               false
               (or (same? super sub)
                   (//.= super sub)))
          (/#pure hypotheses)
          (<<| (exception.with ..does_not_subsume [super sub])
               ((`` (when [super sub]
                      (,, (when_variable hypotheses complete))
                      
                      (,, (when_nominal hypotheses complete))
                      (,, (when_opaque hypotheses complete))
                      (,, (when_named hypotheses complete))
                      
                      (,, (when_reification hypotheses complete))
                      (,, (when_quantification hypotheses complete))

                      _
                      ..silent_failure!))))))))

(the .public (check super sub)
  (-> Type Type
      (Check Any))
  (..subsumption [(list) super sub]))

(the .public (< super sub)
  (-> Type Type
      Bit)
  (when (..value ..fresh_context
                 (..check super sub))
    {try.#Failure _}
    false

    {try.#Success _}
    true))

(the .public (> sub super)
  (-> Type Type
      Bit)
  (< super sub))

(the .public context
  (Check Type_Context)
  (function (_ context)
    {try.#Success [context context]}))

(the .public (with context)
  (-> Type_Context
      (Check Any))
  (function (_ _)
    {try.#Success [context []]}))

(the .public (clean aliases inputT)
  (-> (List //.Variable) Type
      (Check Type))
  (`` (when inputT
        {.#Nominal name paramsT+}
        (|> paramsT+
            (list.each' ..monad (function (_ [polarity it])
                                  (by ..functor each
                                      (|>> [polarity])
                                      (clean aliases it))))
            (/#each (|>> {.#Nominal name})))

        (^.or {.#Parameter _}
              {.#Opaque _}
              {.#Named _})
        (/#pure inputT)

        {.#Reification leftT rightT}
        (monad.let ..monad
          [leftT' (clean aliases leftT)]
          (|> (clean aliases rightT)
              (/#each (|>> {.#Reification leftT'}))))

        {.#Variable @it}
        (when aliases
          (list)
          (monad.let ..monad
            [?subT (..binding @it)]
            (when ?subT
              {.#Some subT}
              (clean aliases subT)

              _
              (pure inputT)))

          _
          (monad.let ..monad
            [:it: (..try (..identity aliases @it))]
            (when :it:
              {try.#Success :it:}
              (when :it:
                {.#Variable _}
                (pure inputT)

                _
                (clean aliases :it:))

              failure
              (pure inputT))))

        {.#Quantification quantification envT+ unquantifiedT}
        (monad.let [! ..monad]
          [envT+' (stack.each' !
                               (function (_ [abstraction argument])
                                 (monad.let !
                                   [abstraction (clean aliases abstraction)
                                    argument (clean aliases argument)]
                                   (pure [abstraction argument])))
                               envT+)
           unquantifiedT' (clean aliases unquantifiedT)]
          (pure {.#Quantification quantification envT+' unquantifiedT'}))
        )))
