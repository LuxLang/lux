(.module:
  [library
   [lux (#- type)
    [abstract
     [equivalence (#+ Equivalence)]]
    [control
     [parser
      ["<.>" code]]]
    [macro (#+ with_identifiers)
     [syntax (#+ syntax:)]]
    ["." type
     abstract]]])

(abstract: .public (Class t c %)
  {#.doc (example "The class knows how to classify/label values that are meant to be equivalent to one another.")}
  
  (-> t c)

  (def: .public class
    (All [t c]
      (Ex [%]
        (-> (-> t c) (Class t c %))))
    (|>> :abstraction))

  (abstract: .public (Quotient t c %)
    {#.doc (example "A quotient value has been labeled with a class."
                    "All equivalent values will belong to the same class."
                    "This means all equivalent values possess the same label.")}
    
    {#value t
     #label c}

    (def: .public (quotient class value)
      (All [t c %]
        (-> (Class t c %) t
            (Quotient t c %)))
      (:abstraction {#value value
                     #label ((:representation Class class) value)}))

    (template [<name> <output> <slot>]
      [(def: .public <name>
         (All [t c %] (-> (Quotient t c %) <output>))
         (|>> :representation (value@ <slot>)))]

      [value t #value]
      [label c #label]
      )
    )
  )

(syntax: .public (type [class <code>.any])
  {#.doc (example "The Quotient type associated with a Class type."
                  (def: even
                    (class even?))

                  (def: Even
                    Type
                    (type even))

                  (: Even
                     (quotient even 123)))}
  (with_identifiers [g!t g!c g!%]
    (in (list (` ((~! type.:by_example)
                  [(~ g!t) (~ g!c) (~ g!%)]

                  (..Class (~ g!t) (~ g!c) (~ g!%))
                  (~ class)
                  
                  (..Quotient (~ g!t) (~ g!c) (~ g!%))))))))

(implementation: .public (equivalence super)
  (All [t c %] (-> (Equivalence c) (Equivalence (..Quotient t c %))))

  (def: (= reference sample)
    (\ super = (..label reference) (..label sample))))
