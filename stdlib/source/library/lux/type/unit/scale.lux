... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except type)
   [data
    [collection
     ["[0]" list
      ["?[1]" \\projection]]]]
   [math
    [number
     [/64
      ["i" integer]
      ["[0]" fraction]]]]
   ["[0]" macro (.only)
    ["[0]" syntax]
    ["[0]" template]]
   [meta
    ["[0]" code
     ["<[1]>" \\projection]]]
   [aspect
    ["[0]" case]]]]
 ["[0]" // (.only)
  ["/[1]" //]])

(.every .public (Scale s)
  (Interface
   (is (for_any (_ u) (-> (//.Measure Any u) (//.Measure s u)))
       up)
   (is (for_any (_ u) (-> (//.Measure s u) (//.Measure Any u)))
       down)
   (is fraction.Number
       fraction)))

(the .public (scale it)
  (for_some (_ s)
    (-> fraction.Number
        (Scale s)))
  (let [numerator (.integer (fraction.numerator it))
        denominator (.integer (fraction.denominator it))]
    (implementation
     (the up
       (|>> //.number
            (i.x numerator)
            (i./ denominator)
            //.measure))
     (the down
       (|>> //.number
            (i.x denominator)
            (i./ numerator)
            //.measure))
     (the fraction
       it))))

(the .public (re_scaled from to measure)
  (for_any (_ si so u)
    (-> (Scale si) (Scale so) (//.Measure si u)
        (//.Measure so u)))
  (let [it (fraction./ (by from fraction)
                       (by to fraction))]
    (|> measure
        //.number
        (i.x (.integer (fraction.numerator it)))
        (i./ (.integer (fraction.denominator it)))
        //.measure)))

(syntax.the .public (type [it ?list.any])
  (macro.with_names ['of]
    (pure (list (` (///.by_example [(, 'of)]
                     (is (..Scale (, 'of))
                         (, it))
                     (, 'of)))))))

(template.with [<order_of_magnitude> <up> <up_type> <down> <down_type>]
  [(the .public <up>
     (scale (case.some fraction.natural <order_of_magnitude>)))
   
   (the .public <up_type>
     (let [[module _] (name .._)
           [_ proper] (name <up_type>)]
       {.#Named [module proper]
                (..type <up>)}))
   
   (the .public <down>
     (|> (by <up> fraction)
         fraction.reciprocal
         scale))
   
   (the .public <down_type>
     (let [[module _] (name .._)
           [_ proper] (name <down_type>)]
       {.#Named [module proper]
                (..type <down>)}))]

  [[        1,000 kilo Kilo milli Milli]
   [    1,000,000 mega Mega micro Micro]
   [1,000,000,000 giga Giga nano  Nano]])
