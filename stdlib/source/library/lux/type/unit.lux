... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... https://en.wikipedia.org/wiki/Dimensional_analysis
(.using
 [library
  [lux (.except type as)
   [abstract
    [equivalence (.only Equivalence)]
    [enum (.only Enum)]
    ["[0]" order (.only Order)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list
      ["?[1]" \\projection]]]]
   [math
    [number
     ["i" integer]]]
   ["[0]" macro (.only)
    ["[0]" syntax]
    ["[0]" template]]
   [meta
    ["[0]" code
     ["<[1]>" \\projection]]]]]
 ["[0]" // (.only)
  ["[0]" nominal]])

(nominal.every .public (Measure scale unit)
  Integer

  (the .public measure
    (for_any (_ scale unit)
      (-> Integer
          (Measure scale unit)))
    (|>> nominal.abstraction))

  (the .public number
    (for_any (_ scale unit)
      (-> (Measure scale unit)
          Integer))
    (|>> nominal.reification))

  (the .public equivalence
    (for_any (_ scale unit)
      (Equivalence (Measure scale unit)))
    (implementation
     (the (= expected actual)
       (i.= (nominal.reification expected) (nominal.reification actual)))))

  (alias [=]
         ..equivalence)

  (the .public order
    (for_any (_ scale unit)
      (Order (Measure scale unit)))
    (implementation
     (the equivalence ..equivalence)
     
     (the (< reference it)
       (i.< (nominal.reification reference) (nominal.reification it)))))

  (order.for [scale unit] (..Measure scale unit) ..order)

  (the .public enum
    (for_any (_ scale unit)
      (Enum (Measure scale unit)))
    (implementation
     (the order ..order)
     (the succ (|>> nominal.reification ++ nominal.abstraction))
     (the pred (|>> nominal.reification -- nominal.abstraction))))

  (template.with [<name> <op>]
    [(the .public (<name> param subject)
       (for_any (_ scale unit)
         (-> (Measure scale unit) (Measure scale unit)
             (Measure scale unit)))
       (nominal.abstraction
        (<op> (nominal.reification param)
              (nominal.reification subject))))]

    [+ i.+]
    [- i.-]
    )

  (template.with [<name> <op> <p> <s> <p*s>]
    [(the .public (<name> param subject)
       (for_any (_ scale p s)
         (-> (Measure scale <p>) (Measure scale <s>)
             (Measure scale <p*s>)))
       (nominal.abstraction
        (<op> (nominal.reification param)
              (nominal.reification subject))))]

    [* i.* p s [p s]]
    [/ i./ p [p s] s]
    )

  (.every .public (Unit it)
    (Interface
     (is (-> Integer
             (Measure Any it))
         in)
     (is (-> (Measure Any it)
             Integer)
         out)
     (is (-> (Measure Any it)
             Text)
         as)))

  (the .public (unit descriptor)
    (for_some (_ it)
      (-> Text
          (Unit it)))
    (implementation
     (the in ..measure)
     (the out ..number)
     (the as (|>> ..number %.integer (text.suffix descriptor)))))
  )

(the .public type
  (syntax.macro (_ [it ?list.any])
    (macro.with_names ['it]
      (in (list (` (//.by_example [(, 'it)]
                     (is (..Unit (, 'it))
                         (, it))
                     (, 'it))))))))

(template.with [<descriptor> <unit> <type>]
  [(the .public <unit>
     (..unit <descriptor>))
   
   (the .public <type>
     (let [[module _] (name .._)
           [_ proper] (name <type>)]
       {.#Named [module proper]
                (..type <unit>)}))]

  ["g" gram Gram]
  ["m" meter Meter]
  ["l" litre Litre]
  ["s" second Second]
  )
