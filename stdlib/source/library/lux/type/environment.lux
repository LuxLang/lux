... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except its has revised type)
   [abstract
    [monad (.only do)]]
   [error
    ["[0]" try (.only Try) (.use "[1]#[0]" functor)]
    ["[0]" exception (.only Exception)]]
   [data
    [collection
     ["[0]" stack]]]
   [math
    [number
     ["[0]" natural (.use "[1]#[0]" base_10)]]]
   [macro
    ["^" pattern]
    ["[0]" template]]]]
 ["[0]" //])

(every .public Binding
  (Maybe Type))

(the .public un_bound
  (template.macro (_)
    [{.#None}]))

(the .public bound
  (template.macro (_ ,type)
    [{.#Some ,type}]))

(template.with [,name]
  [(exception.the .public (,name [it])
     (Exception //.Variable)
     (exception.report
      (list ["Variable" (natural#as it)])))]

  [not_known]
  [not_bound]
  )

(every .public Environment
  (Stack [//.Variable Binding]))

(the It ..Environment)

(the .public empty
  ..It
  (stack))

(the .public (has variable)
  (-> //.Variable ..It
      ..It)
  (|>> (stack.partial [variable (..un_bound)])))

(the variable#=
  (template.macro (_ ,super ,sub)
    [(.i64_=# (.is //.Variable ,super)
              (.is //.Variable ,sub))]))

(every .public (Query it)
  (-> ..It
      (Try it)))

(the .public (its expected it)
  (-> //.Variable
      (Query Binding))
  (when it
    (stack.partial [actual var_type]
                   it')
    (if (variable#= expected actual)
      {try.#Success var_type}
      (its expected it'))

    (stack)
    (exception.except ..not_known [expected])))

(the (revised expected binding it)
  (-> //.Variable Binding ..It
      (Try ..It))
  (when it
    (stack.partial [actual var_type] it')
    (if (variable#= expected actual)
      {try.#Success (stack.partial [actual binding]
                                   it')}
      (try#each (|>> (stack.partial [actual var_type]))
                (revised expected binding it')))

    (stack)
    (exception.except ..not_known [expected])))

(the .public (binding variable it)
  (-> //.Variable
      (Query Binding))
  (|> (..its variable it)
      (try#each (function (_ it)
                  (when it
                    (^.or (..bound {.#Variable _})
                          (..un_bound))
                    (..un_bound)

                    bound
                    bound)))))

(the .public (type variable it)
  (-> //.Variable
      (Query Type))
  (do try.monad
    [it (..binding variable it)]
    (when it
      (..bound it)
      (in it)

      un_bound
      (exception.except ..not_bound [variable]))))

(the .public (bound? variable it)
  (-> //.Variable
      (Query Bit))
  (do try.monad
    [it (..binding variable it)]
    (in (when it
          (..bound it)
          true

          un_bound
          false))))

(every .public Change
  (-> ..It
      (Try ..It)))

(exception.the .public (cannot_bind_again [variable wanted current])
  (Exception [//.Variable Type Type])
  (exception.report
   (list ["Variable" (natural#as variable)]
         ["Wanted type" (//.absolute_text wanted)]
         ["Current type" (//.absolute_text current)])))

(the .public (new variable type it)
  (-> //.Variable Type
      Change)
  (do try.monad
    [binding (..its variable it)]
    (when binding
      (..un_bound)
      (..revised variable {.#Some type} it)

      (..bound current)
      (exception.except ..cannot_bind_again [variable type current]))))

(the .public (changed variable binding it)
  (-> //.Variable Binding
      Change)
  (do try.monad
    [variable_is_known! (..its variable it)]
    (..revised variable binding it)))

(every .public Ring
  (Stack //.Variable))

(the link
  (template.macro (_ ,it)
    [{.#Variable ,it}]))

(the .public (ring start it)
  (-> //.Variable
      (Query Ring))
  (loop (ring [current start
               output (stack start)])
    (when (..its current it)
      {try.#Success (..bound type)}
      (when type
        (..link next)
        (if (variable#= start next)
          {try.#Success output}
          (ring next (stack.partial next output)))
        
        invalid
        {try.#Success (stack)})

      {try.#Success (..un_bound)}
      {try.#Success output}
      
      {try.#Failure error}
      {try.#Failure error})))

... TODO: Can be optimized...
(the .public (two left right it)
  (-> //.Variable //.Variable
      Change)
  (do try.monad
    [it (..new left {.#Variable right} it)]
    (..new right {.#Variable left} it)))

... TODO: Can be optimized...
(the .public (three interpose to from it)
  (-> //.Variable //.Variable //.Variable
      Change)
  (do try.monad
    [it (..changed from (..bound {.#Variable interpose}) it)]
    (..changed interpose (..bound {.#Variable to}) it)))
