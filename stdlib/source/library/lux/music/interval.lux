... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [function
    [predicate (.only Predicate)]]
   [math
    ["[0]" number (.only)
     ["n" natural]]]
   [macro
    ["[0]" template]]]])

... https://en.wikipedia.org/wiki/Interval_(music)
(every .public Interval
  Natural)

(template.with [<steps> <name>]
  [(the .public <name>
     Interval
     (number.hex <steps>))]

  ["0" perfect_unison]
  ["1" minor_2nd]
  ["2" major_2nd]
  ["3" minor_3rd]
  ["4" major_3rd]
  ["5" perfect_4th]
  ["6" tritone] ... https://en.wikipedia.org/wiki/Tritone
  ["7" perfect_5th]
  ["8" minor_6th]
  ["9" major_6th]
  ["A" minor_7th]
  ["B" major_7th]
  ["C" perfect_8ve]
  )

... https://en.wikipedia.org/wiki/Semitone
(alias [semi_tone half_tone step]
       ..minor_2nd)

(alias [tone whole_tone]
       ..major_2nd)

(alias [octave]
       ..perfect_8ve)

(the .public simple?
  (Predicate Interval)
  (|>> (n./ ..octave)
       (n.= 0)))

(the .public compound?
  (Predicate Interval)
  (|>> (n./ ..octave)
       (n.> 0)))

... https://en.wikipedia.org/wiki/Complement_(music)
... https://en.wikipedia.org/wiki/Inversion_(music)
(the .public (inversion it)
  (-> Interval
      Interval)
  (let [[octave class] (n./% ..octave it)]
    (|> ..octave
        (n.- class)
        (n.% ..octave)
        (n.+ (n.* octave ..octave)))))
