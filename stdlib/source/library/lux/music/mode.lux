... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Mode)
   [control
    ["[0]" try]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" monoid)]]]
   [math
    [number
     ["n" nat]]]]]
 [//
  ["[0]" scale (.only Scale) (.use "[1]#[0]" equivalence)]])

... https://en.wikipedia.org/wiki/Mode_(music)
(type .public Mode
  (-> Scale
      Scale))

(def .public (next it)
  Mode
  (when (scale.intervals it)
    (list.partial head tail)
    (|> (list#composite tail (list head))
        scale.scale
        try.trusted)

    _
    (undefined)))

(def .public (previous it)
  Mode
  (when (list.reversed (scale.intervals it))
    (list.partial last prevs)
    (|> (list#composite prevs (list last))
        list.reversed
        scale.scale
        try.trusted)

    _
    (undefined)))

(def .public (of? base it)
  (-> Scale Scale
      Bit)
  (let [maximum (scale.size base)]
    (and (n.= maximum (scale.size it))
         (loop (next [tries 0
                      it' it])
           (and (n.< maximum tries)
                (or (scale#= base it')
                    (next (++ tries) (..next it'))))))))

(with_template [<base> <0> <1> <2> <3> <4> <5> <6>]
  [(with_template [<name> <previous>]
     [(def .public <name>
        Scale
        (..next <previous>))]

     [<1> <base>]
     [<2> <1>]
     [<3> <2>]
     [<4> <3>]
     [<5> <4>]
     [<6> <5>]
     [<0> <6>]
     )]

  [scale.major
   ionian dorian phrygian lydian mixo_lydian aeolian locrian]
  [scale.melodic_minor
   athenian cappadocian asgardian pontikonisian olympian sisyphean palamidian]
  )
