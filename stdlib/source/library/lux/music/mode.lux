... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Mode)
   [error
    ["[0]" try]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" monoid)]]]
   [math
    [number
     [/64
      ["n" natural]]]]
   [macro
    ["[0]" template]]]]
 [//
  ["[0]" scale (.only Scale)]])

... https://en.wikipedia.org/wiki/Mode_(music)
(every .public Mode
  (-> Scale
      Scale))

(the .public (next it)
  Mode
  (let [it (scale.intervals it)]
    (|> (all list#composite
             (list.after 1 it)
             (list.first 1 it))
        scale.scale
        try.trusted)))

(the .public (previous it)
  Mode
  (let [it (list.in_reverse (scale.intervals it))]
    (|> (all list#composite
             (list.after 1 it)
             (list.first 1 it))
        list.in_reverse
        scale.scale
        try.trusted)))

(the .public (of? base it)
  (-> Scale Scale
      Bit)
  (let [maximum (scale.degrees base)]
    (and (n.= maximum (scale.degrees it))
         (loop (next [tries 0
                      it' it])
           (and (n.< maximum tries)
                (or (scale.= base it')
                    (next (++ tries) (..next it'))))))))

(template.with [<base> <0> <1> <2> <3> <4> <5> <6>]
  [(template.with [<name> <previous>]
     [(the .public <name>
        Scale
        (..next <previous>))]

     [[<1> <base>]
      [<2> <1>]
      [<3> <2>]
      [<4> <3>]
      [<5> <4>]
      [<6> <5>]
      [<0> <6>]])]

  [[scale.major
    ionian dorian phrygian lydian mixo_lydian aeolian locrian]
   [scale.melodic_minor
    athenian cappadocian asgardian pontikonisian olympian sisyphean palamidian]])
