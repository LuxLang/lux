... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [control
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]
    [function
     [predicate (.only Predicate)]]]
   [data
    [text
     ["%" \\format]]
    [collection
     ["[0]" list (.use "[1]#[0]" mix)]
     ["[0]" array
      ["[1]" \\unsafe (.only Array)]]]]
   [math
    [number
     ["n" nat]
     ["[0]" int]]]
   [meta
    [macro
     ["[0]" template]]
    [type
     ["[0]" nominal]]]]]
 [//
  ["[0]" interval (.only Interval)]])

(exception.def .public (not_an_octave [it])
  (Exception (List Interval))
  (exception.report
   (list ["Intervals" (exception.listing %.nat it)])))

(exception.def .public (has_perfect_unison [it])
  (Exception (List Interval))
  (exception.report
   (list ["Intervals" (exception.listing %.nat it)])))

... https://en.wikipedia.org/wiki/Scale_(music)
(nominal.def .public Scale
  (Array Interval)

  (def .public (scale intervals)
    (-> (List Interval)
        (Try Scale))
    (if (|> intervals
            (list#mix n.+ interval.perfect_unison)
            (n.= interval.octave))
      (if (list.every? (n.> interval.perfect_unison) intervals)
        {try.#Success (nominal.abstraction (array.of_list intervals))}
        (exception.except ..has_perfect_unison [intervals]))
      (exception.except ..not_an_octave [intervals])))

  (def .public intervals
    (-> Scale
        (List Interval))
    (|>> nominal.representation
         (array.list {.#None})))

  (def .public degrees
    (-> Scale
        Nat)
    (|>> nominal.representation
         array.size))

  (def .public equivalence
    (Equivalence Scale)
    (implementation
     (def (= reference it)
       (array.= n.equivalence
                (nominal.representation reference)
                (nominal.representation it)))))

  (def .public hash
    (Hash Scale)
    (implementation
     (def equivalence ..equivalence)
     
     (def hash
       (|>> nominal.representation
            (array.list {.#None})
            (of (list.hash n.hash) hash)))))
  )

(with_template [<intervals> <name>]
  [(def .public <name>
     Scale
     (|> (`` (list (,, (template.spliced <intervals>))))
         ..scale
         try.trusted))]

  ... https://en.wikipedia.org/wiki/Chromatic_scale
  [[1 1 1 1 1 1 1 1 1 1 1 1] chromatic]

  ... https://en.wikipedia.org/wiki/Pentatonic_scale
  [[2 2 3 2 3] major_pentatonic]
  [[2 3 2 3 2] suspended_pentatonic]
  [[3 2 3 2 2] blues_minor]
  [[2 3 2 2 3] blues_major]
  [[3 2 2 3 2] minor_pentatonic]
  [[1 2 4 1 4] balinese]
  [[4 2 1 4 1] chinese]
  [[2 1 4 1 4] japanese_hirajoshi]
  [[1 4 1 4 2] japanese_iwato]
  [[1 4 2 3 2] japanese_in_sen] ... https://en.wikipedia.org/wiki/Insen_scale

  ... https://en.wikipedia.org/wiki/Hexatonic_scale
  [[3 2 1 1 3 2] blues]
  [[3 1 3 1 3 1] augmented]
  [[1 3 1 2 3 2] iberian]
  [[2 2 2 3 1 2] mystic]
  [[2 2 2 2 2 2] whole_tone] ... https://en.wikipedia.org/wiki/Whole-tone_scale

  ... https://en.wikipedia.org/wiki/Heptatonic_scale
  [[2 2 1 2 1 3 1] harmonic_major]
  [[2 1 2 2 1 3 1] harmonic_minor]
  [[2 1 2 2 2 2 1] melodic_minor]
  [[1 3 1 2 1 3 1] byzantine]
  [[1 3 2 2 2 1 1] enigmatic]
  [[2 1 3 1 1 3 1] hungarian]
  [[1 2 2 2 1 3 1] neapolitan]
  [[1 3 1 1 3 1 2] oriental]
  [[2 1 3 1 2 1 2] romanian_minor]
  [[1 3 1 2 1 2 2] spanish_gypsy]
  [[1 3 1 1 2 3 1] persian] ... https://en.wikipedia.org/wiki/Persian_scale
  [[2 2 1 1 2 2 2] arabic]
  [[1 2 1 2 2 2 2] super_locrian]
  [[2 2 1 2 1 2 2] hindu]
  [[2 2 2 1 2 1 2] lydian_dominant]
  ... https://en.wikipedia.org/wiki/Diatonic_scale
  [[2 2 1 2 2 2 1] major] ... https://en.wikipedia.org/wiki/Major_scale
  [[2 1 2 2 1 2 2] minor] ... https://en.wikipedia.org/wiki/Minor_scale

  ... https://en.wikipedia.org/wiki/Octatonic_scale
  [[2 1 2 1 2 1 2 1] diminished]
  [[2 2 1 2 1 1 2 1] bebop]
  [[2 1 2 1 1 1 3 1] algerian]
  [[1 2 1 1 1 2 2 2] eight_tone]

  [[2 1 1 2 1 1 1 2 1] nine_tone]
  )

(alias [dominant_altered diminished_whole_tone]
       ..super_locrian)

(alias [octatonic]
       ..diminished)

(alias [egyptian]
       ..suspended_pentatonic)

(alias [ethiopian_geez]
       ..minor)

(alias [ethiopian_ararai]
       ..major)

(alias [prometheus]
       ..mystic)

(alias [hawaiian]
       ..melodic_minor)

(alias [japanese_yo]
       ..blues_major)

(alias [double_harmonic]
       ..byzantine)

... https://en.wikipedia.org/wiki/Degree_(music)
(every .public Degree
  Nat)

(with_template [<step> <name>]
  [(def .public <name>
     Degree
     <step>)]

  [0 tonic]
  [1 super_tonic]
  [2 mediant]
  [3 sub_dominant]
  [4 dominant]
  [5 sub_mediant]
  [6 leading]
  )

(alias [sub_tonic]
       ..leading)

... https://en.wikipedia.org/wiki/Steps_and_skips
(every .public Motion
  Int)

(with_template [<order> <name>]
  [(def .public <name>
     (Predicate Motion)
     (|>> int.abs
          (<order> +1)))]

  [int.< null?]
  [int.= step?]
  [int.> skip?]
  )

(alias [conjunct?]
       ..step?)

(alias [leap? disjunct?]
       ..skip?)
