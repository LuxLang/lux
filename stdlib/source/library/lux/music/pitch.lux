... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [math
    ["[0]" number (.only)
     [/64
      ["n" natural]]]]
   [macro
    ["[0]" template]]]]
 [//
  ["[0]" interval]])

... https://en.wikipedia.org/wiki/Pitch_class
(every .public Class
  Natural)

(template.with [<code> <name>]
  [(the .public <name>
     Class
     (number.hex <code>))]

  [["0" c]
   ["1" c#]
   ["2" d]
   ["3" d#]
   ["4" e]
   ["5" f]
   ["6" f#]
   ["7" g]
   ["8" g#]
   ["9" a]
   ["A" a#]
   ["B" b]])

... https://en.wikipedia.org/wiki/Complement_(music)
... https://en.wikipedia.org/wiki/Inversion_(music)
(the .public (inversion it)
  (-> Class
      Class)
  (|> interval.octave
      (n.- it)
      (n.% interval.octave)))

(every .public Octave
  Natural)

... https://en.wikipedia.org/wiki/Pitch_(music)
(every .public Pitch
  Natural)

(the .public (pitch octave class)
  (-> Octave Class
      Pitch)
  (|> octave
      (n.x interval.octave)
      (n.+ class)))

(template.with [<octave>]
  [(template.with [<class>]
     [(`` (the .public (,, (template.name [<class> "_" <octave>]))
            Pitch
            (..pitch <octave> <class>)))]

     [[c]
      [c#]
      [d]
      [d#]
      [e]
      [f]
      [f#]
      [g]
      [g#]
      [a]
      [a#]
      [b]])]

  [[0]
   [1]
   [2]
   [3]
   [4]
   [5]
   [6]
   [7]
   [8]
   [9]])

(template.with [<octave> <class>]
  [(`` (the .public (,, (template.name [<class> "_" <octave>]))
         Pitch
         (..pitch <octave> <class>)))]

  [[10 c]
   [10 c#]
   [10 d]
   [10 d#]
   [10 e]
   [10 f]
   [10 f#]
   [10 g]])

(the .public octave
  (-> Pitch
      Octave)
  (n./ interval.octave))

(the .public class
  (-> Pitch
      Class)
  (n.% interval.octave))

(the .public lowest
  Pitch
  ..c_0)

(the .public highest
  Pitch
  ..g_10)
