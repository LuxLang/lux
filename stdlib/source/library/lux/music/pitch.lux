... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [math
    ["[0]" number (.only)
     ["n" nat]]]
   [meta
    [macro
     ["[0]" template]]]]]
 [//
  ["[0]" interval]])

... https://en.wikipedia.org/wiki/Pitch_class
(type .public Class
  Nat)

(with_template [<code> <name>]
  [(def .public <name>
     Class
     (number.hex <code>))]

  ["0" c]
  ["1" c#]
  ["2" d]
  ["3" d#]
  ["4" e]
  ["5" f]
  ["6" f#]
  ["7" g]
  ["8" g#]
  ["9" a]
  ["A" a#]
  ["B" b]
  )

(type .public Octave
  Nat)

... https://en.wikipedia.org/wiki/Pitch_(music)
(type .public Pitch
  Nat)

(def .public (pitch octave class)
  (-> Octave Class
      Pitch)
  (|> octave
      (n.* interval.octave)
      (n.+ class)))

(with_template [<octave>]
  [(with_template [<class>]
     [(`` (def .public (,, (template.symbol [<class> "_" <octave>]))
            Pitch
            (..pitch <octave> <class>)))]

     [c]
     [c#]
     [d]
     [d#]
     [e]
     [f]
     [f#]
     [g]
     [g#]
     [a]
     [a#]
     [b]
     )]

  [0]
  [1]
  [2]
  [3]
  [4]
  [5]
  [6]
  [7]
  [8]
  [9]
  )

(with_template [<octave> <class>]
  [(`` (def .public (,, (template.symbol [<class> "_" <octave>]))
         Pitch
         (..pitch <octave> <class>)))]

  [10 c]
  [10 c#]
  [10 d]
  [10 d#]
  [10 e]
  [10 f]
  [10 f#]
  [10 g]
  )

(def .public octave
  (-> Pitch
      Octave)
  (n./ interval.octave))

(def .public class
  (-> Pitch
      Class)
  (n.% interval.octave))

(def .public lowest
  Pitch
  ..c_0)

(def .public highest
  Pitch
  ..g_10)
