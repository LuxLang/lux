... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" sequence (.only Sequence sequence) (.use "[1]#[0]" mix monoid)]
     ["[0]" list (.use "[1]#[0]" mix)]]]
   [math
    [number
     ["[0]" nat]]]
   [meta
    [macro
     ["[0]" template]]]]]
 [//
  ["[0]" interval (.only Interval)]
  ["[0]" pitch (.only Pitch)]])

... https://en.wikipedia.org/wiki/Chord_(music)
(every .public Chord
  (Sequence Interval))

(the .public equivalence
  (Equivalence Chord)
  (sequence.equivalence nat.equivalence))

(alias [=]
       ..equivalence)

(the .public injection
  (text.Injection Chord)
  (sequence#mix (function (_ interval it)
                  (when it
                    "" (%.nat interval)
                    _ (%.message it " " (%.nat interval))))
                ""))

(the .public size
  (-> Chord
      Nat)
  (|>> sequence.size
       ++))

... https://en.wikipedia.org/wiki/Chord_notation
... https://en.wikipedia.org/wiki/Chord_notation#Triads

... https://en.wikipedia.org/wiki/Monad_(music)
(the .public monad
  Chord
  (sequence))

... https://en.wikipedia.org/wiki/Dyad_(music)
... https://en.wikipedia.org/wiki/Power_chord
(the .public power
  Chord
  (sequence interval.perfect_5th))

... https://en.wikipedia.org/wiki/Triad_(music)
... https://en.wikipedia.org/wiki/Major_chord
(the .public major_triad
  Chord
  (sequence interval.major_3rd
            interval.minor_3rd))

... https://en.wikipedia.org/wiki/Minor_chord
(the .public minor_triad
  Chord
  (sequence interval.minor_3rd
            interval.major_3rd))

... https://en.wikipedia.org/wiki/Augmented_triad
(the .public augmented_triad
  Chord
  (sequence interval.major_3rd
            interval.major_3rd))

... https://en.wikipedia.org/wiki/Diminished_triad
(the .public diminished_triad
  Chord
  (sequence interval.minor_3rd
            interval.minor_3rd))

... https://en.wikipedia.org/wiki/Suspended_chord
(the .public suspended_2
  Chord
  (sequence interval.major_2nd
            interval.perfect_4th))

(the .public suspended_4
  Chord
  (sequence interval.perfect_4th
            interval.major_2nd))

... https://en.wikipedia.org/wiki/Chord_notation#Chord_quality
(every .public Quality
  (-> Chord
      Chord))

... https://en.wikipedia.org/wiki/Added_tone_chord
(the .public added
  (-> Interval
      Quality)
  sequence.suffix)

(the .public major
  Quality
  (..added interval.major_3rd))

(the .public minor
  Quality
  (..added interval.minor_3rd))

... https://en.wikipedia.org/wiki/Tetrad_(music)
(template.with [<base> <major> <minor>]
  [(the .public <major>
     Chord
     (..major <base>))

   (the .public <minor>
     Chord
     (..minor <base>))]

  [..major_triad major_7th dominant_7th]
  [..minor_triad minor_major_7th minor_7th]
  [..augmented_triad augmented_major_7th augmented_7th]

  [..dominant_7th dominant_9th dominant_minor_9th]
  )

... https://en.wikipedia.org/wiki/Pentachord
(template.with [<base> <major> <minor>]
  [(the .public <major>
     Chord
     (..major (..major <base>)))

   (the .public <minor>
     Chord
     (..major (..minor <base>)))]

  [..minor_triad minor_major_9th minor_9th]
  [..augmented_triad augmented_major_9th augmented_dominant_9th]
  )

... https://en.wikipedia.org/wiki/Polychord
(the .public (poly lower higher)
  (-> Chord Chord
      Chord)
  (let [range (|> lower
                  (sequence#mix nat.+ 0)
                  (nat.% interval.octave))
        offset (nat.- range interval.octave)]
    (sequence#composite (sequence.suffix offset lower)
                        higher)))

(the .public (instance tonic it)
  (-> Pitch Chord
      (Sequence Pitch))
  (let [[pitch chord] (sequence#mix (function (_ interval [pitch chord])
                                      [(nat.+ interval pitch)
                                       (sequence.suffix pitch chord)])
                                    [tonic (sequence)]
                                    it)]
    (sequence.suffix pitch chord)))

(the .public (chord it)
  (-> (Sequence Pitch)
      Chord)
  (|> it
      sequence.list
      (list.sorted nat.<)
      (list#mix (function (_ pitch [chord first? offset])
                  [(if first?
                     chord
                     (sequence.suffix (nat.- offset pitch) chord))
                   false
                   pitch])
                [(sequence) true 0])
      product.left))

... https://en.wikipedia.org/wiki/Factor_(chord)
(every .public Factor
  Nat)

(template.with [<it> <name>]
  [(the .public <name>
     Factor
     <it>)]

  [00 root] ... https://en.wikipedia.org/wiki/Root_(chord)
  [02 third] ... https://en.wikipedia.org/wiki/Third_(chord)
  [04 fifth] ... https://en.wikipedia.org/wiki/Fifth_(chord)
  [05 sixth] ... https://en.wikipedia.org/wiki/Sixth_chord
  [06 seventh] ... https://en.wikipedia.org/wiki/Seventh_(chord)
  [08 ninth] ... https://en.wikipedia.org/wiki/Ninth
  [10 eleventh] ... https://en.wikipedia.org/wiki/Eleventh
  [12 thirteenth] ... https://en.wikipedia.org/wiki/Thirteenth
  )
