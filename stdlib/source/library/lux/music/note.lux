... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [math
    [number
     ["n" natural]]]
   [meta
    [macro
     ["[0]" template]]]]]
 [//
  [pitch (.only Pitch)]])

(every .public Tick
  Natural)

... https://en.wikipedia.org/wiki/Note_value
(every .public Value
  Tick)

(the triplet
  3)

(the scale
  2)

(the .public /64
  Value
  ..triplet)

(template.with [<2> <1>]
  [(the .public <2>
     Value
     (n.* ..scale <1>))]

  [/32 /64]
  [/16 /32]
  [/08 /16]
  [/04 /08]
  [/02 /04]
  [/01 /02]
  )

... https://en.wikipedia.org/wiki/Tuplet#Triplet
(the .public /03
  (-> Value
      Value)
  (|>> (n.* ..scale)
       (n./ ..triplet)))

(the .public (dot_1 it)
  (-> Value
      Value)
  (n.+ it
       (n./ 2 it)))

(the .public (dot_2 it)
  (-> Value
      Value)
  (n.+ (dot_1 it)
       (n./ 4 it)))

(the .public (dot_3 it)
  (-> Value
      Value)
  (n.+ (dot_2 it)
       (n./ 8 it)))

... https://en.wikipedia.org/wiki/Rest_(music)
(every .public Rest
  Value)

... https://en.wikipedia.org/wiki/Musical_note
(every .public Node
  (Record
   [#pitch Pitch
    #value Value]))
