... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [math
    [number
     ["n" nat]]]]]
 [//
  [pitch (.only Pitch)]])

(every .public Tick
  Nat)

... https://en.wikipedia.org/wiki/Note_value
(every .public Value
  Tick)

(def triplet
  3)

(def scale
  2)

(def .public /64
  Value
  ..triplet)

(with_template [<2> <1>]
  [(def .public <2>
     Value
     (n.* ..scale <1>))]

  [/32 /64]
  [/16 /32]
  [/08 /16]
  [/04 /08]
  [/02 /04]
  [/01 /02]
  )

... https://en.wikipedia.org/wiki/Tuplet#Triplet
(def .public /03
  (-> Value
      Value)
  (|>> (n.* ..scale)
       (n./ ..triplet)))

(def .public (dot_1 it)
  (-> Value
      Value)
  (n.+ it
       (n./ 2 it)))

(def .public (dot_2 it)
  (-> Value
      Value)
  (n.+ (dot_1 it)
       (n./ 4 it)))

(def .public (dot_3 it)
  (-> Value
      Value)
  (n.+ (dot_2 it)
       (n./ 8 it)))

... https://en.wikipedia.org/wiki/Rest_(music)
(every .public Rest
  Value)

... https://en.wikipedia.org/wiki/Musical_note
(every .public Node
  (Record
   [#pitch Pitch
    #value Value]))
