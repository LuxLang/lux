... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... https://en.wikipedia.org/wiki/Mathematical_induction
(.using
 [library
  [lux (.except function)
   [abstract
    ["[0]" monad]]
   [error
    ["[0]" try]]
   [meta
    ["[0]" provenance]]
   [compiler
    ["@[0]" type]
    [language
     [lux
      ["[0]" analysis]]]]
   [math
    [number
     [/64
      ["[0]" natural]]]]]]
 ["[0]" // (.only)
  ["?" proposition]
  ["in" substitution]
  ["by" rule]])

(the (valid? [left right])
  (-> [analysis.Term analysis.Term]
      Bit)
  (with analysis.equivalence
    (= left right)))

(every .public Case
  (-> ?.Identity
      //.Proof))

(the .public (bit if_false if_true
                  context it)
  (-> Case Case
      //.Proof)
  (monad.let try.monad
    [.let [variable (?.variable provenance.dummy (its in.:next_variable context))
           context (revised in.:next_variable ++ context)]
     false! (if_false [variable
                       (analysis.bit provenance.dummy false)]
                      context
                      it)
     true! (if_true [variable
                     (analysis.bit provenance.dummy true)]
                    context
                    it)]
    (pure (if (valid? false!)
              true!
              false!))))

(the .public (case if_left if_right
                   context it)
  (-> Case Case
      //.Proof)
  (monad.let try.monad
    [.let [content (?.term provenance.dummy (its in.:next_term context))
           variable (?.variable provenance.dummy (its in.:next_variable context))
           context (|> context
                       (revised in.:next_term ++)
                       (revised in.:next_variable ++))]
     left! (if_left [variable
                     (analysis.variant provenance.dummy [0 .false content])]
                    context
                    it)
     right! (if_right [variable
                       (analysis.variant provenance.dummy [0 .true content])]
                      context
                      it)]
    (pure (if (valid? left!)
              right!
              left!))))

(the .public (function by_application
               context [expectation reality])
  (Change //.Proof)
  (monad.let try.monad
    [.let [argument (?.term provenance.dummy (its in.:next_term context))]]
    (by_application (revised in.:next_term ++ context)
                    [{@type.:Application provenance.dummy argument expectation}
                     {@type.:Application provenance.dummy argument reality}])))

(the .public (pair if_pair
                   context it)
  (-> Case
      //.Proof)
  (monad.let try.monad
    [.let [left (?.term provenance.dummy (its in.:next_term context))
           right (?.term provenance.dummy (++ (its in.:next_term context)))
           variable (?.variable provenance.dummy (its in.:next_variable context))]]
    (if_pair [variable
              (analysis.tuple provenance.dummy (list left right))]
             (|> context
                 (revised in.:next_term (|>> ++ ++))
                 (revised in.:next_variable ++))
             it)))
