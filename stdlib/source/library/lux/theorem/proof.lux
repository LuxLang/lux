... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except let)
   [abstract
    ["[0]" monad]]
   [error
    ["[0]" try]]
   [data
    ["[0]" product]]]]
 [//
  ["?" proposition]
  ["in" substitution]])

... https://en.wikipedia.org/wiki/Mathematical_proof
... https://en.wikipedia.org/wiki/Formal_proof
(every .public Proof
  (in.Substitution ?.Identity))

(the .public (let [identity proof] body
               context it)
  (for_any (_ it)
    (-> [?.Identity (in.Fact ?.Identity it)]
        (-> it Proof)
        Proof))
  (monad.let try.monad
    [value (proof context identity)]
    (body value context it)))

... https://en.wikipedia.org/wiki/Q.E.D.
(the .public (qed context identity)
  Proof
  {try.:Success identity})

(the .public (= left right
                context it)
  (-> in.Rule in.Rule
      Proof)
  (monad.let try.monad
    [left (left context (product.left it))
     right (right context (product.right it))]
    (pure [left right])))

... https://en.wikipedia.org/wiki/Axiom
(the .public (axiom context it)
  Proof
  (.let [term (its ?.:fast it)]
    {try.:Success [?.:slow term
                   ?.:fast term]}))
