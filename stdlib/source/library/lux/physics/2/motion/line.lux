... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)]]
 ["/" /// (.only)
  [//
   ["[0]" /1 (.only)
    ["[1]/[0]" motion]]]])

(every .public Velocity
  /.Vector)

(the .public (average_velocity interval displacement)
  (-> /1.Interval /.Displacement
      Velocity)
  ((/.contraction (/1.value interval)) (/.value displacement)))

(every .public Acceleration
  /.Vector)

(the .public (average_acceleration interval difference_in_velocity)
  (-> /1.Interval (/1.Difference Velocity)
      Acceleration)
  ((/.contraction (/1.value interval)) (/.value difference_in_velocity)))

(the .public (velocity acceleration initial)
  (-> Acceleration Velocity
      (/1/motion.Motion Velocity))
  (function (_ time)
    (|> acceleration
        (/.dilation time)
        (/.+ initial))))

(the (average left right)
  (-> /.Vector /.Vector
      /.Vector)
  ((/.contraction +2.0) (/.+ left right)))

(the .public (position acceleration initial_velocity initial)
  (-> Acceleration Velocity /.Position
      (/1/motion.Motion /.Position))
  (function (_ time)
    (let [velocity (average initial_velocity
                            (..velocity acceleration initial_velocity time))]
      (|> velocity
          (/.dilation time)
          (/.+ initial)))))

... https://en.wikipedia.org/wiki/Standard_gravity
(the .public standard_gravity
  Acceleration
  [/.:left/right /1/motion.uniform
   /.:forward/backward -9.806,65])
