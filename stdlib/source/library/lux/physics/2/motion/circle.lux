... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [math
    [number
     [/64
      ["[0]" decimal]]]]]]
 [//
  ["[0]" line]
  ["/" // (.only)
   [// (.only Scalar)
    ["[0]" /1 (.only)
     ["[1]/[0]" motion]]]]])

... https://en.wikipedia.org/wiki/Angle
(every .public Angle
  Scalar)

... https://en.wikipedia.org/wiki/Arc_length
(every .public Arc_Length
  /1.Vector)

(the .public (arc_length radius angle)
  (-> /.Radius Angle
      Arc_Length)
  (decimal.x radius angle))

... https://en.wikipedia.org/wiki/Angular_displacement
(every .public Displacement
  (/.Difference Angle))

(the .public displacement
  (-> Angle Angle
      Displacement)
  /1.difference)

... https://en.wikipedia.org/wiki/Angular_velocity
(every .public Velocity
  /1.Vector)

(the .public (average_velocity interval displacement)
  (-> /1.Interval Displacement
      Velocity)
  (|> (/1.value displacement)
      (decimal./ (/1.value interval))))

... https://en.wikipedia.org/wiki/Angular_acceleration
(every .public Acceleration
  /1.Vector)

(the .public (average_acceleration interval velocity)
  (-> /1.Interval (/.Difference Velocity)
      Acceleration)
  (|> (/1.value velocity)
      (decimal./ (/1.value interval))))

(the .public (velocity acceleration initial)
  (-> Acceleration Velocity
      (/1/motion.Motion Velocity))
  (function (_ time)
    (|> acceleration
        (decimal.x time)
        (decimal.+ initial))))

(the .public (angle acceleration initial_velocity initial)
  (-> Acceleration Velocity Angle
      (/1/motion.Motion Angle))
  (function (_ time)
    (let [velocity (/1/motion.average initial_velocity
                                      (..velocity acceleration initial_velocity time))]
      (|> velocity
          (decimal.x time)
          (decimal.+ initial)))))

... https://en.wikipedia.org/wiki/Acceleration#Tangential_and_centripetal_acceleration
(every .public Tangential_acceleration
  /1.Vector)

(the .public (tangential_acceleration radius acceleration)
  (-> /.Radius Acceleration
      Tangential_acceleration)
  (decimal.x radius acceleration))

... https://en.wikipedia.org/wiki/Centripetal_force
(every .public Centripetal_Acceleration
  /1.Vector)

(the (square it)
  (.Change /1.Vector)
  (decimal.x it it))

(the .public (centripetal_acceleration radius velocity)
  (-> /.Radius Velocity
      Centripetal_Acceleration)
  (decimal.x radius (square velocity)))
