... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   ["[0]" function]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["/" decimal]
      ["[0]" integer]]]]]])

... https://en.wikipedia.org/wiki/Dimension
(every .public Dimension
  Decimal)

... https://en.wikipedia.org/wiki/Position_(geometry)
(every .public Position
  Dimension)

... https://en.wikipedia.org/wiki/Time
(every .public Time
  Dimension)

... https://en.wikipedia.org/wiki/Origin_(mathematics)
(the .public origin
  Dimension
  /.zero)

... https://en.wikipedia.org/wiki/Subtraction#Notation_and_terminology
(every .public (Difference it)
  (-> it
      it))

(the .public stasis
  Difference
  function.identity)

(the .public (difference initial final)
  (-> Dimension Dimension
      (Difference Dimension))
  (/.+ (/.- initial final)))

(the .public value
  (-> (Difference Dimension)
      Dimension)
  (function.of ..origin))

(every .public Interval
  (Difference Time))

(the .public interval
  ..difference)

... https://en.wikipedia.org/wiki/Displacement_(geometry)
(every .public Displacement
  (Difference Position))

(the .public displacement
  ..difference)

(the .public (random range)
  (-> Natural
      (Random Dimension))
  (with random.monad
    (|> random.integer
        (each (|>> (integer.% (.integer range))
                   integer.decimal)))))
