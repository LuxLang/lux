... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [math
    [number
     [/64
      ["[0]" decimal]]]
    [geometry
     ["[0]" circle]]]
   [macro
    ["[0]" template]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]]]
 [// (.only Scalar)
  ["[0]" /1 (.only)
   ["[1]/[0]" motion]]
  ["[0]" /2 (.only)
   [motion
    ["[1]/[0]" line]
    ["[1]/[0]" circle]]]])

... https://en.wikipedia.org/wiki/Mass
(every .public Mass
  Scalar)

... https://en.wikipedia.org/wiki/Force
(every .public Force
  /2.Vector)

(the .public (net_force acceleration mass)
  (-> /2/line.Acceleration Mass
      Force)
  (/2.dilation mass acceleration))

... (the law_2_of_newton
...   Algebra
...   (with_variables [net_force mass acceleration]
...     (= net_force
...        (/2.dilation mass acceleration))))

(the .public gravitational_force
  (-> Mass
      Force)
  (net_force /2/line.standard_gravity))

... https://en.wikipedia.org/wiki/Friction
(every .public Static_Friction
  /1.Vector)

(the .public (static_friction coefficient normal_force)
  (-> Static_Friction Force
      Force)
  (/2.dilation coefficient normal_force))

(every .public Kinetic_Friction
  /1.Vector)

(the .public (kinetic_friction coefficient normal_force)
  (-> Kinetic_Friction Force
      Force)
  (/2.dilation coefficient normal_force))

(template.with [,static ,kinetic ,surface ,material]
  [(`` (the .public (,, (template.name ["static_friction_of_" ,material "_on_" ,surface]))
         Static_Friction
         ,static))

   (`` (the .public (,, (template.name ["kinetic_friction_of_" ,material "_on_" ,surface]))
         Kinetic_Friction
         ,kinetic))]

  [[+1.00 +0.80 concrete rubber]
   [+0.78 +0.57 steel steel]
   [+0.61 +0.47 steel aluminium]
   [+0.53 +0.36 steel copper]
   [+0.94 +0.40 glass glass]
   [+0.14 +0.10 wet_snow waxed_wood]
   [+0.10 +0.03 ice ice]])

... https://en.wikipedia.org/wiki/Mechanical_energy
(every .public Mechanical
  Scalar)

... https://en.wikipedia.org/wiki/Kinetic_energy
(every .public Kinetic
  Mechanical)

(the .public (kinetic mass velocity)
  (-> Mass /2/line.Velocity
      Kinetic)
  (|> (/2.x velocity velocity)
      (decimal.x mass)
      (decimal./ +2.0)))

... https://en.wikipedia.org/wiki/Work_(physics)
(every .public Work
  (/2.Difference Mechanical))

(the .public (work force displacement)
  (-> Force /2.Displacement
      Work)
  (decimal.+ (/2.* force (/2.value displacement))))

... https://en.wikipedia.org/wiki/Hooke%27s_law
... https://en.wikipedia.org/wiki/Stiffness
(every .public Stiffness
  Scalar)

... https://en.wikipedia.org/wiki/Spring_(device)
(the .public (spring stiffness displacement)
  (-> Stiffness /2.Displacement
      Force)
  (/2.opposite (/2.dilation stiffness (/2.value displacement))))

... https://en.wikipedia.org/wiki/Potential_energy
(every .public Potential
  Mechanical)

... https://en.wikipedia.org/wiki/Elastic_energy
(the .public (elastic_potential stiffness displacement)
  (-> Stiffness /2.Displacement
      Potential)
  (kinetic stiffness (/2.value displacement)))

(the .public (mechanical kinetic potential)
  (-> Kinetic Potential
      Mechanical)
  (decimal.+ kinetic potential))

... https://en.wikipedia.org/wiki/Power_(physics)
(every .public Power
  Scalar)

(the .public (average_power interval work)
  (-> /1.Interval Work
      Power)
  (decimal./ (/1.value interval)
             (/1.value work)))

... https://en.wikipedia.org/wiki/Momentum
(every .public Momentum
  /2.Vector)

(the .public (momentum mass velocity)
  (-> Mass /2/line.Velocity
      Momentum)
  (/2.dilation mass velocity))

... https://en.wikipedia.org/wiki/Impulse_(physics)
(every .public Impulse
  (/2.Difference Momentum))

(the .public impulse
  (-> Momentum Momentum
      Impulse)
  /2.difference)

... https://en.wikipedia.org/wiki/Particle
(every .public Particle
  (Record
   [#mass Mass
    #position /2.Position]))

(every .public System
  (List Particle))

... https://en.wikipedia.org/wiki/Center_of_mass#Barycentric_coordinates
(the .public (center_of_mass system)
  (-> System
      /2.Position)
  (when system
    (list)
    /2.origin

    else
    (let [total_mass (|> system
                         (list#each (its #mass))
                         (list.mix decimal.+ +0.0))
          distribution (|> system
                           (list#each (function (_ it)
                                        (/2.dilation (its #mass it)
                                                     (its #position it))))
                           (list.mix /2.+ /2.origin))]
      (/2.contraction total_mass
                      distribution))))

... https://en.wikipedia.org/wiki/Torque
(every .public Torque
  /1.Vector)

(the .public (torque radius force)
  (-> /2.Radius Force
      Torque)
  (decimal.x radius (/2.magnitude force)))

(the .public (torque' position force)
  (-> /2.Vector Force
      Torque)
  (/2.x position force))

... https://en.wikipedia.org/wiki/Moment_of_inertia
(every .public Moment_of_Inertia
  Scalar)

(the (square it)
  (.Change /1.Vector)
  (decimal.x it it))

(the .public (moment_of_inertia mass radius)
  (-> Mass /2.Radius
      Moment_of_Inertia)
  (decimal.x mass (square radius)))

... https://en.wikipedia.org/wiki/Rotational_energy
(every .public Rotational
  Scalar)

(the .public (rotational moment_of_inertia velocity)
  (-> Moment_of_Inertia /2/circle.Velocity
      Rotational)
  (|> (square velocity)
      (decimal.x moment_of_inertia)
      (decimal./ +2.0)))

(the .public (power torque velocity)
  (-> Torque /2/circle.Velocity
      Power)
  (decimal.x torque velocity))

... https://en.wikipedia.org/wiki/Angular_momentum
(every .public Angular_Momentum
  Scalar)

(the .public (angular_momentum position linear_momentum)
  (-> /2.Vector Momentum
      Angular_Momentum)
  (/2.x position linear_momentum))

(the .public (angular_momentum' moment_of_inertia velocity)
  (-> Moment_of_Inertia /2/circle.Velocity
      Angular_Momentum)
  (decimal.x moment_of_inertia velocity))

... https://en.wikipedia.org/wiki/Area
(every Area
  Scalar)

... https://en.wikipedia.org/wiki/Sphere
... https://en.wikipedia.org/wiki/Surface_area
(the area_of_surface_of_sphere'
  Scalar
  (decimal.x +2.0 circle.tau))

(the (area_of_surface_of_sphere radius)
  (-> /2.Radius
      Area)
  (decimal.x area_of_surface_of_sphere'
             (square radius)))

... https://en.wikipedia.org/wiki/Gravitational_constant
(the universal_gravitation
  Scalar
  (decimal.x area_of_surface_of_sphere' -6.6743e-11))

(the .public (relative_position origin it)
  (-> Particle Particle
      /2.Position)
  (/2.- (its #position origin)
        (its #position it)))

(the .public (distance origin it)
  (-> Particle Particle
      Scalar)
  (/2.magnitude (relative_position origin it)))

... https://en.wikipedia.org/wiki/Gravity
(the .public (gravitation origin it)
  (-> Particle Particle
      Force)
  (let [magnitude (decimal./ (area_of_surface_of_sphere (distance origin it))
                             (all decimal.x
                                  universal_gravitation
                                  (its #mass origin)
                                  (its #mass it)))
        orientation (/2.unit (relative_position origin it))]
    (/2.dilation magnitude orientation)))

... https://en.wikipedia.org/wiki/Gravitational_energy
(the .public (potential_from_gravitation origin it)
  (-> Particle Particle
      /2.Vector)
  (/2.dilation (distance origin it)
               (gravitation origin it)))
