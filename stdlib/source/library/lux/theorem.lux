... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except the with)
   [abstract
    ["??" projection]
    ["[0]" monad]]
   [error
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    [text
     ["%" \\injection]]
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]:[0]" functor)
      ["?[1]" \\projection]]]]
   [math
    [number
     [/64
      ["n" natural]]]]
   ["[0]" meta (.only)
    ["[0]" name]
    ["[0]" provenance]
    ["[0]" extension]
    ["[0]" code (.only)
     ["?[1]" \\projection (.only Projection)]
     ["[0]" export]]]
   [macro
    ["[0]" syntax]]
   [compiler
    [language
     [lux
      ["[0]" phase]
      ["[0]" analysis (.only)
       ["[0]" type]]]]]]]
 [/
  ["?" proposition]
  ["by" rule]
  ["in" substitution]])

(every .public (Condition it)
  (-> ?.Identity
      (Try it)))

(exception.the .public (not_valid [where [left right]])
  (Exception [Name ?.Identity])
  (exception.report
   (list ["Where" (name.as_text where)]
         ["Left" (analysis.as_text left)]
         ["Right" (analysis.as_text right)])))

(.the .public (left_of_pair it)
  (Condition ?.Identity)
  (when it
    [(analysis.tuple _ (list left right))
     (analysis.tuple _ (list left' right'))]
    {try.:Success [left left']}

    else
    (exception.except ..not_valid [(name ..left_of_pair) it])))

(.the .public (right_of_pair it)
  (Condition ?.Identity)
  (when it
    [(analysis.tuple _ (list left right))
     (analysis.tuple _ (list left' right'))]
    {try.:Success [right right']}

    else
    (exception.except ..not_valid [(name ..right_of_pair) it])))

(.the .public (pair it)
  (Condition [?.Identity ?.Identity])
  (monad.and try.monad
             (left_of_pair it)
             (right_of_pair it)))

(.the .public (with condition then
                lemma
                environment it)
  (for_any (_ it)
    (-> (Condition it) (in.Case it)
        (in.Case ?.Identity)))
  (monad.let try.monad
    [lemma (condition lemma)]
    ((then lemma) environment it)))

... https://en.wikipedia.org/wiki/Mathematical_proof
... https://en.wikipedia.org/wiki/Formal_proof
(every .public Proof
  (in.Substitution ?.Identity))

... https://en.wikipedia.org/wiki/Q.E.D.
(.the .public (qed environment identity)
  Proof
  {try.:Success identity})

(.the .public (= left right
                 environment it)
  (-> in.Rule in.Rule
      Proof)
  (monad.let try.monad
    [left (left environment (product.left it))
     right (right environment (product.right it))]
    (pure [left right])))

(exception.the .public (entailment_mismatch [expectation reality])
  (Exception [Natural Natural])
  (exception.report
   (list ["Expectation" (%.natural expectation)]
         ["Reality" (%.natural reality)])))

(.the the'
  (extension.analysis (_ analysis archive
                         [every_premise (?code.tuple (??.some ?list.any))
                          'proposition ?list.any
                          'proof ?list.any])
    (let [code_of_identity (is (-> ?.Identity
                                   Code)
                               (function (_ [this that])
                                 (` [(, (?.code_of_analysis this))
                                     (, (?.code_of_analysis that))])))]
      (if (list.empty? every_premise)
          (monad.let [! phase.monad]
            [identity (meta.eval (type (?.Proposition ?.Identity)) 'proposition)
             identity ((as (-> Any (?.Proposition ?.Identity)) identity) [(list) (list)] analysis archive)
             proof (meta.eval Proof 'proof)
             state_of_compiler phase.state]
            (when (monad.let try.monad
                    [[left right] ((as (-> Any Proof) proof)
                                   [in.:every_global state_of_compiler
                                    in.:next_variable 0
                                    in.:next_term 0]
                                   identity)]
                    (if (.with analysis.equivalence
                          (= left right))
                        {try.:Success [left right]}
                        (exception.except by.contradiction [left right])))
              {try.:Success theorem}
              (type.with ?.Identity
                (analysis archive (code_of_identity identity)))
              
              {try.:Failure error}
              (meta.failure error)))
          (monad.let [! phase.monad]
            [entailment (meta.eval (type (?.Proposition ?.Entailment)) 'proposition)
             entailment ((as (-> Any (?.Proposition ?.Entailment)) entailment) [(list) (list)] analysis archive)
             .let [expectation (list.size (its ?.:every_premise entailment))
                   reality (list.size every_premise)]]
            (if (n.= expectation reality)
                (monad.let !
                  ['_ (name.new "'_")
                   proof (meta.eval (list.mix (function (_ premise proof)
                                                (-> ?.Identity proof))
                                              Proof
                                              every_premise)
                                    (` (function ((, '_) (,* every_premise))
                                         (, 'proof))))
                   state_of_compiler phase.state]
                  (when (monad.let try.monad
                          [[left right] ((list.mix (function (_ premise proof)
                                                     ((as (-> Proof (-> ?.Identity Proof)) proof) premise))
                                                   (as (-> Any Proof) proof)
                                                   (its ?.:every_premise entailment))
                                         [in.:every_global state_of_compiler
                                          in.:next_variable 0
                                          in.:next_term 0]
                                         (its ?.:conclusion entailment))]
                          (if (.with analysis.equivalence
                                (= left right))
                              {try.:Success [left right]}
                              (exception.except by.contradiction [left right])))
                    {try.:Success theorem}
                    (type.with ?.Entailment
                      (analysis archive (` [?.:every_premise (list (,* (list:each code_of_identity (its ?.:every_premise entailment))))
                                            ?.:conclusion (, (code_of_identity (its ?.:conclusion entailment)))])))
                    
                    {try.:Failure error}
                    (meta.failure error)))
                (phase.except ..entailment_mismatch [expectation reality])))))))

(.the .public the
  (syntax.macro (_ state_of_compiler
                   [['export_policy 'declaration] (export.with (??.or (?code.form (??.and ?list.any (??.many ?list.any)))
                                                                      ?list.any))
                    'proposition ?list.any
                    'proof ?list.any])
    (pure (list (when 'declaration
                  {.:Right 'name}
                  (` (.the (, 'export_policy) (, 'name)
                       ?.Identity
                       (..the' []
                               (, 'proposition)
                               (, 'proof))))

                  {.:Left ['name 'every_premise]}
                  (` (.the (, 'export_policy) (, 'name)
                       ?.Entailment
                       (..the' [(,* 'every_premise)]
                               (, 'proposition)
                               (, 'proof)))))))))
