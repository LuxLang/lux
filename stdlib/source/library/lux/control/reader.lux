(.module:
  [library
   [lux (#- local)
    [abstract
     [apply (#+ Apply)]
     ["." functor (#+ Functor)]
     ["." monad (#+ Monad do)]]]])

(type: .public (Reader r a)
  {#.doc "Computations that have access to some environmental value."}
  (-> r a))

(def: .public ask
  {#.doc "Get the environment."}
  (All [r] (Reader r r))
  (function (_ env) env))

(def: .public (local change proc)
  {#.doc "Run computation with a locally-modified environment."}
  (All [r a] (-> (-> r r) (Reader r a) (Reader r a)))
  (|>> change proc))

(def: .public (run env proc)
  {#.doc (doc "Executes the reader against the given environment.")}
  (All [r a] (-> r (Reader r a) a))
  (proc env))

(implementation: .public functor
  (All [r] (Functor (Reader r)))
  
  (def: (map f fa)
    (function (_ env)
      (f (fa env)))))

(implementation: .public apply
  (All [r] (Apply (Reader r)))
  
  (def: &functor ..functor)

  (def: (apply ff fa)
    (function (_ env)
      ((ff env) (fa env)))))

(implementation: .public monad
  (All [r] (Monad (Reader r)))
  
  (def: &functor ..functor)

  (def: (in x)
    (function (_ env) x))

  (def: (join mma)
    (function (_ env)
      (mma env env))))

(implementation: .public (with monad)
  {#.doc "Monad transformer for Reader."}
  (All [M] (-> (Monad M) (All [e] (Monad (All [a] (Reader e (M a)))))))

  (def: &functor (functor.compose ..functor (get@ #monad.&functor monad)))

  (def: in (|>> (\ monad in) (\ ..monad in)))
  
  (def: (join eMeMa)
    (function (_ env)
      (do monad
        [eMa (run env eMeMa)]
        (run env eMa)))))

(def: .public lift
  {#.doc "Lift monadic values to the Reader wrapper."}
  (All [M e a] (-> (M a) (Reader e (M a))))
  (\ ..monad in))
