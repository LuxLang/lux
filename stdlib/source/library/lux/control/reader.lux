... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except local with)
   [abstract
    [apply (.only Apply)]
    ["[0]" functor (.only Functor)]
    ["[0]" monad (.only Monad do)]]]])

(type .public (Reader context of)
  (-> context
      of))

(def .public read
  (All (_ context)
    (Reader context context))
  (|>>))

(def .public (local change proc)
  (All (_ context of)
    (-> (-> context context) (Reader context of)
        (Reader context of)))
  (|>> change
       proc))

(def .public (result env proc)
  (All (_ context of)
    (-> context (Reader context of)
        of))
  (proc env))

(def .public functor
  (All (_ context)
    (Functor (Reader context)))
  (implementation
   (def (each f fa)
     (function (_ env)
       (f (fa env))))))

(def .public apply
  (All (_ context)
    (Apply (Reader context)))
  (implementation
   (def functor ..functor)

   (def (on fa ff)
     (function (_ env)
       ((ff env) (fa env))))))

(def .public monad
  (All (_ context)
    (Monad (Reader context)))
  (implementation
   (def functor ..functor)

   (def (in x)
     (function (_ env) x))

   (def (conjoint mma)
     (function (_ env)
       (mma env env)))))

(def .public (with monad)
  (All (_ !)
    (-> (Monad !)
        (All (_ context)
          (Monad (All (_ of)
                   (Reader context (! of)))))))
  (implementation
   (def functor
     (functor.composite ..functor (its monad.functor monad)))

   (def in
     (|>> (of monad in)
          (of ..monad in)))
   
   (def (conjoint eMeMa)
     (function (_ env)
       (do monad
         [eMa (result env eMeMa)]
         (result env eMa))))))

(def .public lifted
  (All (_ ! context of)
    (-> (! of)
        (Reader context (! of))))
  (of ..monad in))
