... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monoid (.only Monoid)]]]])

(the .public identity
  (All (_ a) (-> a a))
  (|>>))

(the .public (composite f g)
  (All (_ a b c)
    (-> (-> b c) (-> a b) (-> a c)))
  (|>> g f))

(the .public (constant value)
  (All (_ o) (-> o (All (_ i) (-> i o))))
  (function (_ _) value))

(the .public (flipped f)
  (All (_ a b c)
    (-> (-> a b c) (-> b a c)))
  (function (_ x y) (f y x)))

(the .public (on input function)
  (All (_ i o)
    (-> i (-> i o) o))
  (function input))

(the .public monoid
  (All (_ a) (Monoid (-> a a)))
  (implementation
   (the identity ..identity)
   (the composite ..composite)))
