... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except or and not only)
   [abstract
    [functor (.only Functor)]
    [apply (.only Apply)]
    [monad (.only Monad do)]
    [format (.only Format)]]
   [control
    ["[0]" try (.only Try)]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" functor monoid)]]]
   [meta
    ["[0]" location]]]])

(every .public (Projection state of)
  (-> state
      (Try [state of])))

(the .public functor
  (for_any (_ state)
    (Functor (Projection state)))
  (implementation
   (the (each f ma)
     (function (_ input)
       (when (ma input)
         {try.#Success [input' a]}
         {try.#Success [input' (f a)]}
         
         {try.#Failure msg}
         {try.#Failure msg})))))

(the .public apply
  (for_any (_ state)
    (Apply (Projection state)))
  (implementation
   (the functor ..functor)

   (the (on fa ff)
     (function (_ input)
       (when (ff input)
         {try.#Success [input' f]}
         (when (fa input')
           {try.#Success [input'' a]}
           {try.#Success [input'' (f a)]}

           {try.#Failure msg}
           {try.#Failure msg})

         {try.#Failure msg}
         {try.#Failure msg})))))

(the .public monad
  (for_any (_ state)
    (Monad (Projection state)))
  (implementation
   (the functor ..functor)

   (the (in x)
     (function (_ input)
       {try.#Success [input x]}))

   (the (conjoint mma)
     (function (_ input)
       (when (mma input)
         {try.#Success [input' ma]}
         (ma input')

         {try.#Failure msg}
         {try.#Failure msg})))))

(the .public (assertion message test)
  (for_any (_ state)
    (-> Text Bit
        (Projection state Any)))
  (function (_ input)
    (if test
      {try.#Success [input []]}
      {try.#Failure message})))

(the .public (maybe projection)
  (for_any (_ state of)
    (-> (Projection state of)
        (Projection state (Maybe of))))
  (function (_ input)
    (when (projection input)
      {try.#Success [input' x]}
      {try.#Success [input' {.#Some x}]}

      {try.#Failure _}
      {try.#Success [input {.#None}]})))

(the .public (value projection input)
  (for_any (_ state of)
    (-> (Projection state of) state
        (Try [state of])))
  (projection input))

(the .public (either this that)
  (for_any (_ state of)
    (-> (Projection state of) (Projection state of)
        (Projection state of)))
  (function (_ tokens)
    (when (this tokens)
      {try.#Failure _}
      (that tokens)
      
      success
      success)))

(with_expansions [<failure> {try.#Failure error}
                  <handle_failure!> (these <failure>
                                           <failure>)]
  (these (the .public (and left right)
           (for_any (_ state left right)
             (-> (Projection state left) (Projection state right)
                 (Projection state (And left right))))
           (function (_ state)
             (when (left state)
               {try.#Success [state left]}
               (when (right state)
                 {try.#Success [state right]}
                 {try.#Success [state [left right]]}
                 

                 <handle_failure!>)

               <handle_failure!>)))

         (the .public (or left right)
           (for_any (_ state left right)
             (-> (Projection state left) (Projection state right)
                 (Projection state (Or left right))))
           (function (_ tokens)
             (when (left tokens)
               {try.#Success [tokens' output]}
               {try.#Success [tokens' {#0 output}]}
               
               {try.#Failure _}
               (when (right tokens)
                 {try.#Success [tokens' output]}
                 {try.#Success [tokens' {#1 output}]}
                 
                 <handle_failure!>))))

         (the .public (some it)
           (for_any (_ state of)
             (-> (Projection state of)
                 (Projection state (List of))))
           (function (_ state)
             (loop (next [state state
                          output (list)])
               (when (it state)
                 {try.#Success [state head]}
                 (next state (list.partial head output))

                 {try.#Failure _}
                 {try.#Success [state (list.reversed output)]}))))

         (the .public (many projection)
           (for_any (_ state of)
             (-> (Projection state of)
                 (Projection state (List of))))
           (function (_ state)
             (when (projection state)
               {try.#Success [state head]}
               (when (..some projection state)
                 {try.#Success [state tail]}
                 {try.#Success [state {.#Item head tail}]}

                 <handle_failure!>)

               <handle_failure!>)))

         (the .public (after parameter it)
           (for_any (_ state _ of)
             (-> (Projection state _) (Projection state of)
                 (Projection state of)))
           (function (_ state)
             (when (parameter state)
               {try.#Success [state _]}
               (when (it state)
                 <handle_failure!>

                 success
                 success)

               <handle_failure!>)))

         (the .public (before parameter it)
           (for_any (_ state _ of)
             (-> (Projection state _) (Projection state of)
                 (Projection state of)))
           (function (_ state)
             (when (it state)
               {try.#Success [state it]}
               (when (parameter state)
                 {try.#Success [state _]}
                 {try.#Success [state it]}

                 <handle_failure!>)

               <handle_failure!>)))

         (the .public (of_try operation)
           (for_any (_ state of)
             (-> (Try of)
                 (Projection state of)))
           (function (_ input)
             (when operation
               {try.#Success output}
               {try.#Success [input output]}
               
               <handle_failure!>)))

         (the .public (parses projection)
           (for_any (_ state of)
             (-> (Projection state of)
                 (Projection state Any)))
           (function (_ input)
             (when (projection input)
               {try.#Success [input' _]}
               {try.#Success [input' []]}

               <handle_failure!>)))

         (the .public (format format projection)
           (for_any (_ state medium of)
             (-> (Format medium of) (Projection state medium)
                 (Projection state of)))
           (function (_ input)
             (when (projection input)
               {try.#Success [input' to_decode]}
               (when (of format projection to_decode)
                 {try.#Success value}
                 {try.#Success [input' value]}

                 <handle_failure!>)

               <handle_failure!>))))
  )

(the .public (exactly amount projection)
  (for_any (_ state of)
    (-> Nat (Projection state of)
        (Projection state (List of))))
  (when amount
    0 (of ..monad in (list))
    _ (do [! ..monad]
        [x projection]
        (|> projection
            (exactly (-- amount))
            (of ! each (|>> {.#Item x}))))))

(the .public (at_least amount projection)
  (for_any (_ state of)
    (-> Nat (Projection state of)
        (Projection state (List of))))
  (do [! ..monad]
    [minimum (..exactly amount projection)]
    (of ! each (list#composite minimum) (..some projection))))

(the .public (at_most amount projection)
  (for_any (_ state of)
    (-> Nat (Projection state of)
        (Projection state (List of))))
  (when amount
    0 (of ..monad in (list))
    _ (function (_ input)
        (when (projection input)
          {try.#Success [input' x]}
          (..value (of ..monad each (|>> {.#Item x})
                       (at_most (-- amount) projection))
                   input')

          {try.#Failure msg}
          {try.#Success [input (list)]}))))

(the .public (between minimum additional projection)
  (for_any (_ state of)
    (-> Nat Nat (Projection state of)
        (Projection state (List of))))
  (do [! ..monad]
    [minimum (..exactly minimum projection)]
    (when additional
      0 (in minimum)
      _ (of ! each (list#composite minimum)
            (..at_most additional projection)))))

(the .public (separated_by separator projection)
  (for_any (_ state separator of)
    (-> (Projection state separator) (Projection state of)
        (Projection state (List of))))
  (do [! ..monad]
    [?x (..maybe projection)]
    (when ?x
      {.#Some x}
      (|> projection
          (..and separator)
          ..some
          (of ! each (|>> (list#each product.right) {.#Item x})))

      {.#None}
      (in {.#End}))))

(the .public did_not_fail
  Error
  (location.with (location.here)
    "Expected to fail; yet succeeded."))

(the .public (not projection)
  (for_any (_ state of)
    (-> (Projection state of)
        (Projection state Any)))
  (function (_ input)
    (when (projection input)
      {try.#Failure msg}
      {try.#Success [input []]}
      
      _
      {try.#Failure ..did_not_fail})))

(the .public (failure message)
  (for_any (_ state of)
    (-> Text
        (Projection state of)))
  (function (_ input)
    {try.#Failure message}))

(the .public (else value projection)
  (for_any (_ state of)
    (-> of (Projection state of)
        (Projection state of)))
  (function (_ input)
    (when (projection input)
      {try.#Failure error}
      {try.#Success [input value]}

      success
      success)))

(the .public remaining
  (for_any (_ state)
    (Projection state state))
  (function (_ inputs)
    {try.#Success [inputs inputs]}))

(the .public (rec projection)
  (for_any (_ state of)
    (-> (-> (Projection state of)
            (Projection state of))
        (Projection state of)))
  (function (_ inputs)
    (..value (projection (rec projection))
             inputs)))

(the .public (only test projection)
  (for_any (_ state of)
    (-> (-> of Bit) (Projection state of)
        (Projection state of)))
  (do ..monad
    [output projection
     _ (..assertion "Constraint failed." (test output))]
    (in output)))

(the .public (parses? projection)
  (for_any (_ state of)
    (-> (Projection state of)
        (Projection state Bit)))
  (function (_ input)
    (when (projection input)
      {try.#Success [input' _]}
      {try.#Success [input' true]}

      {try.#Failure error}
      {try.#Success [input false]})))

(the .public (speculative projection)
  (for_any (_ state of)
    (-> (Projection state of)
        (Projection state of)))
  (function (_ input)
    (when (projection input)
      {try.#Success [input' output]}
      {try.#Success [input output]}

      failure
      failure)))
