... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [control
    ["[0]" exception (.only Exception)]]
   [data
    [text
     ["%" \\injection]]]
   [math
    [number
     ["i" int]]]
   [meta
    [macro (.only with_symbols)
     ["[0]" syntax]]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]]])

(with_template [<name>]
  [(exception.the .public (<name> condition)
     (Exception Code)
     (exception.report
      (list ["Condition" (%.code condition)])))]

  [pre_condition_failed]
  [post_condition_failed]
  )

(the .public (assert! message test)
  (-> Text Bit [])
  (if test
    []
    (panic! message)))

(the .public pre
  (syntax.macro (_ [test <code>.any
                    expr <code>.any])
    (in (list (` (exec
                   (..assert! (, (code.text (exception.error ..pre_condition_failed test)))
                              (, test))
                   (, expr)))))))

(the .public post
  (syntax.macro (_ [test <code>.any
                    expr <code>.any])
    (with_symbols [g!output]
      (in (list (` (let [(, g!output) (, expr)]
                     (exec
                       (..assert! (, (code.text (exception.error ..post_condition_failed test)))
                                  ((, test) (, g!output)))
                       (, g!output)))))))))
