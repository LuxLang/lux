... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Declaration)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["<>" projection]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" monad)]]]
   ["[0]" meta (.only)
    ["[0]" code (.only)
     ["<[1]>" \\projection (.only Projection)]]
    ["[0]" macro (.only with_symbols)
     ["[0]" syntax]]]]])

(every Declaration
  [Text (List Code)])

(the declaration
  (Projection Declaration)
  (<code>.form (<>.and <code>.local (<>.some <code>.any))))

(the inline
  (Projection [Declaration Code Code])
  (all <>.and
       ..declaration
       <code>.any
       <code>.any
       ))

(the .public inlined
  (syntax.macro (_ [[[name parameters] type term] ..inline])
    (with_symbols [g!_]
      (do [! meta.monad]
        [@ meta.current_module_name
         g!parameters (|> (macro.symbol "parameter")
                          (list.repeated (list.size parameters))
                          (monad.all !))
         .let [inlined (` ((.in_module#
                            (, (code.text @))
                            (.is (, type)
                                 (.function ((, (code.local name)) (,* parameters))
                                   (, term))))
                           (,* (list#each (function (_ g!parameter)
                                            (` ((,' ,) (, g!parameter))))
                                          g!parameters))))
               g!parameters (|> g!parameters
                                (list#each (function (_ parameter)
                                             (list parameter (` <code>.any))))
                                list#conjoint)]]
        (in (list (` (syntax.macro ((, g!_) [(,* g!parameters)])
                       (.of meta.monad (,' in) (.list (.`' (, inlined))))))))))))
