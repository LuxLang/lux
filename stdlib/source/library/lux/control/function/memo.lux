... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... Inspired by;
... "The Different Aspects of Monads and Mixins" by Bruno C. d. S. Oliveira

(.require
 [library
  [lux (.except open)
   [abstract
    [hash (.only Hash)]
    [monad (.only do)]]
   [control
    ["[0]" state (.only State)]]
   [data
    ["[0]" product]
    [collection
     ["[0]" dictionary (.only Dictionary)]]]
   [meta
    ["[0]" type]]]]
 ["[0]" //
  ["[1]" mixin (.only Mixin Recursive)]])

(def .public memoization
  (All (_ i o)
    (Mixin i (State (Dictionary i o) o)))
  (function (_ delegate recur)
    (function (_ input)
      (do [! state.monad]
        [memory state.get]
        (when (dictionary.value input memory)
          {.#Some output}
          (in output)

          {.#None}
          (do !
            [output (delegate input)
             _ (state.update (dictionary.has input output))]
            (in output)))))))

(every .public (Memo i o)
  (Recursive i (State (Dictionary i o) o)))

(def .public (open memo)
  (All (_ i o)
    (type.let [Memory (Dictionary i o)]
      (-> (Memo i o) (-> [Memory i] [Memory o]))))
  (let [memo (//.fixed (//.mixed ..memoization (//.of_recursive memo)))]
    (function (_ [memory input])
      (|> input memo (state.value memory)))))

(def .public (closed hash memo)
  (All (_ i o)
    (-> (Hash i) (Memo i o) (-> i o)))
  (let [memo (//.fixed (//.mixed ..memoization (//.of_recursive memo)))
        empty (dictionary.empty hash)]
    (|>> memo (state.value empty) product.right)))

(def .public (none hash memo)
  (All (_ i o)
    (-> (Hash i) (Memo i o) (-> i o)))
  (let [memo (//.fixed (//.of_recursive memo))
        empty (dictionary.empty hash)]
    (|>> memo (state.value empty) product.right)))
