... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except all or and not)
   [abstract
    [monoid (.only Monoid)]
    [functor
     ["[0]" contravariant]]]]]
 ["[0]" //])

(type .public (Predicate a)
  (-> a Bit))

(with_template [<identity_name> <identity_value> <composition_name> <composition>]
  [(def .public <identity_name>
     Predicate
     (//.constant <identity_value>))

   (def .public (<composition_name> left right)
     (All (_ a) (-> (Predicate a) (Predicate a) (Predicate a)))
     (function (_ value)
       (<composition> (left value)
                      (right value))))]

  [none #0 or .or]
  [all  #1 and .and]
  )

(with_template [<name> <identity> <composition>]
  [(def .public <name>
     (All (_ a) (Monoid (Predicate a)))
     (implementation
      (def identity <identity>)
      (def composite <composition>)))]

  [union        ..none ..or]
  [intersection ..all  ..and]
  )

(def .public (complement predicate)
  (All (_ a) (-> (Predicate a) (Predicate a)))
  (|>> predicate .not))

(def .public not
  (All (_ a) (-> (Predicate a) (Predicate a)))
  ..complement)

(def .public (difference sub base)
  (All (_ a) (-> (Predicate a) (Predicate a) (Predicate a)))
  (function (_ value)
    (.and (base value)
          (.not (sub value)))))

(def .public (rec predicate)
  (All (_ a)
    (-> (-> (Predicate a) (Predicate a))
        (Predicate a)))
  (function (again input)
    (predicate again input)))

(def .public functor
  (contravariant.Functor Predicate)
  (implementation
   (def (each f fb)
     (|>> f fb))))
