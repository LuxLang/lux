... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except all or and not)
   [abstract
    [monoid (.only Monoid)]
    [functor
     ["[0]" contravariant]]]]]
 ["[0]" //])

(every .public (Predicate of)
  (-> of
      Bit))

(with_template [<identity_value> <identity_name> <composition_name> <composition>]
  [(the .public <identity_name>
     Predicate
     (//.constant <identity_value>))

   (the .public (<composition_name> left right)
     (All (_ of)
       (-> (Predicate of) (Predicate of)
           (Predicate of)))
     (function (_ value)
       (<composition> (left value)
                      (right value))))]

  [#0 none or  .or]
  [#1 all  and .and]
  )

(with_template [<name> <identity> <composition>]
  [(the .public <name>
     (All (_ of)
       (Monoid (Predicate of)))
     (implementation
      (the identity <identity>)
      (the composite <composition>)))]

  [union        ..none ..or]
  [intersection ..all  ..and]
  )

(the .public (complement predicate)
  (All (_ of)
    (-> (Predicate of)
        (Predicate of)))
  (|>> predicate .not))

(alias [not]
       ..complement)

(the .public (difference sub base)
  (All (_ of)
    (-> (Predicate of) (Predicate of)
        (Predicate of)))
  (function (_ value)
    (.and (base value)
          (.not (sub value)))))

(the .public (rec predicate)
  (All (_ of)
    (-> (-> (Predicate of) (Predicate of))
        (Predicate of)))
  (function (again input)
    (predicate again input)))

(the .public functor
  (contravariant.Functor Predicate)
  (implementation
   (the (each $ it)
     (|>> $ it))))
