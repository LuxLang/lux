... Inspired by;
... "The Different Aspects of Monads and Mixins" by Bruno C. d. S. Oliveira

(.module:
  [library
   [lux #*
    [abstract
     [monoid (#+ Monoid)]
     [predicate (#+ Predicate)]
     [monad (#+ Monad do)]]]])

(type: .public (Mixin i o)
  (-> (-> i o) (-> i o) (-> i o)))

(def: .public (mixin f)
  (All [i o] (-> (Mixin i o) (-> i o)))
  (function (mix input)
    ((f mix mix) input)))

(def: .public nothing
  Mixin
  (function (_ delegate recur)
    delegate))

(def: .public (with parent child)
  (All [i o] (-> (Mixin i o) (Mixin i o) (Mixin i o)))
  (function (_ delegate recur)
    (parent (child delegate recur) recur)))

(implementation: .public monoid
  (All [i o] (Monoid (Mixin i o)))

  (def: identity ..nothing)
  (def: composite ..with))

(def: .public (advice when then)
  (All [i o] (-> (Predicate i) (Mixin i o) (Mixin i o)))
  (function (_ delegate recur input)
    (if (when input)
      ((then delegate recur) input)
      (delegate input))))

(def: .public (before monad action)
  (All [! i o] (-> (Monad !) (-> i (! Any)) (Mixin i (! o))))
  (function (_ delegate recur input)
    (do monad
      [_ (action input)]
      (delegate input))))

(def: .public (after monad action)
  (All [! i o] (-> (Monad !) (-> i o (! Any)) (Mixin i (! o))))
  (function (_ delegate recur input)
    (do monad
      [output (delegate input)
       _ (action input output)]
      (in output))))

(type: .public (Recursive i o)
  (-> (-> i o) (-> i o)))

(def: .public (of_recursive recursive)
  (All [i o] (-> (Recursive i o) (Mixin i o)))
  (function (_ delegate recur)
    (recursive recur)))
