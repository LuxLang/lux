... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [functor (.only Functor)]
    [monad (.only Monad)]]
   [meta
    ["[0]" code
     ["?[1]" \\parser]]
    [macro (.only with_symbols)
     [syntax (.only syntax)]]]]])

(every .public (Trampoline a)
  (Variant
   {#Return a}
   {#Jump (-> Any (Trampoline a))}))

(the .public return
  (All (_ value)
    (-> value
        (Trampoline value)))
  (|>> {#Return}))

(the .public jump
  (syntax (_ [thunk ?code.any])
    (with_symbols [g!_]
      (in (list (` {#Jump (function ((, g!_) (, g!_))
                            (, thunk))}))))))

(the .public (value it)
  (All (_ value)
    (-> (Trampoline value)
        value))
  (when it
    {#Return it}
    it
    
    {#Jump next}
    (value (next []))))

(the .public functor
  (Functor Trampoline)
  (implementation
   (the (each $ it)
     (when it
       {#Return it}
       {#Return ($ it)}
       
       {#Jump next}
       (each $ (next []))))))

(the .public monad
  (Monad Trampoline)
  (implementation
   (the functor ..functor)
   (the in ..return)
   (the conjoint ..value)))
