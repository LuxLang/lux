... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [functor (.only Functor)]
    [monad (.only Monad
                  do)]]
   [meta
    ["[0]" code
     ["<[1]>" \\projection]]
    [macro (.only with_symbols)
     ["[0]" syntax]
     ["[0]" template]]
    [type
     ["[0]" nominal]]]]])

(the Computation
  (template.macro (_ of)
    [(-> Any
         of)]))

(nominal.every .public (IO of)
  (Computation of)

  (the .public as_io
    (for_any (_ of)
      (-> (Computation of)
          (IO of)))
    (|>> nominal.abstraction))

  (the !io
    (template.macro (!io computation)
      [(nominal.abstraction
        (template.with_locals [g!func g!arg]
          (function (g!func g!arg)
            computation)))]))

  (the value'
    (template.macro (_ io)
      ... creatio ex nihilo
      [((nominal.representation io) [])]))

  (the .public io
    (syntax.macro (_ [computation <code>.any])
      (with_symbols [g!func g!arg]
        (in (list (` (..as_io (function ((, g!func) (, g!arg))
                                (, computation)))))))))

  (the .public value
    (for_any (_ of)
      (-> (IO of)
          of))
    (|>> ..value'))

  (the .public functor
    (Functor IO)
    (implementation
     (the (each f)
       (|>> ..value' f !io))))

  (the .public monad
    (Monad IO)
    (implementation
     (the functor ..functor)

     (the in
       (|>> !io))
     
     (the conjoint
       (|>> ..value'
            ..value'
            !io))))
  )
