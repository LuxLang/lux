... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except with)
   [abstract
    [monoid (.only Monoid)]
    [apply (.only Apply)]
    ["[0]" functor (.only Functor)]
    ["[0]" monad (.only Monad do)]]]])

(every .public (Writer trace value)
  (Record
   [#trace trace
    #value value]))

(the .public (write message)
  (for_any (_ trace)
    (-> trace
        (Writer trace Any)))
  [message []])

(the .public functor
  (for_any (_ trace)
    (Functor (Writer trace)))
  (implementation
   (the (each f fa)
     (let [[trace datum] fa]
       [trace (f datum)]))))

(the .public (apply monoid)
  (for_any (_ trace)
    (-> (Monoid trace)
        (Apply (Writer trace))))
  (implementation
   (the functor ..functor)

   (the (on fa ff)
     (let [[trace0 f] ff
           [trace1 a] fa]
       [(of monoid composite trace0 trace1) (f a)]))))

(the .public (monad monoid)
  (for_any (_ trace)
    (-> (Monoid trace)
        (Monad (Writer trace))))
  (implementation
   (the functor ..functor)

   (the in
     (|>> [(of monoid identity)]))

   (the (conjoint mma)
     (let [[trace0 [trace1 a]] mma]
       [(of monoid composite trace0 trace1) a]))))

(every .public (With trace ! of)
  (! (Writer trace of)))

(the .public (with monoid monad)
  (for_any (_ ! trace)
    (-> (Monoid trace) (Monad !)
        (Monad (With trace !))))
  (implementation
   (the (functor it)
     (|> it
         (of ..functor each)
         (of monad each)))

   (the in
     (let [writer (..monad monoid)]
       (|>> (of writer in) (of monad in))))
   
   (the (conjoint MlMla)
     (do monad
       [[l0 Mla] MlMla
        [l1 a] Mla]
       (in [(of monoid composite l0 l1) a])))))

(the .public (lifted monoid monad)
  (for_any (_ ! trace of)
    (-> (Monoid trace) (Monad !)
        (-> (! of)
            (! (Writer trace of)))))
  (of monad each (|>> [(of monoid identity)])))
