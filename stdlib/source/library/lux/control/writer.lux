... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except with)
   [abstract
    [monoid (.only Monoid)]
    [functor (.only Functor)]
    [monad (.only Monad do)]]]])

(every .public (Writer trace value)
  (Record
   [#trace trace
    #value value]))

(the .public (write message)
  (for_any (_ trace)
    (-> trace
        (Writer trace Any)))
  [message []])

(the .public functor
  (for_any (_ trace)
    (Functor (Writer trace)))
  (implementation
   (the (each f fa)
     (let [[trace datum] fa]
       [trace (f datum)]))))

(the .public (monad monoid)
  (for_any (_ trace)
    (-> (Monoid trace)
        (Monad (Writer trace))))
  (implementation
   (the functor ..functor)

   (the pure
     (|>> [(by monoid identity)]))

   (the (conjoint mma)
     (let [[trace0 [trace1 a]] mma]
       [(by monoid composite trace0 trace1) a]))))

(every .public (With trace ! it)
  (! (Writer trace it)))

(the .public (with monoid monad)
  (for_any (_ ! trace)
    (-> (Monoid trace) (Monad !)
        (Monad (With trace !))))
  (implementation
   (the (functor it)
     (|> it
         (by ..functor each)
         (by monad each)))

   (the pure
     (let [writer (..monad monoid)]
       (|>> (by writer pure) (by monad pure))))
   
   (the (conjoint MlMla)
     (do monad
       [[l0 Mla] MlMla
        [l1 a] Mla]
       (pure [(by monoid composite l0 l1) a])))))

(the .public (lifted monoid monad)
  (for_any (_ ! trace it)
    (-> (Monoid trace) (Monad !)
        (-> (! it)
            (! (Writer trace it)))))
  (by monad each (|>> [(by monoid identity)])))
