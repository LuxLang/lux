(.module:
  [library
   [lux #*
    ["@" target]
    [abstract
     monoid
     [apply (#+ Apply)]
     ["." functor (#+ Functor)]
     ["." monad (#+ Monad do)]]]])

(type: .public (Writer l a)
  {#.doc "Represents a value with an associated 'log' value to record arbitrary information."}
  {#log l
   #value a})

(def: .public (write l)
  {#.doc "Set the log to a particular value."}
  (All [l] (-> l (Writer l Any)))
  [l []])

(implementation: .public functor
  (All [l]
    (Functor (Writer l)))
  
  (def: (map f fa)
    (let [[log datum] fa]
      [log (f datum)])))

(implementation: .public (apply monoid)
  (All [l]
    (-> (Monoid l) (Apply (Writer l))))
  
  (def: &functor ..functor)

  (def: (apply ff fa)
    (let [[log1 f] ff
          [log2 a] fa]
      [(\ monoid compose log1 log2) (f a)])))

(implementation: .public (monad monoid)
  (All [l]
    (-> (Monoid l) (Monad (Writer l))))
  
  (def: &functor ..functor)

  (def: in
    (|>> [(\ monoid identity)]))

  (def: (join mma)
    (let [[log1 [log2 a]] mma]
      [(\ monoid compose log1 log2) a])))

(implementation: .public (with monoid monad)
  {#.doc (doc "Enhances a monad with Writer functionality.")}
  (All [l M] (-> (Monoid l) (Monad M) (Monad (All [a] (M (Writer l a))))))

  (def: &functor
    (functor.compose (get@ #monad.&functor monad)
                     ..functor))

  (def: in
    (let [writer (..monad monoid)]
      (|>> (\ writer in) (\ monad in))))
  
  (def: (join MlMla)
    (do monad
      [[l1 Mla] (for {@.old
                      (: ((:parameter 1) (Writer (:parameter 0) ((:parameter 1) (Writer (:parameter 0) (:parameter 2)))))
                         MlMla)}
                     ... On new compiler
                     MlMla)
       [l2 a] Mla]
      (in [(\ monoid compose l1 l2) a]))))

(def: .public (lift monoid monad)
  {#.doc (doc "Wraps a monadic value with Writer machinery.")}
  (All [l M a]
    (-> (Monoid l) (Monad M)
        (-> (M a) (M (Writer l a)))))
  (\ monad map (|>> [(\ monoid identity)])))
