(.module:
  [library
   [lux #*
    [abstract
     [monad (#+ do)]]
    [control
     ["." io]
     ["." try]
     ["." exception (#+ exception:)]
     ["<>" parser ("#\." functor)
      ["<c>" code (#+ Parser)]]]
    [data
     ["." text
      ["%" format (#+ format)]]]
    [time
     ["." instant]
     ["." date (#+ Date) ("#\." order)]]
    ["." meta]
    [macro
     ["." code]
     ["." template]
     [syntax (#+ syntax:)]]]])

(exception: .public (must_remember {deadline Date} {today Date} {message Text} {focus (Maybe Code)})
  (exception.report
   ["Deadline" (%.date deadline)]
   ["Today" (%.date today)]
   ["Message" message]
   ["Code" (case focus
             (#.Some focus)
             (%.code focus)

             #.None
             "")]))

(def: deadline
  (Parser Date)
  ($_ <>.either
      (<>\map (|>> instant.of_millis instant.date)
              <c>.int)
      (do <>.monad
        [raw <c>.text]
        (case (\ date.codec decode raw)
          (#try.Success date)
          (in date)
          
          (#try.Failure message)
          (<>.failure message)))))

(syntax: .public (remember {deadline ..deadline} {message <c>.text} {focus (<>.maybe <c>.any)})
  {#.doc (example "A message with an expiration date."
                  "Can have an optional piece of code to focus on."
                  (remember "2022-04-01"
                    "Do this, that and the other.")
                  (remember "2022-04-01"
                    "Improve the performace."
                    (some (complicated (computation 123)))))}
  (let [now (io.run! instant.now)
        today (instant.date now)]
    (if (date\< deadline today)
      (in (case focus
            (#.Some focus)
            (list focus)

            #.None
            (list)))
      (meta.failure (exception.error ..must_remember [deadline today message focus])))))

(template [<name> <message>]
  [(`` (syntax: .public (<name> {deadline ..deadline} {message <c>.text} {focus (<>.maybe <c>.any)})
         {#.doc (example (~~ (template.text ["A " <message> " message with an expiration date."]))
                         "Can have an optional piece of code to focus on."
                         (<name> "2022-04-01"
                                 "Do this, that and the other.")
                         (<name> "2022-04-01"
                                 "Improve the performace."
                                 (some (complicated (computation 123)))))}
         (in (list (` (..remember (~ (code.text (%.date deadline)))
                        (~ (code.text (format <message> " " message)))
                        (~+ (case focus
                              (#.Some focus)
                              (list focus)

                              #.None
                              (list)))))))))]

  [to_do  "TODO"]
  [fix_me "FIXME"]
  )
