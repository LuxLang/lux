(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["<>" parser (.use "[1]#[0]" functor)]
    ["[0]" io]
    ["[0]" try]
    ["[0]" exception (.only exception)]]
   [data
    ["[0]" text
     ["%" \\format (.only format)]]]
   ["[0]" meta (.only)
    ["[0]" code (.only)
     ["<[1]>" \\parser (.only Parser)]]
    [macro
     [syntax (.only syntax)]
     ["[0]" template]]]
   [world
    [time
     ["[0]" instant]
     ["[0]" date (.only Date) (.use "[1]#[0]" order)]]]]])

(exception .public (must_remember [deadline Date
                                   today Date
                                   message Text
                                   focus (Maybe Code)])
  (exception.report
   (list ["Deadline" (%.date deadline)]
         ["Today" (%.date today)]
         ["Message" message]
         ["Code" (case focus
                   {.#Some focus}
                   (%.code focus)

                   {.#None}
                   "")])))

(def deadline
  (Parser Date)
  (all <>.either
       (<>#each (|>> instant.of_millis instant.date)
                <code>.int)
       (do <>.monad
         [raw <code>.text]
         (case (at date.codec decoded raw)
           {try.#Success date}
           (in date)
           
           {try.#Failure message}
           (<>.failure message)))))

(def .public remember
  (syntax (_ [deadline ..deadline
              message <code>.text
              focus (<>.maybe <code>.any)])
    (let [now (io.run! instant.now)
          today (instant.date now)]
      (if (date#< deadline today)
        (in (case focus
              {.#Some focus}
              (list focus)

              {.#None}
              (list)))
        (meta.failure (exception.error ..must_remember [deadline today message focus]))))))

(with_template [<name> <message>]
  [(`` (def .public <name>
         (syntax (_ [deadline ..deadline
                     message <code>.text
                     focus (<>.maybe <code>.any)])
           (in (list (` (..remember (, (code.text (%.date deadline)))
                          (, (code.text (format <message> " " message)))
                          (,* (case focus
                                {.#Some focus}
                                (list focus)

                                {.#None}
                                (list))))))))))]

  [to_do  "TODO"]
  [fix_me "FIXME"]
  )
