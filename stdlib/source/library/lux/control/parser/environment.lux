(.module:
  [library
   [lux #*
    [control
     ["." try (#+ Try)]
     ["." exception (#+ exception:)]]
    [data
     ["." product]
     ["." text
      ["%" format (#+ format)]]
     [collection
      ["." dictionary (#+ Dictionary)]]]]]
  ["." //])

(type: .public Property
  {#.doc (doc "A property in the environment.")}
  Text)

(type: .public Environment
  {#.doc (doc "An abstraction for environment variables of a program.")}
  (Dictionary Property Text))

(exception: .public (unknown_property {property Property})
  (exception.report
   ["Property" (%.text property)]))

(type: .public (Parser a)
  {#.doc (doc "A parser of environment variables of a program.")}
  (//.Parser Environment a))

(def: .public empty
  {#.doc (doc "An empty environment.")}
  Environment
  (dictionary.empty text.hash))

(def: .public (property name)
  (-> Property (Parser Text))
  (function (_ environment)
    (case (dictionary.get name environment)
      (#.Some value)
      (exception.return [environment value])
      
      #.None
      (exception.except ..unknown_property [name]))))

(def: .public (run parser environment)
  {#.doc (doc "Executes a parser against the given environment variables."
              "Does not check whether all environment variables were parsed, since they're usually an open set.")}
  (All [a] (-> (Parser a) Environment (Try a)))
  (\ try.monad map product.right (parser environment)))
