(.require
 [library
  [lux (.except left right)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try (.only Try)]
    ["[0]" exception (.only exception:)]]
   [data
    [collection
     [tree (.only Tree)
      ["[0]" zipper (.only Zipper)]]]]]]
 ["[0]" //])

(type: .public (Parser t a)
  (//.Parser (Zipper t) a))

(def .public (result' parser zipper)
  (All (_ t a) (-> (Parser t a) (Zipper t) (Try a)))
  (do try.monad
    [[zipper output] (//.result parser zipper)]
    (in output)))

(def .public (result parser tree)
  (All (_ t a) (-> (Parser t a) (Tree t) (Try a)))
  (result' parser (zipper.zipper tree)))

(def .public value
  (All (_ t) (Parser t t))
  (function (_ zipper)
    {try.#Success [zipper (zipper.value zipper)]}))

(exception: .public cannot_move_further)

(with_template [<name> <direction>]
  [(def .public <name>
     (All (_ t) (Parser t []))
     (function (_ zipper)
       (case (<direction> zipper)
         {.#None}
         (exception.except ..cannot_move_further [])

         {.#Some next}
         {try.#Success [next []]})))]

  [down      zipper.down]
  [up        zipper.up]

  [right     zipper.right]
  [rightmost zipper.rightmost]

  [left      zipper.left]
  [leftmost  zipper.leftmost]
  
  [next      zipper.next]
  [end       zipper.end]
  
  [previous  zipper.previous]
  [start     zipper.start]
  )
