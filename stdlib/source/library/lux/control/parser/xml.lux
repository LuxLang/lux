(.module:
  [library
   [lux #*
    [abstract
     [monad (#+ do)]]
    [control
     ["." try (#+ Try) ("#\." functor)]
     ["." exception (#+ exception:)]]
    [data
     ["." name ("#\." equivalence codec)]
     ["." text
      ["%" format (#+ format)]]
     [collection
      ["." list]
      ["." dictionary]]
     [format
      ["/" xml (#+ Attribute Attrs Tag XML)]]]]]
  ["." //])

(type: #export (Parser a)
  {#.doc (doc "A parser of XML-encoded data.")}
  (//.Parser [Attrs (List XML)] a))

(exception: #export empty_input)
(exception: #export unexpected_input)

(exception: #export (wrong_tag {expected Tag} {actual Tag})
  (exception.report
   ["Expected" (%.text (/.tag expected))]
   ["Actual" (%.text (/.tag actual))]))

(exception: #export (unknown_attribute {expected Attribute} {available (List Attribute)})
  (exception.report
   ["Expected" (%.text (/.attribute expected))]
   ["Available" (exception.listing (|>> /.attribute %.text) available)]))

(exception: #export (unconsumed_inputs {inputs (List XML)})
  (exception.report
   ["Inputs" (exception.listing (\ /.codec encode) inputs)]))

(def: (run' parser attrs documents)
  (All [a] (-> (Parser a) Attrs (List XML) (Try a)))
  (case (//.run parser [attrs documents])
    (#try.Success [[attrs' remaining] output])
    (if (list.empty? remaining)
      (#try.Success output)
      (exception.except ..unconsumed_inputs remaining))
    
    (#try.Failure error)
    (#try.Failure error)))

(def: #export (run parser documents)
  {#.doc (doc "Applies a parser against a stream of XML documents."
              "Verifies that all of the inputs are consumed by the parser.")}
  (All [a] (-> (Parser a) (List XML) (Try a)))
  (..run' parser /.attributes documents))

(def: #export text
  {#.doc (doc "Yields text from a text node.")}
  (Parser Text)
  (function (_ [attrs documents])
    (case documents
      #.End
      (exception.except ..empty_input [])
      
      (#.Item head tail)
      (case head
        (#/.Text value)
        (#try.Success [[attrs tail] value])
        
        (#/.Node _)
        (exception.except ..unexpected_input [])))))

(def: #export tag
  {#.doc (doc "Yields the tag from the next node.")}
  (Parser Tag)
  (function (_ [attrs documents])
    (case documents
      #.End
      (exception.except ..empty_input [])
      
      (#.Item head _)
      (case head
        (#/.Text _)
        (exception.except ..unexpected_input [])
        
        (#/.Node tag _ _)
        (#try.Success [[attrs documents] tag])))))

(def: #export (attribute name)
  {#.doc (doc "Yields the value of an attribute in the current node.")}
  (-> Attribute (Parser Text))
  (function (_ [attrs documents])
    (case (dictionary.get name attrs)
      #.None
      (exception.except ..unknown_attribute [name (dictionary.keys attrs)])
      
      (#.Some value)
      (#try.Success [[attrs documents] value]))))

(def: #export (node expected parser)
  {#.doc (doc "Parses the contents of the next node if the tag matches.")}
  (All [a] (-> Tag (Parser a) (Parser a)))
  (function (_ [attrs documents])
    (case documents
      #.End
      (exception.except ..empty_input [])
      
      (#.Item head tail)
      (case head
        (#/.Text _)
        (exception.except ..unexpected_input [])
        
        (#/.Node actual attrs' children)
        (if (name\= expected actual)
          (|> children
              (..run' parser attrs')
              (try\map (|>> [[attrs tail]])))
          (exception.except ..wrong_tag [expected actual]))))))

(def: #export ignore
  {#.doc (doc "Skips the next node.")}
  (Parser Any)
  (function (_ [attrs documents])
    (case documents
      #.End
      (exception.except ..empty_input [])
      
      (#.Item head tail)
      (#try.Success [[attrs tail] []]))))

(exception: #export nowhere)

(def: #export (somewhere parser)
  {#.doc (doc "Applies the parser somewhere among the remaining inputs; instead of demanding that the parser succeeds against the immediate inputs.")}
  (All [a] (-> (Parser a) (Parser a)))
  (function (recur [attrs input])
    (case (//.run parser [attrs input])
      (#try.Success [[attrs remaining] output])
      (#try.Success [[attrs remaining] output])
      
      (#try.Failure error)
      (case input
        #.End
        (exception.except ..nowhere [])
        
        (#.Item head tail)
        (do try.monad
          [[[attrs tail'] output] (recur [attrs tail])]
          (in [[attrs (#.Item head tail')]
               output]))))))
