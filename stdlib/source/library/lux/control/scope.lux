... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Scope
                local with)
   [abstract
    [functor (.only Functor)]
    [monad (.only Monad do)]]]])

... https://en.wikipedia.org/wiki/Scope_(computer_science)
(every .public (Scope context it)
  (-> context
      it))

(the .public read
  (for_any (_ context)
    (Scope context context))
  (|>>))

(the .public (local change proc)
  (for_any (_ context it)
    (-> (-> context context) (Scope context it)
        (Scope context it)))
  (|>> change
       proc))

(the .public (value env proc)
  (for_any (_ context it)
    (-> context (Scope context it)
        it))
  (proc env))

(the .public functor
  (for_any (_ context)
    (Functor (Scope context)))
  (implementation
   (the (each f fa)
     (function (_ env)
       (f (fa env))))))

(the .public monad
  (for_any (_ context)
    (Monad (Scope context)))
  (implementation
   (the functor ..functor)

   (the (pure x)
     (function (_ env) x))

   (the (conjoint mma)
     (function (_ env)
       (mma env env)))))

(every .public (With context ! it)
  (Scope context (! it)))

(the .public (with monad)
  (for_any (_ !)
    (-> (Monad !)
        (for_any (_ context)
          (Monad (With context !)))))
  (implementation
   (the (functor it)
     (|> it
         (by monad each)
         (by ..functor each)))

   (the pure
     (|>> (by monad pure)
          (by ..monad pure)))
   
   (the (conjoint eMeMa)
     (function (_ env)
       (do monad
         [eMa (value env eMeMa)]
         (value env eMa))))))

(the .public lifted
  (for_any (_ ! context it)
    (-> (! it)
        (Scope context (! it))))
  (by ..monad pure))
