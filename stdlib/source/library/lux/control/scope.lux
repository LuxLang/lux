... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Scope
                local with)
   [abstract
    ["[0]" functor (.only Functor)]
    ["[0]" monad (.only Monad do)]]]])

... https://en.wikipedia.org/wiki/Scope_(computer_science)
(every .public (Scope context of)
  (-> context
      of))

(the .public read
  (for_any (_ context)
    (Scope context context))
  (|>>))

(the .public (local change proc)
  (for_any (_ context of)
    (-> (-> context context) (Scope context of)
        (Scope context of)))
  (|>> change
       proc))

(the .public (value env proc)
  (for_any (_ context of)
    (-> context (Scope context of)
        of))
  (proc env))

(the .public functor
  (for_any (_ context)
    (Functor (Scope context)))
  (implementation
   (the (each f fa)
     (function (_ env)
       (f (fa env))))))

(the .public monad
  (for_any (_ context)
    (Monad (Scope context)))
  (implementation
   (the functor ..functor)

   (the (in x)
     (function (_ env) x))

   (the (conjoint mma)
     (function (_ env)
       (mma env env)))))

(every .public (With context ! of)
  (Scope context (! of)))

(the .public (with monad)
  (for_any (_ !)
    (-> (Monad !)
        (for_any (_ context)
          (Monad (With context !)))))
  (implementation
   (the (functor it)
     (|> it
         (by monad each)
         (by ..functor each)))

   (the in
     (|>> (by monad in)
          (by ..monad in)))
   
   (the (conjoint eMeMa)
     (function (_ env)
       (do monad
         [eMa (value env eMeMa)]
         (value env eMa))))))

(the .public lifted
  (for_any (_ ! context of)
    (-> (! of)
        (Scope context (! of))))
  (by ..monad in))
