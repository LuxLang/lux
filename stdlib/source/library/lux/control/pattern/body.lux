... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except or and)
   [meta
    ["[0]" type]]]]
 [//
  ["[0]" input]
  ["[0]" output]])

(every .public (Then arity value)
  (-> arity
      value))

(every .public Constant
  (Then input.Zero))

(every .public Else
  Constant)

(every .public (Body outer inner value)
  (-> (Then inner value) (Else value)
      (Then outer value)))

(every .public Zero
  (All (_ arity value)
    (Body arity arity value)))

(the .public success
  Zero
  (function (_ next exit)
    next))

(every .public (Succ of)
  (All (_ arity value)
    (|> (Body arity (input.Succ of arity) value)
        (output.Succ of))))

(the .public (succ value)
  Succ
  (function (_ next exit stack)
    (next (input.succ value stack))))

(the .public failure
  Body
  (function (_ next exit stack)
    (exit (input.zero))))

(the .public (or left right)
  (All (_ arity_0 arity_1 value)
    (type.let [choice (Body arity_0 arity_1 value)]
      (-> choice choice
          choice)))
  (function (_ next exit stack)
    (left next
          (function (_ _)
            (right next exit stack))
          stack)))

(the .public (and left right)
  (All (_ arity_0 arity_1 arity_2 value)
    (-> (Body arity_1 arity_0 value)
        (Body arity_2 arity_1 value)
        (Body arity_2 arity_0 value)))
  (function (_ input exit stack)
    (right (left input exit) exit stack)))
