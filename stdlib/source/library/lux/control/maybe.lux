... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except list with when)
   [abstract
    [monoid (.only Monoid)]
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    [apply (.only Apply)]
    ["[0]" functor (.only Functor)]
    ["[0]" monad (.only Monad do)]]
   [meta
    ["[0]" location]]]])

... (every (Maybe a)
...   {.#None}
...   {.#Some a})

(the .public monoid
  (All (_ a) (Monoid (Maybe a)))
  (implementation
   (the identity
     {.#None})
   
   (the (composite mx my)
     (.when mx
       {.#None}
       my
       
       _
       mx))))

(the .public functor
  (Functor Maybe)
  (implementation
   (the (each f ma)
     (.when ma
       {.#Some a}
       {.#Some (f a)}
       
       ... {.#None}
       it
       (as_expected it)))))

(the .public apply
  (Apply Maybe)
  (implementation
   (the functor ..functor)

   (the (on fa ff)
     (.when [ff fa]
       [{.#Some f} {.#Some a}]
       {.#Some (f a)}

       _
       {.#None}))))

(the .public monad
  (Monad Maybe)
  (implementation
   (the functor ..functor)

   (the (in x)
     {.#Some x})

   (the (conjoint mma)
     (.when mma
       {.#Some mx}
       mx
       
       ... {.#None}
       it
       (as_expected it)))))

(the .public (equivalence super)
  (All (_ a) (-> (Equivalence a) (Equivalence (Maybe a))))
  (implementation
   (the (= mx my)
     (.when [mx my]
       [{.#None} {.#None}]
       true

       [{.#Some x} {.#Some y}]
       (of super = x y)
       
       _
       false))))

(the .public (hash super)
  (All (_ a) (-> (Hash a) (Hash (Maybe a))))
  (implementation
   (the equivalence
     (..equivalence (of super equivalence)))
   
   (the (hash value)
     (.when value
       {.#None}
       1

       {.#Some value}
       (of super hash value)))))

(the .public (with monad)
  (All (_ M) (-> (Monad M) (Monad (All (_ a) (M (Maybe a))))))
  (implementation
   (the functor
     (functor.composite (its monad.functor monad)
                        ..functor))

   (the in
     (|>> (of ..monad in)
          (of monad in)))
   
   (the (conjoint MmMma)
     (do monad
       [mMma MmMma]
       (.when mMma
         {.#Some Mma}
         Mma

         ... {.#None}
         it
         (in (as_expected it)))))))

(the .public (lifted monad)
  (All (_ M a) (-> (Monad M) (-> (M a) (M (Maybe a)))))
  (of monad each (of ..monad in)))

(the .public else
  (macro (_ tokens state)
    (.when tokens
      (.list else maybe)
      (let [g!temp (is Code [location.dummy {.#Symbol ["" ""]}])]
        {.#Right [state (.list (` (.when (, maybe)
                                    {.#Some (, g!temp)}
                                    (, g!temp)

                                    ... {.#None}
                                    (, g!temp)
                                    (, else))))]})

      _
      {.#Left "Wrong syntax for 'else'"})))

(the .public trusted
  (All (_ a) (-> (Maybe a) a))
  (|>> (..else (undefined))))

(the .public (list value)
  (All (_ a) (-> (Maybe a) (List a)))
  (.when value
    {.#Some value}
    (.list value)

    ... {.#None}
    _
    (.list)))

(the .public when
  (template (_ <test> <then>)
    [(if <test>
       <then>
       {.#None})]))
