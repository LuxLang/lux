... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" pipe]
    ["[0]" try (.only Try)]
    ["[0]" exception]
    ["[0]" io (.only IO io)]
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" bit]
    ["[0]" product]]
   [meta
    [macro
     ["[0]" local]]
    [type (.only sharing)
     ["[0]" nominal]]]]]
 [//
  ["[0]" atom (.only Atom atom)]
  ["[0]" async (.only Async Resolver) (.use "[1]#[0]" monad)]
  ["[0]" frp (.only Channel Channel')]])

(exception.the .public poisoned)
(exception.the .public dead)

(local.let [<Mail> (template (_ Actor s)
                     [(-> s (Actor s) (Async (Try s)))])
            <Obituary> (template (_ Actor s)
                         [[Text s (List (<Mail> Actor s))]])
            <Mailbox> (template (_ Actor s)
                        [(Rec Mailbox
                           [(Async [(<Mail> Actor s) Mailbox])
                            (Resolver [(<Mail> Actor s) Mailbox])])])]
  (these (the (pending [read write])
           (All (_ a)
             (-> (Rec Mailbox
                   [(Async [a Mailbox])
                    (Resolver [a Mailbox])])
                 (IO (List a))))
           (do [! io.monad]
             [current (async.value read)]
             (when current
               {.#Some [head tail]}
               (of ! each (|>> {.#Item head})
                   (pending tail))
               
               {.#None}
               (in {.#End}))))
         
         (nominal.every .public (Actor s)
           (Record
            [#obituary [(Async (<Obituary> Actor s))
                        (Resolver (<Obituary> Actor s))]
             #mailbox (Atom (<Mailbox> Actor s))])

           (every .public (Mail s)
             (<Mail> Actor s))

           (every .public (Obituary s)
             (<Obituary> Actor s))

           (every .public (Behavior s)
             (-> (Mail s) s (Actor s) (Async (Try s))))

           (the .public (spawn! behavior init)
             (All (_ s) (-> (Behavior s) s (IO (Actor s))))
             (io (let [self (sharing [s]
                              (is (Behavior s)
                                  behavior)
                              (is (Actor s)
                                  (nominal.abstraction
                                   [#obituary (async.async [])
                                    #mailbox (atom (async.async []))])))
                       process (loop (again [state init
                                             [|mailbox| _] (io.run! (atom.read! (its #mailbox (nominal.representation self))))])
                                 (do [! async.monad]
                                   [[head tail] |mailbox|
                                    ?state' (behavior head state self)]
                                   (when ?state'
                                     {try.#Failure error}
                                     (let [[_ resolve] (its #obituary (nominal.representation self))]
                                       (exec
                                         (io.run!
                                          (do io.monad
                                            [pending (..pending tail)]
                                            (resolve [error state {.#Item head pending}])))
                                         (in [])))

                                     {try.#Success state'}
                                     (again state' tail))))]
                   self)))

           (the .public (alive? actor)
             (All (_ s) (-> (Actor s) (IO Bit)))
             (let [[obituary _] (its #obituary (nominal.representation actor))]
               (|> obituary
                   async.value
                   (of io.functor each
                       (|>> (pipe.when
                              {.#None}
                              bit.yes

                              _
                              bit.no))))))

           (the .public (obituary' actor)
             (All (_ s) (-> (Actor s) (IO (Maybe (Obituary s)))))
             (let [[obituary _] (its #obituary (nominal.representation actor))]
               (async.value obituary)))

           (the .public obituary
             (All (_ s) (-> (Actor s) (Async (Obituary s))))
             (|>> nominal.representation
                  (its #obituary)
                  product.left))

           (the .public (mail! mail actor)
             (All (_ s) (-> (Mail s) (Actor s) (IO (Try Any))))
             (do [! io.monad]
               [alive? (..alive? actor)]
               (if alive?
                 (let [entry [mail (async.async [])]]
                   (do !
                     [|mailbox|&resolve (atom.read! (its #mailbox (nominal.representation actor)))]
                     (loop (again [[|mailbox| resolve] |mailbox|&resolve])
                       (do !
                         [|mailbox| (async.value |mailbox|)]
                         (when |mailbox|
                           {.#None}
                           (do !
                             [resolved? (resolve entry)]
                             (if resolved?
                               (do !
                                 [_ (atom.write! (product.right entry) (its #mailbox (nominal.representation actor)))]
                                 (in {try.#Success []}))
                               (again |mailbox|&resolve)))
                           
                           {.#Some [_ |mailbox|']}
                           (again |mailbox|'))))))
                 (in (exception.except ..dead [])))))

           (every .public (Message s o)
             (-> s (Actor s) (Async (Try [s o]))))

           (the (mail message)
             (All (_ s o) (-> (Message s o) [(Async (Try o)) (Mail s)]))
             (let [[async resolve] (sharing [s o]
                                     (is (Message s o)
                                         message)
                                     (is [(Async (Try o))
                                          (Resolver (Try o))]
                                         (async.async [])))]
               [async
                (function (_ state self)
                  (do [! async.monad]
                    [outcome (message state self)]
                    (when outcome
                      {try.#Success [state' return]}
                      (exec
                        (io.run! (resolve {try.#Success return}))
                        (async.resolved {try.#Success state'}))
                      
                      {try.#Failure error}
                      (exec
                        (io.run! (resolve {try.#Failure error}))
                        (async.resolved {try.#Failure error})))))]))

           (the .public (request! message actor)
             (All (_ s o) (-> (Message s o) (Actor s) (Async (Try o))))
             (let [[async mail] (..mail message)]
               (do async.monad
                 [outcome (async.future (..mail! mail actor))]
                 (when outcome
                   {try.#Success _}
                   async
                   
                   {try.#Failure error}
                   (in {try.#Failure error})))))
           )))

(the .public default
  Behavior
  (function (_ mail state self)
    (mail state self)))

(the .public (poison! actor)
  (All (_ s) (-> (Actor s) (IO (Try Any))))
  (..mail! (function (_ state self)
             (async.resolved (exception.except ..poisoned [])))
           actor))

(every .public Stop
  (IO Any))

(the continue! true)
(the stop! false)

(the .public (observe! action channel actor)
  (All (_ r w s) (-> (-> r Stop (Mail s)) (Channel' r w) (Actor s) (IO Any)))
  (let [signal (is (Atom Bit)
                   (atom.atom ..continue!))
        stop (is Stop
                 (atom.write! ..stop! signal))]
    (frp.subscribe! (function (_ event)
                      (do [! io.monad]
                        [continue? (atom.read! signal)]
                        (if continue?
                          (|> actor
                              (..mail! (action event stop))
                              (of ! each try.maybe))
                          (in {.#None}))))
                    channel)))

... The following behavior and messages allow Lux's actors to behave like Clojure's agents.
... https://clojure.org/reference/agents

(exception.the .public invalid)

(the .public (validated ? it)
  (All (_ state)
    (-> (Predicate state) (Behavior state)
        (Behavior state)))
  (function (_ mail before actor)
    (do (try.with async.monad)
      [after (mail before actor)]
      (if (? after)
        (in after)
        (async#in (exception.except ..invalid []))))))

(the .public state
  (All (_ state)
    (Message state state))
  (function (_ state self)
    (async#in {try.#Success [state state]})))

(the .public (update $)
  (All (_ state)
    (-> (-> state state)
        (Message state [state state])))
  (function (_ before self)
    (let [after ($ before)]
      (async#in {try.#Success [after [before after]]}))))

(the .public (reset after)
  (All (_ state)
    (-> state
        (Message state state)))
  (function (_ before self)
    (async#in {try.#Success [after before]})))
