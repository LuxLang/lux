... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [functor (.only Functor)]
    [apply (.only Apply)]
    ["[0]" monad (.only Monad do)]]
   [control
    ["[0]" io (.only IO io)]
    ["[0]" maybe]
    ["[0]" try]
    ["[0]" state]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list]]]
   [meta
    [macro
     ["^" pattern]]
    [type (.only sharing)
     ["[0]" nominal]
     ["[0]" variance (.only Mutable)]]]]]
 [//
  ["[0]" atom (.only Atom atom)]
  ["[0]" async (.only Async Resolver)]
  ["[0]" frp (.only Channel Sink)]])

(every (Observer of)
  (-> of
      (IO Any)))

(nominal.every (Var'' of)
  (Atom [of (List (Sink of))])

  (every .public (Var' read write)
    (Var'' (Mutable read write)))
  
  (every .public (Var of)
    (Var'' (Mutable of of)))

  (the .public (var value)
    (All (_ of)
      (-> of
          (Var of)))
    (nominal.abstraction
     (atom.atom [(variance.write value) (list)])))

  (the read!
    (All (_ read write)
      (-> (Var' read write)
          read))
    (|>> nominal.representation
         atom.read!
         io.run!
         product.left
         variance.read))

  (the (write! new_value var)
    (All (_ read write)
      (-> write (Var' read write)
          (IO Any)))
    (do [! io.monad]
      [.let [var' (nominal.representation var)]
       (^.let old [_ observers]) (atom.read! var')
       succeeded? (atom.compare_and_swap! old [(variance.write new_value) observers] var')]
      (if succeeded?
        (do !
          [banned (monad.only ! (function (_ sink)
                                  (do !
                                    [result (of sink feed (variance.write new_value))]
                                    (in (when result
                                          {try.#Success _}
                                          false
                                          
                                          {try.#Failure _}
                                          true))))
                              observers)
           _ (atom.update! (function (_ [value audience])
                             (|> audience
                                 (list.only (function (_ it)
                                              (not (list.any? (same? it) banned))))
                                 [value]))
                           var')]
          (in []))
        (write! new_value var))))

  (the .public (changes target)
    (All (_ of)
      (-> (Var of)
          (IO [(Channel of)
               (Sink of)])))
    (do io.monad
      [.let [[channel sink] (sharing [a]
                              (is (Var a)
                                  target)
                              (is [(Channel a) (Sink a)]
                                  (frp.channel [])))]
       _ (atom.update! (function (_ [value observers])
                         [value {.#Item (implementation
                                         (the close (of sink close))
                                         (the feed (|>> variance.read (of sink feed))))
                                        observers}])
                       (nominal.representation target))]
      (in [channel sink])))
  )

(every (Tx_Frame read write)
  (Record
   [#var (Var' read write)
    #original read
    #current write]))

(every Tx
  (List (Ex (_ read write)
          (Tx_Frame read write))))

(every .public (STM of)
  (state.State Tx of))

(the (var_value var tx)
  (All (_ read write)
    (-> (Var' read write) Tx
        (Maybe read)))
  (|> tx
      (list.example (function (_ [_var _original _current])
                      (same? (as (Var Any) var)
                             (as (Var Any) _var))))
      (of maybe.monad each (function (_ [_var _original _current])
                             _current))
      as_expected))

(the .public (read var)
  (All (_ read write)
    (-> (Var' read write)
        (STM read)))
  (function (_ tx)
    (when (var_value var tx)
      {.#Some value}
      [tx value]

      {.#None}
      (let [value (..read! var)]
        [{.#Item [#var var
                  #original value
                  #current (as_expected value)]
                 tx}
         value]))))

(the (with_updated_var var value tx)
  (All (_ read write)
    (-> (Var' read write) write Tx
        Tx))
  (when tx
    {.#End}
    {.#End}
    
    {.#Item [_var _original _current] tx'}
    (if (same? (as (Var Any) var)
               (as (Var Any) _var))
      {.#Item [#var _var
               #original _original
               #current (as_expected value)]
              tx'}
      {.#Item [#var _var
               #original _original
               #current _current]
              (with_updated_var var value tx')})))

(the .public (write value var)
  (All (_ read write)
    (-> write (Var' read write)
        (STM Any)))
  (function (_ tx)
    (when (var_value var tx)
      {.#Some _}
      [(with_updated_var var value tx)
       []]

      {.#None}
      [{.#Item [#var var
                #original (..read! var)
                #current value]
               tx}
       []])))

(the .public functor
  (Functor STM)
  state.functor)

(the .public apply
  (Apply STM)
  state.apply)

(the .public monad
  (Monad STM)
  state.monad)

(the .public (update f var)
  (All (_ read write)
    (-> (-> read write) (Var' read write)
        (STM [read write])))
  (do ..monad
    [before (..read var)
     .let [after (f before)]
     _ (..write after var)]
    (in [before after])))

(the (can_commit? tx)
  (-> Tx
      Bit)
  (list.every? (function (_ [_var _original _current])
                 (same? _original (..read! _var)))
               tx))

(the (commit_var! [_var _original _current])
  (-> (Ex (_ read write)
        (Tx_Frame read write))
      (IO Any))
  (if (same? (as Any _original) (as Any _current))
    (io [])
    (..write! _current _var)))

(the fresh_tx
  Tx
  (list))

(every (Commit of)
  [(STM of)
   (Async of)
   (Resolver of)])

(the pending_commits
  (Atom (Rec Commits
          [(Async [(Ex (_ of) (Commit of)) Commits])
           (Resolver [(Ex (_ of) (Commit of)) Commits])]))
  (atom (async.async [])))

(the commit_processor_flag
  (Atom Bit)
  (atom false))

(the (issue_commit! commit)
  (All (_ of)
    (-> (Commit of)
        (IO Any)))
  (let [entry [commit (async.async [])]]
    (do [! io.monad]
      [|commits|&resolve (atom.read! pending_commits)]
      (loop (again [[|commits| resolve] |commits|&resolve])
        (do !
          [|commits| (async.value |commits|)]
          (when |commits|
            {.#None}
            (do io.monad
              [resolved? (resolve entry)]
              (if resolved?
                (atom.write! (product.right entry) pending_commits)
                (again |commits|&resolve)))
            
            {.#Some [head tail]}
            (again tail)))))))

(the (process_commit! commit)
  (All (_ of)
    (-> (Commit of)
        (IO Any)))
  (let [[stm_proc output resolve] commit
        [finished_tx value] (stm_proc fresh_tx)]
    (if (can_commit? finished_tx)
      (do [! io.monad]
        [_ (monad.each ! ..commit_var! finished_tx)]
        (resolve value))
      (..issue_commit! commit))))

(the start_commit_processor!
  (IO Any)
  (do [! io.monad]
    [flag (atom.read! commit_processor_flag)]
    (if flag
      (in [])
      (do !
        [was_first? (atom.compare_and_swap! flag true commit_processor_flag)]
        (if was_first?
          (do !
            [[async resolve] (atom.read! pending_commits)]
            (async.upon! (function (again [head [tail _resolve]])
                           (do !
                             [_ (..process_commit! head)]
                             (async.upon! again tail)))
                         async))
          (in [])))
      )))

(the .public (commit! stm_proc)
  (All (_ of)
    (-> (STM of)
        (Async of)))
  (let [[output resolver] (async.async [])]
    (exec
      (io.run! (do io.monad
                 [_ ..start_commit_processor!]
                 (..issue_commit! [stm_proc output resolver])))
      output)))
