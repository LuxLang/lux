(.module:
  [library
   [lux (#- and or)
    [abstract
     [functor (#+ Functor)]
     [apply (#+ Apply)]
     ["." monad (#+ Monad do)]]
    [control
     [pipe (#+ case>)]
     ["." function]
     ["." io (#+ IO io)]]
    [data
     ["." product]]
    [type (#+ :sharing)
     abstract]]]
  [//
   ["." thread]
   ["." atom (#+ Atom atom)]])

(abstract: .public (Async a)
  {#.doc "Represents values produced by asynchronous computations (unlike IO, which is synchronous)."}

  (Atom [(Maybe a) (List (-> a (IO Any)))])

  (type: .public (Resolver a)
    {#.doc (example "The function used to give a value to an async."
                    "Will signal 'true' if the async has been resolved for the 1st time, 'false' otherwise.")}
    (-> a (IO Bit)))

  (def: (resolver async)
    {#.doc "Sets an async's value if it has not been done yet."}
    (All [a] (-> (Async a) (Resolver a)))
    (function (resolve value)
      (let [async (:representation async)]
        (do {! io.monad}
          [(^@ old [_value _observers]) (atom.read! async)]
          (case _value
            (#.Some _)
            (in #0)

            #.None
            (do !
              [.let [new [(#.Some value) #.None]]
               succeeded? (atom.compare_and_swap! old new async)]
              (if succeeded?
                (do !
                  [_ (monad.map ! (function (_ f) (f value))
                                _observers)]
                  (in #1))
                (resolve value))))))))

  (def: .public (resolved value)
    {#.doc (example "Produces an async that has already been resolved to the given value.")}
    (All [a] (-> a (Async a)))
    (:abstraction (atom [(#.Some value) (list)])))

  (def: .public (async _)
    {#.doc (example "Creates a fresh async that has not been resolved yet.")}
    (All [a] (-> Any [(Async a) (Resolver a)]))
    (let [async (:abstraction (atom [#.None (list)]))]
      [async (..resolver async)]))

  (def: .public poll
    {#.doc "Polls an async for its value."}
    (All [a] (-> (Async a) (IO (Maybe a))))
    (|>> :representation
         atom.read!
         (\ io.functor map product.left)))

  (def: .public (upon! f async)
    {#.doc (example "Executes the given function as soon as the async has been resolved.")}
    (All [a] (-> (-> a (IO Any)) (Async a) (IO Any)))
    (do {! io.monad}
      [.let [async (:representation async)]
       (^@ old [_value _observers]) (atom.read! async)]
      (case _value
        (#.Some value)
        (f value)

        #.None
        (let [new [_value (#.Item f _observers)]]
          (do !
            [swapped? (atom.compare_and_swap! old new async)]
            (if swapped?
              (in [])
              (upon! f (:abstraction async))))))))
  )

(def: .public resolved?
  {#.doc "Checks whether an async's value has already been resolved."}
  (All [a] (-> (Async a) (IO Bit)))
  (|>> ..poll
       (\ io.functor map
          (|>> (case> #.None
                      #0

                      (#.Some _)
                      #1)))))

(implementation: .public functor
  (Functor Async)
  
  (def: (map f fa)
    (let [[fb resolve] (..async [])]
      (exec
        (io.run! (..upon! (|>> f resolve) fa))
        fb))))

(implementation: .public apply
  (Apply Async)
  
  (def: &functor ..functor)

  (def: (apply ff fa)
    (let [[fb resolve] (..async [])]
      (exec
        (io.run! (..upon! (function (_ f)
                            (..upon! (|>> f resolve) fa))
                          ff))
        fb))))

(implementation: .public monad
  (Monad Async)
  
  (def: &functor ..functor)

  (def: in ..resolved)

  (def: (join mma)
    (let [[ma resolve] (async [])]
      (exec
        (io.run! (..upon! (..upon! resolve) mma))
        ma))))

(def: .public (and left right)
  {#.doc (example "Combines the results of both asyncs, in-order.")}
  (All [a b] (-> (Async a) (Async b) (Async [a b])))
  (let [[read! write!] (:sharing [a b]
                                 [(Async a) (Async b)]
                                 [left right]

                                 [(Async [a b])
                                  (Resolver [a b])]
                                 (..async []))
        _ (io.run! (..upon! (function (_ left)
                              (..upon! (function (_ right)
                                         (write! [left right]))
                                       right))
                            left))]
    read!))

(def: .public (or left right)
  {#.doc (example "Yields the results of whichever async gets resolved first."
                  "You can tell which one was resolved first through pattern-matching.")}
  (All [a b] (-> (Async a) (Async b) (Async (Or a b))))
  (let [[a|b resolve] (..async [])]
    (with_expansions
      [<sides> (template [<async> <tag>]
                 [(io.run! (upon! (|>> <tag> resolve) <async>))]

                 [left  #.Left]
                 [right #.Right]
                 )]
      (exec
        <sides>
        a|b))))

(def: .public (either left right)
  {#.doc (example "Yields the results of whichever async gets resolved first."
                  "You cannot tell which one was resolved first.")}
  (All [a] (-> (Async a) (Async a) (Async a)))
  (let [[left||right resolve] (..async [])]
    (`` (exec
          (~~ (template [<async>]
                [(io.run! (upon! resolve <async>))]

                [left]
                [right]))
          left||right))))

(def: .public (schedule! millis_delay computation)
  {#.doc (example "Runs an I/O computation on its own thread (after a specified delay)."
                  "Returns an async that will eventually host its result.")}
  (All [a] (-> Nat (IO a) (Async a)))
  (let [[!out resolve] (..async [])]
    (exec
      (|> (do io.monad
            [value computation]
            (resolve value))
          (thread.schedule! millis_delay)
          io.run!)
      !out)))

(def: .public future
  {#.doc (example "Runs an I/O computation on its own thread."
                  "Returns an async that will eventually host its result.")}
  (All [a] (-> (IO a) (Async a)))
  (..schedule! 0))

(def: .public (delayed time_millis value)
  {#.doc "Delivers a value after a certain period has passed."}
  (All [a] (-> Nat a (Async a)))
  (..schedule! time_millis (io value)))

(def: .public (delay time_millis)
  {#.doc "An async that will be resolved after the specified amount of milliseconds."}
  (-> Nat (Async Any))
  (..delayed time_millis []))

(def: .public (time_out time_millis async)
  {#.doc "Wait for an async to be resolved within the specified amount of milliseconds."}
  (All [a] (-> Nat (Async a) (Async (Maybe a))))
  (..or (..delay time_millis) async))
