(.module:
  [library
   [lux {"-" [and or]}
    [abstract
     [functor {"+" [Functor]}]
     [apply {"+" [Apply]}]
     ["[0]" monad {"+" [Monad do]}]]
    [control
     [pipe {"+" [case>]}]
     ["[0]" function]
     ["[0]" io {"+" [IO io]}]]
    [data
     ["[0]" product]]
    [type {"+" [:sharing]}
     abstract]]]
  [//
   ["[0]" thread]
   ["[0]" atom {"+" [Atom atom]}]])

(abstract: .public (Async a)
  (Atom [(Maybe a) (List (-> a (IO Any)))])

  (type: .public (Resolver a)
    (-> a (IO Bit)))

  ... Sets an async's value if it has not been done yet.
  (def: (resolver async)
    (All (_ a) (-> (Async a) (Resolver a)))
    (function (resolve value)
      (let [async (:representation async)]
        (do [! io.monad]
          [(^@ old [_value _observers]) (atom.read! async)]
          (case _value
            {.#Some _}
            (in #0)

            {.#None}
            (do !
              [.let [new [{.#Some value} {.#None}]]
               succeeded? (atom.compare_and_swap! old new async)]
              (if succeeded?
                (do !
                  [_ (monad.each ! (function (_ f) (f value))
                                 _observers)]
                  (in #1))
                (resolve value))))))))

  (def: .public (resolved value)
    (All (_ a) (-> a (Async a)))
    (:abstraction (atom [{.#Some value} (list)])))

  (def: .public (async _)
    (All (_ a) (-> Any [(Async a) (Resolver a)]))
    (let [async (:abstraction (atom [{.#None} (list)]))]
      [async (..resolver async)]))

  (def: .public value
    (All (_ a) (-> (Async a) (IO (Maybe a))))
    (|>> :representation
         atom.read!
         (# io.functor each product.left)))

  (def: .public (upon! f async)
    (All (_ a) (-> (-> a (IO Any)) (Async a) (IO Any)))
    (do [! io.monad]
      [.let [async (:representation async)]
       (^@ old [_value _observers]) (atom.read! async)]
      (case _value
        {.#Some value}
        (f value)

        {.#None}
        (let [new [_value {.#Item f _observers}]]
          (do !
            [swapped? (atom.compare_and_swap! old new async)]
            (if swapped?
              (in [])
              (upon! f (:abstraction async))))))))
  )

(def: .public resolved?
  (All (_ a) (-> (Async a) (IO Bit)))
  (|>> ..value
       (# io.functor each
          (|>> (case> {.#None}
                      #0

                      {.#Some _}
                      #1)))))

(implementation: .public functor
  (Functor Async)
  
  (def: (each f fa)
    (let [[fb resolve] (..async [])]
      (exec
        (io.run! (..upon! (|>> f resolve) fa))
        fb))))

(implementation: .public apply
  (Apply Async)
  
  (def: &functor ..functor)

  (def: (on fa ff)
    (let [[fb resolve] (..async [])]
      (exec
        (io.run! (..upon! (function (_ f)
                            (..upon! (|>> f resolve) fa))
                          ff))
        fb))))

(implementation: .public monad
  (Monad Async)
  
  (def: &functor ..functor)

  (def: in ..resolved)

  (def: (conjoint mma)
    (let [[ma resolve] (async [])]
      (exec
        (io.run! (..upon! (..upon! resolve) mma))
        ma))))

(def: .public (and left right)
  (All (_ a b) (-> (Async a) (Async b) (Async [a b])))
  (let [[read! write!] (:sharing [a b]
                                 [(Async a) (Async b)]
                                 [left right]

                                 [(Async [a b])
                                  (Resolver [a b])]
                                 (..async []))
        _ (io.run! (..upon! (function (_ left)
                              (..upon! (function (_ right)
                                         (write! [left right]))
                                       right))
                            left))]
    read!))

(def: .public (or left right)
  (All (_ a b) (-> (Async a) (Async b) (Async (Or a b))))
  (let [[a|b resolve] (..async [])]
    (with_expansions
      [<sides> (template [<async> <tag>]
                 [(io.run! (upon! (|>> {<tag>} resolve) <async>))]

                 [left  .#Left]
                 [right .#Right]
                 )]
      (exec
        <sides>
        a|b))))

(def: .public (either left right)
  (All (_ a) (-> (Async a) (Async a) (Async a)))
  (let [[left||right resolve] (..async [])]
    (`` (exec
          (~~ (template [<async>]
                [(io.run! (upon! resolve <async>))]

                [left]
                [right]))
          left||right))))

(def: .public (schedule! milli_seconds computation)
  (All (_ a) (-> Nat (IO a) (Async a)))
  (let [[!out resolve] (..async [])]
    (exec
      (|> (do io.monad
            [value computation]
            (resolve value))
          (thread.schedule! milli_seconds)
          io.run!)
      !out)))

(def: .public future
  (All (_ a) (-> (IO a) (Async a)))
  (..schedule! 0))

(def: .public (after milli_seconds value)
  (All (_ a) (-> Nat a (Async a)))
  (..schedule! milli_seconds (io value)))

(def: .public (delay milli_seconds)
  (-> Nat (Async Any))
  (..after milli_seconds []))

(def: .public (within milli_seconds async)
  (All (_ a) (-> Nat (Async a) (Async (Maybe a))))
  (..or (..delay milli_seconds)
        async))
