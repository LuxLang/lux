(.module:
  [library
   [lux #*
    [abstract
     [equivalence (#+ Equivalence)]
     [functor (#+ Functor)]
     [apply (#+ Apply)]
     ["." monad (#+ Monad do)]]
    [control
     ["." try (#+ Try)]
     ["." exception (#+ exception:)]
     ["." io (#+ IO io)]]
    [data
     ["." maybe ("#\." functor)]]
    [type (#+ :sharing)
     abstract]]]
  [//
   ["." atom (#+ Atom)]
   ["." async (#+ Async) ("#\." functor)]])

(type: #export (Channel a)
  {#.doc "An asynchronous channel to distribute values."}
  (Async (Maybe [a (Channel a)])))

(exception: #export channel_is_already_closed)

(interface: #export (Sink a)
  {#.doc (doc "The tail-end of a channel, which can be written-to to fee the channel.")}
  (: (IO (Try Any))
     close)
  (: (-> a (IO (Try Any)))
     feed))

(def: (sink resolve)
  (All [a]
    (-> (async.Resolver (Maybe [a (Channel a)]))
        (Sink a)))
  (let [sink (atom.atom resolve)]
    (implementation
     (def: close
       (loop [_ []]
         (do {! io.monad}
           [current (atom.read sink)
            stopped? (current #.None)]
           (if stopped?
             ## I closed the sink.
             (wrap (exception.return []))
             ## Someone else interacted with the sink.
             (do !
               [latter (atom.read sink)]
               (if (is? current latter)
                 ## Someone else closed the sink.
                 (wrap (exception.throw ..channel_is_already_closed []))
                 ## Someone else fed the sink while I was closing it.
                 (recur [])))))))
     
     (def: (feed value)
       (loop [_ []]
         (do {! io.monad}
           [current (atom.read sink)
            #let [[next resolve_next] (:sharing [a]
                                                (async.Resolver (Maybe [a (Channel a)]))
                                                current
                                                
                                                [(Async (Maybe [a (Channel a)]))
                                                 (async.Resolver (Maybe [a (Channel a)]))]
                                                (async.async []))]
            fed? (current (#.Some [value next]))]
           (if fed?
             ## I fed the sink.
             (do !
               [_ (atom.compare_and_swap current resolve_next sink)]
               (wrap (exception.return [])))
             ## Someone else interacted with the sink.
             (do !
               [latter (atom.read sink)]
               (if (is? current latter)
                 ## Someone else closed the sink while I was feeding it.
                 (wrap (exception.throw ..channel_is_already_closed []))
                 ## Someone else fed the sink.
                 (recur []))))))))))

(def: #export (channel _)
  {#.doc (doc "Creates a brand-new channel and hands it over, along with the sink to write to it.")}
  (All [a] (-> Any [(Channel a) (Sink a)]))
  (let [[async resolve] (async.async [])]
    [async (..sink resolve)]))

(implementation: #export functor
  (Functor Channel)
  
  (def: (map f)
    (async\map
     (maybe\map
      (function (_ [head tail])
        [(f head) (map f tail)])))))

(implementation: #export apply
  (Apply Channel)
  
  (def: &functor ..functor)

  (def: (apply ff fa)
    (do async.monad
      [cons_f ff
       cons_a fa]
      (case [cons_f cons_a]
        [(#.Some [head_f tail_f]) (#.Some [head_a tail_a])]
        (wrap (#.Some [(head_f head_a) (apply tail_f tail_a)]))

        _
        (wrap #.None)))))

(def: empty
  Channel
  (async.resolved #.None))

(implementation: #export monad
  (Monad Channel)
  
  (def: &functor ..functor)

  (def: (wrap a)
    (async.resolved (#.Some [a ..empty])))

  (def: (join mma)
    (let [[output sink] (channel [])]
      (exec (: (Async Any)
               (loop [mma mma]
                 (do {! async.monad}
                   [?mma mma]
                   (case ?mma
                     (#.Some [ma mma'])
                     (do !
                       [_ (loop [ma ma]
                            (do !
                              [?ma ma]
                              (case ?ma
                                (#.Some [a ma'])
                                (exec (io.run (\ sink feed a))
                                  (recur ma'))
                                
                                #.None
                                (wrap []))))]
                       (recur mma'))
                     
                     #.None
                     (wrap (: Any (io.run (\ sink close))))))))
        output))))

(type: #export (Subscriber a)
  {#.doc (doc "A function that can receive every value fed into a channel.")}
  (-> a (IO (Maybe Any))))

(def: #export (subscribe subscriber channel)
  (All [a] (-> (Subscriber a) (Channel a) (IO Any)))
  (io (exec (: (Async Any)
               (loop [channel channel]
                 (do async.monad
                   [cons channel]
                   (case cons
                     (#.Some [head tail])
                     (case (io.run (subscriber head))
                       (#.Some _)
                       (recur tail)
                       
                       #.None
                       (wrap []))
                     
                     #.None
                     (wrap [])))))
        [])))

(def: #export (only pass? channel)
  {#.doc (doc "Produces a new channel based on the old one, only with values"
              "that pass the test.")}
  (All [a] (-> (-> a Bit) (Channel a) (Channel a)))
  (do async.monad
    [cons channel]
    (case cons
      (#.Some [head tail])
      (let [tail' (only pass? tail)]
        (if (pass? head)
          (wrap (#.Some [head tail']))
          tail'))
      
      #.None
      (wrap #.None))))

(def: #export (of_async async)
  {#.doc (doc "A one-element channel containing the output from an async.")}
  (All [a] (-> (Async a) (Channel a)))
  (async\map (function (_ value)
               (#.Some [value ..empty]))
             async))

(def: #export (fold f init channel)
  {#.doc "Asynchronous fold over channels."}
  (All [a b]
    (-> (-> b a (Async a)) a (Channel b)
        (Async a)))
  (do {! async.monad}
    [cons channel]
    (case cons
      #.None
      (wrap init)
      
      (#.Some [head tail])
      (do !
        [init' (f head init)]
        (fold f init' tail)))))

(def: #export (folds f init channel)
  (All [a b]
    (-> (-> b a (Async a)) a (Channel b)
        (Channel a)))
  (do {! async.monad}
    [cons channel]
    (case cons
      #.None
      (wrap (#.Some [init (wrap #.None)]))
      
      (#.Some [head tail])
      (do !
        [init' (f head init)]
        (wrap (#.Some [init (folds f init' tail)]))))))

(def: #export (poll milli_seconds action)
  (All [a]
    (-> Nat (IO a) [(Channel a) (Sink a)]))
  (let [[output sink] (channel [])]
    (exec (io.run (loop [_ []]
                    (do io.monad
                      [value action
                       _ (\ sink feed value)]
                      (async.await recur (async.wait milli_seconds)))))
      [output sink])))

(def: #export (periodic milli_seconds)
  (-> Nat [(Channel Any) (Sink Any)])
  (..poll milli_seconds (io [])))

(def: #export (iterate f init)
  (All [s o] (-> (-> s (Async (Maybe [s o]))) s (Channel o)))
  (do async.monad
    [?next (f init)]
    (case ?next
      (#.Some [state output])
      (wrap (#.Some [output (iterate f state)]))
      
      #.None
      (wrap #.None))))

(def: (distinct' equivalence previous channel)
  (All [a] (-> (Equivalence a) a (Channel a) (Channel a)))
  (do async.monad
    [cons channel]
    (case cons
      (#.Some [head tail])
      (if (\ equivalence = previous head)
        (distinct' equivalence previous tail)
        (wrap (#.Some [head (distinct' equivalence head tail)])))
      
      #.None
      (wrap #.None))))

(def: #export (distinct equivalence channel)
  (All [a] (-> (Equivalence a) (Channel a) (Channel a)))
  (do async.monad
    [cons channel]
    (case cons
      (#.Some [head tail])
      (wrap (#.Some [head (distinct' equivalence head tail)]))
      
      #.None
      (wrap #.None))))

(def: #export (consume channel)
  (All [a] (-> (Channel a) (Async (List a))))
  (do {! async.monad}
    [cons channel]
    (case cons
      (#.Some [head tail])
      (\ ! map (|>> (#.Cons head))
         (consume tail))

      #.None
      (wrap #.Nil))))

(def: #export (sequential milli_seconds values)
  {#.doc (doc "Transforms the given list into a channel with the same elements.")}
  (All [a] (-> Nat (List a) (Channel a)))
  (case values
    #.Nil
    ..empty

    (#.Cons head tail)
    (async.resolved (#.Some [head (do async.monad
                                    [_ (async.wait milli_seconds)]
                                    (sequential milli_seconds tail))]))))
