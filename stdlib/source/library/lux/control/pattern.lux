... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... ["Type-safe pattern combinators" by Morten Rhiger](https://core.ac.uk/works/9613163)
(.require
 [library
  [lux (.except Pattern
                when)
   [control
    [pure (.only Pure)]
    [function
     [predicate (.only Predicate)]]]
   [meta
    ["[0]" type]]]]
 [/
  ["[0]" input]
  ["[0]" match]
  ["[0]" pattern]
  ["[0]" clause (.only Clause Pattern)]])

(the .public (when input clause)
  (All (_ input value)
    (-> input (Clause input value)
        value))
  (clause input
          (function (_ _)
            (undefined))))

(the .public (match? pattern input)
  (All (_ of)
    (-> (Pattern of Bit Bit)
        (Predicate of)))
  (<| (..when input)
      (all clause.or
           (clause.clause pattern true)
           (clause.clause pattern.any false))))

(the .public (abstraction pattern body)
  (All (_ term input value)
    (-> (Pattern input term value) term
        (-> input value)))
  (function (_ input)
    (..when input (clause.clause pattern body))))
