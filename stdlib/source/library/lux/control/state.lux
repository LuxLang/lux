... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except local with as)
   [abstract
    [functor (.only Functor)]
    ["[0]" monad (.only Monad)]]]])

(every .public (State state it)
  (-> state
      [state it]))

(the .public state
  (for_any (_ state)
    (State state state))
  (function (_ state)
    [state state]))

(the .public (put new_state)
  (for_any (_ state)
    (-> state
        (State state Any)))
  (function (_ state)
    [new_state []]))

(the .public (update change)
  (for_any (_ state)
    (-> (Change state)
        (State state Any)))
  (function (_ state)
    [(change state) []]))

(the .public (as user)
  (for_any (_ state it)
    (-> (-> state it)
        (State state it)))
  (function (_ state)
    [state (user state)]))

(the .public (local change it)
  (for_any (_ state it)
    (-> (Change state)
        (Change (State state it))))
  (function (_ state)
    (let [[state' output] (it (change state))]
      [state output])))

(the .public (value state it)
  (for_any (_ state it)
    (-> state (State state it)
        [state it]))
  (it state))

(the .public functor
  (for_any (_ state)
    (Functor (State state)))
  (implementation
   (the (each f ma)
     (function (_ state)
       (let [[state' a] (ma state)]
         [state' (f a)])))))

(the .public monad
  (for_any (_ state)
    (Monad (State state)))
  (implementation
   (the functor ..functor)

   (the (pure it)
     (function (_ state)
       [state it]))

   (the (conjoint mma)
     (function (_ state)
       (let [[state' ma] (mma state)]
         (ma state'))))))

(the .public (while condition body)
  (for_any (_ state)
    (-> (State state Bit)
        (Change (State state Any))))
  (monad.let [! ..monad]
    [execute? condition]
    (if execute?
        (monad.let !
          [_ body]
          (while condition body))
        (pure []))))

(the .public (do_while condition body)
  (for_any (_ state)
    (-> (State state Bit)
        (Change (State state Any))))
  (monad.let ..monad
    [_ body]
    (while condition body)))

(every .public (With state ! it)
  (-> state
      (! [state it])))

(the (with//functor functor)
  (for_any (_ ! state)
    (-> (Functor !)
        (Functor (With state !))))
  (implementation
   (the (each f sfa)
     (function (_ state)
       (by functor each (function (_ [s a]) [s (f a)])
           (sfa state))))))

(the .public (value' state it)
  (for_any (_ ! state it)
    (-> state (With state ! it)
        (! [state it])))
  (it state))

(the .public (with monad)
  (for_any (_ ! state)
    (-> (Monad !)
        (Monad (With state !))))
  (implementation
   (the functor (with//functor (by monad functor)))

   (the (pure a)
     (function (_ state)
       (by monad pure [state a])))
   
   (the (conjoint sMsMa)
     (function (_ state)
       (monad.let monad
         [[state' sMa] (sMsMa state)]
         (sMa state'))))))

(the .public (lifted monad ma)
  (for_any (_ ! state it)
    (-> (Monad !) (! it)
        (With state ! it)))
  (function (_ state)
    (monad.let monad
      [a ma]
      (pure [state a]))))
