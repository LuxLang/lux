... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except local with use)
   [abstract
    [functor (.only Functor)]
    [apply (.only Apply)]
    [monad (.only Monad do)]]]])

(every .public (State state of)
  (-> state
      [state of]))

(the .public get
  (for_any (_ state)
    (State state state))
  (function (_ state)
    [state state]))

(the .public (put new_state)
  (for_any (_ state)
    (-> state
        (State state Any)))
  (function (_ state)
    [new_state []]))

(the .public (update change)
  (for_any (_ state)
    (-> (-> state state)
        (State state Any)))
  (function (_ state)
    [(change state) []]))

(the .public (use user)
  (for_any (_ state of)
    (-> (-> state of)
        (State state of)))
  (function (_ state)
    [state (user state)]))

(the .public (local change action)
  (for_any (_ state of)
    (-> (-> state state) (State state of)
        (State state of)))
  (function (_ state)
    (let [[state' output] (action (change state))]
      [state output])))

(the .public (value state action)
  (for_any (_ state of)
    (-> state (State state of)
        [state of]))
  (action state))

(the .public functor
  (for_any (_ state)
    (Functor (State state)))
  (implementation
   (the (each f ma)
     (function (_ state)
       (let [[state' a] (ma state)]
         [state' (f a)])))))

(the .public apply
  (for_any (_ state)
    (Apply (State state)))
  (implementation
   (the functor ..functor)

   (the (on fa ff)
     (function (_ state)
       (let [[state' f] (ff state)
             [state'' a] (fa state')]
         [state'' (f a)])))))

(the .public monad
  (for_any (_ state)
    (Monad (State state)))
  (implementation
   (the functor ..functor)

   (the (in a)
     (function (_ state)
       [state a]))

   (the (conjoint mma)
     (function (_ state)
       (let [[state' ma] (mma state)]
         (ma state'))))))

(the .public (while condition body)
  (for_any (_ state)
    (-> (State state Bit) (State state Any)
        (State state Any)))
  (do [! ..monad]
    [execute? condition]
    (if execute?
      (do !
        [_ body]
        (while condition body))
      (in []))))

(the .public (do_while condition body)
  (for_any (_ state)
    (-> (State state Bit) (State state Any)
        (State state Any)))
  (do ..monad
    [_ body]
    (while condition body)))

(every .public (With state ! of)
  (-> state
      (! [state of])))

(the (with//functor functor)
  (for_any (_ ! state)
    (-> (Functor !)
        (Functor (With state !))))
  (implementation
   (the (each f sfa)
     (function (_ state)
       (of functor each (function (_ [s a]) [s (f a)])
           (sfa state))))))

(the (with//apply monad)
  (for_any (_ ! state)
    (-> (Monad !)
        (Apply (With state !))))
  (implementation
   (the functor (with//functor (of monad functor)))

   (the (on sFa sFf)
     (function (_ state)
       (do monad
         [[state f] (sFf state)
          [state a] (sFa state)]
         (in [state (f a)]))))))

(the .public (value' state action)
  (for_any (_ ! state of)
    (-> state (With state ! of)
        (! [state of])))
  (action state))

(the .public (with monad)
  (for_any (_ ! state)
    (-> (Monad !)
        (Monad (With state !))))
  (implementation
   (the functor (with//functor (of monad functor)))

   (the (in a)
     (function (_ state)
       (of monad in [state a])))
   
   (the (conjoint sMsMa)
     (function (_ state)
       (do monad
         [[state' sMa] (sMsMa state)]
         (sMa state'))))))

(the .public (lifted monad ma)
  (for_any (_ ! state of)
    (-> (Monad !) (! of)
        (With state ! of)))
  (function (_ state)
    (do monad
      [a ma]
      (in [state a]))))
