... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except local with use)
   [abstract
    [functor (.only Functor)]
    [apply (.only Apply)]
    [monad (.only Monad do)]]]])

(every .public (State s a)
  (-> s [s a]))

(the .public get
  (All (_ s) (State s s))
  (function (_ state)
    [state state]))

(the .public (put new_state)
  (All (_ s) (-> s (State s Any)))
  (function (_ state)
    [new_state []]))

(the .public (update change)
  (All (_ s) (-> (-> s s) (State s Any)))
  (function (_ state)
    [(change state) []]))

(the .public (use user)
  (All (_ s a) (-> (-> s a) (State s a)))
  (function (_ state)
    [state (user state)]))

(the .public (local change action)
  (All (_ s a) (-> (-> s s) (State s a) (State s a)))
  (function (_ state)
    (let [[state' output] (action (change state))]
      [state output])))

(the .public (value state action)
  (All (_ s a) (-> s (State s a) [s a]))
  (action state))

(the .public functor
  (All (_ s) (Functor (State s)))
  (implementation
   (the (each f ma)
     (function (_ state)
       (let [[state' a] (ma state)]
         [state' (f a)])))))

(the .public apply
  (All (_ s) (Apply (State s)))
  (implementation
   (the functor ..functor)

   (the (on fa ff)
     (function (_ state)
       (let [[state' f] (ff state)
             [state'' a] (fa state')]
         [state'' (f a)])))))

(the .public monad
  (All (_ s) (Monad (State s)))
  (implementation
   (the functor ..functor)

   (the (in a)
     (function (_ state)
       [state a]))

   (the (conjoint mma)
     (function (_ state)
       (let [[state' ma] (mma state)]
         (ma state'))))))

(the .public (while condition body)
  (All (_ s) (-> (State s Bit) (State s Any) (State s Any)))
  (do [! ..monad]
    [execute? condition]
    (if execute?
      (do !
        [_ body]
        (while condition body))
      (in []))))

(the .public (do_while condition body)
  (All (_ s) (-> (State s Bit) (State s Any) (State s Any)))
  (do ..monad
    [_ body]
    (while condition body)))

(the (with//functor functor)
  (All (_ M s) (-> (Functor M) (Functor (All (_ a) (-> s (M [s a]))))))
  (implementation
   (the (each f sfa)
     (function (_ state)
       (of functor each (function (_ [s a]) [s (f a)])
           (sfa state))))))

(the (with//apply monad)
  (All (_ M s) (-> (Monad M) (Apply (All (_ a) (-> s (M [s a]))))))
  (implementation
   (the functor (with//functor (of monad functor)))

   (the (on sFa sFf)
     (function (_ state)
       (do monad
         [[state f] (sFf state)
          [state a] (sFa state)]
         (in [state (f a)]))))))

(every .public (+State M s a)
  (-> s (M [s a])))

(the .public (value' state action)
  (All (_ M s a) (-> s (+State M s a) (M [s a])))
  (action state))

(the .public (with monad)
  (All (_ M s) (-> (Monad M) (Monad (+State M s))))
  (implementation
   (the functor (with//functor (of monad functor)))

   (the (in a)
     (function (_ state)
       (of monad in [state a])))
   
   (the (conjoint sMsMa)
     (function (_ state)
       (do monad
         [[state' sMa] (sMsMa state)]
         (sMa state'))))))

(the .public (lifted monad ma)
  (All (_ M s a) (-> (Monad M) (M a) (+State M s a)))
  (function (_ state)
    (do monad
      [a ma]
      (in [state a]))))
