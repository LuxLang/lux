(.module:
  [library
   [lux #*
    [abstract
     [functor (#+ Functor)]
     [apply (#+ Apply)]
     [monad (#+ Monad do)]]
    [control
     ["." function]
     [parser
      ["s" code]]]
    [macro (#+ with_identifiers)
     [syntax (#+ syntax:)]
     ["." code]]]])

(type: .public (Cont i o)
  {#.doc "Continuations."}
  (-> (-> i o) o))

(def: .public (continue next cont)
  {#.doc "Continues a continuation thunk."}
  (All [i o] (-> (-> i o) (Cont i o) o))
  (cont next))

(def: .public (result cont)
  {#.doc "Forces a continuation thunk to be evaluated."}
  (All [a] (-> (Cont a a) a))
  (cont function.identity))

(def: .public (call/cc f)
  {#.doc "Call with current continuation."}
  (All [a b z]
    (-> (-> (-> a (Cont b z))
            (Cont a z))
        (Cont a z)))
  (function (_ k)
    (f (function (_ a) (function (_ _) (k a)))
       k)))

(syntax: .public (pending expr)
  {#.doc (example "Turns any expression into a function that is pending a continuation."
                  (pending (some_function some_input)))}
  (with_identifiers [g!_ g!k]
    (in (list (` (.function ((~ g!_) (~ g!k)) ((~ g!k) (~ expr))))))))

(def: .public (reset scope)
  (All [i o] (-> (Cont i i) (Cont i o)))
  (function (_ k)
    (k (result scope))))

(def: .public (shift f)
  (All [a]
    (-> (-> (-> a (Cont a a))
            (Cont a a))
        (Cont a a)))
  (function (_ oc)
    (f (function (_ a) (function (_ ic) (ic (oc a))))
       function.identity)))

(implementation: .public functor
  (All [o] (Functor (All [i] (Cont i o))))
  
  (def: (map f fv)
    (function (_ k) (fv (function.compose k f)))))

(implementation: .public apply
  (All [o] (Apply (All [i] (Cont i o))))
  
  (def: &functor ..functor)

  (def: (apply ff fv)
    (function (_ k)
      (|> (k (f v))
          (function (_ v)) fv
          (function (_ f)) ff))))

(implementation: .public monad
  (All [o] (Monad (All [i] (Cont i o))))
  
  (def: &functor ..functor)

  (def: (in value)
    (function (_ k) (k value)))

  (def: (join ffa)
    (function (_ k)
      (ffa (continue k)))))

(def: .public (portal init)
  (All [i o z]
    (-> i
        (Cont [(-> i (Cont o z))
               i]
              z)))
  (call/cc (function (_ k)
             (do ..monad
               [.let [nexus (function (nexus val)
                              (k [nexus val]))]
                _ (k [nexus init])]
               (in (undefined))))))
