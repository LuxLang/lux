... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [functor (.only Functor)]
    [monad (.only Monad do)]]
   [control
    ["[0]" function]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro (.only with_symbols)
     ["[0]" syntax]
     ["[0]" template]]]]])

(every .public (Context of answer)
  (-> of
      answer))

(the .public empty
  (for_any (_ of)
    (Context of of))
  function.identity)

(every .public (Cont of)
  (for_any (_ answer)
    (-> (Context of answer)
        answer)))

(the .public (continued next cont)
  (for_any (_ of answer)
    (-> (Context of answer) (Cont of answer)
        answer))
  (cont next))

(the .public value
  (for_any (_ of)
    (-> (Cont of)
        of))
  (..continued ..empty))

(the Implementation
  (template.macro (_ <interface>)
    [(for_any (_ answer)
       (<interface> (for_any (_ of)
                      (Cont of answer))))]))

(the .public functor
  (Implementation Functor)
  (implementation
   (the (each $ it)
     (function (_ after)
       (it (function.composite after $))))))

(the .public monad
  (Implementation Monad)
  (implementation
   (the functor ..functor)

   (the (in value)
     (function (_ after)
       (after value)))

   (the (conjoint ffa)
     (function (_ after)
       (ffa (continued after))))))

(the .public (call/cc context)
  (for_any (_ local_return global_return final_return)
    (-> (-> (-> local_return (Cont global_return final_return))
            (Cont local_return final_return))
        (Cont local_return final_return)))
  (function (_ after_pause)
    (context (function (_ value)
               (function (_ after_resumption)
                 (after_pause value)))
             after_pause)))

(alias [with_current]
       ..call/cc)

(the .public pending
  (syntax.macro (_ [expr <code>.any])
    (with_symbols ['_ 'after]
      (in (list (` (.function ((, '_) (, 'after))
                     ((, 'after) (, expr)))))))))
