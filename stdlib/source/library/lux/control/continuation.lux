... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [functor (.only Functor)]
    [apply (.only Apply)]
    [monad (.only Monad do)]]
   [control
    ["[0]" function]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\parser]]
    [macro (.only with_symbols)
     [syntax (.only syntax)]]]]])

(every .public (Cont i o)
  (-> (-> i o) o))

(the .public (continued next cont)
  (All (_ i o) (-> (-> i o) (Cont i o) o))
  (cont next))

(the .public value
  (All (_ a) (-> (Cont a a) a))
  (..continued function.identity))

(the .public (with_current f)
  (All (_ a b z)
    (-> (-> (-> a (Cont b z))
            (Cont a z))
        (Cont a z)))
  (function (_ k)
    (f (function (_ a) (function (_ _) (k a)))
       k)))

(the .public pending
  (syntax (_ [expr <code>.any])
    (with_symbols [g!_ g!k]
      (in (list (` (.function ((, g!_) (, g!k)) ((, g!k) (, expr)))))))))

(the .public (reset scope)
  (All (_ i o) (-> (Cont i i) (Cont i o)))
  (function (_ k)
    (k (value scope))))

(the .public (shift f)
  (All (_ a)
    (-> (-> (-> a (Cont a a))
            (Cont a a))
        (Cont a a)))
  (function (_ oc)
    (f (function (_ a) (function (_ ic) (ic (oc a))))
       function.identity)))

(the .public functor
  (All (_ o) (Functor (All (_ i) (Cont i o))))
  (implementation
   (the (each f fv)
     (function (_ k)
       (fv (function.composite k f))))))

(the .public apply
  (All (_ o) (Apply (All (_ i) (Cont i o))))
  (implementation
   (the functor ..functor)

   (the (on fv ff)
     (function (_ k)
       (|> (k (f v))
           (function (_ v)) fv
           (function (_ f)) ff)))))

(the .public monad
  (All (_ o) (Monad (All (_ i) (Cont i o))))
  (implementation
   (the functor ..functor)

   (the (in value)
     (function (_ k) (k value)))

   (the (conjoint ffa)
     (function (_ k)
       (ffa (continued k))))))

(the .public (portal init)
  (All (_ i o z)
    (-> i
        (Cont [(-> i (Cont o z))
               i]
              z)))
  (with_current
    (function (_ k)
      (do ..monad
        [.let [nexus (function (nexus val)
                       (k [nexus val]))]
         _ (k [nexus init])]
        (in (undefined))))))
