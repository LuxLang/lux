... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [control
    [error (.only error)]
    ["[0]" try (.only Try)]
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" product]]]]
 ["[0]" //])

... https://en.wikipedia.org/wiki/Algorithm
(every .public (Algorithm ! of)
  (-> (Predicate of) (! of)
      (Try of)))

(the .public none
  (..error "No one value to be found."))

(the .public (constant ? _)
  (for_any (_ it)
    (Algorithm (//.Constant it)))
  {try.#Failure ..none})

(the .public (variable ? it)
  (Algorithm //.Variable)
  (if (? it)
    {try.#Success it}
    {try.#Failure ..none}))

(the .public (sum left right
                  ? it)
  (for_any (_ left right)
    (-> (Algorithm left) (Algorithm right)
        (Algorithm (//.Sum left right))))
  (when it
    {#0 it} (left ? it)
    {#1 it} (right ? it)))

(the .public (product left right
                      ? it)
  (for_any (_ left right)
    (-> (Algorithm left) (Algorithm right)
        (Algorithm (//.Product left right))))
  (try.or (left ? (product.left it))
          (right ? (product.right it))))

(the .public (recursive it)
  (for_any (_ of)
    (-> (-> (Algorithm of)
            (Algorithm of))
        (Algorithm of)))
  (function (recursion value)
    (it recursion value)))
