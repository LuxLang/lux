... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except left right)
   [control
    ["[0]" function]]
   [math
    [number
     ["n" nat]]]]])

(every .public (Size ! of)
  (-> (! of)
      Nat))

(every .public (Constant it)
  (All (_ of)
    it))

(the .public constant
  (All (_ it)
    (Size (Constant it)))
  (function.constant 0))

(every .public Variable
  (All (_ of)
    of))

(the .public variable
  (Size Variable)
  (function.constant 1))

(every .public (Sum left right)
  (All (_ of)
    (Or (left of)
        (right of))))

(the .public (sum left right)
  (All (_ left right)
    (-> (Size left) (Size right)
        (Size (Sum left right))))
  (function (_ it)
    (when it
      {#0 it} (left it)
      {#1 it} (right it))))

(every .public (Product left right)
  (All (_ of)
    (And (left of)
         (right of))))

(the .public (product left right)
  (All (_ left right)
    (-> (Size left) (Size right)
        (Size (Product left right))))
  (function (_ [itL itR])
    (n.+ (left itL)
         (right itR))))

(every .public (Recursive it)
  (All (self of)
    (it self of)))

(the .public (recursive size)
  (All (_ of)
    (-> (-> (Size of)
            (Size of))
        (Size of)))
  (function (recursion value)
    (size recursion value)))
