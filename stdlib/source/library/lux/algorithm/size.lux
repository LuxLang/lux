... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except left right)
   [control
    ["[0]" function]]
   [math
    [number
     ["n" nat]]]]]
 ["[0]" //])

(every .public (Size ! of)
  (-> (! of)
      Nat))

(the .public constant
  (All (_ it)
    (Size (//.Constant it)))
  (function.constant 0))

(the .public variable
  (Size //.Variable)
  (function.constant 1))

(the .public (sum left right)
  (All (_ left right)
    (-> (Size left) (Size right)
        (Size (//.Sum left right))))
  (function (_ it)
    (when it
      {#0 it} (left it)
      {#1 it} (right it))))

(the .public (product left right)
  (All (_ left right)
    (-> (Size left) (Size right)
        (Size (//.Product left right))))
  (function (_ [itL itR])
    (n.+ (left itL)
         (right itR))))

(the .public (recursive size)
  (All (_ of)
    (-> (-> (Size of)
            (Size of))
        (Size of)))
  (function (recursion value)
    (size recursion value)))
