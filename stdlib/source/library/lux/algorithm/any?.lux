... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [control
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" product]]]]
 ["[0]" //])

(every .public (It ! of)
  (-> (Predicate of)
      (Predicate (! of))))

(the .public (constant ? _)
  (for_any (_ it)
    (It (//.Constant it)))
  false)

(the .public (variable ? it)
  (It //.Variable)
  (? it))

(the .public (sum left right
                  ? it)
  (for_any (_ left right)
    (-> (It left) (It right)
        (It (//.Sum left right))))
  (when it
    {#0 it} (left ? it)
    {#1 it} (right ? it)))

(the .public (product left right
                      ? it)
  (for_any (_ left right)
    (-> (It left) (It right)
        (It (//.Product left right))))
  (or (left ? (product.left it))
      (right ? (product.right it))))

(the .public (recursive it)
  (for_any (_ of)
    (-> (-> (It of)
            (It of))
        (It of)))
  (function (recursion value)
    (it recursion value)))
