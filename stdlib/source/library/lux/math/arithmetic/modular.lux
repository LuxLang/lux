... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [order (.only Order)]
    [monoid (.only Monoid)]
    [codec (.only Codec)]
    [monad (.only do)]]
   [control
    ["<>" projection]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" product]
    ["[0]" text (.use "[1]#[0]" monoid)
     ["<[1]>" \\projection (.only Projection)]]]
   [math
    [number
     ["i" int (.use "[1]#[0]" decimal)]]]
   [meta
    ["[0]" location]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [type
     ["[0]" nominal]]]]]
 [///
  ["/" modulus (.only Modulus)]])

(nominal.every .public (Mod m)
  (Record
   [#modulus (Modulus m)
    #value Int])

  (the .public (modular modulus value)
    (All (_ %)
      (-> (Modulus %) Int
          (Mod %)))
    (nominal.abstraction
     [#modulus modulus
      #value (i.mod (/.divisor modulus) value)]))

  (with_template [<name> <type> <side>]
    [(the .public <name>
       (All (_ %)
         (-> (Mod %)
             <type>))
       (|>> nominal.representation
            <side>))]

    [modulus (Modulus %) product.left]
    [value Int product.right]
    )
  
  (exception.the .public (incorrect_modulus [modulus parsed])
    (All (_ %)
      (Exception [(Modulus %) Int]))
    (exception.report
     (list ["Expected" (i#injection (/.divisor modulus))]
           ["Actual" (i#injection parsed)])))

  (the separator
    " mod ")

  (the intL
    (Projection Int)
    (<>.codec i.decimal
              (<text>.and (<text>.one_of "-+") (<text>.many <text>.decimal))))

  (the .public (codec expected)
    (All (_ %)
      (-> (Modulus %)
          (Codec Text (Mod %))))
    (implementation
     (the (injection modular)
       (let [[_ value] (nominal.representation modular)]
         (all text#composite
              (i#injection value)
              ..separator
              (i#injection (/.divisor expected)))))

     (the projection
       (<text>.value
        (do <>.monad
          [[value _ actual] (all <>.and intL (<text>.this ..separator) intL)
           _ (<>.assertion (exception.error ..incorrect_modulus [expected actual])
                           (i.= (/.divisor expected) actual))]
          (in (..modular expected value)))))))

  (with_template [<name> <op>]
    [(the .public (<name> expected actual)
       (All (_ %)
         (-> (Mod %) (Mod %)
             Bit))
       (let [[_ expected] (nominal.representation expected)
             [_ actual] (nominal.representation actual)]
         (<op> expected actual)))]

    [= i.=]
    [< i.<]
    [<= i.<=]
    [> i.>]
    [>= i.>=]
    )

  (the .public equivalence
    (All (_ %)
      (Equivalence (Mod %)))
    (implementation
     (the = ..=)))

  (the .public order
    (All (_ %)
      (Order (Mod %)))
    (implementation
     (the equivalence ..equivalence)
     (the < ..<)))

  (with_template [<name> <op>]
    [(the .public (<name> param subject)
       (All (_ %)
         (-> (Mod %) (Mod %)
             (Mod %)))
       (let [[modulus param] (nominal.representation param)
             [_ subject] (nominal.representation subject)]
         (nominal.abstraction
          [#modulus modulus
           #value (|> subject
                      (<op> param)
                      (i.mod (/.divisor modulus)))])))]

    [+ i.+]
    [- i.-]
    [* i.*]
    )

  (with_template [<composition> <identity> <monoid>]
    [(the .public (<monoid> modulus)
       (All (_ %)
         (-> (Modulus %)
             (Monoid (Mod %))))
       (implementation
        (the identity
          (..modular modulus <identity>))
        (the composite
          <composition>)))]
    
    [..+ +0 addition]
    [..* +1 multiplication]
    )

  (exception.the .public (lacks_inverse it)
    (All (_ %)
      (Exception (Mod %)))
    (exception.report
     (list ["Number" (of (..codec (..modulus it)) injection it)])))
  
  (the .public (inverse modular)
    (All (_ %)
      (-> (Mod %)
          (Maybe (Mod %))))
    (let [[modulus value] (nominal.representation modular)
          [[vk mk] gcd] (i.extended_gcd value (/.divisor modulus))]
      (when gcd
        +1 {try.#Success (..modular modulus vk)}
        _ (exception.except ..lacks_inverse [modular]))))
  )

(exception.the .public (moduli_are_not_equal [expected actual])
  (All (_ r% s%)
    (Exception [(Modulus r%) (Modulus s%)]))
  (exception.report
   (list ["Expected" (i#injection (/.divisor expected))]
         ["Actual" (i#injection (/.divisor actual))])))

(the .public (adapter expected actual)
  (All (_ r% s%)
    (-> (Modulus r%) (Modulus s%)
        (Try (-> (Mod s%)
                 (Mod r%)))))
  (if (/.= expected actual)
    {try.#Success (|>> ..value
                       (..modular expected))}
    (exception.except ..moduli_are_not_equal [expected actual])))
