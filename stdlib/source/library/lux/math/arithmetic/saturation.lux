... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" order (.only Order)]]
   [meta
    [macro
     ["[0]" template]]]]]
 [// (.only Arithmetic)])

(the .public (arithmetic order [left right] it)
  (for_any (_ of)
    (-> (Order of) [of of] (Arithmetic of)
        (Arithmetic of)))
  (let [min (order.min order left right)
        max (order.max order left right)]
    (`` (implementation
         (,, (template.with [<method>]
               [(the (<method> left right)
                  (|> (of it <method> left right)
                      (order.max order min)
                      (order.min order max)))]

               [+]
               [-]
               [*]
               [/]
               ))))))
