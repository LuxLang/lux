... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [math
    [number
     ["d" dec]]]
   [meta
    [macro
     ["[0]" template]]]]])

(every .public Angle
  Dec)

(template.with [<name> <value>]
  [(the .public <name>
     Angle
     <value>)]

  ... ["Ï€ is wrong!" by Bob Palais](https://www.math.utah.edu/~palais/pi.html)
  [pi  +3.14159265358979323846]
  ... [The Tau Manifesto](https://tauday.com/tau-manifesto)
  [tau +6.28318530717958647692]
  )

... https://en.wikipedia.org/wiki/Turn_(angle)
(the .public turn
  Angle
  ..tau)

... https://en.wikipedia.org/wiki/Radian
(the .public radian
  Angle
  +1.0)

... https://en.wikipedia.org/wiki/Degree_(angle)
(the .public degree
  Angle
  (d.of_degree ..tau))

... https://en.wikipedia.org/wiki/Gradian
(the .public gradian
  Angle
  (d./ +400.0 ..tau))

... https://en.wikipedia.org/wiki/Trigonometric_functions
... https://en.wikipedia.org/wiki/Inverse_trigonometric_functions
(for .jvm
     (these (the !double
              (template.macro (_ value)
                [(|> value
                     (as (Nominal "java.lang.Double"))
                     .jvm_object_cast#)]))
            
            (the !dec
              (template.macro (_ value)
                [(|> value
                     .jvm_object_cast#
                     (is (Nominal "java.lang.Double"))
                     (as Dec))]))
            
            (template.with [<input> <output> <name> <method>]
              [(the .public <name>
                 (-> <input>
                     <output>)
                 (|>> !double
                      ["D"]
                      (.jvm_member_invoke_static# [] "java.lang.Math" <method> [])
                      !dec))]

              [Angle Dec cos "cos"]
              [Angle Dec sin "sin"]
              [Angle Dec tan "tan"]

              [Dec Angle acos "acos"]
              [Dec Angle asin "asin"]
              [Dec Angle atan "atan"]
              ))

     .js
     (these (template.with [<input> <output> <name> <method>]
              [(the .public <name>
                 (-> <input>
                     <output>)
                 (|>> []
                      (.js_apply# (.js_constant# <method>))
                      (as Dec)))]

              [Angle Dec cos "Math.cos"]
              [Angle Dec sin "Math.sin"]
              [Angle Dec tan "Math.tan"]

              [Dec Angle acos "Math.acos"]
              [Dec Angle asin "Math.asin"]
              [Dec Angle atan "Math.atan"]
              ))

     .python
     (these (template.with [<input> <output> <name> <method>]
              [(the .public <name>
                 (-> <input>
                     <output>)
                 (|>> []
                      (.python_object_do# <method> (.python_import# "math"))
                      (as Dec)))]

              [Angle Dec cos "cos"]
              [Angle Dec sin "sin"]
              [Angle Dec tan "tan"]

              [Dec Angle acos "acos"]
              [Dec Angle asin "asin"]
              [Dec Angle atan "atan"]
              ))

     .lua
     (these (template.with [<input> <output> <name> <method>]
              [(the .public <name>
                 (-> <input>
                     <output>)
                 (|>> []
                      (.lua_apply# (.lua_constant# <method>))
                      (as Dec)))]

              [Angle Dec cos "math.cos"]
              [Angle Dec sin "math.sin"]
              [Angle Dec tan "math.tan"]

              [Dec Angle acos "math.acos"]
              [Dec Angle asin "math.asin"]
              [Dec Angle atan "math.atan"]
              ))

     .ruby
     (these (template.with [<input> <output> <name> <method>]
              [(the .public <name>
                 (-> <input>
                     <output>)
                 (|>> []
                      (.ruby_apply# (.ruby_constant# <method>))
                      (as Dec)))]

              [Angle Dec cos "Math.cos"]
              [Angle Dec sin "Math.sin"]
              [Angle Dec tan "Math.tan"]

              [Dec Angle acos "Math.acos"]
              [Dec Angle asin "Math.asin"]
              [Dec Angle atan "Math.atan"]
              ))

     .php
     (these (template.with [<input> <output> <name> <method>]
              [(the .public <name>
                 (-> <input>
                     <output>)
                 (|>> ("php apply" ("php constant" <method>))
                      (as Dec)))]

              [Angle Dec cos "cos"]
              [Angle Dec sin "sin"]
              [Angle Dec tan "tan"]

              [Dec Angle acos "acos"]
              [Dec Angle asin "asin"]
              [Dec Angle atan "atan"]
              ))

     .scheme
     (these (template.with [<input> <output> <name> <method>]
              [(the .public <name>
                 (-> <input>
                     <output>)
                 (|>> ("scheme apply" ("scheme constant" <method>))
                      (as Dec)))]

              [Angle Dec cos "cos"]
              [Angle Dec sin "sin"]
              [Angle Dec tan "tan"]

              [Dec Angle acos "acos"]
              [Dec Angle asin "asin"]
              [Dec Angle atan "atan"]
              ))
     )

(the .public (atan_2 x y)
  (-> Dec Dec
      Dec)
  (cond (d.< x +0.0)
        (..atan (d./ x y))

        (d.< +0.0 x)
        (if (or (d.< y +0.0)
                (d.= +0.0 y))
          (|> y (d./ x) atan (d.+ pi))
          (|> y (d./ x) atan (d.- pi)))

        ... (d.= +0.0 x)
        (cond (d.< y +0.0)
              (|> pi (d./ +2.0))
              
              (d.< +0.0 y)
              (|> pi (d./ -2.0))
              
              ... (d.= +0.0 y)
              (d./ +0.0 +0.0))))

(the .public (hypotenuse catA catB)
  (-> Dec Dec
      Dec)
  (d.pow +0.5 (d.+ (d.* catA catA)
                   (d.* catB catB))))

(alias [sine]
       ..sin)

(alias [co_sine]
       ..cos)

(alias [tangent]
       ..tan)

(template.with [<name> <reciprocal>]
  [(the .public <name>
     (-> Angle
         Dec)
     (|>> <reciprocal>
          d.reciprocal))]

  [secant co_sine]
  [co_secant sine]
  [co_tangent tangent]
  )

(alias [sec]
       ..secant)

(alias [csc]
       ..co_secant)

(alias [cot]
       ..co_tangent)

(template.with [<name> <reciprocal>]
  [(the .public <name>
     (-> Angle
         Dec)
     (|>> d.reciprocal
          <reciprocal>))]

  [arc_secant ..acos]
  [arc_co_secant ..asin]
  [arc_co_tangent ..atan]
  )

(alias [arc_sine]
       ..asin)

(alias [arc_co_sine]
       ..acos)

(alias [arc_tangent]
       ..atan)

(alias [arc_tangent_2]
       ..atan_2)

(alias [asec]
       ..arc_secant)

(alias [acsc]
       ..arc_co_secant)

(alias [acot]
       ..arc_co_tangent)
