... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [format (.only Format)]]
   [control
    ["[0]" try (.only Try)]]
   [data
    ["[0]" text]]
   [meta
    [macro
     ["^" pattern]]]]]
 ["[0]" /
  ["[1][0]" natural]
  ["[1][0]" integer]
  ["[1][0]" revolution]
  ["[1][0]" decimal]])

(the with_template' (.in_module# .prelude .with_template))

(the separator
  ",")

(the (separator_prefixed? number)
  (-> Text Bit)
  (when (.text_index# 0 ..separator number)
    {.#Some 0}
    true

    _
    false))

(the without_separators
  (-> Text Text)
  (text.replaced ..separator ""))

(the .public base_10
  (let [<error> "Invalid decimal syntax."]
    (macro (_ tokens state)
      (when tokens
        {.#Item [meta {.#Text repr'}] {.#End}}
        (if (..separator_prefixed? repr')
          {try.#Failure <error>}
          (let [repr (..without_separators repr')]
            (`` (when (by /natural.base_10 projection repr)
                  {try.#Success value}
                  {try.#Success [state (list [meta {.#Natural value}])]}

                  (^.multi {try.#Failure _}
                           [(by /integer.base_10 projection repr)
                            {try.#Success value}])
                  {try.#Success [state (list [meta {.#Integer value}])]}

                  (^.multi {try.#Failure _}
                           [(by /revolution.base_10 projection repr)
                            {try.#Success value}])
                  {try.#Success [state (list [meta {.#Revolution value}])]}

                  (,, (with_template' [<format>]
                        [(^.multi {try.#Failure _}
                                  [(by <format> projection repr)
                                   {try.#Success value}])
                         {try.#Success [state (list [meta {.#Decimal value}])]}]

                        [/decimal.degree]
                        [/decimal.percentage]
                        [/decimal.permille]
                        [/decimal.permyriad]
                        [/decimal.base_10]
                        ))

                  _
                  {try.#Failure <error>}))))

        _
        {try.#Failure <error>}))))

(with_template' [<macro> <natural> <integer> <revolution> <decimal> <error>]
  [(the .public <macro>
     (macro (_ tokens state)
       (when tokens
         {.#Item [meta {.#Text repr'}] {.#End}}
         (if (..separator_prefixed? repr')
           {try.#Failure <error>}
           (let [repr (..without_separators repr')]
             (when (by <natural> projection repr)
               {try.#Success value}
               {try.#Success [state (list [meta {.#Natural value}])]}

               (^.multi {try.#Failure _}
                        [(by <integer> projection repr)
                         {try.#Success value}])
               {try.#Success [state (list [meta {.#Integer value}])]}

               (^.multi {try.#Failure _}
                        [(by <revolution> projection repr)
                         {try.#Success value}])
               {try.#Success [state (list [meta {.#Revolution value}])]}

               (^.multi {try.#Failure _}
                        [(by <decimal> projection repr)
                         {try.#Success value}])
               {try.#Success [state (list [meta {.#Decimal value}])]}

               _
               {try.#Failure <error>})))

         _
         {try.#Failure <error>})))]

  [bin /natural.binary /integer.binary /revolution.binary /decimal.binary "Invalid binary syntax."]
  [oct /natural.octal  /integer.octal  /revolution.octal  /decimal.octal  "Invalid octal syntax."]
  [hex /natural.hex    /integer.hex    /revolution.hex    /decimal.hex    "Invalid hexadecimal syntax."]
  )

(alias [binary]
       ..bin)

(alias [octal]
       ..oct)

(alias [hexadecimal]
       ..hex)

(alias [base_02] ..binary)
(alias [base_08] ..octal)
(alias [decimal] ..base_10)
(alias [base_16] ..hex)
