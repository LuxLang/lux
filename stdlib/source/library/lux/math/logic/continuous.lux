... https://en.wikipedia.org/wiki/Many-valued_logic
(.module:
  [library
   [lux {"-" [false true or and not]}
    [abstract
     [monoid {"+" [Monoid]}]]
    [math
     [number
      ["/" rev ("#\." interval)]]]]])

(def: .public false Rev /\bottom)
(def: .public true  Rev /\top)

(template [<name> <chooser> <monoid> <identity>]
  [(def: .public <name>
     (-> Rev Rev Rev)
     <chooser>)

   (implementation: .public <monoid>
     (Monoid Rev)

     (def: identity <identity>)
     (def: composite <name>))]

  [or  /.max disjunction ..false]
  [and /.min conjunction ..true]
  )

(def: .public (not input)
  (-> Rev Rev)
  (/.- input ..true))

(def: .public (implies consequent antecedent)
  (-> Rev Rev Rev)
  (or (not antecedent)
      consequent))

(def: .public (= left right)
  (-> Rev Rev Rev)
  (and (or (not left) right)
       (or left (not right))))
