... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except text natural)
   [abstract
    ["<>" projection]]
   [control
    ["[0]" maybe]]
   [data
    [collection
     ["[0]" list
      ["?[1]" \\projection]]]]
   [macro
    ["[0]" syntax]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]]]]
 [//
  ["//" natural]
  [//
   ["/" fraction]]])

(/.module //.Number //.small
          //.= //.< //.greatest_common_divisor
          //.zero //.one
          //.+ //.- //.x //./ //.%
          /.delimiter //.base_10)

(the .public new
  (syntax.macro (_ [numerator ?list.any
                    ?denominator (<>.maybe ?list.any)])
    (pure (list (` (..normal [..#numerator (, numerator)
                              ..#denominator (, (maybe.else (' //.one) ?denominator))]))))))

(the .public (random digits)
  (-> Natural
      (Random ..Fraction))
  (let [part (//.random digits)]
    (|> (random.and part
                    part)
        (random#each ..normal)
        (random.only ..finite?))))
