... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Integer as for)
   [abstract
    ["[0]" equivalence]
    ["[0]" hash]
    ["[0]" order]]
   [function
    ["[0]" predicate]]
   [data
    ["[0]" binary
     ["[1]" \\injection]]]
   [math
    ["[0]" random]]
   [type
    ["[0]" nominal]]
   [macro
    ["[0]" template]]]]
 [///
  ["[0]" /64 (.only)
   ["//" integer]
   ["[0]" natural]]])

(the .public for
  (template.macro (_ ,size ,injection)
    [(nominal.every .public Integer
       .Integer

       (alias [Number]
              Integer)

       (the .public as
         (-> Integer
             .Integer)
         (|>> nominal.as))

       (the .public hash
         (hash.Hash Integer)
         (by hash.functor each as //.hash))

       (the .public order
         (order.Order Integer)
         (by order.functor each as //.order))

       (order.for [] Integer order)

       (the .public equivalence
         (equivalence.Equivalence Integer)
         (by order equivalence))

       (alias [=]
              equivalence)

       (the .public size ,size)

       (the .public maximum
         Integer
         (|> size
             (natural.x /64.bits_per_byte)
             /64.mask
             .integer
             nominal.of))

       (the .public minimum
         Integer
         (|> maximum
             nominal.as
             /64.not
             nominal.of))

       (the (small it)
         (-> .Integer
             .Integer)
         (if (//.< +0 it)
             (/64.or (nominal.as minimum) it)
             (/64.and (nominal.as maximum) it)))

       (the .public (valid? it)
         (predicate.Predicate .Integer)
         (not (or (//.> (nominal.as maximum) it)
                  (//.< (nominal.as minimum) it))))

       (the .public of
         (-> .Integer
             Integer)
         (|>> small
              nominal.of))

       (the .public (+ parameter it)
         (-> Integer Integer
             Integer)
         (|> (//.+ (nominal.as parameter)
                   (nominal.as it))
             small
             nominal.of))

       (the .public (- parameter it)
         (-> Integer Integer
             Integer)
         (|> (//.- (nominal.as parameter)
                   (nominal.as it))
             small
             nominal.of))

       (the .public as_binary
         (binary.Injection Integer)
         (|>> nominal.as
              ,injection))

       (the .public random
         (random.Random Integer)
         (by random.functor each of random.integer))
       )]))

(..for 1
       binary.bits_08)
