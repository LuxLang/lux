... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... [Rational number](https://en.wikipedia.org/wiki/Rational_number)
(.require
 [library
  [lux (.except integer)
   [abstract
    [equivalence (.only Equivalence)]
    [order (.only Order)]
    [monoid (.only Monoid)]
    [format (.only Format)]
    [monad (.only do)]]
   [control
    ["<>" projection]
    ["[0]" function]
    ["[0]" maybe]
    ["[0]" try (.only Try)]
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" product]
    ["[0]" text (.use "[1]#[0]" monoid)]]
   [meta
    ["[0]" location]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro
     ["[0]" syntax]
     ["[0]" template]]]]]
 [//
  ["i" integer (.use "[1]#[0]" base_10)]
  ["d" decimal]
  [//
   [arithmetic (.only Arithmetic)]]])

(every .public Rational
  (Record
   [#numerator Integer
    #denominator Integer]))

(the It ..Rational)

(alias [Number]
       ..It)

(the .public not_an_integer
  Error
  (location.with (location.here)
    "Not an integer."))

(the .public (integer value)
  (-> It
      (Try Integer))
  (when (its #denominator value)
    +1 {try.#Success (its #numerator value)}
    _ {try.#Failure ..not_an_integer}))

(the .public (normal (open "/[0]"))
  (-> It
      It)
  (let [common (i.gcd /#numerator /#denominator)]
    [..#numerator (i./ common /#numerator)
     ..#denominator (i./ common /#denominator)]))

(the .public rational
  (syntax.macro (_ [numerator <code>.any
                    ?denominator (<>.maybe <code>.any)])
    (in (list (` (normal [..#numerator (, numerator)
                          ..#denominator (, (maybe.else (' +1) ?denominator))]))))))

(the .public (= parameter subject)
  (-> It It
      Bit)
  (i.= (i.* (its #numerator parameter)
            (its #denominator subject))
       (i.* (its #denominator parameter)
            (its #numerator subject))))

(the .public equivalence
  (Equivalence It)
  (implementation
   (the = ..=)))

(the (equalized parameter subject)
  (-> It It
      [Integer Integer])
  [(i.* (its #denominator subject)
        (its #numerator parameter))
   (i.* (its #denominator parameter)
        (its #numerator subject))])

(the .public (< parameter subject)
  (-> It It
      Bit)
  (let [[parameter' subject'] (..equalized parameter subject)]
    (i.< parameter' subject')))

(the .public (<= parameter subject)
  (-> It It
      Bit)
  (or (< parameter subject)
      (= parameter subject)))

(the .public (> parameter subject)
  (-> It It
      Bit)
  (..< subject parameter))

(the .public (>= parameter subject)
  (-> It It
      Bit)
  (or (> parameter subject)
      (= parameter subject)))

(the .public order
  (Order It)
  (implementation
   (the equivalence ..equivalence)
   (the < ..<)))

(the .public (+ parameter subject)
  (-> It It
      It)
  (let [[parameter' subject'] (..equalized parameter subject)]
    (normal [(i.+ parameter' subject')
             (i.* (its #denominator parameter)
                  (its #denominator subject))])))

(the .public (- parameter subject)
  (-> It It
      It)
  (let [[parameter' subject'] (..equalized parameter subject)]
    (normal [(i.- parameter' subject')
             (i.* (its #denominator parameter)
                  (its #denominator subject))])))

(the .public (* parameter subject)
  (-> It It
      It)
  (normal [(i.* (its #numerator parameter)
                (its #numerator subject))
           (i.* (its #denominator parameter)
                (its #denominator subject))]))

(the .public (/ parameter subject)
  (-> It It
      It)
  (let [[parameter' subject'] (..equalized parameter subject)]
    (normal [subject' parameter'])))

(the .public (% parameter subject)
  (-> It It
      It)
  (let [[parameter' subject'] (..equalized parameter subject)
        quot (i./ parameter' subject')]
    (..- (revised #numerator (i.* quot) parameter)
         subject)))

(the .public arithmetic
  (Arithmetic It)
  (implementation
   (the + ..+)
   (the - ..-)
   (the * ..*)
   (the / ../)))

(the separator "/")

(the .public format
  (Format Text It)
  (implementation
   (the (injection (open "/[0]"))
     (all text#composite
          (i#injection /#numerator)
          ..separator (i#injection /#denominator)))

   (the (projection input)
     (when (text.split_by ..separator input)
       {.#Some [num denom]}
       (do try.monad
         [numerator (i#projection num)
          denominator (i#projection denom)]
         (in (normal [#numerator numerator
                      #denominator denominator])))
       
       {.#None}
       {.#Left (text#composite "Invalid syntax for rational: " input)}))))

(template.with [<identity> <composite> <name>]
  [(the .public <name>
     (Monoid It)
     (implementation
      (the identity (..rational <identity>))
      (the composite <composite>)))]
  
  [+0 ..+ addition]
  [+1 ..* multiplication]
  )

(template.with [,numerator ,name]
  [(the .public ,name
     It
     (..rational ,numerator))]

  [+0 zero]
  [+1 one]
  [-1 negative]
  )

(alias [positive]
       ..one)

(template.with [,order ,name]
  [(the .public ,name
     (Predicate It)
     (,order ..zero))]

  [< negative?]
  [> positive?]
  )

(the .public (reciprocal (open "/[0]"))
  (-> It
      It)
  [..#numerator /#denominator
   ..#denominator /#numerator])

(the .public opposite
  (-> It
      It)
  (* ..negative))

(template.with [<numerator> <predicate> ,name]
  [(the .public ,name
     It
     [#numerator <numerator>
      #denominator +0])

   (the .public <predicate>
     (Predicate It)
     (|>> (= ,name)
          not))]

  [+1 finite? infinity]
  ... https://en.wikipedia.org/wiki/Indeterminate_form
  [+0 determinate? indeterminate]
  )

(template.with [,/% ,name]
  [(the .public (,name it)
     (-> It
         It)
     (..rational (,/% (its #denominator it)
                      (its #numerator it))
                 (its #denominator it)))]

  [i./ whole]
  [i.% partial]
  )

... https://en.wikipedia.org/wiki/Number#Classification
(alias [Q]
       ..It)

(the .public (decimal it)
  (-> It
      Decimal)
  (d./ (i.decimal (its #denominator it))
       (i.decimal (its #numerator it))))
