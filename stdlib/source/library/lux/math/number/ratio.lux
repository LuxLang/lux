... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except nat)
   [abstract
    [equivalence (.only Equivalence)]
    [order (.only Order)]
    [monoid (.only Monoid)]
    [codec (.only Codec)]
    [monad (.only do)]]
   [control
    ["<>" parser]
    ["[0]" function]
    ["[0]" maybe]
    ["[0]" try (.only Try)]
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" product]
    ["[0]" text (.use "[1]#[0]" monoid)]]
   [meta
    ["[0]" location]
    ["[0]" code (.only)
     ["<[1]>" \\parser (.only Parser)]]
    [macro
     [syntax (.only syntax)]]]]]
 [//
  ["n" nat (.use "[1]#[0]" decimal)]
  [//
   [arithmetic (.only Arithmetic)]]])

(every .public Ratio
  (Record
   [#numerator Nat
    #denominator Nat]))

(the .public not_a_nat
  Error
  (location.with (location.here)
    "Not a natural number."))

(the .public (nat value)
  (-> Ratio
      (Try Nat))
  (when (its #denominator value)
    1 {try.#Success (its #numerator value)}
    _ {try.#Failure ..not_a_nat}))

(the .public (normal (open "/[0]"))
  (-> Ratio
      Ratio)
  (let [common (n.gcd /#numerator /#denominator)]
    [..#numerator (n./ common /#numerator)
     ..#denominator (n./ common /#denominator)]))

(the .public ratio
  (syntax (_ [numerator <code>.any
              ?denominator (<>.maybe <code>.any)])
    (in (list (` (normal [..#numerator (, numerator)
                          ..#denominator (, (maybe.else (' 1) ?denominator))]))))))

(the .public (= parameter subject)
  (-> Ratio Ratio
      Bit)
  (n.= (n.* (its #numerator parameter)
            (its #denominator subject))
       (n.* (its #denominator parameter)
            (its #numerator subject))))

(the .public equivalence
  (Equivalence Ratio)
  (implementation
   (the = ..=)))

(the (equalized parameter subject)
  (-> Ratio Ratio
      [Nat Nat])
  [(n.* (its #denominator subject)
        (its #numerator parameter))
   (n.* (its #denominator parameter)
        (its #numerator subject))])

(the .public (< parameter subject)
  (-> Ratio Ratio
      Bit)
  (let [[parameter' subject'] (..equalized parameter subject)]
    (n.< parameter' subject')))

(the .public (<= parameter subject)
  (-> Ratio Ratio
      Bit)
  (or (< parameter subject)
      (= parameter subject)))

(the .public (> parameter subject)
  (-> Ratio Ratio
      Bit)
  (..< subject parameter))

(the .public (>= parameter subject)
  (-> Ratio Ratio
      Bit)
  (or (> parameter subject)
      (= parameter subject)))

(the .public order
  (Order Ratio)
  (implementation
   (the equivalence ..equivalence)
   (the < ..<)))

(the .public (+ parameter subject)
  (-> Ratio Ratio
      Ratio)
  (let [[parameter' subject'] (..equalized parameter subject)]
    (normal [(n.+ parameter' subject')
             (n.* (its #denominator parameter)
                  (its #denominator subject))])))

(the .public (- parameter subject)
  (-> Ratio Ratio
      Ratio)
  (let [[parameter' subject'] (..equalized parameter subject)]
    (normal [(n.- parameter' subject')
             (n.* (its #denominator parameter)
                  (its #denominator subject))])))

(the .public (* parameter subject)
  (-> Ratio Ratio
      Ratio)
  (normal [(n.* (its #numerator parameter)
                (its #numerator subject))
           (n.* (its #denominator parameter)
                (its #denominator subject))]))

(the .public (/ parameter subject)
  (-> Ratio Ratio
      Ratio)
  (let [[parameter' subject'] (..equalized parameter subject)]
    (normal [subject' parameter'])))

(the .public (% parameter subject)
  (-> Ratio Ratio
      Ratio)
  (let [[parameter' subject'] (..equalized parameter subject)
        quot (n./ parameter' subject')]
    (..- (revised #numerator (n.* quot) parameter)
         subject)))

(the .public arithmetic
  (Arithmetic Ratio)
  (implementation
   (the + ..+)
   (the - ..-)
   (the * ..*)
   (the / ../)))

(the .public (reciprocal (open "/[0]"))
  (-> Ratio
      Ratio)
  [..#numerator /#denominator
   ..#denominator /#numerator])

(the separator ":")

(the .public codec
  (Codec Text Ratio)
  (implementation
   (the (encoded (open "/[0]"))
     (all text#composite
          (n#encoded /#numerator)
          ..separator (n#encoded /#denominator)))

   (the (decoded input)
     (when (text.split_by ..separator input)
       {.#Some [num denom]}
       (do try.monad
         [numerator (n#decoded num)
          denominator (n#decoded denom)]
         (in (normal [#numerator numerator
                      #denominator denominator])))
       
       {.#None}
       {.#Left (text#composite "Invalid syntax for ratio: " input)}))))

(with_template [<identity> <composite> <name>]
  [(the .public <name>
     (Monoid Ratio)
     (implementation
      (the identity (..ratio <identity>))
      (the composite <composite>)))]
  
  [0 ..+ addition]
  [1 ..* multiplication]
  )

(the .public zero
  Ratio
  [#numerator 0
   #denominator 1])

(the .public one
  Ratio
  [#numerator 1
   #denominator 1])

(with_template [<numerator> <predicate> <ratio>]
  [(the .public <ratio>
     Ratio
     [#numerator <numerator>
      #denominator 0])

   (the .public <predicate>
     (Predicate Ratio)
     (|>> (= <ratio>)
          not))]

  [1 finite? infinity]
  ... https://en.wikipedia.org/wiki/Indeterminate_form
  [0 determinate? indeterminate]
  )
