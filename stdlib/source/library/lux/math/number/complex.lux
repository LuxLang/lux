... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [codec (.only Codec)]
    [monad (.only do)]]
   [control
    ["<>" parser]
    ["[0]" maybe]
    ["[0]" try]]
   [data
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    [number
     ["f" frac]
     ["[0]" int]]]
   [meta
    ["[0]" code
     ["<[1]>" \\parser]]
    [macro
     [syntax (.only syntax)]]]]]
 [///
  [arithmetic (.only Arithmetic)]])

(type .public Complex
  (Record
   [#real Frac
    #imaginary Frac]))

(def .public complex
  (syntax (_ [real <code>.any
              ?imaginary (<>.maybe <code>.any)])
    (in (list (` [..#real (, real)
                  ..#imaginary (, (maybe.else (' +0.0) ?imaginary))])))))

(def .public i
  Complex
  (..complex +0.0 +1.0))

(def .public +one
  Complex
  (..complex +1.0 +0.0))

(def .public -one
  Complex
  (..complex -1.0 +0.0))

(def .public zero
  Complex
  (..complex +0.0 +0.0))

(def .public (not_a_number? complex)
  (-> Complex
      Bit)
  (or (f.not_a_number? (the #real complex))
      (f.not_a_number? (the #imaginary complex))))

(def .public (= param input)
  (-> Complex Complex
      Bit)
  (and (f.= (the #real param)
            (the #real input))
       (f.= (the #imaginary param)
            (the #imaginary input))))

(with_template [<name> <op>]
  [(def .public (<name> param input)
     (-> Complex Complex
         Complex)
     [#real (<op> (the #real param)
                  (the #real input))
      #imaginary (<op> (the #imaginary param)
                       (the #imaginary input))])]

  [+ f.+]
  [- f.-]
  )

(def .public equivalence
  (Equivalence Complex)
  (implementation
   (def = ..=)))

(with_template [<name> <transform>]
  [(def .public <name>
     (-> Complex
         Complex)
     (|>> (revised #real <transform>)
          (revised #imaginary <transform>)))]

  [opposite f.opposite]
  [signum f.signum]
  )

(def .public conjugate
  (-> Complex
      Complex)
  (revised #imaginary f.opposite))

(def .public (*' param input)
  (-> Frac Complex
      Complex)
  [#real (f.* param
              (the #real input))
   #imaginary (f.* param
                   (the #imaginary input))])

(def .public (* param input)
  (-> Complex Complex
      Complex)
  [#real (f.- (f.* (the #imaginary param)
                   (the #imaginary input))
              (f.* (the #real param)
                   (the #real input)))
   #imaginary (f.+ (f.* (the #real param)
                        (the #imaginary input))
                   (f.* (the #imaginary param)
                        (the #real input)))])

(def .public (/ param input)
  (-> Complex Complex
      Complex)
  (let [(open "[0]") param]
    (if (f.< (f.abs #imaginary)
             (f.abs #real))
      (let [quot (f./ #imaginary #real)
            denom (|> #real (f.* quot) (f.+ #imaginary))]
        [..#real (|> (the ..#real input) (f.* quot) (f.+ (the ..#imaginary input)) (f./ denom))
         ..#imaginary (|> (the ..#imaginary input) (f.* quot) (f.- (the ..#real input)) (f./ denom))])
      (let [quot (f./ #real #imaginary)
            denom (|> #imaginary (f.* quot) (f.+ #real))]
        [..#real (|> (the ..#imaginary input) (f.* quot) (f.+ (the ..#real input)) (f./ denom))
         ..#imaginary (|> (the ..#imaginary input) (f.- (f.* quot (the ..#real input))) (f./ denom))]))))

(def .public (/' param subject)
  (-> Frac Complex
      Complex)
  (let [(open "[0]") subject]
    [..#real (f./ param #real)
     ..#imaginary (f./ param #imaginary)]))

(def .public (% param input)
  (-> Complex Complex
      Complex)
  (let [scaled (/ param input)
        quotient (|> scaled
                     (revised #real f.floor)
                     (revised #imaginary f.floor))]
    (- (* quotient param)
       input)))

(def .public arithmetic
  (Arithmetic Complex)
  (implementation
   (def + ..+)
   (def - ..-)
   (def * ..*)
   (def / ../)))

(def .public (cos subject)
  (-> Complex
      Complex)
  (let [(open "[0]") subject]
    [..#real (f.* (f.cosh #imaginary)
                  (f.cos #real))
     ..#imaginary (f.opposite (f.* (f.sinh #imaginary)
                                   (f.sin #real)))]))

(def .public (cosh subject)
  (-> Complex
      Complex)
  (let [(open "[0]") subject]
    [..#real (f.* (f.cos #imaginary)
                  (f.cosh #real))
     ..#imaginary (f.* (f.sin #imaginary)
                       (f.sinh #real))]))

(def .public (sin subject)
  (-> Complex
      Complex)
  (let [(open "[0]") subject]
    [..#real (f.* (f.cosh #imaginary)
                  (f.sin #real))
     ..#imaginary (f.* (f.sinh #imaginary)
                       (f.cos #real))]))

(def .public (sinh subject)
  (-> Complex
      Complex)
  (let [(open "[0]") subject]
    [..#real (f.* (f.cos #imaginary)
                  (f.sinh #real))
     ..#imaginary (f.* (f.sin #imaginary)
                       (f.cosh #real))]))

(def .public (tan subject)
  (-> Complex
      Complex)
  (let [(open "[0]") subject
        r2 (f.* +2.0 #real)
        i2 (f.* +2.0 #imaginary)
        d (f.+ (f.cos r2) (f.cosh i2))]
    [..#real (f./ d (f.sin r2))
     ..#imaginary (f./ d (f.sinh i2))]))

(def .public (tanh subject)
  (-> Complex
      Complex)
  (let [(open "[0]") subject
        r2 (f.* +2.0 #real)
        i2 (f.* +2.0 #imaginary)
        d (f.+ (f.cosh r2) (f.cos i2))]
    [..#real (f./ d (f.sinh r2))
     ..#imaginary (f./ d (f.sin i2))]))

(def .public (abs subject)
  (-> Complex
      Frac)
  (let [(open "[0]") subject]
    (if (f.< (f.abs #imaginary)
             (f.abs #real))
      (if (f.= +0.0 #imaginary)
        (f.abs #real)
        (let [q (f./ #imaginary #real)]
          (f.* (f.pow +0.5 (f.+ +1.0 (f.* q q)))
               (f.abs #imaginary))))
      (if (f.= +0.0 #real)
        (f.abs #imaginary)
        (let [q (f./ #real #imaginary)]
          (f.* (f.pow +0.5 (f.+ +1.0 (f.* q q)))
               (f.abs #real)))))))

(def .public (exp subject)
  (-> Complex
      Complex)
  (let [(open "[0]") subject
        r_exp (f.exp #real)]
    [..#real (f.* r_exp (f.cos #imaginary))
     ..#imaginary (f.* r_exp (f.sin #imaginary))]))

(def .public (log subject)
  (-> Complex
      Complex)
  (let [(open "[0]") subject]
    [..#real (|> subject ..abs f.log)
     ..#imaginary (f.atan_2 #real #imaginary)]))

(with_template [<name> <type> <op>]
  [(def .public (<name> param input)
     (-> <type> Complex
         Complex)
     (|> input log (<op> param) exp))]

  [pow  Complex ..*]
  [pow' Frac    ..*']
  )

(def (with_sign sign magnitude)
  (-> Frac Frac
      Frac)
  (f.* (f.signum sign) magnitude))

(def .public (root_2 input)
  (-> Complex
      Complex)
  (let [(open "[0]") input
        t (|> input ..abs (f.+ (f.abs #real)) (f./ +2.0) (f.pow +0.5))]
    (if (f.< +0.0 #real)
      [..#real (f./ (f.* +2.0 t)
                    (f.abs #imaginary))
       ..#imaginary (f.* t (..with_sign #imaginary +1.0))]
      [..#real t
       ..#imaginary (f./ (f.* +2.0 t)
                         #imaginary)])))

(def (root_2-1z input)
  (-> Complex
      Complex)
  (|> (complex +1.0) (- (* input input)) ..root_2))

(def .public (reciprocal (open "[0]"))
  (-> Complex
      Complex)
  (if (f.< (f.abs #imaginary)
           (f.abs #real))
    (let [q (f./ #imaginary #real)
          scale (f./ (|> #real (f.* q) (f.+ #imaginary))
                     +1.0)]
      [..#real (f.* q scale)
       ..#imaginary (f.opposite scale)])
    (let [q (f./ #real #imaginary)
          scale (f./ (|> #imaginary (f.* q) (f.+ #real))
                     +1.0)]
      [..#real scale
       ..#imaginary (|> scale f.opposite (f.* q))])))

(def .public (acos input)
  (-> Complex
      Complex)
  (|> input
      (..+ (|> input ..root_2-1z (..* ..i)))
      ..log
      (..* (..opposite ..i))))

(def .public (asin input)
  (-> Complex
      Complex)
  (|> input
      ..root_2-1z
      (..+ (..* ..i input))
      ..log
      (..* (..opposite ..i))))

(def .public (atan input)
  (-> Complex
      Complex)
  (|> input
      (..+ ..i)
      (../ (..- input ..i))
      ..log
      (..* (../ (..complex +2.0) ..i))))

(def .public (argument (open "[0]"))
  (-> Complex
      Frac)
  (f.atan_2 #real #imaginary))

(def .public (roots nth input)
  (-> Nat Complex
      (List Complex))
  (when nth
    0 (list)
    _ (let [r_nth (|> nth .int int.frac)
            nth_root_of_abs (|> input ..abs (f.pow (f./ r_nth +1.0)))
            nth_phi (|> input ..argument (f./ r_nth))
            slice (|> f.pi (f.* +2.0) (f./ r_nth))]
        (|> (list.indices nth)
            (list#each (function (_ nth')
                         (let [inner (|> nth' .int int.frac
                                         (f.* slice)
                                         (f.+ nth_phi))
                               real (f.* nth_root_of_abs
                                         (f.cos inner))
                               imaginary (f.* nth_root_of_abs
                                              (f.sin inner))]
                           [..#real real
                            ..#imaginary imaginary])))))))

(def .public (approximately? margin_of_error standard value)
  (-> Frac Complex Complex
      Bit)
  (and (f.approximately? margin_of_error
                         (the ..#real standard)
                         (the ..#real value))
       (f.approximately? margin_of_error
                         (the ..#imaginary standard)
                         (the ..#imaginary value))))

(def .public (format it)
  (-> Complex
      Text)
  (.text_composite# (of f.decimal encoded (the ..#real it))
                    " " (of f.decimal encoded (the ..#imaginary it)) "i"))

(def .public codec
  (Codec Text Complex)
  (implementation
   (def encoded ..format)
   (def (decoded it)
     (when (text.split_by " " it)
       {.#Some [real imaginary]}
       (do try.monad
         [real (of f.decimal decoded real)
          imaginary (of f.decimal decoded (text.replaced_once "i" "" imaginary))]
         (in [#real real
              #imaginary imaginary]))

       {.#None}
       (do try.monad
         [real (of f.decimal decoded it)]
         (in [#real real
              #imaginary +0.0]))))))
