... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [hash (.only Hash)]
    [enum (.only Enum)]
    [interval (.only Interval)]
    [monoid (.only Monoid)]
    [equivalence (.only Equivalence)]
    [format (.only Format)]
    ["[0]" order (.only Order)]]
   [control
    ["[0]" function]
    ["[0]" maybe]
    ["[0]" try (.only Try)]]]]
 [///
  [arithmetic (.only Arithmetic)]])

(the with_template' (.in_module# .prelude .with_template))

(with_template' [<extension> <output> <name>]
  [(the .public (<name> parameter subject)
     (-> Natural Natural
         <output>)
     (<extension> parameter subject))]

  [.i64_=# Bit =]
  [.i64_+# Natural +]
  [.i64_-# Natural -]
  )

(the high
  (-> (I64 Any)
      I64)
  (|>> (.i64_right# 32)))

(the low
  (-> (I64 Any)
      I64)
  (let [mask (|> 1 (.i64_left# 32) (.i64_-# 1))]
    (|>> (.i64_and# mask))))

(the .public (< reference it)
  (-> Natural Natural
      Bit)
  (let [referenceH (..high reference)
        itH (..high it)]
    (if (.int_<# referenceH itH)
      true
      (if (.i64_=# referenceH itH)
        (.int_<#
         (..low reference)
         (..low it))
        false))))

(the .public (<= reference it)
  (-> Natural Natural
      Bit)
  (or (..< reference it)
      (.i64_=# reference it)))

(the .public (> reference it)
  (-> Natural Natural
      Bit)
  (..< it reference))

(the .public (>= reference it)
  (-> Natural Natural
      Bit)
  (or (..< it reference)
      (.i64_=# reference it)))

(with_template' [<name> <test>]
  [(the .public (<name> left right)
     (-> Natural Natural
         Natural)
     (if (<test> right left)
       left
       right))]

  [min ..<]
  [max ..>]
  )

(the .public (* parameter subject)
  (-> Natural Natural
      Natural)
  (.natural (.int_*# (.integer parameter)
                     (.integer subject))))

(the .public (/ parameter subject)
  (-> Natural Natural
      Natural)
  (if (.int_<# +0 (.integer parameter))
    (if (..< parameter subject)
      0
      1)
    (let [quotient (|> subject
                       (.i64_right# 1)
                       (.int_/# (.integer parameter))
                       (.i64_left# 1))
          flat (.int_*# (.integer parameter)
                        (.integer quotient))
          remainder (.i64_-# flat subject)]
      (if (..< parameter remainder)
        quotient
        (.i64_+# 1 quotient)))))

(the .public (/% parameter subject)
  (-> Natural Natural
      [Natural Natural])
  (let [quotient (../ parameter subject)
        flat (.int_*# (.integer parameter)
                      (.integer quotient))]
    [quotient (.i64_-# flat subject)]))

(the .public (% parameter subject)
  (-> Natural Natural
      Natural)
  (let [flat (.int_*# (.integer parameter)
                      (.integer (../ parameter subject)))]
    (.i64_-# flat subject)))

(the .public arithmetic
  (Arithmetic Natural)
  (implementation
   (the + ..+)
   (the - ..-)
   (the * ..*)
   (the / ../)))

(the .public (gcd a b)
  (-> Natural Natural
      Natural)
  (when b
    0 a
    _ (gcd b (..% b a))))

(the .public (co_prime? a b)
  (-> Natural Natural
      Bit)
  (..= 1 (..gcd a b)))

(`` (the .public (lcm a b)
      (-> Natural Natural
          Natural)
      (when [a b]
        (,, (with_template' [<pattern>]
              [<pattern>
               0]

              [[_ 0]]
              [[0 _]]))

        _
        (|> a (../ (..gcd a b)) (..* b)))))

(the .public even?
  (-> Natural
      Bit)
  (|>> (..% 2) (.i64_=# 0)))

(the .public odd?
  (-> Natural
      Bit)
  (|>> ..even? not))

(the .public decimal
  (-> Natural
      Decimal)
  (|>> .integer .int_f64#))

(the .public equivalence
  (Equivalence Natural)
  (implementation
   (the = ..=)))

(the .public order
  (Order Natural)
  (implementation
   (the equivalence ..equivalence)
   (the < ..<)))

(the .public enum
  (Enum Natural)
  (implementation
   (the order ..order)
   (the succ ++)
   (the pred --)))

(the .public interval
  (Interval Natural)
  (implementation
   (the enum ..enum)
   (the top (-- 0))
   (the bottom 0)))

(with_template' [<name> <composite> <identity>]
  [(the .public <name>
     (Monoid Natural)
     (implementation
      (the identity <identity>)
      (the composite <composite>)))]

  [addition       ..+   0]
  [multiplication ..*   1]
  [minimum        ..min (of ..interval top)]
  [maximum        ..max (of ..interval bottom)]
  )

(the (binary_character value)
  (-> Natural
      Text)
  (when value
    0 "0"
    1 "1"
    _ (undefined)))

(the (binary_value digit)
  (-> Natural
      (Maybe Natural))
  (when digit
    (character "0") {.#Some 0}
    (character "1") {.#Some 1}
    _ {.#None}))

(the (octal_character value)
  (-> Natural
      Text)
  (when value
    0 "0"
    1 "1"
    2 "2"
    3 "3"
    4 "4"
    5 "5"
    6 "6"
    7 "7"
    _ (undefined)))

(the (octal_value digit)
  (-> Natural
      (Maybe Natural))
  (when digit
    (character "0") {.#Some 0}
    (character "1") {.#Some 1}
    (character "2") {.#Some 2}
    (character "3") {.#Some 3}
    (character "4") {.#Some 4}
    (character "5") {.#Some 5}
    (character "6") {.#Some 6}
    (character "7") {.#Some 7}
    _ {.#None}))

(the (decimal_value digit)
  (-> Natural
      (Maybe Natural))
  (when digit
    (character "0") {.#Some 0}
    (character "1") {.#Some 1}
    (character "2") {.#Some 2}
    (character "3") {.#Some 3}
    (character "4") {.#Some 4}
    (character "5") {.#Some 5}
    (character "6") {.#Some 6}
    (character "7") {.#Some 7}
    (character "8") {.#Some 8}
    (character "9") {.#Some 9}
    _ {.#None}))

(the (hexadecimal_character value)
  (-> Natural
      Text)
  (when value
    0 "0"
    1 "1"
    2 "2"
    3 "3"
    4 "4"
    5 "5"
    6 "6"
    7 "7"
    8 "8"
    9 "9"
    10 "A"
    11 "B"
    12 "C"
    13 "D"
    14 "E"
    15 "F"
    _ (undefined)))

(`` (the (hexadecimal_value digit)
      (-> Natural
          (Maybe Natural))
      (when digit
        (,, (with_template' [<character> <number>]
              [(character <character>)
               {.#Some <number>}]

              ["0" 0] ["1" 1] ["2" 2] ["3" 3] ["4" 4]
              ["5" 5] ["6" 6] ["7" 7] ["8" 8] ["9" 9]))

        (,, (with_template' [<lower> <upper> <number>]
              [(character <lower>)
               {.#Some <number>}
               
               (character <upper>)
               {.#Some <number>}]

              ["a" "A" 10] ["b" "B" 11] ["c" "C" 12]
              ["d" "D" 13] ["e" "E" 14] ["f" "F" 15]))
        
        _
        {.#None})))

(with_template' [<shift> <struct> <to_character> <to_value> <error>]
  [(the .public <struct>
     (Format Text Natural)
     (implementation
      (the injection
        (let [mask (|> 1 (.i64_left# <shift>) --)]
          (function (_ value)
            (loop (again [input value
                          output ""])
              (let [output' (.text_composite# (<to_character> (.i64_and# mask input))
                                              output)]
                (when (is Natural (.i64_right# <shift> input))
                  0
                  output'

                  input'
                  (again input' output')))))))

      (the (projection repr)
        (let [input_size (.text_size# repr)]
          (if (..> 0 input_size)
            (loop (again [idx 0
                          output 0])
              (if (..< input_size idx)
                (when (<to_value> (.text_char# idx repr))
                  {.#Some digit_value}
                  (again (++ idx)
                         (|> output
                             (.i64_left# <shift>)
                             (.i64_or# digit_value)))

                  _
                  {try.#Failure (.text_composite# <error> repr)})
                {try.#Success output}))
            {try.#Failure (.text_composite# <error> repr)})))))]

  [1 binary binary_character      binary_value      "Invalid binary syntax for Natural: "]
  [3 octal  octal_character       octal_value       "Invalid octal syntax for Natural: "]
  [4 hex    hexadecimal_character hexadecimal_value "Invalid hexadecimal syntax for Natural: "]
  )

(the _expansion#let (.in_module# .prelude .expansion#let))

(the as_natural (.in_module# .prelude .as_natural))
(the as_text (.in_module# .prelude .as_text))
(the natural#injection (.in_module# .prelude .natural#injection))

(the single_digit_range 10)
(the last_single_digit (-- single_digit_range))

(the double_digit_range (* ..single_digit_range ..single_digit_range))
(the last_double_digit (-- double_digit_range))

(the multi_digit_range (* ..single_digit_range ..double_digit_range))
(the last_multi_digit (-- multi_digit_range))

(with_template' [,name ,last ,padding ,limit]
  [(the ,name
     (macro (_ _ lux)
       {.#Right [lux (loop (next [natural ,last
                                  output (is (List Code) (list))])
                       (let [output {.#Item (as_natural natural)
                                            {.#Item (as_text (.text_composite# ,padding (natural#injection natural)))
                                                    output}}]
                         (when natural
                           ,limit output
                           _ (next (-- natural) output))))]}))]

  [leading_multi_decimals ..last_multi_digit "" 0]
  
  [non_leading_single_decimals ..last_single_digit "00" 0]
  [non_leading_double_decimals ..last_double_digit "0" ..single_digit_range]
  [non_leading_multi_decimals ..last_multi_digit "" ..double_digit_range]
  )

(the (leading_decimals value)
  (-> Natural
      Text)
  (`` (when value
        (,, (leading_multi_decimals))

        _
        (undefined))))

(the (non_leading_decimals value)
  (-> Natural
      Text)
  (`` (when value
        (,, (non_leading_single_decimals))
        (,, (non_leading_double_decimals))
        (,, (non_leading_multi_decimals))

        _
        (undefined))))

(the .public base_10
  (Format Text Natural)
  (implementation
   (the (injection value)
     (loop (injection [input value
                       output ""])
       (if (..< ..multi_digit_range input)
         (.text_composite# (leading_decimals input)
                           output)
         (injection (../ ..multi_digit_range input)
                    (.text_composite# (non_leading_decimals (..% ..multi_digit_range input))
                                      output)))))

   (the (projection repr)
     (let [input_size (.text_size# repr)]
       (<| (_expansion#let [<failure> {try.#Failure (.text_composite# "Invalid decimal syntax for Natural: " repr)}])
           (if (..> 0 input_size)
             (loop (again [idx 0
                           output 0])
               (if (..< input_size idx)
                 (when (decimal_value (.text_char# idx repr))
                   {.#None}
                   <failure>

                   {.#Some digit_value}
                   (again (++ idx)
                          (|> output (..* 10) (..+ digit_value))))
                 {try.#Success output}))
             <failure>))))))

(the .public hash
  (Hash Natural)
  (implementation
   (the equivalence ..equivalence)
   (the hash function.identity)))

(the .public (padded expected_size format)
  (-> Natural (Format Text Natural)
      (Format Text Natural))
  (implementation
   (the (injection it)
     (loop (next [it (of format injection it)])
       (if (< expected_size (.text_size# it))
         (next (.text_composite# "0" it))
         it)))

   (the projection
     (of format projection))))

(alias [base_02] ..binary)
(alias [base_08] ..octal)
... (alias [base_10] ..decimal)
(alias [base_16] ..hex)
