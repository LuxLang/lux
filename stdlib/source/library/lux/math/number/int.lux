... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [hash (.only Hash)]
    [enum (.only Enum)]
    [interval (.only Interval)]
    [monoid (.only Monoid)]
    [equivalence (.only Equivalence)]
    [format (.only Format)]
    ["[0]" order (.only Order)]]
   [control
    ["[0]" maybe]
    ["[0]" try (.only Try)]
    [function
     [predicate (.only Predicate)]]]]]
 ["[0]" //
  ["[1][0]" nat]
  ["[1][0]" i64]
  [//
   [arithmetic (.only Arithmetic)]]])

(with_template [<name> <op>]
  [(the .public (<name> param subject)
     (-> Int Int
         Bit)
     (<op> param subject))]

  [= .i64_=#]
  [< .int_<#]
  )

(the .public (<= reference it)
  (-> Int Int
      Bit)
  (if (.int_<# reference it)
    true
    (.i64_=# reference it)))

(the .public (> reference it)
  (-> Int Int
      Bit)
  (.int_<# it reference))

(the .public (>= reference it)
  (-> Int Int
      Bit)
  (if (.int_<# it reference)
    true
    (.i64_=# reference it)))

(with_template [<comparison> <name>]
  [(the .public <name>
     (Predicate Int)
     (<comparison> +0))]

  [..> positive?]
  [..< negative?]
  [..= zero?]
  )

(with_template [<name> <test>]
  [(the .public (<name> left right)
     (-> Int Int
         Int)
     (if (<test> right left)
       left
       right))]

  [min ..<]
  [max ..>]
  )

(with_template [<name> <op>]
  [(the .public (<name> param subject)
     (-> Int Int
         Int)
     (<op> param subject))]

  [+ .i64_+#]
  [- .i64_-#]
  [* .int_*#]
  [/ .int_/#]
  [% .int_%#]
  )

(the .public arithmetic
  (Arithmetic Int)
  (implementation
   (the + ..+)
   (the - ..-)
   (the * ..*)
   (the / ../)))

(the .public (/% param subject)
  (-> Int Int
      [Int Int])
  [(../ param subject)
   (..% param subject)])

(the .public (opposite it)
  (-> Int
      Int)
  (..- it +0))

(the .public (abs it)
  (-> Int
      Int)
  (if (..< +0 it)
    (..* -1 it)
    it))

(the .public (signum it)
  (-> Int
      Int)
  (cond (..= +0 it) +0
        (..< +0 it) -1
        ... else
        +1))

... https://rob.conery.io/2018/08/21/mod-and-remainder-are-not-the-same/
(the .public (mod divisor dividend)
  (-> Int Int
      Int)
  (let [remainder (..% divisor dividend)]
    (if (or (and (..< +0 divisor)
                 (..> +0 remainder))
            (and (..> +0 divisor)
                 (..< +0 remainder)))
      (..+ divisor remainder)
      remainder)))

(the .public even?
  (-> Int
      Bit)
  (|>> (..% +2) (.i64_=# +0)))

(the .public odd?
  (-> Int
      Bit)
  (|>> ..even? not))

... https://en.wikipedia.org/wiki/Greatest_common_divisor
(the .public (gcd a b)
  (-> Int Int
      Int)
  (when b
    +0 a
    _ (gcd b (..% b a))))

(the .public (co_prime? a b)
  (-> Int Int
      Bit)
  (..= +1 (..gcd a b)))

... https://en.wikipedia.org/wiki/Extended_Euclidean_algorithm
(the .public (extended_gcd a b)
  (-> Int Int
      [[Int Int] Int])
  (loop (again [x +1 x1 +0
                y +0 y1 +1
                a1 a b1 b])
    (when b1
      +0 [[x y] a1]
      _ (let [q (/ b1 a1)]
          (again x1 (- (* q x1) x)
                 y1 (- (* q y1) y)
                 b1 (- (* q b1) a1))))))

... https://en.wikipedia.org/wiki/Least_common_multiple
(`` (the .public (lcm a b)
      (-> Int Int
          Int)
      (when [a b]
        (,, (with_template [<pattern>]
              [<pattern>
               +0]

              [[_ +0]]
              [[+0 _]]))

        _
        (|> a (/ (gcd a b)) (* b)))))

(the .public dec
  (-> Int
      Dec)
  (|>> .int_f64#))

(the .public equivalence
  (Equivalence Int)
  (implementation
   (the = ..=)))

(the .public order
  (Order Int)
  (implementation
   (the equivalence ..equivalence)
   (the < ..<)))

(the .public enum
  (Enum Int)
  (implementation
   (the order ..order)
   (the succ ++)
   (the pred --)))

... TODO: Find out why the numeric literals fail during JS compilation.
(the .public interval
  (Interval Int)
  (implementation
   (the enum ..enum)
   (the top
     ... +9,223,372,036,854,775,807
     (let [half (//i64.left_shifted 62 +1)]
       (+ half
          (-- half))))
   (the bottom
     ... -9,223,372,036,854,775,808
     (//i64.left_shifted 63 +1))))

(with_template [<name> <composite> <identity>]
  [(the .public <name>
     (Monoid Int)
     (implementation
      (the identity <identity>)
      (the composite <composite>)))]

  [addition       ..+   +0]
  [multiplication ..*   +1]
  [maximum        ..max (of ..interval bottom)]
  [minimum        ..min (of ..interval top)]
  )

(the -sign "-")
(the +sign "+")

(with_template [<struct> <format> <error>]
  [(the .public <struct>
     (Format Text Int)
     (implementation
      (the (injection value)
        (if (..< +0 value)
          (|> value ++ ..opposite .nat ++ (of <format> injection) (.text_composite# ..-sign))
          (|> value .nat (of <format> injection) (.text_composite# ..+sign))))

      (the (projection repr)
        (let [input_size (.text_size# repr)]
          (if (//nat.> 1 input_size)
            (when (.text_clip# 0 1 repr)
              ..+sign
              (|> repr
                  (.text_clip# 1 (-- input_size))
                  (of <format> projection)
                  (of try.functor each (|>> .int)))
              
              ..-sign
              (|> repr
                  (.text_clip# 1 (-- input_size))
                  (of <format> projection)
                  (of try.functor each (|>> -- .int ..opposite --)))
              
              _
              {try.#Failure <error>})
            {try.#Failure <error>})))))]

  [binary  //nat.binary  "Invalid binary syntax for Int: "]
  [octal   //nat.octal   "Invalid octal syntax for Int: "]
  [decimal //nat.decimal "Invalid syntax for Int: "]
  [hex     //nat.hex     "Invalid hexadecimal syntax for Int: "]
  )

(the .public hash
  (Hash Int)
  (implementation
   (the equivalence ..equivalence)
   (the hash (|>> .nat))))

(the expansion#let (.in_module# .prelude .expansion#let))

(the .public (right_shifted parameter subject)
  (-> Nat Int
      Int)
  (<| (expansion#let [<positive> (//i64.right_shifted parameter subject)])
      (if (< +0 subject)
        (|> +1
            (//i64.left_shifted parameter)
            --
            (//i64.left_shifted (//nat.- parameter //i64.width))
            (//i64.or <positive>))
        <positive>)))

(alias [base_02] ..binary)
(alias [base_08] ..octal)
(alias [base_10] ..decimal)
(alias [base_16] ..hex)
