... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Natural as for)
   [abstract
    ["[0]" equivalence]
    ["[0]" hash]
    ["[0]" order]]
   [data
    ["[0]" binary
     ["[1]" \\injection]]]
   [math
    ["[0]" random]]
   [type
    ["[0]" nominal]]
   [macro
    ["[0]" template]]]]
 ["[0]" // (.only)
  [//
   ["[0]" i64]]])

(the .public for
  (template.macro (_ ,size ,injection)
    [(nominal.every .public Natural
       .Natural

       (alias [Number]
              Natural)

       (the .public as
         (-> Natural
             .Natural)
         (|>> nominal.representation))

       (the .public hash
         (hash.Hash Natural)
         (by hash.functor each as //.hash))

       (the .public order
         (order.Order Natural)
         (by order.functor each as //.order))

       (order.for [] Natural order)

       (the .public equivalence
         (equivalence.Equivalence Natural)
         (by order equivalence))

       (alias [=]
              equivalence)

       (the .public size ,size)

       (the .public maximum
         Natural
         (|> size
             (//.* i64.bits_per_byte)
             i64.mask
             nominal.abstraction))

       (the .public of
         (-> .Natural
             Natural)
         (|>> (i64.and (nominal.representation maximum))
              nominal.abstraction))

       (the .public (+ parameter it)
         (-> Natural Natural
             Natural)
         (|> (//.+ (nominal.representation parameter)
                   (nominal.representation it))
             (i64.and (nominal.representation maximum))
             nominal.abstraction))

       (the .public (- parameter it)
         (-> Natural Natural
             Natural)
         (|> (//.- (nominal.representation parameter)
                   (nominal.representation it))
             (i64.and (nominal.representation maximum))
             nominal.abstraction))

       (the .public as_binary
         (binary.Injection Natural)
         (|>> nominal.representation
              ,injection))

       (the .public random
         (random.Random Natural)
         (by random.functor each of random.natural))
       )]))

(..for 1
       binary.bits_08)
