(.require
 [library
  [lux (.except)
   ["@" target]
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" io]
    [concurrency
     ["[0]" thread]]
    ["<>" parser (.only)
     ["<[0]>" code]
     ["<[0]>" cli]]]
   [macro (.only with_symbols)
    [syntax (.only syntax)]
    ["[0]" code]]]])

(type: Arguments
  (Variant
   {#Raw Text}
   {#Parsed (List Code)}))

(def arguments^
  (<code>.Parser Arguments)
  (<>.or <code>.local
         (<code>.tuple (<>.some <code>.any))))

(def .public program:
  (syntax (_ [args ..arguments^
              body <code>.any])
    (with_symbols [g!program g!args g!_ g!output g!message]
      (let [initialization+event_loop (for @.old body
                                           @.jvm body
                                           @.js body
                                           @.python body
                                           ... else
                                           (` ((~! do) (~! io.monad)
                                               [(~ g!output) (~ body)
                                                (~ g!_) (~! thread.run!)]
                                               ((~' in) (~ g!output)))))]
        (in (list (` ("lux def program"
                      (~ (case args
                           {#Raw args}
                           (` (.function ((~ g!program) (~ (code.symbol ["" args])))
                                (~ initialization+event_loop)))
                           
                           {#Parsed args}
                           (` (.function ((~ g!program) (~ g!args))
                                (case ((~! <cli>.result) (.is (~! (<cli>.Parser (io.IO .Any)))
                                                              ((~! do) (~! <>.monad)
                                                               [(~+ args)
                                                                (~ g!_) (~! <cli>.end)]
                                                               ((~' in) (~ initialization+event_loop))))
                                       (~ g!args))
                                  {.#Right (~ g!output)}
                                  (~ g!output)

                                  {.#Left (~ g!message)}
                                  (.panic! (~ g!message)))))))))))))))
