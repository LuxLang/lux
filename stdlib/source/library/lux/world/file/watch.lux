... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except all and)
   ["[0]" ffi (.only import)]
   [abstract
    [monad (.only do)]]
   [concurrency
    ["[0]" future (.only Future)]
    ["[0]" stm (.only STM Var)]]
   [control
    ["[0]" io (.only IO)]
    ["[0]" maybe]]
   [error
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [function
    [predicate (.only Predicate)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" dictionary (.only Dictionary)]
     ["[0]" stack (.use "[1]#[0]" functor monoid mix)]
     ["[0]" set]
     ["[0]" array]]]
   [math
    [number
     ["n" natural]]]
   [macro
    ["[0]" expansion]
    ["[0]" template]]
   [type
    ["[0]" nominal]]
   [time
    ["[0]" instant (.only Instant)]]]]
 ["[0]" //])

(nominal.every .public Concern
  (Record
   [#creation Bit
    #modification Bit
    #deletion Bit])

  (the none
    Concern
    (nominal.abstraction
     [#creation false
      #modification false
      #deletion false]))

  (template.with [<concern> <predicate> <event> <create> <modify> <delete>]
    [(the .public <concern>
       Concern
       (nominal.abstraction
        [#creation <create>
         #modification <modify>
         #deletion <delete>]))

     (the .public <predicate>
       (Predicate Concern)
       (|>> nominal.reification
            (its <event>)))]

    [creation creation? #creation
     true false false]
    [modification modification? #modification
     false true false]
    [deletion deletion? #deletion
     false false true]
    )

  (the .public (and left right)
    (-> Concern Concern
        Concern)
    (nominal.abstraction
     [#creation (or (..creation? left) (..creation? right))
      #modification (or (..modification? left) (..modification? right))
      #deletion (or (..deletion? left) (..deletion? right))]))

  (the .public all
    Concern
    (.all ..and
          ..creation
          ..modification
          ..deletion
          ))
  )

(every .public (Watcher !)
  (Interface
   (is (-> Concern //.Path
           (! (Try Any)))
       start)
   (is (-> //.Path
           (! (Try Concern)))
       concern)
   (is (-> //.Path
           (! (Try Concern)))
       stop)
   (is (-> []
           (! (Try (Stack [Concern //.Path]))))
       poll)))

(template.with [<name>]
  [(exception.the .public (<name> path)
     (Exception //.Path)
     (exception.report
      (list ["Path" (%.text path)])))]

  [not_being_watched]
  [cannot_poll_a_non_existent_directory]
  )

(every File_Tracker
  (Dictionary //.Path Instant))

(every Directory_Tracker
  (Dictionary //.Path [Concern File_Tracker]))

(the (update_watch! new_concern path tracker)
  (-> Concern //.Path (Var Directory_Tracker)
      (STM Bit))
  (do [! stm.monad]
    [@tracker (stm.read tracker)]
    (when (dictionary.value path @tracker)
      {try.#Success [old_concern last_modified]}
      (do !
        [_ (stm.update (dictionary.has path [new_concern last_modified]) tracker)]
        (in true))
      
      failure
      (in false))))

(the (file_tracker fs directory)
  (-> (//.System Future) //.Path
      (Future (Try File_Tracker)))
  (do [! (try.with future.monad)]
    [files (by fs directory_files directory)]
    (stack.mix' !
                (function (_ file tracker)
                  (do !
                    [last_modified (by fs last_modified file)]
                    (in (dictionary.has file last_modified tracker))))
                (is File_Tracker
                    (dictionary.empty text.hash))
                files)))

(the (available_files fs directory)
  (-> (//.System Future) //.Path
      (Future (Try (Stack [//.Path Instant]))))
  (do [! (try.with future.monad)]
    [files (by fs directory_files directory)]
    (stack.each' ! (function (_ file)
                     (|> file
                         (by fs last_modified)
                         (by ! each (|>> [file]))))
                 files)))

(the (available_directory_changes fs [directory [the_concern file_tracker]])
  (-> (//.System Future) [//.Path [Concern File_Tracker]]
      (Future (Try [[//.Path [Concern File_Tracker]]
                    [(Stack [//.Path Instant])
                     (Stack [//.Path Instant Instant])
                     (Stack //.Path)]])))
  (do [! (try.with future.monad)]
    [current_files (..available_files fs directory)
     .let [creations (if (..creation? the_concern)
                       (stack.only (|>> product.left (dictionary.key? file_tracker) not)
                                   current_files)
                       (stack))
           available (|> current_files
                         (stack#each product.left)
                         (set.of_stack text.hash))
           deletions (if (..deletion? the_concern)
                       (|> (dictionary.entries file_tracker)
                           (stack#each product.left)
                           (stack.only (|>> (set.member? available) not)))
                       (stack))
           modifications (stack.all (function (_ [path current_modification])
                                      (do maybe.monad
                                        [previous_modification (dictionary.value path file_tracker)]
                                        (in [path previous_modification current_modification])))
                                    current_files)]]
    (in [[directory
          [the_concern
           (let [with_deletions (stack#mix dictionary.lacks file_tracker deletions)
                 with_creations (stack#mix (function (_ [path last_modified] tracker)
                                             (dictionary.has path last_modified tracker))
                                           with_deletions
                                           creations)
                 with_modifications (stack#mix (function (_ [path previous_modification current_modification] tracker)
                                                 (dictionary.has path current_modification tracker))
                                               with_creations
                                               modifications)]
             with_modifications)]]
         [creations
          modifications
          deletions]])))

(the .public (polling fs)
  (-> (//.System Future)
      (Watcher Future))
  (let [tracker (is (Var Directory_Tracker)
                    (stm.var (dictionary.empty text.hash)))]
    (implementation
     (the (start new_concern path)
       (do [! future.monad]
         [exists? (by fs directory? path)]
         (if exists?
           (do !
             [updated? (stm.commit! (..update_watch! new_concern path tracker))]
             (if updated?
               (in {try.#Success []})
               (do (try.with !)
                 [file_tracker (..file_tracker fs path)]
                 (do !
                   [_ (stm.commit! (stm.update (dictionary.has path [new_concern file_tracker]) tracker))]
                   (in {try.#Success []})))))
           (in (exception.except ..cannot_poll_a_non_existent_directory [path])))))
     (the (concern path)
       (stm.commit!
        (do stm.monad
          [@tracker (stm.read tracker)]
          (in (when (dictionary.value path @tracker)
                {try.#Success [it file_tracker]}
                {try.#Success it}

                failure
                (exception.except ..not_being_watched [path]))))))
     (the (stop path)
       (stm.commit!
        (do [! stm.monad]
          [@tracker (stm.read tracker)]
          (when (dictionary.value path @tracker)
            {try.#Success [the_concern file_tracker]}
            (do !
              [_ (stm.update (dictionary.lacks path) tracker)]
              (in {try.#Success the_concern}))

            failure
            (in (exception.except ..not_being_watched [path]))))))
     (the (poll _)
       (do future.monad
         [@tracker (stm.commit! (stm.read tracker))]
         (do [! (try.with future.monad)]
           [changes (|> @tracker
                        dictionary.entries
                        (stack.each' ! (..available_directory_changes fs)))
            _ (do future.monad
                [_ (stm.commit! (stm.write (|> changes
                                               (stack#each product.left)
                                               (dictionary.of_stack text.hash))
                                           tracker))]
                (in {try.#Success []}))
            .let [[creations modifications deletions]
                  (stack#mix (function (_ [_ [creations modifications deletions]]
                                          [all_creations all_modifications all_deletions])
                               [(stack#composite creations all_creations)
                                (stack#composite modifications all_modifications)
                                (stack#composite deletions all_deletions)])
                             [(stack) (stack) (stack)]
                             changes)]]
           (in (.all stack#composite
                     (stack#each (|>> product.left [..creation]) creations)
                     (|> modifications
                         (stack.only (function (_ [path previous_modification current_modification])
                                       (not (instant.= previous_modification current_modification))))
                         (stack#each (|>> product.left [..modification])))
                     (stack#each (|>> [..deletion]) deletions)
                     )))))
     )))

(the .public (memory delimiter)
  (-> Text
      [(//.System Future) (Watcher Future)])
  (let [fs (//.memory delimiter)]
    [fs
     (..polling fs)]))

(for .jvm
     (these (import java/lang/Object
              "[1]::[0]")

            (import java/lang/String
              "[1]::[0]")

            (import (java/util/List a)
              "[1]::[0]"
              (size [] int)
              (get [int] a))

            (the (default_list stack)
              (for_any (_ it)
                (-> (java/util/List it)
                    (Stack it)))
              (let [size (.natural (ffi.of_int (java/util/List::size [] stack)))]
                (loop (again [idx 0
                              output {.#Empty}])
                  (if (n.< size idx)
                    (again (++ idx)
                           {.#Top (java/util/List::get [(ffi.as_int (.integer idx))] stack)
                                  output})
                    output))))
            
            (import (java/nio/file/WatchEvent$Kind it)
              "[1]::[0]")

            (import (java/nio/file/WatchEvent it)
              "[1]::[0]"
              (kind [] (java/nio/file/WatchEvent$Kind it)))

            (import java/nio/file/Watchable
              "[1]::[0]")

            (import java/nio/file/Path
              "[1]::[0]"
              (register [java/nio/file/WatchService [(java/nio/file/WatchEvent$Kind [? < java/lang/Object])]] "io" "try" java/nio/file/WatchKey)
              (toString [] java/lang/String))

            (import java/nio/file/StandardWatchEventKinds
              "[1]::[0]"
              ("read_only" "static" ENTRY_CREATE (java/nio/file/WatchEvent$Kind java/nio/file/Path))
              ("read_only" "static" ENTRY_MODIFY (java/nio/file/WatchEvent$Kind java/nio/file/Path))
              ("read_only" "static" ENTRY_DELETE (java/nio/file/WatchEvent$Kind java/nio/file/Path)))

            (the (default_event_concern event)
              (for_any (_ it)
                (-> (java/nio/file/WatchEvent it)
                    Concern))
              (let [kind (as (java/nio/file/WatchEvent$Kind java/nio/file/Path)
                             (java/nio/file/WatchEvent::kind [] event))]
                (if (same? (java/nio/file/StandardWatchEventKinds::ENTRY_CREATE)
                           kind)
                  ..creation
                  
                  (same? (java/nio/file/StandardWatchEventKinds::ENTRY_MODIFY)
                         kind)
                  ..modification
                  
                  (same? (java/nio/file/StandardWatchEventKinds::ENTRY_DELETE)
                         kind)
                  ..deletion
                  
                  ... else
                  ..none
                  )))

            (import java/nio/file/WatchKey
              "[1]::[0]"
              (reset [] "io" boolean)
              (cancel [] "io" void)
              (watchable [] java/nio/file/Watchable)
              (pollEvents [] "io" (java/util/List (java/nio/file/WatchEvent ?))))

            (the default_key_concern
              (-> java/nio/file/WatchKey
                  (IO Concern))
              (|>> (java/nio/file/WatchKey::pollEvents [])
                   (by io.monad each (|>> ..default_list
                                          (stack#each default_event_concern)
                                          (stack#mix ..and ..none)))))

            (import java/nio/file/WatchService
              "[1]::[0]"
              (poll [] "io" "try" "?" java/nio/file/WatchKey))

            (import java/nio/file/FileSystem
              "[1]::[0]"
              (newWatchService [] "io" "try" java/nio/file/WatchService))

            (import java/nio/file/FileSystems
              "[1]::[0]"
              ("static" getDefault [] java/nio/file/FileSystem))

            (import java/io/File
              "[1]::[0]"
              (new [java/lang/String])
              (toPath [] java/nio/file/Path))

            (every Watch_Event
              (java/nio/file/WatchEvent$Kind java/lang/Object))

            (the (default_start watch_events watcher path)
              (-> (Stack Watch_Event) java/nio/file/WatchService //.Path
                  (Future (Try java/nio/file/WatchKey)))
              (let [watch_events' (stack#mix (function (_ [index watch_event] watch_events')
                                               (ffi.write! index watch_event watch_events'))
                                             (ffi.array (java/nio/file/WatchEvent$Kind java/lang/Object)
                                                        (stack.size watch_events))
                                             (stack.enumeration watch_events))]
                (future.soon
                 (java/nio/file/Path::register [watcher
                                                watch_events']
                                               (|> path ffi.as_string [] java/io/File::new (java/io/File::toPath []))))))

            (the (default_poll watcher)
              (-> java/nio/file/WatchService
                  (IO (Try (Stack [Concern //.Path]))))
              (loop (again [output (is (Stack [Concern //.Path])
                                       (stack))])
                (do (try.with io.monad)
                  [?key (java/nio/file/WatchService::poll [] watcher)]
                  (when ?key
                    {.#Some key}
                    (do [! io.monad]
                      [valid? (java/nio/file/WatchKey::reset [] key)]
                      (if (ffi.of_boolean valid?)
                        (do !
                          [.let [path (|> key
                                          (java/nio/file/WatchKey::watchable [])
                                          (as java/nio/file/Path)
                                          (java/nio/file/Path::toString [])
                                          ffi.of_string
                                          (as //.Path))]
                           the_concern (..default_key_concern key)]
                          (again {.#Top [the_concern path]
                                        output}))
                        (again output)))
                    
                    {.#None}
                    (in output)))))

            (the (watch_events concern)
              (-> Concern
                  (Stack Watch_Event))
              (.all stack#composite
                    (if (..creation? concern)
                      (stack (as Watch_Event (java/nio/file/StandardWatchEventKinds::ENTRY_CREATE)))
                      (stack))
                    (if (..modification? concern)
                      (stack (as Watch_Event (java/nio/file/StandardWatchEventKinds::ENTRY_MODIFY)))
                      (stack))
                    (if (..deletion? concern)
                      (stack (as Watch_Event (java/nio/file/StandardWatchEventKinds::ENTRY_DELETE)))
                      (stack))
                    ))

            (the .public default
              (IO (Try (Watcher Future)))
              (do (try.with io.monad)
                [watcher (|> (java/nio/file/FileSystems::getDefault [])
                             (java/nio/file/FileSystem::newWatchService []))
                 .let [tracker (stm.var (is (Dictionary //.Path [Concern java/nio/file/WatchKey])
                                            (dictionary.empty text.hash)))

                       stop (is (-> //.Path (Future (Try Concern)))
                                (function (_ path)
                                  (do [! future.monad]
                                    [@tracker (stm.commit! (stm.read tracker))]
                                    (when (dictionary.value path @tracker)
                                      {try.#Success [the_concern key]}
                                      (do !
                                        [_ (future.soon
                                            (java/nio/file/WatchKey::cancel [] key))
                                         _ (stm.commit! (stm.update (dictionary.lacks path) tracker))]
                                        (in {try.#Success the_concern}))

                                      failure
                                      (in (exception.except ..not_being_watched [path]))))))]]
                (in (is (Watcher Future)
                        (implementation
                         (the (start the_concern path)
                           (do future.monad
                             [?concern (stop path)]
                             (do (try.with future.monad)
                               [key (..default_start (..watch_events (..and (try.else ..none ?concern)
                                                                            the_concern))
                                                     watcher
                                                     path)]
                               (do future.monad
                                 [_ (stm.commit! (stm.update (dictionary.has path [the_concern key]) tracker))]
                                 (in {try.#Success []})))))
                         (the (concern path)
                           (do future.monad
                             [@tracker (stm.commit! (stm.read tracker))]
                             (when (dictionary.value path @tracker)
                               {try.#Success [it key]}
                               (in {try.#Success it})

                               failure
                               (in (exception.except ..not_being_watched [path])))))
                         (the stop stop)
                         (the (poll _)
                           (future.soon (..default_poll watcher)))
                         )))))
            )

     ... else
     (these))
