... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except with)
   [abstract
    [monad (.only Monad do)]]
   [error
    ["[0]" try (.only Try)]]
   [color
    ["[0]" terminal]]
   [data
    [text
     ["%" \\injection]]]
   [macro
    ["[0]" template]]
   [time
    ["[0]" instant (.only Instant)]]
   [world
    ["[0]" console (.only Console)]]]])

(every .public (Logger !)
  (Interface
   (is (-> Text
           (! (Try Any)))
       log)))

(template.with [<prefix> <name> <foreground>]
  [(the .public (<name> message it)
     (for_any (_ !)
       (-> Text (Logger !)
           (! (Try Any))))
     (by it log (terminal.with <foreground>
                  (text <prefix> message))))]

  [["[ERROR]   " error terminal.red_foreground]
   ["[WARNING] " warn terminal.yellow_foreground]
   ["[INFO]    " info terminal.white_foreground]
   ["[DEBUG]   " debug terminal.cyan_foreground]])

(the .public (with scope it)
  (for_any (_ !)
    (-> (-> Text Text)
        (-> (Logger !) (Logger !))))
  (implementation
   (the log
     (|>> scope
          (by it log)))))

(the .public (timed ! now it)
  (for_any (_ !)
    (-> (Monad !) (-> Any (! Instant))
        (-> (Logger !) (Logger !))))
  (implementation
   (the (log message)
     (do !
       [now (now [])]
       (by it log (text (terminal.with terminal.green_foreground
                          (text "[" (instant.as_text now) "]"))
                        " "
                        message))))))

(the .public (console it)
  (for_any (_ !)
    (-> (Console !)
        (Logger !)))
  (implementation
   (the (log message)
     (console.write_line message it))))
