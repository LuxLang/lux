... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except with)
   [abstract
    [monad (.only Monad do)]]
   [control
    ["[0]" try (.only Try)]]
   [data
    [color
     ["[0]" terminal]]
    [text
     ["%" \\injection]]]
   [meta
    [macro
     ["[0]" template]]]
   [world
    ["[0]" console (.only Console)]
    [time
     [instant (.only Instant)]]]]])

(every .public (Logger !)
  (Interface
   (is (-> Text
           (! (Try Any)))
       log)))

(template.with [<prefix> <name> <foreground>]
  [(the .public (<name> message it)
     (for_any (_ !)
       (-> Text (Logger !)
           (! (Try Any))))
     (of it log (terminal.with <foreground>
                  (%.message <prefix> message))))]

  ["[ERROR]   " error terminal.red_foreground]
  ["[WARNING] " warn terminal.yellow_foreground]
  ["[INFO]    " info terminal.white_foreground]
  ["[DEBUG]   " debug terminal.cyan_foreground]
  )

(the .public (with scope it)
  (for_any (_ !)
    (-> (-> Text Text)
        (-> (Logger !) (Logger !))))
  (implementation
   (the log
     (|>> scope
          (of it log)))))

(the .public (timed ! now it)
  (for_any (_ !)
    (-> (Monad !) (-> Any (! Instant))
        (-> (Logger !) (Logger !))))
  (implementation
   (the (log message)
     (do !
       [now (now [])]
       (of it log (%.message (terminal.with terminal.green_foreground
                               (%.message "[" (%.instant now) "]"))
                             " "
                             message))))))

(the .public (console it)
  (for_any (_ !)
    (-> (Console !)
        (Logger !)))
  (implementation
   (the (log message)
     (console.write_line message it))))
