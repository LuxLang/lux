(.module:
  [library
   [lux #*
    ["@" target]
    ["jvm" ffi (#+ import:)]
    [abstract
     [monad (#+ do)]]
    [control
     ["." function]
     ["." try (#+ Try)]
     ["." exception (#+ exception:)]
     ["." io (#+ IO)]
     [security
      ["?" policy (#+ Context Safety Safe)]]
     [concurrency
      ["." atom (#+ Atom)]
      ["." async (#+ Async)]]
     [parser
      [environment (#+ Environment)]]]
    [data
     ["." product]
     ["." text
      ["%" format (#+ format)]
      [encoding
       ["." utf8]]]
     [collection
      ["." array (#+ Array)]
      ["." list ("#\." mix functor)]
      ["." dictionary]]]
    [math
     [number (#+ hex)
      ["n" nat]]]]]
  [//
   [file (#+ Path)]])

(type: .public Exit
  {#.doc (example "A program exit code.")}
  Int)

(template [<code> <name>]
  [(def: .public <name>
     Exit
     <code>)]

  [+0 normal]
  [+1 error]
  )

(interface: .public (Process !)
  {#.doc (example "The means for communicating with a program/process being executed by the operating system.")}
  
  (: (-> [] (! (Try Text)))
     read)
  (: (-> [] (! (Try Text)))
     error)
  (: (-> Text (! (Try Any)))
     write)
  (: (-> [] (! (Try Any)))
     destroy)
  (: (-> [] (! (Try Exit)))
     await))

(def: (async_process process)
  (-> (Process IO) (Process Async))
  (`` (implementation
       (~~ (template [<method>]
             [(def: <method>
                (|>> (\ process <method>)
                     async.future))]

             [read]
             [error]
             [write]
             [destroy]
             [await]
             )))))

(type: .public Command
  {#.doc (example "A command that can be executed by the operating system.")}
  Text)

(type: .public Argument
  {#.doc (example "A parameter for a command.")}
  Text)

(interface: .public (Shell !)
  {#.doc (example "The means for issuing commands to the operating system.")}
  
  (: (-> [Environment Path Command (List Argument)] (! (Try (Process !))))
     execute))

(def: .public (async shell)
  (-> (Shell IO) (Shell Async))
  (implementation
   (def: (execute input)
     (async.future
      (do (try.with io.monad)
        [process (\ shell execute input)]
        (in (..async_process process)))))))

... https://en.wikipedia.org/wiki/Code_injection#Shell_injection
(interface: (Policy ?)
  (: (-> Command (Safe Command ?))
     command)
  (: (-> Argument (Safe Argument ?))
     argument)
  (: (All [a] (-> (Safe a ?) a))
     value))

(type: (Sanitizer a)
  (-> a a))

(type: Replacer
  (-> Text Text))

(def: (replaced bad replacer)
  (-> Text Replacer (-> Text Text))
  (text.replaced bad (replacer bad)))

(def: safe_common_command
  (-> Replacer (Sanitizer Command))
  (let [x0A (text.of_char (hex "0A"))
        xFF (text.of_char (hex "FF"))]
    (function (_ replacer)
      (|>> (..replaced x0A replacer)
           (..replaced xFF replacer)
           (..replaced "\" replacer)
           (..replaced "&" replacer)
           (..replaced "#" replacer)
           (..replaced ";" replacer)
           (..replaced "`" replacer)
           (..replaced "|" replacer)
           (..replaced "*" replacer)
           (..replaced "?" replacer)
           (..replaced "~" replacer)
           (..replaced "^" replacer)
           (..replaced "$" replacer)
           (..replaced "<" replacer) (..replaced ">" replacer)
           (..replaced "(" replacer) (..replaced ")" replacer)
           (..replaced "[" replacer) (..replaced "]" replacer)
           (..replaced "{" replacer) (..replaced "}" replacer)))))

(def: (policy safe_command safe_argument)
  (Ex [?] (-> (Sanitizer Command) (Sanitizer Argument) (Policy ?)))
  (?.with_policy
    (: (Context Safety Policy)
       (function (_ (^open "?\."))
         (implementation
          (def: command (|>> safe_command ?\can_upgrade))
          (def: argument (|>> safe_argument ?\can_upgrade))
          (def: value ?\can_downgrade))))))

(def: unix_policy
  (let [replacer (: Replacer
                    (|>> (format "\")))
        safe_command (: (Sanitizer Command)
                        (..safe_common_command replacer))
        safe_argument (: (Sanitizer Argument)
                         (|>> (..replaced "'" replacer)
                              (text.enclosed' "'")))]
    (..policy safe_command safe_argument)))

(def: windows_policy
  (let [replacer (: Replacer
                    (function.constant " "))
        safe_command (: (Sanitizer Command)
                        (|>> (..safe_common_command replacer)
                             (..replaced "%" replacer)
                             (..replaced "!" replacer)))
        safe_argument (: (Sanitizer Argument)
                         (|>> (..replaced "%" replacer)
                              (..replaced "!" replacer)
                              (..replaced text.double_quote replacer)
                              (text.enclosed' text.double_quote)))]
    (..policy safe_command safe_argument)))

(with_expansions [<jvm> (as_is (import: java/lang/String
                                 ["#::."
                                  (toLowerCase [] java/lang/String)])

                               (def: (jvm::arguments_array arguments)
                                 (-> (List Argument) (Array java/lang/String))
                                 (product.right
                                  (list\mix (function (_ argument [idx output])
                                              [(++ idx) (jvm.write! idx
                                                                    (:as java/lang/String argument)
                                                                    output)])
                                            [0 (jvm.array java/lang/String (list.size arguments))]
                                            arguments)))

                               (import: (java/util/Map k v)
                                 ["#::."
                                  (put [k v] v)])

                               (def: (jvm::load_environment input target)
                                 (-> Environment
                                     (java/util/Map java/lang/String java/lang/String)
                                     (java/util/Map java/lang/String java/lang/String))
                                 (list\mix (function (_ [key value] target')
                                             (exec
                                               (java/util/Map::put (:as java/lang/String key)
                                                                   (:as java/lang/String value)
                                                                   target')
                                               target'))
                                           target
                                           (dictionary.entries input)))
                               
                               (import: java/io/Reader
                                 ["#::."
                                  (read [] #io #try int)])

                               (import: java/io/BufferedReader
                                 ["#::."
                                  (new [java/io/Reader])
                                  (readLine [] #io #try #? java/lang/String)])

                               (import: java/io/InputStream)
                               
                               (import: java/io/InputStreamReader
                                 ["#::."
                                  (new [java/io/InputStream])])

                               (import: java/io/OutputStream
                                 ["#::."
                                  (write [[byte]] #io #try void)])

                               (import: java/lang/Process
                                 ["#::."
                                  (getInputStream [] #io #try java/io/InputStream)
                                  (getErrorStream [] #io #try java/io/InputStream)
                                  (getOutputStream [] #io #try java/io/OutputStream)
                                  (destroy [] #io #try void)
                                  (waitFor [] #io #try int)])

                               (exception: .public no_more_output)

                               (def: (default_process process)
                                 (-> java/lang/Process (IO (Try (Process IO))))
                                 (do {! (try.with io.monad)}
                                   [jvm_input (java/lang/Process::getInputStream process)
                                    jvm_error (java/lang/Process::getErrorStream process)
                                    jvm_output (java/lang/Process::getOutputStream process)
                                    .let [jvm_input (|> jvm_input
                                                        java/io/InputStreamReader::new
                                                        java/io/BufferedReader::new)
                                          jvm_error (|> jvm_error
                                                        java/io/InputStreamReader::new
                                                        java/io/BufferedReader::new)]]
                                   (in (: (Process IO)
                                          (`` (implementation
                                               (~~ (template [<name> <stream>]
                                                     [(def: (<name> _)
                                                        (do !
                                                          [output (java/io/BufferedReader::readLine <stream>)]
                                                          (case output
                                                            (#.Some output)
                                                            (in output)

                                                            #.None
                                                            (\ io.monad in (exception.except ..no_more_output [])))))]

                                                     [read jvm_input]
                                                     [error jvm_error]
                                                     ))
                                               (def: (write message)
                                                 (java/io/OutputStream::write (\ utf8.codec encoded message) jvm_output))
                                               (~~ (template [<name> <method>]
                                                     [(def: (<name> _)
                                                        (<method> process))]

                                                     [destroy java/lang/Process::destroy]
                                                     [await java/lang/Process::waitFor]
                                                     ))))))))

                               (import: java/io/File
                                 ["#::."
                                  (new [java/lang/String])])

                               (import: java/lang/ProcessBuilder
                                 ["#::."
                                  (new [[java/lang/String]])
                                  (environment [] #try (java/util/Map java/lang/String java/lang/String))
                                  (directory [java/io/File] java/lang/ProcessBuilder)
                                  (start [] #io #try java/lang/Process)])

                               (import: java/lang/System
                                 ["#::."
                                  (#static getProperty [java/lang/String] #io #try java/lang/String)])

                               ... https://en.wikipedia.org/wiki/Code_injection#Shell_injection
                               (def: windows?
                                 (IO (Try Bit))
                                 (\ (try.with io.monad) each
                                    (|>> java/lang/String::toLowerCase (text.starts_with? "windows"))
                                    (java/lang/System::getProperty "os.name")))

                               (implementation: .public default
                                 (Shell IO)

                                 (def: (execute [environment working_directory command arguments])
                                   (do {! (try.with io.monad)}
                                     [.let [builder (|> (list& command arguments)
                                                        ..jvm::arguments_array
                                                        java/lang/ProcessBuilder::new
                                                        (java/lang/ProcessBuilder::directory (java/io/File::new working_directory)))]
                                      _ (|> builder
                                            java/lang/ProcessBuilder::environment
                                            (\ try.functor each (..jvm::load_environment environment))
                                            (\ io.monad in))
                                      process (java/lang/ProcessBuilder::start builder)]
                                     (..default_process process))))
                               )]
  (for {@.old (as_is <jvm>)
        @.jvm (as_is <jvm>)}
       (as_is)))

(interface: .public (Mock s)
  {#.doc (example "A simulated process.")}
  
  (: (-> s (Try [s Text]))
     on_read)
  (: (-> s (Try [s Text]))
     on_error)
  (: (-> Text s (Try s))
     on_write)
  (: (-> s (Try s))
     on_destroy)
  (: (-> s (Try [s Exit]))
     on_await))

(`` (implementation: (mock_process state mock)
      (All [s] (-> (Atom s) (Mock s) (Process IO)))

      (~~ (template [<name> <mock>]
            [(def: (<name> _)
               (do {! io.monad}
                 [|state| (atom.read! state)]
                 (case (\ mock <mock> |state|)
                   (#try.Success [|state| output])
                   (do !
                     [_ (atom.write! |state| state)]
                     (in (#try.Success output)))
                   
                   (#try.Failure error)
                   (in (#try.Failure error)))))]

            [read on_read]
            [error on_error]
            [await on_await]
            ))
      (def: (write message)
        (do {! io.monad}
          [|state| (atom.read! state)]
          (case (\ mock on_write message |state|)
            (#try.Success |state|)
            (do !
              [_ (atom.write! |state| state)]
              (in (#try.Success [])))
            
            (#try.Failure error)
            (in (#try.Failure error)))))
      (def: (destroy _)
        (do {! io.monad}
          [|state| (atom.read! state)]
          (case (\ mock on_destroy |state|)
            (#try.Success |state|)
            (do !
              [_ (atom.write! |state| state)]
              (in (#try.Success [])))
            
            (#try.Failure error)
            (in (#try.Failure error)))))))

(implementation: .public (mock mock init)
  (All [s]
    (-> (-> [Environment Path Command (List Argument)]
            (Try (Mock s)))
        s
        (Shell IO)))

  (def: execute
    (|>> mock
         (\ try.monad each (..mock_process (atom.atom init)))
         io.io)))
