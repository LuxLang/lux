... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    ["[0]" hash (.only Hash)]]
   [control
    ["[0]" maybe (.use "[1]#[0]" functor)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]
     ["[0]" encoding (.only Encoding)]]]
   [meta
    [type
     ["[0]" nominal]]]]]
 [/
  ["[0]" language (.only Language)]
  ["[0]" territory (.only Territory)]])

(nominal.every .public Locale
  Text

  (the territory_separator "_")
  (the encoding_separator ".")

  (the .public (locale language territory encoding)
    (-> Language (Maybe Territory) (Maybe Encoding) Locale)
    (nominal.abstraction
     (%.message (language.code language)
                (|> territory
                    (maybe#each (|>> territory.long_code (%.message ..territory_separator)))
                    (maybe.else ""))
                (|> encoding
                    (maybe#each (|>> encoding.name (%.message ..encoding_separator)))
                    (maybe.else "")))))

  (the .public code
    (-> Locale Text)
    (|>> nominal.representation))

  (the .public hash
    (Hash Locale)
    (of hash.functor each ..code text.hash))

  (the .public equivalence
    (Equivalence Locale)
    (of ..hash equivalence))

  (alias [=]
         ..equivalence)
  )
