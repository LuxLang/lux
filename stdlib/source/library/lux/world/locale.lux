... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    ["[0]" hash (.only Hash)]]
   [control
    ["[0]" maybe (.use "[1]#[0]" functor)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]
     ["[0]" encoding (.only Encoding)]]]
   [type
    ["[0]" nominal]]]]
 [/
  ["[0]" language (.only Language)]
  ["[0]" territory (.only Territory)]])

(nominal.every .public Locale
  Text

  ... https://en.wikipedia.org/wiki/Delimiter
  (the territory_delimiter "_")
  (the encoding_delimiter ".")

  (the .public (locale language territory encoding)
    (-> Language (Maybe Territory) (Maybe Encoding) Locale)
    (nominal.of
     (text (language.code language)
           (|> territory
               (maybe#each (|>> territory.long_code (text ..territory_delimiter)))
               (maybe.else ""))
           (|> encoding
               (maybe#each (|>> encoding.name (text ..encoding_delimiter)))
               (maybe.else "")))))

  (the .public code
    (-> Locale Text)
    (|>> nominal.as))

  (the .public hash
    (Hash Locale)
    (by hash.functor each ..code text.hash))

  (the .public equivalence
    (Equivalence Locale)
    (by ..hash equivalence))

  (alias [=]
         ..equivalence)
  )
