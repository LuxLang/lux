... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    ["[0]" hash (.only Hash)]]
   [control
    ["[0]" maybe (.use "[1]#[0]" functor)]]
   [data
    ["[0]" text (.only)
     ["%" \\format (.only format)]
     ["[0]" encoding (.only Encoding)]]]
   [meta
    [type
     ["[0]" nominal (.except def)]]]]]
 [/
  ["[0]" language (.only Language)]
  ["[0]" territory (.only Territory)]])

(nominal.def .public Locale
  Text

  (def territory_separator "_")
  (def encoding_separator ".")

  (def .public (locale language territory encoding)
    (-> Language (Maybe Territory) (Maybe Encoding) Locale)
    (abstraction (format (language.code language)
                         (|> territory
                             (maybe#each (|>> territory.long_code (format ..territory_separator)))
                             (maybe.else ""))
                         (|> encoding
                             (maybe#each (|>> encoding.name (format ..encoding_separator)))
                             (maybe.else "")))))

  (def .public code
    (-> Locale Text)
    (|>> representation))

  (def .public hash
    (Hash Locale)
    (of hash.functor each ..code text.hash))

  (def .public equivalence
    (Equivalence Locale)
    (of ..hash equivalence))
  )
