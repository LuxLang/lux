... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Analysis)
   [control
    ["[0]" maybe]]
   [math
    [number
     ["n" nat]]]
   [world
    [time
     ["[0]" series (.only Series) (.use "[1]#[0]" functor)]]]]]
 [// (.only Analysis)
  [//
   [price (.only Price)]
   [//
    ["[0]" money]
    [trade
     ["[0]" session (.only Session)]]]]])

... https://en.wikipedia.org/wiki/Typical_price
(def .public (typical_price it)
  (All (_ $)
    (-> (Session $)
        (Price $)))
  (|> (all money.+
           (the session.#high it)
           (the session.#low it)
           
           (the session.#open it)
           (the session.#close it))
      money.amount
      (n./ 4)
      (money.money (money.currency (the session.#close it)))))

... https://en.wikipedia.org/wiki/Pivot_point_(technical_analysis)
(type .public (Central_Pivot_Range $)
  (Record
   [#top_central (Price $)
    #pivot_point (Price $)
    #bottom_central (Price $)]))

(def (bottom_central it)
  (All (_ $)
    (-> (Session $)
        (Price $)))
  (|> (all money.+
           (the session.#high it)
           (the session.#low it))
      money.amount
      (n./ 2)
      (money.money (money.currency (the session.#high it)))))

(def (top_central pivot_point bottom_central)
  (All (_ $)
    (-> (Price $) (Price $)
        (Price $)))
  (|> pivot_point
      (money.+ pivot_point)
      (money.- bottom_central)
      maybe.trusted))

(def .public (central_pivot_range it)
  (All (_ $)
    (-> (Session $)
        (Central_Pivot_Range $)))
  (let [pivot_point (typical_price it)
        bottom_central (bottom_central it)]
    [#top_central (top_central pivot_point bottom_central)
     #pivot_point pivot_point
     #bottom_central bottom_central]))

(def .public analysis
  (All (_ $)
    (Analysis (Session $) (Central_Pivot_Range $)))
  (series#each ..central_pivot_range))
