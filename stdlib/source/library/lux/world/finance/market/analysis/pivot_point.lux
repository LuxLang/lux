... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Analysis)
   [control
    ["[0]" maybe]]
   [math
    [number
     ["n" natural]]]
   [world
    [time
     ["[0]" series (.only Series) (.use "[1]#[0]" functor)]]]]]
 [// (.only Analysis)
  [//
   [price (.only Price)]
   [//
    ["[0]" money]
    [trade
     ["[0]" session (.only Session)]]]]])

... https://en.wikipedia.org/wiki/Typical_price
(the .public (typical_price it)
  (for_any (_ $)
    (-> (Session $)
        (Price $)))
  (|> (all money.+
           (session.high it)
           (session.low it)
           
           (session.open it)
           (session.close it))
      money.amount
      (n./ 4)
      (money.money (money.currency (session.close it)))))

... https://en.wikipedia.org/wiki/Pivot_point_(technical_analysis)
(every .public (Central_Pivot_Range $)
  (Record
   [#maximum_central (Price $)
    #pivot_point (Price $)
    #minimum_central (Price $)]))

(the (minimum_central it)
  (for_any (_ $)
    (-> (Session $)
        (Price $)))
  (|> (all money.+
           (session.high it)
           (session.low it))
      money.amount
      (n./ 2)
      (money.money (money.currency (session.high it)))))

(the (maximum_central pivot_point minimum_central)
  (for_any (_ $)
    (-> (Price $) (Price $)
        (Price $)))
  (|> pivot_point
      (money.+ pivot_point)
      (money.- minimum_central)
      maybe.trusted))

(the .public (central_pivot_range it)
  (for_any (_ $)
    (-> (Session $)
        (Central_Pivot_Range $)))
  (let [pivot_point (typical_price it)
        minimum_central (minimum_central it)]
    [#maximum_central (maximum_central pivot_point minimum_central)
     #pivot_point pivot_point
     #minimum_central minimum_central]))

(the .public analysis
  (for_any (_ $)
    (Analysis (Session $) (Central_Pivot_Range $)))
  (series#each ..central_pivot_range))
