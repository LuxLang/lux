... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Analysis)
   [control
    ["[0]" maybe]]
   [math
    [number
     ["n" nat]]]
   [world
    [time
     ["[0]" series (.only Series) (.use "[1]#[0]" functor)]]]]]
 [// (.only Analysis)
  [//
   [price (.only Price)]
   [//
    ["[0]" money]
    [trade
     ["[0]" session (.only Session)]]]]])

... https://en.wikipedia.org/wiki/Typical_price
(the .public (typical_price it)
  (All (_ $)
    (-> (Session $)
        (Price $)))
  (|> (all money.+
           (its session.#high it)
           (its session.#low it)
           
           (its session.#open it)
           (its session.#close it))
      money.amount
      (n./ 4)
      (money.money (money.currency (its session.#close it)))))

... https://en.wikipedia.org/wiki/Pivot_point_(technical_analysis)
(every .public (Central_Pivot_Range $)
  (Record
   [#top_central (Price $)
    #pivot_point (Price $)
    #bottom_central (Price $)]))

(the (bottom_central it)
  (All (_ $)
    (-> (Session $)
        (Price $)))
  (|> (all money.+
           (its session.#high it)
           (its session.#low it))
      money.amount
      (n./ 2)
      (money.money (money.currency (its session.#high it)))))

(the (top_central pivot_point bottom_central)
  (All (_ $)
    (-> (Price $) (Price $)
        (Price $)))
  (|> pivot_point
      (money.+ pivot_point)
      (money.- bottom_central)
      maybe.trusted))

(the .public (central_pivot_range it)
  (All (_ $)
    (-> (Session $)
        (Central_Pivot_Range $)))
  (let [pivot_point (typical_price it)
        bottom_central (bottom_central it)]
    [#top_central (top_central pivot_point bottom_central)
     #pivot_point pivot_point
     #bottom_central bottom_central]))

(the .public analysis
  (All (_ $)
    (Analysis (Session $) (Central_Pivot_Range $)))
  (series#each ..central_pivot_range))
