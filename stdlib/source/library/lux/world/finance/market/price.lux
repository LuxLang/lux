(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    ["[0]" order (.only Order)]]
   [data
    [text
     ["%" \\format]]]
   [math
    [number
     ["n" nat]
     ["i" int]]]
   [meta
    [type
     ["[0]" nominal]]]]]
 [///
  ["[0]" money (.only Money)
   ["[0]" currency (.only Currency)]]])

(type .public (Price $)
  (Money $))

(def .public (free $)
  (All (_ $)
    (-> (Currency $)
        (Price $)))
  (money.money $ 0))

... https://en.wikipedia.org/wiki/Price_action_trading
(nominal.def .public (Action $)
  (Record
   [#currency (Currency $)
    #movement Int])

  (def .public (action from to)
    (All (_ $)
      (-> (Price $) (Price $)
          (Action $)))
    (nominal.abstraction
     [#currency (money.currency from)
      #movement (.int (n.- (money.amount from)
                           (money.amount to)))]))

  (with_template [<*> <name>]
    [(def .public (<name> currency it)
       (All (_ $)
         (-> (Currency $) Int
             (Action $)))
       (nominal.abstraction
        [#currency currency
         #movement (<*> (.int (currency.sub_divisions currency))
                        it)]))]

    [i.* of_units]
    [i.% of_sub_units]
    )

  (with_template [<name> <slot> <type>]
    [(def .public <name>
       (All (_ $)
         (-> (Action $)
             <type>))
       (|>> nominal.representation
            (the <slot>)))]

    [currency #currency (Currency $)]
    [movement #movement Int]
    )

  (def .public order
    (All (_ $)
      (Order (Action $)))
    (of order.functor each
        ..movement
        i.order))

  (def .public <
    (All (_ $)
      (-> (Action $) (Action $)
          Bit))
    (of ..order <))

  (with_template [<name> <order>]
    [(def .public <name>
       (All (_ $)
         (-> (Action $) (Action $)
             Bit))
       (<order> order))]

    [<= order.<=]
    [> order.>]
    [>= order.>=]
    )

  (def .public equivalence
    (All (_ $)
      (Equivalence (Action $)))
    (of ..order equivalence))

  (def .public =
    (All (_ $)
      (-> (Action $) (Action $)
          Bit))
    (of ..equivalence =))

  (with_template [<p> <i>]
    [(def .public (<p> parameter subject)
       (All (_ $)
         (-> (Action $) (Action $)
             (Action $)))
       (let [parameter (nominal.representation parameter)
             subject (nominal.representation subject)]
         (nominal.abstraction
          [#currency (the #currency subject)
           #movement (<i> (the #movement parameter)
                          (the #movement subject))])))]

    [+ i.+]
    [- i.-]
    )

  (def .public (format it)
    (All (_ $)
      (%.Format (Action $)))
    (let [[currency movement] (nominal.representation it)
          [macro micro] (i./% (.int (currency.sub_divisions currency))
                              movement)]
      (%.format (%.int macro)
                (when micro
                  +0 ""
                  _ (%.format "." (%.nat (.nat micro))))
                " " (currency.alphabetic_code currency))))
  )

(with_template [<order> <name>]
  [(def .public (<name> left right)
     (All (_ $)
       (-> (Action $) (Action $)
           (Action $)))
     (if (<order> (..movement left)
                  (..movement right))
       right
       left))]

  [i.< min]
  [i.> max]
  )

(with_template [<*> <name>]
  [(def .public (<name> it)
     (All (_ $)
       (-> (Action $)
           Int))
     (<*> (.int (currency.sub_divisions (..currency it)))
          (..movement it)))]

  [i./ units]
  [i.% sub_units]
  )
