... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    ["[0]" order (.only Order)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    [number
     ["n" natural]
     ["i" integer]]]
   [meta
    [type
     ["[0]" nominal]]
    [macro
     ["[0]" template]]]]]
 [///
  ["[0]" money (.only Money)
   ["[0]" currency (.only Currency)]]])

(every .public (Price $)
  (Money $))

(the .public (free $)
  (for_any (_ $)
    (-> (Currency $)
        (Price $)))
  (money.money $ 0))

(the .public (maximum $)
  (for_any (_ $)
    (-> (Currency $)
        (Price $)))
  (money.money $ (of n.interval top)))

... https://en.wikipedia.org/wiki/Price_action_trading
(nominal.every .public (Action $)
  (Record
   [#currency (Currency $)
    #movement Integer])

  (the .public (none $)
    (for_any (_ $)
      (-> (Currency $)
          (Action $)))
    (nominal.abstraction
     [#currency $
      #movement +0]))

  (the .public (action from to)
    (for_any (_ $)
      (-> (Price $) (Price $)
          (Action $)))
    (nominal.abstraction
     [#currency (money.currency from)
      #movement (.integer (n.- (money.amount from)
                               (money.amount to)))]))

  (template.with [<*> <name>]
    [(the .public (<name> currency it)
       (for_any (_ $)
         (-> (Currency $) Integer
             (Action $)))
       (nominal.abstraction
        [#currency currency
         #movement (<*> (.integer (currency.sub_divisions currency))
                        it)]))]

    [i.* of_units]
    [i.% of_sub_units]
    )

  (template.with [<name> <slot> <type>]
    [(the .public <name>
       (for_any (_ $)
         (-> (Action $)
             <type>))
       (|>> nominal.representation
            (its <slot>)))]

    [currency #currency (Currency $)]
    [movement #movement Integer]
    )

  (the .public order
    (for_any (_ $)
      (Order (Action $)))
    (of order.functor each
        ..movement
        i.order))

  (the .public <
    (for_any (_ $)
      (-> (Action $) (Action $)
          Bit))
    (of ..order <))

  (template.with [<name> <order>]
    [(the .public <name>
       (for_any (_ $)
         (-> (Action $) (Action $)
             Bit))
       (<order> order))]

    [<= order.<=]
    [> order.>]
    [>= order.>=]
    )

  (the .public equivalence
    (for_any (_ $)
      (Equivalence (Action $)))
    (of ..order equivalence))

  (alias [=]
         ..equivalence)

  (template.with [<p> <i>]
    [(the .public (<p> parameter subject)
       (for_any (_ $)
         (-> (Action $) (Action $)
             (Action $)))
       (let [parameter (nominal.representation parameter)
             subject (nominal.representation subject)]
         (nominal.abstraction
          [#currency (its #currency subject)
           #movement (<i> (its #movement parameter)
                          (its #movement subject))])))]

    [+ i.+]
    [- i.-]
    )

  (the too_negative
    (of i.interval bottom))

  (the .public (injection it)
    (for_any (_ $)
      (text.Injection (Action $)))
    (when (..movement it)
      ..too_negative
      (|> ..too_negative
          ++
          i.abs
          .natural
          ++
          (money.money (..currency it))
          money.injection
          (%.message "-"))

      amount
      (let [-|+ (if (i.< +0 amount)
                  "-"
                  "+")]
        (|> (.natural (i.abs amount))
            (money.money (..currency it))
            money.injection
            (%.message -|+)))))
  )

(template.with [<order> <name>]
  [(the .public (<name> left right)
     (for_any (_ $)
       (-> (Action $) (Action $)
           (Action $)))
     (if (<order> (..movement left)
                  (..movement right))
       right
       left))]

  [i.< min]
  [i.> max]
  )

(template.with [<*> <name>]
  [(the .public (<name> it)
     (for_any (_ $)
       (-> (Action $)
           Integer))
     (<*> (.integer (currency.sub_divisions (..currency it)))
          (..movement it)))]

  [i./ units]
  [i.% sub_units]
  )
