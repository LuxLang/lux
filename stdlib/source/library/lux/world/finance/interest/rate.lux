... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monoid (.only Monoid)]]
   [control
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    [number
     ["n" nat]
     ["d" dec]]]
   [meta
    [macro
     ["[0]" template]]]]]
 [///
  ["[0]" money (.only Money)]])

... https://en.wikipedia.org/wiki/Interest_rate
(every .public Rate
  Dec)

... https://en.wikipedia.org/wiki/Break-even
(the .public break_even
  Rate
  +1.0)

... https://en.wikipedia.org/wiki/Compound_interest
(the .public compound
  (-> Rate Rate
      Rate)
  d.*)

(template.with [<order> <name>]
  [(the .public <name>
     (Predicate Rate)
     (<order> ..break_even))]

  [d.< loss?]
  [d.> gain?]
  [d.= break_even?]
  )

(the .public monoid
  (Monoid Rate)
  (implementation
   (the identity ..break_even)
   (the composite ..compound)))

(the .public injection
  (text.Injection Rate)
  (|>> (d.- ..break_even)
       %.percentage))

(the .public (rate before after)
  (for_any (_ $)
    (-> (Money $) (Money $)
        Rate))
  (d./ (n.dec (money.amount before))
       (n.dec (money.amount after))))
