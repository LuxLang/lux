... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monoid (.only Monoid)]]
   [control
    [function
     [predicate (.only Predicate)]]]
   [data
    [text
     ["%" \\format]]]
   [math
    [number
     ["n" nat]
     ["f" frac]]]]]
 [///
  ["[0]" money (.only Money)]])

... https://en.wikipedia.org/wiki/Interest_rate
(every .public Rate
  Frac)

... https://en.wikipedia.org/wiki/Break-even
(the .public break_even
  Rate
  +1.0)

... https://en.wikipedia.org/wiki/Compound_interest
(the .public compound
  (-> Rate Rate
      Rate)
  f.*)

(with_template [<order> <name>]
  [(the .public <name>
     (Predicate Rate)
     (<order> ..break_even))]

  [f.< loss?]
  [f.> gain?]
  [f.= break_even?]
  )

(the .public monoid
  (Monoid Rate)
  (implementation
   (the identity ..break_even)
   (the composite ..compound)))

(the .public format
  (%.Format Rate)
  (|>> (f.- ..break_even)
       %.percentage))

(the .public (rate before after)
  (All (_ $)
    (-> (Money $) (Money $)
        Rate))
  (f./ (n.frac (money.amount before))
       (n.frac (money.amount after))))
