... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    ["[0]" order (.only Order)]
    [monoid (.only Monoid)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    [number
     ["n" nat]]]
   [meta
    ["[0]" static]
    [type
     ["[0]" nominal]]]]]
 [/
  ["/" currency]])

(nominal.every .public (Money $)
  (Record
   [#currency (/.Currency $)
    #amount Nat])

  (the .public (money currency amount)
    (All (_ $)
      (-> (/.Currency $) Nat
          (Money $)))
    (nominal.abstraction
     [#currency currency
      #amount amount]))

  (with_template [<*> <name>]
    [(the .public (<name> currency it)
       (All (_ $)
         (-> (/.Currency $) Nat
             (Money $)))
       (money currency
              (<*> (/.sub_divisions currency)
                   it)))]

    [n.* of_units]
    [n.% of_sub_units]
    )

  (with_template [<name> <slot> <type>]
    [(the .public <name>
       (All (_ $)
         (-> (Money $)
             <type>))
       (|>> nominal.representation
            (its <slot>)))]

    [currency #currency (/.Currency $)]
    [amount #amount Nat]
    )

  (the .public order
    (All (_ $)
      (Order (Money $)))
    (of order.functor each
        ..amount
        n.order))

  (the .public <
    (All (_ $)
      (-> (Money $) (Money $)
          Bit))
    (of ..order <))

  (with_template [<name> <order>]
    [(the .public <name>
       (All (_ $)
         (-> (Money $) (Money $)
             Bit))
       (<order> order))]

    [<= order.<=]
    [> order.>]
    [>= order.>=]
    )

  (the .public equivalence
    (All (_ $)
      (Equivalence (Money $)))
    (of ..order equivalence))

  (the .public (monoid $)
    (All (_ $)
      (-> (/.Currency $)
          (Monoid (Money $))))
    (implementation
     (the identity
       (nominal.abstraction
        [#currency $
         #amount 0]))
     (the (composite left right)
       (nominal.abstraction
        [#currency $
         #amount (n.+ (its #amount (nominal.representation left))
                      (its #amount (nominal.representation right)))]))))

  (the .public =
    (All (_ $)
      (-> (Money $) (Money $)
          Bit))
    (of ..equivalence =))

  (the .public (+ parameter subject)
    (All (_ $)
      (-> (Money $) (Money $)
          (Money $)))
    (|> subject
        nominal.representation
        (revised #amount (n.+ (|> parameter nominal.representation (its #amount))))
        nominal.abstraction))

  (the .public (- parameter subject)
    (All (_ $)
      (-> (Money $) (Money $)
          (Maybe (Money $))))
    (let [parameter (nominal.representation parameter)
          subject (nominal.representation subject)]
      (if (n.< (its #amount parameter)
               (its #amount subject))
        {.#None}
        {.#Some (nominal.abstraction
                 [#currency (its #currency subject)
                  #amount (n.- (its #amount parameter)
                               (its #amount subject))])})))

  (the .public (format it)
    (All (_ $)
      (%.Injection (Money $)))
    (let [[currency amount] (nominal.representation it)
          range (/.sub_divisions currency)
          [macro micro] (n./% range amount)
          sub (of (n.padded (|> range -- %.nat text.size)
                            n.decimal)
                  injection)]
      (%.format (%.nat macro)
                (when range
                  1 ""
                  _ (%.format "." (sub micro)))
                " " (/.alphabetic_code currency))))
  )

(with_template [<order> <name>]
  [(the .public (<name> left right)
     (All (_ $)
       (-> (Money $) (Money $)
           (Money $)))
     (if (<order> (..amount left)
                  (..amount right))
       right
       left))]

  [n.< min]
  [n.> max]
  )

(with_template [<*> <name>]
  [(the .public (<name> it)
     (All (_ $)
       (-> (Money $)
           Nat))
     (<*> (/.sub_divisions (..currency it))
          (..amount it)))]

  [n./ units]
  [n.% sub_units]
  )
