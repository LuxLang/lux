... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    ["[0]" order (.only Order)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\format]]]
   [math
    [number
     ["n" nat]]]
   [meta
    ["[0]" static]
    [type
     ["[0]" nominal]]]]]
 [/
  ["/" currency]])

(nominal.def .public (Money $)
  (Record
   [#currency (/.Currency $)
    #amount Nat])

  (def .public (money currency amount)
    (All (_ $)
      (-> (/.Currency $) Nat
          (Money $)))
    (nominal.abstraction
     [#currency currency
      #amount amount]))

  (with_template [<*> <name>]
    [(def .public (<name> currency it)
       (All (_ $)
         (-> (/.Currency $) Nat
             (Money $)))
       (money currency
              (<*> (/.sub_divisions currency)
                   it)))]

    [n.* of_units]
    [n.% of_sub_units]
    )

  (with_template [<name> <slot> <type>]
    [(def .public <name>
       (All (_ $)
         (-> (Money $)
             <type>))
       (|>> nominal.representation
            (the <slot>)))]

    [currency #currency (/.Currency $)]
    [amount #amount Nat]
    )

  (def .public order
    (All (_ $)
      (Order (Money $)))
    (of order.functor each
        ..amount
        n.order))

  (def .public <
    (All (_ $)
      (-> (Money $) (Money $)
          Bit))
    (of ..order <))

  (with_template [<name> <order>]
    [(def .public <name>
       (All (_ $)
         (-> (Money $) (Money $)
             Bit))
       (<order> order))]

    [<= order.<=]
    [> order.>]
    [>= order.>=]
    )

  (def .public equivalence
    (All (_ $)
      (Equivalence (Money $)))
    (of ..order equivalence))

  (def .public =
    (All (_ $)
      (-> (Money $) (Money $)
          Bit))
    (of ..equivalence =))

  (def .public (+ parameter subject)
    (All (_ $)
      (-> (Money $) (Money $)
          (Money $)))
    (|> subject
        nominal.representation
        (revised #amount (n.+ (|> parameter nominal.representation (the #amount))))
        nominal.abstraction))

  (def .public (- parameter subject)
    (All (_ $)
      (-> (Money $) (Money $)
          (Maybe (Money $))))
    (let [parameter (nominal.representation parameter)
          subject (nominal.representation subject)]
      (if (n.< (the #amount parameter)
               (the #amount subject))
        {.#None}
        {.#Some (nominal.abstraction
                 [#currency (the #currency subject)
                  #amount (n.- (the #amount parameter)
                               (the #amount subject))])})))

  (def .public (format it)
    (All (_ $)
      (%.Format (Money $)))
    (let [[currency amount] (nominal.representation it)
          range (/.sub_divisions currency)
          [macro micro] (n./% range amount)
          sub (of (n.padded (|> range -- %.nat text.size)
                            n.decimal)
                  encoded)]
      (%.format (%.nat macro)
                (when range
                  1 ""
                  _ (%.format "." (sub micro)))
                " " (/.alphabetic_code currency))))
  )

(with_template [<order> <name>]
  [(def .public (<name> left right)
     (All (_ $)
       (-> (Money $) (Money $)
           (Money $)))
     (if (<order> (..amount left)
                  (..amount right))
       right
       left))]

  [n.< min]
  [n.> max]
  )

(with_template [<*> <name>]
  [(def .public (<name> it)
     (All (_ $)
       (-> (Money $)
           Nat))
     (<*> (/.sub_divisions (..currency it))
          (..amount it)))]

  [n./ units]
  [n.% sub_units]
  )
