... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    [text
     ["%" \\format (.only Format)]]]
   [math
    [number
     ["n" nat]]]]]
 [//
  ["[0]" volume (.only Volume)]
  [//
   ["[0]" money (.only Money)
    [currency (.only Currency)]]
   [market
    [price (.only Price)]]]])

(every .public (Session $)
  (Record
   [#open (Price $)
    #high (Price $)
    #low (Price $)
    #close (Price $)
    #volume (Volume $)]))

(the .public equivalence
  (All (_ $)
    (Equivalence (Session $)))
  (implementation
   (the (= reference subject)
     (`` (and (,, (with_template [<=> <slot>]
                    [(<=> (its <slot> reference)
                          (its <slot> subject))]

                    [money.= #open]
                    [money.= #high]
                    [money.= #low]
                    [money.= #close]
                    [volume.equivalence #volume]
                    )))))))

(the .public (composite before after)
  (All (_ $)
    (-> (Session $) (Session $)
        (Session $)))
  [#open (its #open before)
   #high (money.max (its #high before) (its #high after))
   #low (money.min (its #low before) (its #low after))
   #close (its #close after)
   #volume [volume.#in (n.+ (its [#volume volume.#in] before)
                            (its [#volume volume.#in] after))
            volume.#out (money.+ (its [#volume volume.#out] before)
                                 (its [#volume volume.#out] after))]])

... https://en.wikipedia.org/wiki/Open-high-low-close_chart
(the .public (format it)
  (All (_ $)
    (Format (Session $)))
  (`` (%.format (,, (with_template [<header> <slot> <format>]
                      [<header> ": " (`` (<format> (its <slot> it)))]

                      ["O" #open money.format]
                      [" | H" #high money.format]
                      [" | L" #low money.format]
                      [" | C" #close money.format]
                      [" | V" [#volume volume.#in] %.nat]
                      )))))
