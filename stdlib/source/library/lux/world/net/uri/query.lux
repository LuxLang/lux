... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [format (.only Format)]
    [equivalence (.only Equivalence)]]
   [control
    ["?" projection]
    ["[0]" try (.only Try)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]
     ["?[1]" \\projection (.only Projection)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]
     ["[0]" dictionary (.only Dictionary)]]]
   [math
    [number
     ["[0]" natural]]]
   [world
    [net
     ["[0]" uri
      ["[1]" encoding]]]]]])

(every .public Query
  (Dictionary Text Text))

(the .public empty
  Query
  (dictionary.empty text.hash))

(the .public equivalence
  (Equivalence Query)
  (dictionary.equivalence text.equivalence))

(alias [=]
       ..equivalence)

(the component
  (Projection Text)
  (?.rec
   (function (_ component)
     (do [! ?.monad]
       [head (?text.some (?text.none_of "+%&;"))]
       (all ?.either
            (?.after (?.either ?text.end
                               (?text.this "&"))
                     (in head))
            (do !
              [_ (?text.this "+")
               tail component]
              (in (%.message head " " tail)))
            (do !
              [_ (?text.this "%")
               code (|> (?text.exactly 2 ?text.hexadecimal)
                        (?.format natural.hex)
                        (of ! each text.of_char))
               tail component]
              (in (%.message head code tail))))))))

(the separators
  "&;")

(the assignment
  "=")

(the invalid
  (%.message "=" "&;"))

(the (form query)
  (-> Query (Projection Query))
  (all ?.either
       (do ?.monad
         [_ ?text.end]
         (in query))
       (do [! ?.monad]
         [key (?text.some (?text.none_of ..invalid))
          key (?text.local key ..component)
          key (?.of_try (uri.projection key))]
         (?.either (do !
                     [_ (?text.this ..assignment)
                      value ..component
                      value (?.of_try (uri.projection value))]
                     (form (dictionary.has key value query)))
                   (do !
                     [_ (all ?.or
                             (?text.one_of ..separators)
                             ?text.end)]
                     (form (dictionary.has key "" query)))))
       ... if invalid form data, just stop parsing...
       (of ?.monad in query)))

(the injection
  (text.Injection Query)
  (|>> dictionary.entries
       (list#each (function (_ [key value])
                    (%.message (uri.injection key) "=" (uri.injection value))))
       (text.interposed "&")))

(the query
  (-> Text (Try Query))
  (?text.value (..form ..empty)))

(the .public format
  (Format Text Query)
  (implementation
   (the injection ..injection)
   (the projection ..query)))
