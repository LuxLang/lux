... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except text)
   [abstract
    [monad (.only do)]
    [format (.only Format)]
    [equivalence (.only Equivalence)]
    ["?" projection]]
   [control
    ["[0]" try (.only Try)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]
     ["?[1]" \\projection (.only Projection)]]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor)]
     ["[0]" dictionary (.only Dictionary)]]]
   [math
    [number
     ["[0]" natural]]]
   [world
    [net
     ["[0]" uri
      ["[1]" encoding]]]]]])

(every .public Query
  (Dictionary Text Text))

(the .public empty
  Query
  (dictionary.empty text.hash))

(the .public equivalence
  (Equivalence Query)
  (dictionary.equivalence text.equivalence))

(alias [=]
       ..equivalence)

(the component
  (Projection Text)
  (?.rec
   (function (_ component)
     (do [! ?.monad]
       [head (?text.some (?text.none_of "+%&;"))]
       (all ?.either
            (?.after (?.either ?text.end
                               (?text.this "&"))
                     (in head))
            (do !
              [_ (?text.this "+")
               tail component]
              (in (.text head " " tail)))
            (do !
              [_ (?text.this "%")
               code (|> (?text.exactly 2 ?text.hexadecimal)
                        (?.of natural.hex)
                        (by ! each text.of_character))
               tail component]
              (in (.text head code tail))))))))

... https://en.wikipedia.org/wiki/Delimiter
(the delimiters
  "&;")

(the assignment
  "=")

(the invalid
  (.text "=" "&;"))

(the (form query)
  (-> Query (Projection Query))
  (all ?.either
       (do ?.monad
         [_ ?text.end]
         (in query))
       (do [! ?.monad]
         [key (?text.some (?text.none_of ..invalid))
          key (?text.local key ..component)
          key (?.of_try (uri.of_text key))]
         (?.either (do !
                     [_ (?text.this ..assignment)
                      value ..component
                      value (?.of_try (uri.of_text value))]
                     (form (dictionary.has key value query)))
                   (do !
                     [_ (all ?.or
                             (?text.one_of ..delimiters)
                             ?text.end)]
                     (form (dictionary.has key "" query)))))
       ... if invalid form data, just stop parsing...
       (by ?.monad in query)))

(the as_text
  (text.Injection Query)
  (|>> dictionary.entries
       (stack#each (function (_ [key value])
                     (.text (uri.as_text key) "=" (uri.as_text value))))
       (text.interposed "&")))

(the of_text
  (-> Text
      (Try Query))
  (?text.value (..form ..empty)))

(the .public text
  (Format Text Query)
  (implementation
   (the as ..as_text)
   (the of ..of_text)))
