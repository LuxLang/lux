(.require
 [library
  [lux (.except #version)
   [abstract
    [monad (.only Monad)]]
   [control
    ["[0]" try]]
   [data
    ["[0]" binary (.only Binary)]
    [text
     [encoding
      ["[0]" utf8 (.use "[1]#[0]" codec)]]]
    [format
     ["[0]" json (.only JSON) (.use "[1]#[0]" codec)]]]]]
 ["[0]" // (.only Method Body)
  ["[0]" version (.only Version)]
  ["[0]" header (.only Header)]
  ["/[1]" // (.only Address)
   ["[0]" mime]
   [uri (.only URI)
    ["[0]" scheme (.only Scheme)]
    ["[0]" query (.only Query) (.use "[1]#[0]" codec)]]]])

(type .public Identification
  (Record
   [#local Address
    #remote Address]))

(type .public Protocol
  (Record
   [#version Version
    #scheme Scheme]))

(type .public Resource
  (Record
   [#method Method
    #uri URI]))

(type .public (Request !)
  (Record
   [#identification Identification
    #protocol Protocol
    #resource Resource
    #message (//.Message !)]))

(def (body ! it)
  (All (_ !)
    (-> (Monad !) Binary
        (//.Body !)))
  (function (_ _)
    (of ! in {try.#Success [(binary.size it) it]})))

(def .public (utf8 ! it)
  (All (_ !)
    (-> (Monad !) Text
        (Request !)))
  [#identification [#local [///.#host ""
                            ///.#port 0]
                    #remote [///.#host ""
                             ///.#port 0]]
   #protocol [#version version.v1_1
              #scheme scheme.http]
   #resource [#method {//.#Post}
              #uri ""]
   #message [//.#headers (|> header.empty
                             (header.has header.content_type mime.utf_8))
             //.#body (body ! (utf8#encoded it))]])

(def .public text ..utf8)

(def .public (json ! it)
  (All (_ !)
    (-> (Monad !) JSON
        (Request !)))
  [#identification [#local [///.#host ""
                            ///.#port 0]
                    #remote [///.#host ""
                             ///.#port 0]]
   #protocol [#version version.v1_1
              #scheme scheme.http]
   #resource [#method {//.#Post}
              #uri ""]
   #message [//.#headers (|> header.empty
                             (header.has header.content_type mime.json))
             //.#body (body ! (utf8#encoded (json#encoded it)))]])

(def .public (form ! it)
  (All (_ !)
    (-> (Monad !) Query
        (Request !)))
  [#identification [#local [///.#host ""
                            ///.#port 0]
                    #remote [///.#host ""
                             ///.#port 0]]
   #protocol [#version version.v1_1
              #scheme scheme.http]
   #resource [#method {//.#Post}
              #uri ""]
   #message [//.#headers (|> header.empty
                             (header.has header.content_type mime.form))
             //.#body (body ! (utf8#encoded (query#encoded it)))]])

(with_template [<name> <scheme>]
  [(def .public <name>
     (All (_ !)
       (-> (Request !)
           (Request !)))
     (|>> (has [#protocol #scheme] <scheme>)))]

  [http scheme.http]
  [https scheme.https]
  )

(with_template [<name> <method>]
  [(def .public <name>
     (All (_ !)
       (-> (Request !)
           (Request !)))
     (has [#resource #method] {<method>}))]

  [post //.#Post]
  [get //.#Get]
  [put //.#Put]
  [patch //.#Patch]
  [delete //.#Delete]
  [head //.#Head]
  [connect //.#Connect]
  [options //.#Options]
  [trace //.#Trace]
  )

(def .public (uri it)
  (All (_ !)
    (-> URI (Request !)
        (Request !)))
  (|>> (has [#resource #uri] it)))

(def .public (with_header it value)
  (All (_ ! of)
    (-> (Header of) of (Request !)
        (Request !)))
  (|>> (revised [#message //.#headers] (header.has it value))))
