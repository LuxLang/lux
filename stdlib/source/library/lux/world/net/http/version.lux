... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equality (.only Equality)]
    [hash (.only Hash)]]
   [data
    ["[0]" text (.use "[1]:[0]" hash)]]
   [macro
    ["[0]" template]]
   [type
    ["[0]" nominal (.only)]]]])

(nominal.every .public Version
  Text

  (the .public equality
    (Equality Version)
    (implementation
     (the (= reference it)
       (text.= (nominal.as reference)
               (nominal.as it)))))

  (alias [=]
         ..equality)

  (the .public hash
    (Hash Version)
    (implementation
     (the equality ..equality)
     (the hash (|>> nominal.as text:hash))))

  (template.with [<name> <version>]
    [(the .public <name>
       Version
       (nominal.of <version>))]

    [[v0_9 "0.9"]
     [v1_0 "1.0"]
     [v1_1 "1.1"]
     [v2_0 "2.0"]])
  )
