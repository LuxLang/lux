(.module:
  [library
   [lux #*
    [control
     [pipe (#+ case>)]]
    [data
     [text
      ["%" format (#+ format)]]
     [collection
      ["." dictionary (#+ Dictionary)]]]]]
  [// (#+ Header)
   ["." mime (#+ MIME)]
   [// (#+ URL)]])

(def: .public (add name value)
  (-> Text Text Header)
  (dictionary.upsert name ""
                     (|>> (case>
                           ""
                           value
                           
                           previous
                           (format previous "," value)))))

(def: .public content_length
  (-> Nat Header)
  (|>> %.nat (..add "Content-Length")))

(def: .public content_type
  (-> MIME Header)
  (|>> mime.name (..add "Content-Type")))

(def: .public location
  (-> URL Header)
  (..add "Location"))
