(.module:
  [library
   [lux #*
    [control
     [pipe (#+ case>)]]
    [data
     [text
      ["%" format (#+ format)]]
     [collection
      ["." dictionary (#+ Dictionary)]]]]]
  [// (#+ Header)
   ["." mime (#+ MIME)]
   [// (#+ URL)]])

(def: #export (add name value)
  (-> Text Text Header)
  (dictionary.upsert name ""
                     (|>> (case>
                           ""
                           value
                           
                           previous
                           (format previous "," value)))))

(def: #export content_length
  (-> Nat Header)
  (|>> %.nat (..add "Content-Length")))

(def: #export content_type
  (-> MIME Header)
  (|>> mime.name (..add "Content-Type")))

(def: #export location
  (-> URL Header)
  (..add "Location"))
