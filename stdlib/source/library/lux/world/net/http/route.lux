(.module:
  [library
   [lux (#- or)
    [control
     [monad (#+ do)]
     [concurrency
      ["." async]]]
    [data
     ["." maybe]
     ["." text]
     [number
      ["n" nat]]]]]
  ["." // (#+ URI Server)
   ["#." status]
   ["#." response]])

(template [<scheme> <name>]
  [(def: #export (<name> server)
     (-> Server Server)
     (function (_ (^@ request [identification protocol resource message]))
       (case (get@ #//.scheme protocol)
         <scheme>
         (server request)

         _
         (async.resolved //response.not_found))))]

  [#//.HTTP  http]
  [#//.HTTPS https]
  )

(template [<method> <name>]
  [(def: #export (<name> server)
     (-> Server Server)
     (function (_ (^@ request [identification protocol resource message]))
       (case (get@ #//.method resource)
         <method>
         (server request)

         _
         (async.resolved //response.not_found))))]

  [#//.Get     get]
  [#//.Post    post]
  [#//.Put     put]
  [#//.Patch   patch]
  [#//.Delete  delete]
  [#//.Head    head]
  [#//.Connect connect]
  [#//.Options options]
  [#//.Trace   trace]
  )

(def: #export (uri path server)
  (-> URI Server Server)
  (function (_ [identification protocol resource message])
    (if (text.starts_with? path (get@ #//.uri resource))
      (server [identification
               protocol
               (update@ #//.uri
                        (|>> (text.clip' (text.size path)) maybe.assume)
                        resource)
               message])
      (async.resolved //response.not_found))))

(def: #export (or primary alternative)
  (-> Server Server Server)
  (function (_ request)
    (do async.monad
      [response (primary request)
       #let [[status message] response]]
      (if (n.= //status.not_found status)
        (alternative request)
        (in response)))))
