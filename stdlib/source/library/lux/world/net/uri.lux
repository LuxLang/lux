... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [data
    [text
     ["%" \\injection]]]]]
 [/
  [port (.only Port)]
  [path (.only Path)]
  ["[0]" scheme (.only Scheme)]
  ["[0]" query (.only Query) (.use "[1]#[0]" text)]]
 ["[0]" // (.only Host)])

(the .public (user_info name password)
  (-> Text Text
      Text)
  (text name ":" password))

(every .public Authority
  (Record
   [#user (Maybe Text)
    #host Host
    #port (Maybe Port)]))

(the (authority it)
  (-> Authority
      Text)
  (text (when (its #user it)
          {.#Some it}
          (text it "@")
          
          {.#None}
          "")
        (its #host it)
        (when (its #port it)
          {.#Some it}
          (text ":" (%.natural it))
          
          {.#None}
          "")))

(every .public Fragment
  Text)

... https://en.wikipedia.org/wiki/Uniform_Resource_Identifier
(every .public URI
  Text)

(the .public (uri scheme authority path query fragment)
  (-> Scheme (Maybe Authority) Path (Maybe Query) (Maybe Fragment)
      URI)
  (text (scheme.name scheme) ":"
        (when authority
          {.#Some authority}
          (text "//" (..authority authority))
          
          {.#None}
          "")
        path
        (when query
          {.#Some query}
          (text "?" (query#as query))
          
          {.#None}
          "")
        (when fragment
          {.#Some fragment}
          (text "#" fragment)
          
          {.#None}
          "")))
