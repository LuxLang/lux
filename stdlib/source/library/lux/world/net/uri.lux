... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except #host)
   [data
    [text
     ["%" \\format]]]]]
 [/
  [port (.only Port)]
  [path (.only Path)]
  ["[0]" scheme (.only Scheme)]
  ["[0]" query (.only Query) (.use "[1]#[0]" codec)]]
 ["[0]" // (.only Host)])

(def .public (user_info name password)
  (-> Text Text Text)
  (%.format name ":" password))

(type .public Authority
  (Record
   [#user (Maybe Text)
    #host Host
    #port (Maybe Port)]))

(def (authority it)
  (-> Authority Text)
  (%.format (when (its #user it)
              {.#Some it}
              (%.format it "@")
              
              {.#None}
              "")
            (its #host it)
            (when (its #port it)
              {.#Some it}
              (%.format ":" (%.nat it))
              
              {.#None}
              "")))

(type .public Fragment
  Text)

... https://en.wikipedia.org/wiki/Uniform_Resource_Identifier
(type .public URI
  Text)

(def .public (uri scheme authority path query fragment)
  (-> Scheme (Maybe Authority) Path (Maybe Query) (Maybe Fragment)
      URI)
  (%.format (scheme.name scheme) ":"
            (when authority
              {.#Some authority}
              (%.format "//" (..authority authority))
              
              {.#None}
              "")
            path
            (when query
              {.#Some query}
              (%.format "?" (query#encoded query))
              
              {.#None}
              "")
            (when fragment
              {.#Some fragment}
              (%.format "#" fragment)
              
              {.#None}
              "")))
