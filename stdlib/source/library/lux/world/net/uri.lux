... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except #host)
   [data
    [text
     ["%" \\injection]]]]]
 [/
  [port (.only Port)]
  [path (.only Path)]
  ["[0]" scheme (.only Scheme)]
  ["[0]" query (.only Query) (.use "[1]#[0]" codec)]]
 ["[0]" // (.only Host)])

(the .public (user_info name password)
  (-> Text Text Text)
  (%.message name ":" password))

(every .public Authority
  (Record
   [#user (Maybe Text)
    #host Host
    #port (Maybe Port)]))

(the (authority it)
  (-> Authority Text)
  (%.message (when (its #user it)
               {.#Some it}
               (%.message it "@")
               
               {.#None}
               "")
             (its #host it)
             (when (its #port it)
               {.#Some it}
               (%.message ":" (%.nat it))
               
               {.#None}
               "")))

(every .public Fragment
  Text)

... https://en.wikipedia.org/wiki/Uniform_Resource_Identifier
(every .public URI
  Text)

(the .public (uri scheme authority path query fragment)
  (-> Scheme (Maybe Authority) Path (Maybe Query) (Maybe Fragment)
      URI)
  (%.message (scheme.name scheme) ":"
             (when authority
               {.#Some authority}
               (%.message "//" (..authority authority))
               
               {.#None}
               "")
             path
             (when query
               {.#Some query}
               (%.message "?" (query#injection query))
               
               {.#None}
               "")
             (when fragment
               {.#Some fragment}
               (%.message "#" fragment)
               
               {.#None}
               "")))
