... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except nat)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    [order (.only Order)]
    [enum (.only Enum)]
    [codec (.only Codec)]]
   [control
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" text (.use "[1]#[0]" monoid)]]
   [math
    [number
     ["n" nat]]]
   [meta
    [macro
     ["^" pattern]
     ["[0]" template]]]]])

(type .public Day
  (Variant
   {#Sunday}
   {#Monday}
   {#Tuesday}
   {#Wednesday}
   {#Thursday}
   {#Friday}
   {#Saturday}))

(def .public equivalence
  (Equivalence Day)
  (implementation
   (def (= reference sample)
     (when [reference sample]
       (^.with_template [<tag>]
         [[{<tag>} {<tag>}]
          true])
       ([#Sunday]
        [#Monday]
        [#Tuesday]
        [#Wednesday]
        [#Thursday]
        [#Friday]
        [#Saturday])

       _
       false))))

(def (nat day)
  (-> Day Nat)
  (when day
    (^.with_template [<nat> <day>]
      [{<day>} <nat>])
    ([0 #Sunday]
     [1 #Monday]
     [2 #Tuesday]
     [3 #Wednesday]
     [4 #Thursday]
     [5 #Friday]
     [6 #Saturday])))

(def .public order
  (Order Day)
  (implementation
   (def equivalence ..equivalence)
   
   (def (< reference sample)
     (n.< (..nat reference) (..nat sample)))))

(def .public enum
  (Enum Day)
  (implementation
   (def order ..order)
   
   (def (succ day)
     (when day
       {#Sunday}    {#Monday}
       {#Monday}    {#Tuesday}
       {#Tuesday}   {#Wednesday}
       {#Wednesday} {#Thursday}
       {#Thursday}  {#Friday}
       {#Friday}    {#Saturday}
       {#Saturday}  {#Sunday}))
   
   (def (pred day)
     (when day
       {#Monday}    {#Sunday}
       {#Tuesday}   {#Monday}
       {#Wednesday} {#Tuesday}
       {#Thursday}  {#Wednesday}
       {#Friday}    {#Thursday}
       {#Saturday}  {#Friday}
       {#Sunday}    {#Saturday}))))

(exception.def .public (not_a_day_of_the_week value)
  (Exception Text)
  (exception.report
   (list ["Value" (text.format value)])))

(def .public codec
  (Codec Text Day)
  (implementation
   (def (encoded value)
     (when value
       (^.with_template [<tag>]
         [{<tag>}
          (text.replaced "#" "" (template.text [<tag>]))])
       ([..#Monday]
        [..#Tuesday]
        [..#Wednesday]
        [..#Thursday]
        [..#Friday]
        [..#Saturday]
        [..#Sunday])))
   (def (decoded value)
     (when (text#composite "#" value)
       (^.with_template [<tag>]
         [(template.text [<tag>])
          {try.#Success {<tag>}}])
       ([..#Monday]
        [..#Tuesday]
        [..#Wednesday]
        [..#Thursday]
        [..#Friday]
        [..#Saturday]
        [..#Sunday])
       _ (exception.except ..not_a_day_of_the_week [value])))))

(def .public week
  (List Day)
  (list {#Sunday}
        {#Monday}
        {#Tuesday}
        {#Wednesday}
        {#Thursday}
        {#Friday}
        {#Saturday}))

(with_expansions [<pairs> (these [01 #Sunday]
                                 [02 #Monday]
                                 [03 #Tuesday]
                                 [04 #Wednesday]
                                 [05 #Thursday]
                                 [06 #Friday]
                                 [07 #Saturday])]
  (def .public (number day)
    (-> Day Nat)
    (when day
      (^.with_template [<number> <day>]
        [{<day>}
         <number>])
      (<pairs>)))

  (exception.def .public (invalid_day number)
    (Exception Nat)
    (exception.report
     (list ["Number" (of n.decimal encoded number)]
           ["Valid range" (.text_composite# (of n.decimal encoded (..number {#Sunday}))
                                            " ~ "
                                            (of n.decimal encoded (..number {#Saturday})))])))

  (def .public (by_number number)
    (-> Nat (Try Day))
    (when number
      (^.with_template [<number> <day>]
        [<number>
         {try.#Success {<day>}}])
      (<pairs>)
      
      _
      (exception.except ..invalid_day [number])))
  )

(def .public hash
  (Hash Day)
  (implementation
   (def equivalence ..equivalence)
   (def (hash day)
     (when day
       (^.with_template [<prime> <day>]
         [{<day>}
          <prime>])
       ([02 #Sunday]
        [03 #Monday]
        [05 #Tuesday]
        [07 #Wednesday]
        [11 #Thursday]
        [13 #Friday]
        [17 #Saturday])))))
