... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    [order (.only Order)]
    [enum (.only Enum)]
    [format (.only Format)]]
   [control
    ["?" projection (.use "[1]#[0]" monad)]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" text (.use "[1]#[0]" monoid)
     ["?[1]" \\projection (.only Projection)]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [meta
    [macro
     ["[0]" template]
     ["[0]" expansion]]]]])

(every .public Month
  (Variant
   {#January}
   {#February}
   {#March}
   {#April}
   {#May}
   {#June}
   {#July}
   {#August}
   {#September}
   {#October}
   {#November}
   {#December}))

(the .public equivalence
  (Equivalence Month)
  (implementation
   (the (= expected actual)
     (`` (when [expected actual]
           (,, (template.with [<tag>]
                 [[{<tag>} {<tag>}]
                  true]

                 [#January]
                 [#February]
                 [#March]
                 [#April]
                 [#May]
                 [#June]
                 [#July]
                 [#August]
                 [#September]
                 [#October]
                 [#November]
                 [#December]))

           _
           false)))))

(alias [=]
       ..equivalence)

(expansion.let [<pairs> (these [01 #January]
                               [02 #February]
                               [03 #March]
                               [04 #April]
                               [05 #May]
                               [06 #June]
                               [07 #July]
                               [08 #August]
                               [09 #September]
                               [10 #October]
                               [11 #November]
                               [12 #December])]
  (these (the .public (number month)
           (-> Month Natural)
           (`` (when month
                 (,, (template.with [<number> <month>]
                       [{<month>}
                        <number>]

                       <pairs>)))))

         (exception.the .public (invalid_month number)
           (Exception Natural)
           (exception.report
            (list ["Number" (of n.decimal injection number)]
                  ["Valid range" (.text_composite# (of n.decimal injection (..number {#January}))
                                                   " ~ "
                                                   (of n.decimal injection (..number {#December})))])))

         (the .public (by_number number)
           (-> Natural (Try Month))
           (`` (when number
                 (,, (template.with [<number> <month>]
                       [<number>
                        {try.#Success {<month>}}]

                       <pairs>))
                 
                 _
                 (exception.except ..invalid_month [number])))))
  )

(the .public hash
  (Hash Month)
  (implementation
   (the equivalence ..equivalence)
   (the (hash month)
     (`` (when month
           (,, (template.with [<prime> <month>]
                 [{<month>}
                  <prime>]

                 [02 #January]
                 [03 #February]
                 [05 #March]
                 [07 #April]
                 [11 #May]
                 [13 #June]
                 [17 #July]
                 [19 #August]
                 [23 #September]
                 [29 #October]
                 [31 #November]
                 [37 #December])))))))

(the .public order
  (Order Month)
  (implementation
   (the equivalence ..equivalence)
   
   (the (< reference it)
     (n.< (..number reference)
          (..number it)))))

(the .public enum
  (Enum Month)
  (implementation
   (the order ..order)
   
   (the (succ month)
     (when month
       {#January}   {#February}
       {#February}  {#March}
       {#March}     {#April}
       {#April}     {#May}
       {#May}       {#June}
       {#June}      {#July}
       {#July}      {#August}
       {#August}    {#September}
       {#September} {#October}
       {#October}   {#November}
       {#November}  {#December}
       {#December}  {#January}))
   
   (the (pred month)
     (when month
       {#February}  {#January}
       {#March}     {#February}
       {#April}     {#March}
       {#May}       {#April}
       {#June}      {#May}
       {#July}      {#June}
       {#August}    {#July}
       {#September} {#August}
       {#October}   {#September}
       {#November}  {#October}
       {#December}  {#November}
       {#January}   {#December}))))

(the .public (days month)
  (-> Month Natural)
  (`` (when month
        (,, (template.with [<days> <month>]
              [{<month>}
               <days>]

              [31 #January]
              [28 #February]
              [31 #March]
              
              [30 #April]
              [31 #May]
              [30 #June]
              
              [31 #July]
              [31 #August]
              [30 #September]
              
              [31 #October]
              [30 #November]
              [31 #December])))))

(the .public (leap_year_days month)
  (-> Month Natural)
  (when month
    {#February} (++ (..days month))
    _ (..days month)))

(the .public year
  (List Month)
  (list {#January}
        {#February}
        {#March}
        {#April}
        {#May}
        {#June}
        {#July}
        {#August}
        {#September}
        {#October}
        {#November}
        {#December}))

(the .public (as_text value)
  (text.Injection Month)
  (`` (when value
        (,, (template.with [<tag>]
              [{<tag>}
               (text.replaced "#" "" (template.text [<tag>]))]

              [..#January]
              [..#February]
              [..#March]
              [..#April]
              [..#May]
              [..#June]
              [..#July]
              [..#August]
              [..#September]
              [..#October]
              [..#November]
              [..#December])))))

(the .public projection
  (Projection Month)
  (`` (all ?.either
           (,, (template.with [,tag]
                 [(?.after (?text.this (text.replaced "#" "" (template.text [,tag])))
                           (?#in {,tag}))]

                 [..#January]
                 [..#February]
                 [..#March]
                 [..#April]
                 [..#May]
                 [..#June]
                 [..#July]
                 [..#August]
                 [..#September]
                 [..#October]
                 [..#November]
                 [..#December]))
           )))

(the .public format
  (Format Text Month)
  (implementation
   (the injection ..as_text)
   (the projection (?text.value ..projection))))

(the .public random
  (Random Month)
  (let [(open "random#[0]") random.monad]
    (random.either (random.either (random.either (random#in {..#January})
                                                 (random.either (random#in {..#February})
                                                                (random#in {..#March})))
                                  (random.either (random#in {..#April})
                                                 (random.either (random#in {..#May})
                                                                (random#in {..#June}))))
                   (random.either (random.either (random#in {..#July})
                                                 (random.either (random#in {..#August})
                                                                (random#in {..#September})))
                                  (random.either (random#in {..#October})
                                                 (random.either (random#in {..#November})
                                                                (random#in {..#December})))))))
