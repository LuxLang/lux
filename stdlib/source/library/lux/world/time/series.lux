... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [functor (.only Functor)]
    [mix (.only Mix)]
    [monad (.only do)]]
   [control
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" product]
    [text
     ["%" \\format]]
    [collection
     ["/" sequence (.use "[1]#[0]" functor mix)]]]
   [math
    [number
     ["n" nat]]]
   [meta
    [type (.only sharing)]]]]
 [//
  ["[0]" duration (.only Duration) (.use "[1]#[0]" equivalence)]
  ["[0]" instant (.only Instant) (.use "[1]#[0]" order)]])

(every .public (Series of)
  (Record
   [#start Instant
    #interval Duration
    #data (/.Sequence of)]))

(def .public (equivalence super)
  (All (_ of)
    (-> (Equivalence of)
        (Equivalence (Series of))))
  (all product.equivalence
       instant.equivalence
       duration.equivalence
       (/.equivalence super)
       ))

(def .public functor
  (Functor Series)
  (implementation
   (def (each $)
     (|>> (revised #data (/#each $))))))

(def .public mix
  (Mix Series)
  (implementation
   (def (mix $ init)
     (|>> (its #data)
          (/#mix $ init)))))

(def .public size
  (All (_ of)
    (-> (Series of)
        Nat))
  (|>> (its #data)
       /.size))

(def .public start
  (All (_ of)
    (-> (Series of)
        Instant))
  (its #start))

(def .public (end it)
  (All (_ of)
    (-> (Series of)
        Instant))
  (instant.after (duration.up (-- (/.size (its #data it)))
                              (its #interval it))
                 (its #start it)))

(def .public (at event it)
  (All (_ of)
    (-> Nat (Series of)
        Instant))
  (instant.after (duration.up event (its #interval it))
                 (its #start it)))

(exception.def .public empty)

(with_template [<index> <name>]
  [(def .public (<name> it)
     (All (_ of)
       (-> (Series of)
           (Try of)))
     (let [data (its #data it)]
       (when (/.size data)
         0 (exception.except ..empty [])
         @ (/.item <index> data))))]

  [(|> 0) first]
  [(-- @) last]
  )

(exception.def .public (window_goes_out_of_bounds [offset size max_size])
  (Exception [Nat Nat Nat])
  (exception.report
   (list ["From" (%.nat offset)]
         ["To" (%.nat (n.+ offset size))]
         ["Maximum" (%.nat max_size)])))

(def .public (window offset size it)
  (All (_ of)
    (-> Nat Nat (Series of)
        (Try (Series of))))
  (if (n.< (n.+ offset size)
           (..size it))
    (exception.except ..window_goes_out_of_bounds [offset size (..size it)])
    (let [input (its #data it)]
      (loop (again [item 0
                    output (sharing [of]
                             (is (/.Sequence of)
                                 input)
                             (is (/.Sequence of)
                                 /.empty))])
        (if (n.< size item)
          (do try.monad
            [it (/.item (n.+ offset item) input)]
            (again (++ item) (/.suffix it output)))
          {try.#Success (let [interval (its #interval it)]
                          [#start (instant.after (duration.up offset interval)
                                                 (its #start it))
                           #interval interval
                           #data output])})))))
