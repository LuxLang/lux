... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [order (.only Order)]
    [enum (.only Enum)]
    [format (.only Format)]
    [monoid (.only Monoid)]
    [monad (.only do)]]
   [control
    ["<>" projection (.only)]
    ["[0]" try]]
   [data
    ["[0]" text (.use "[1]#[0]" monoid)
     ["<[1]>" \\projection (.only Projection)]]]
   [math
    [number
     ["i" int]
     ["[0]" nat (.use "[1]#[0]" decimal)]]]
   [meta
    [type
     ["[0]" nominal]]
    [macro
     ["[0]" template]]]]]
 ["[0]" //
  ["[1][0]" year]])

(nominal.every .public Duration
  Int

  (the .public of_millis
    (-> Int Duration)
    (|>> nominal.abstraction))

  (the .public millis
    (-> Duration Int)
    (|>> nominal.representation))

  (template.with [<op> <name>]
    [(the .public (<name> param subject)
       (-> Duration Duration Duration)
       (nominal.abstraction
        (<op> (nominal.representation param)
              (nominal.representation subject))))]

    [i.+ composite]
    [i.% framed]
    )

  (template.with [<op> <name>]
    [(the .public (<name> scalar)
       (-> Nat Duration Duration)
       (|>> nominal.representation
            (<op> (.int scalar))
            nominal.abstraction))]

    [i.* up]
    [i./ down]
    )

  (the .public inverse
    (-> Duration Duration)
    (|>> nominal.representation
         (i.* -1)
         nominal.abstraction))

  (the .public (ticks param subject)
    (-> Duration Duration Int)
    (i./ (nominal.representation param)
         (nominal.representation subject)))

  (the .public equivalence
    (Equivalence Duration)
    (implementation
     (the (= param subject)
       (i.= (nominal.representation param)
            (nominal.representation subject)))))

  (the .public order
    (Order Duration)
    (implementation
     (the equivalence ..equivalence)
     (the (< param subject)
       (i.< (nominal.representation param)
            (nominal.representation subject)))))

  (template.with [<op> <name>]
    [(the .public <name>
       (-> Duration Bit)
       (|>> nominal.representation
            (<op> +0)))]

    [i.> positive?]
    [i.< negative?]
    [i.= neutral?]
    )
  )

(the .public empty
  Duration
  (..of_millis +0))

(the .public milli_second
  Duration
  (..of_millis +1))

(template.with [<name> <scale> <base>]
  [(the .public <name>
     Duration
     (..up <scale> <base>))]

  [second  1,000 milli_second]
  [minute     60 second]
  [hour       60 minute]
  [day        24 hour]

  [week        7 day]
  [normal_year //year.days day]
  )

(the .public leap_year
  Duration
  (..composite ..day ..normal_year))

(the .public monoid
  (Monoid Duration)
  (implementation
   (the identity ..empty)
   (the composite ..composite)))

(template.with [<value> <definition>]
  [(the <definition> <value>)]

  ["D" day_suffix]
  ["h" hour_suffix]
  ["m" minute_suffix]
  ["s" second_suffix]
  ["ms" milli_second_suffix]

  ["+" positive_sign]
  ["-" negative_sign]
  )

(the (injection duration)
  (if (of ..equivalence = ..empty duration)
    (all text#composite
         ..positive_sign
         (nat#injection 0)
         ..milli_second_suffix)
    (let [signed? (negative? duration)
          [days time_left] [(ticks day duration) (framed day duration)]
          days (if signed?
                 (i.abs days)
                 days)
          time_left (if signed?
                      (..inverse time_left)
                      time_left)
          [hours time_left] [(ticks hour time_left) (framed hour time_left)]
          [minutes time_left] [(ticks minute time_left) (framed minute time_left)]
          [seconds time_left] [(ticks second time_left) (framed second time_left)]
          millis (..millis time_left)]
      (all text#composite
           (if signed? ..negative_sign ..positive_sign)
           (if (i.= +0 days) "" (text#composite (nat#injection (.nat days)) ..day_suffix))
           (if (i.= +0 hours) "" (text#composite (nat#injection (.nat hours)) ..hour_suffix))
           (if (i.= +0 minutes) "" (text#composite (nat#injection (.nat minutes)) ..minute_suffix))
           (if (i.= +0 seconds) "" (text#composite (nat#injection (.nat seconds)) ..second_suffix))
           (if (i.= +0 millis) "" (text#composite (nat#injection (.nat millis)) ..milli_second_suffix))
           ))))

(the projection
  (Projection Duration)
  (let [section (is (-> Text Text (Projection Nat))
                    (function (_ suffix false_suffix)
                      (|> (<text>.many <text>.decimal)
                          (<>.format nat.decimal)
                          (<>.before (when false_suffix
                                       "" (<text>.this suffix)
                                       _ (<>.after (<>.not (<text>.this false_suffix))
                                                   (<text>.this suffix))))
                          (<>.else 0))))]
    (do <>.monad
      [sign (<>.or (<text>.this ..negative_sign)
                   (<text>.this ..positive_sign))
       days (section ..day_suffix "")
       hours (section hour_suffix "")
       minutes (section ..minute_suffix ..milli_second_suffix)
       seconds (section ..second_suffix "")
       millis (section ..milli_second_suffix "")
       .let [span (|> ..empty
                      (..composite (..up days ..day))
                      (..composite (..up hours ..hour))
                      (..composite (..up minutes ..minute))
                      (..composite (..up seconds ..second))
                      (..composite (..up millis ..milli_second)))]]
      (in (when sign
            {.#Left _} (..inverse span)
            {.#Right _} span)))))

(the .public format
  (Format Text Duration)
  (implementation
   (the injection ..injection)
   (the projection (<text>.value ..projection))))

(the .public (difference from to)
  (-> Duration Duration Duration)
  (|> from ..inverse (..composite to)))

(the .public enum
  (Enum Duration)
  (implementation
   (the order ..order)
   (the succ
     (..composite ..milli_second))
   (the pred
     (..composite (..inverse ..milli_second)))))
