... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except false true
                or and if not)
   [abstract
    [equality (.only Equality)]]
   [data
    ["[0]" product]]
   [macro
    ["[0]" template]]]]
 [//
  ["[0]" byte (.only Byte)]])

... https://en.wikipedia.org/wiki/Word_(computer_architecture)
(every .public Word
  (Record
   [:7 Byte
    :6 Byte
    :5 Byte
    :4 Byte
    :3 Byte
    :2 Byte
    :1 Byte
    :0 Byte]))

(template.with [,name ,byte]
  [(the .public ,name
     Word
     [:7 ,byte
      :6 ,byte
      :5 ,byte
      :4 ,byte
      :3 ,byte
      :2 ,byte
      :1 ,byte
      :0 ,byte])]

  [[false byte.false]
   [true byte.true]])

(the .public equality
  (Equality Word)
  (all product.equality
       byte.equality
       byte.equality
       byte.equality
       byte.equality
       byte.equality
       byte.equality
       byte.equality
       byte.equality))

(the .public (not [/7 /6 /5 /4 /3 /2 /1 /0])
  (Change Word)
  [:7 (byte.not /7)
   :6 (byte.not /6)
   :5 (byte.not /5)
   :4 (byte.not /4)
   :3 (byte.not /3)
   :2 (byte.not /2)
   :1 (byte.not /1)
   :0 (byte.not /0)])

(template.with [,name ,per_byte]
  [(the .public (,name this that)
     (-> Word
         (Change Word))
     (let [[l7 l6 l5 l4 l3 l2 l1 l0] this
           [r7 r6 r5 r4 r3 r2 r1 r0] that]
       [:7 (,per_byte l7 r7)
        :6 (,per_byte l6 r6)
        :5 (,per_byte l5 r5)
        :4 (,per_byte l4 r4)
        :3 (,per_byte l3 r3)
        :2 (,per_byte l2 r2)
        :1 (,per_byte l1 r1)
        :0 (,per_byte l0 r0)]))]

  [[xor byte.xor]
   [or byte.or]
   [and byte.and]])
