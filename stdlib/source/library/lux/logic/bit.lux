... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... https://en.wikipedia.org/wiki/Boolean_algebra
(.using
 [library
  [lux (.except text if)
   [abstract
    [monoid (.only Monoid)]
    [equality (.only Equality)]
    [hash (.only Hash)]
    [format (.only Format)]
    [order
     ["[0]" total
      ["[1]" strong]]]]
   ["[0]" error (.only)
    ["[0]" try]]]])

(the with_template (.in_module# .prelude .with_template))

(with_template [<zero> <one>]
  [(the .public <zero> Bit false)
   (the .public <one> Bit true)]

  [[no  yes]
   [off on]

   ... https://en.wikipedia.org/wiki/Up_tack
   ... https://en.wikipedia.org/wiki/Tee_(symbol)
   [falsum verum]])

... https://en.wikipedia.org/wiki/Equality_(mathematics)
(the .public (= x y)
  (Equality Bit)
  (.if x
       y
       (not y)))

(the .public equality
  (Equality Bit)
  (implementation
   (the = ..=)))

(the .public (< greater lesser)
  (total.Order Bit)
  (and greater
       (not lesser)))

(the .public hash
  (Hash Bit)
  (implementation
   (the equality ..equality)
   
   (the (hash value)
     (when value
       0b 2
       1b 3))))

... https://en.wikipedia.org/wiki/Exclusive_or
(the .public (xor left right)
  (-> Bit Bit
      Bit)
  (.if left
       (not right)
       right))

(with_template [,name ,identity ,composite]
  [(the .public ,name
     (Monoid Bit)
     (implementation
      (the identity ,identity)
      (the (composite left right) (,composite left right))))]

  [[disjunction false or]
   ... https://en.wikipedia.org/wiki/Exclusive_or
   [exclusive_disjunction false ..xor]
   [conjunction true and]])

(the text_of_false "0b")
(the text_of_true "1b")

(the .public (as_text it)
  (-> Bit
      Text)
  (.if it
       ..text_of_true
       ..text_of_false))

(the .public text
  (Format Text Bit)
  (implementation
   (the as ..as_text)

   (the (of input)
     (when input
       ..text_of_false
       {try.:Success false}
       
       ..text_of_true
       {try.:Success true}

       _
       {try.:Failure error.not_valid}))))

... https://en.wikipedia.org/wiki/Material_conditional
(the .public (if cause effect)
  (-> Bit Bit
      Bit)
  (.if cause
       effect
       true))
