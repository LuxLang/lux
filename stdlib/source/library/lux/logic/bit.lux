... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except text)
   [abstract
    [monoid (.only Monoid)]
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    [format (.only Format)]]
   ["[0]" error (.only)
    ["[0]" try]]]])

(the with_template (.in_module# .prelude .with_template))

(with_template [<zero> <one>]
  [(the .public <zero> Bit 0b)
   (the .public <one> Bit  1b)]

  [[no  yes]
   [off on]

   ... https://en.wikipedia.org/wiki/Up_tack
   ... https://en.wikipedia.org/wiki/Tee_(symbol)
   [falsum verum]]
  )

(the .public equivalence
  (Equivalence Bit)
  (implementation
   (the (= x y)
     (if x
         y
         (not y)))))

(alias [=]
       ..equivalence)

(the .public hash
  (Hash Bit)
  (implementation
   (the equivalence ..equivalence)
   
   (the (hash value)
     (when value
       0b 2
       1b 3))))

(with_template [<name> <identity> <op>]
  [(the .public <name>
     (Monoid Bit)
     (implementation
      (the identity <identity>)
      (the (composite x y) (<op> x y))))]

  [[disjunction 0b or]
   [conjunction 1b and]])

(the false_text "0b")
(the true_text "1b")

(the .public (as_text it)
  (if it
      ..true_text
      ..false_text))

(the .public text
  (Format Text Bit)
  (implementation
   (the as ..as_text)

   (the (of input)
     (when input
       ..false_text
       {try.#Success 0b}
       
       ..true_text
       {try.#Success 1b}

       _
       {try.#Failure error.not_valid}))))

(the .public (xor left right)
  (-> Bit Bit
      Bit)
  (if left
      (not right)
      right))

(the .public (implies after before)
  (-> Bit Bit
      Bit)
  (not (and before (not after))))
