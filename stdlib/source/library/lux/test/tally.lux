... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except :expected and)
   [data
    [collection
     ["[0]" set (.only Set)]]]
   [math
    [number
     [/64
      ["n" natural]]]]
   [macro
    ["[0]" template]]
   [meta
    ["[0]" name]]]]
 [//
  ["[0]" coverage (.only Coverage)]])

(every .public Tally
  (Record
   [:successes Natural
    :failures Natural
    :expected Coverage
    :actual Coverage]))

(the .public failed?
  (-> Tally Bit)
  (|>> (its :failures) (n.> 0)))

(the .public (and parameter subject)
  (-> Tally Tally Tally)
  [:successes (n.+ (its :successes parameter) (its :successes subject))
   :failures (n.+ (its :failures parameter) (its :failures subject))
   :expected (set.union (its :expected parameter)
                        (its :expected subject))
   :actual (set.union (its :actual parameter)
                      (its :actual subject))])

(the .public empty
  Tally
  [:successes 0
   :failures 0
   :expected (set.empty name.hash)
   :actual (set.empty name.hash)])

(template.with [<name> <category>]
  [(the .public <name>
     Tally
     (revised <category> .++ ..empty))]

  [[success :successes]
   [failure :failures]])
