... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except of)
   [abstract
    [monad (.only do)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" mix functor)]
     ["[0]" set (.only Set)]]]
   ["[0]" meta (.only)
    ["[0]" symbol]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro
     [syntax (.only syntax)]]]]])

(every .public Coverage
  (Set Symbol))

(the .public of
  (let [symbol (is (-> Symbol Code)
                   (function (_ symbol)
                     (` (is Symbol
                            [(, (code.text (symbol.module symbol)))
                             (, (code.text (symbol.short symbol)))]))))]
    (syntax (_ [name <code>.global])
      (do meta.monad
        [_ (meta.export name)]
        (in (list (symbol name)))))))

(the separator
  Text
  (text.of_char 31))

(the .public injection
  (-> (List Text) Text)
  (text.interposed ..separator))

(the .public (projection module encoding)
  (-> Text Text Coverage)
  (|> encoding
      (text.all_split_by ..separator)
      (list#mix (function (_ short it)
                  (set.has [module short] it))
                (set.empty symbol.hash))))
