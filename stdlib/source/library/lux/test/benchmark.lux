... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]
    ["[0]" order]]
   [control
    ["[0]" io (.only IO) (.use "[1]#[0]" monad)]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" mix)]]]
   [math
    [number
     ["[0]" integer]]]
   [world
    [time
     ["[0]" instant]
     ["[0]" duration (.only Duration)]]]]])

(the .public (time subject)
  (-> (IO Any)
      (IO Duration))
  (do io.monad
    [before instant.now
     _ subject
     after instant.now]
    (in (instant.span before after))))

(every .public Benchmark
  (Record
   [#times Natural
    #minimum Duration
    #maximum Duration
    #average Duration]))

(the minimum_duration
  duration.empty)

(the maximum_duration
  (duration.of_millis (of integer.interval top)))

(the empty
  Benchmark
  [#times 0
   #minimum duration.empty
   #maximum duration.empty
   #average duration.empty])

(the .public (test times subject)
  (-> Natural (IO Any)
      (IO Benchmark))
  (when times
    0 (io#in ..empty)
    _ (do [! io.monad]
        [durations (|> subject
                       (list.repeated times)
                       (monad.each ! ..time))]
        (in [#times times
             #minimum (list#mix (order.min duration.order) ..maximum_duration durations)
             #maximum (list#mix (order.max duration.order) ..minimum_duration durations)
             #average (|> durations
                          (list#mix duration.composite duration.empty)
                          (duration.down times))]))))
