... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" order]]
   [control
    ["[0]" io (.only IO) (.use "[1]#[0]" monad)]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" mix)]]]
   [math
    [number
     ["[0]" integer]]]
   [time
    ["[0]" instant]
    ["[0]" duration (.only Duration)]]]])

(the .public (time subject)
  (-> (IO Any)
      (IO Duration))
  (do io.monad
    [before instant.now
     _ subject
     after instant.now]
    (pure (instant.span before after))))

(every .public Benchmark
  (Record
   [#times Natural
    #minimum Duration
    #maximum Duration
    #average Duration]))

(the minimum_duration
  duration.empty)

(the maximum_duration
  (duration.of_millis (by integer.interval maximum)))

(the empty
  Benchmark
  [#times 0
   #minimum duration.empty
   #maximum duration.empty
   #average duration.empty])

(the .public (test times subject)
  (-> Natural (IO Any)
      (IO Benchmark))
  (when times
    0 (io#pure ..empty)
    _ (do [! io.monad]
        [durations (|> subject
                       (list.repeated times)
                       (list.each' ! ..time))]
        (pure [#times times
               #minimum (list#mix (order.minor duration.order) ..maximum_duration durations)
               #maximum (list#mix (order.major duration.order) ..minimum_duration durations)
               #average (|> durations
                            (list#mix duration.composite duration.empty)
                            (duration.down times))]))))
