(.require
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]
    ["[0]" order]]
   [control
    ["[0]" io (.only IO) (.use "[1]#[0]" monad)]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" mix)]]]
   [world
    [time
     ["[0]" instant]
     ["[0]" duration (.only Duration)]]]]])

(def .public (time subject)
  (-> (IO Any)
      (IO Duration))
  (do io.monad
    [before instant.now
     _ subject
     after instant.now]
    (in (instant.span before after))))

(type .public Benchmark
  (Record
   [#times Nat
    #minimum Duration
    #maximum Duration
    #average Duration]))

(def empty
  Benchmark
  [#times 0
   #minimum duration.empty
   #maximum duration.empty
   #average duration.empty])

(def .public (test times subject)
  (-> Nat (IO Any)
      (IO Benchmark))
  (when times
    0 (io#in ..empty)
    _ (do [! io.monad]
        [durations (|> subject
                       (list.repeated times)
                       (monad.each ! ..time))]
        (in [#times times
             #minimum (list#mix (order.min duration.order) duration.empty durations)
             #maximum (list#mix (order.max duration.order) duration.empty durations)
             #average (|> durations
                          (list#mix duration.composite duration.empty)
                          (duration.down times))]))))
