... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except static)
   [abstract
    ["?" projection]
    ["[0]" monad]]
   [error
    ["[0]" try]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list
      ["?[1]" \\projection]]]]
   [math
    ["[0]" number]
    ["[0]" random (.only Random)]]
   [macro
    ["[0]" syntax]]
   ["[0]" meta (.only)
    ["[0]" code (.only)
     ["?[1]" \\projection (.only Projection)]]]]])

(exception.the .public (failure test)
  (Exception Code)
  (exception.report
   (list ["Test" (code.as_text test)])))

(every .public Test
  (Random Bit))

(the pcg_32_magic_inc
  (number.base_16 "FEDCBA9876543210"))

(the ?static
  (Projection [(Maybe Natural)
               Code])
  (all ?.either
       (monad.let ?.monad
         [seed ?code.natural
          term ?list.any]
         (pure [{.#Some seed} term]))
       (monad.let ?.monad
         [term ?list.any]
         (pure [{.#None} term]))
       ))

(the .public static
  (syntax.macro (_ [[seed term] ?static])
    (monad.let [! meta.monad]
      [test (meta.eval Test term)
       seed (when seed
              {.#Some seed}
              (pure seed)

              _
              meta.seed)
       .let [[_ success?] (random.value random.pcg_32
                                        [random.#increase ..pcg_32_magic_inc
                                         random.#seed seed]
                                        (as (-> Any Test) test))]]
      (if success?
          (pure (list))
          (meta.failure (exception.error ..failure [term]))))))

(the .public dynamic
  (syntax.macro (_ [test ?list.any])
    (monad.let [! meta.monad]
      [error_message (meta.try (meta.failure (exception.error ..failure [test])))]
      (pure (list (` (is Any
                         (if (is Bit (, test))
                             []
                             (halt! (, (code.text (when error_message
                                                    {try.#Failure error}
                                                    error
                                                    
                                                    {try.#Success _}
                                                    ""))))))))))))
