(.module:
  [library
   [lux #*
    ["." meta]
    [abstract
     [equivalence (#+ Equivalence)]
     [monad (#+ do)]]
    [control
     ["." exception (#+ exception:)]
     ["<>" parser
      ["<.>" code (#+ Parser)]]]
    [data
     ["." product]]
    [macro
     ["." code]]]])

(def: extension
  "lux check")

(type: .public Check
  (Record
   {#type Code
    #value Code}))

(def: .public equivalence
  (Equivalence Check)
  ($_ product.equivalence
      code.equivalence
      code.equivalence
      ))

(def: .public (format (^slots [#type #value]))
  (-> Check Code)
  (` ((~ (code.text ..extension))
      (~ type)
      (~ value))))

(def: .public parser
  (Parser Check)
  (<| <code>.form
      (<>.after (<code>.text! ..extension))
      (<>.and <code>.any
              <code>.any)))
