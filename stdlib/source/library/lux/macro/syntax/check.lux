... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]]
   [control
    ["<>" projection]]
   [data
    ["[0]" product]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection (.only Projection)]]]]])

(the extension
  "lux check")

(every .public Check
  (Record
   [#type Code
    #value Code]))

(the .public equivalence
  (Equivalence Check)
  (all product.equivalence
       code.equivalence
       code.equivalence
       ))

(alias [=]
       ..equivalence)

(the .public (injection (open "_[0]"))
  (-> Check Code)
  (` ((, (code.text ..extension))
      (, _#type)
      (, _#value))))

(the .public projection
  (Projection Check)
  (<| <code>.form
      (<>.after (<code>.this_text ..extension))
      (<>.and <code>.any
              <code>.any)))
