(.module:
  [library
   [lux #*
    [abstract
     [equivalence (#+ Equivalence)]]
    [control
     ["." function]
     ["<>" parser
      ["<.>" code (#+ Parser)]]]
    [data
     ["." product]
     ["." name]
     [collection
      ["." list ("#\." functor)]]]
    [macro
     ["." code]]]])

(type: .public Annotations
  {#.doc (example "Definition/module annotations.")}
  (List [Name Code]))

(def: .public equivalence
  (Equivalence Annotations)
  (list.equivalence
   (product.equivalence name.equivalence
                        code.equivalence)))

(def: .public empty
  Annotations
  (list))

(def: .public format
  (-> Annotations Code)
  (let [entry (product.then code.tag function.identity)]
    (|>> (list\map entry)
         code.record)))

(def: .public parser
  (Parser Annotations)
  (<code>.record
   (<>.some
    (<>.and <code>.tag
            <code>.any))))
