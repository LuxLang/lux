(.module:
  [library
   [lux #*
    [abstract
     [equivalence (#+ Equivalence)]]
    [control
     ["<>" parser ("#\." monad)
      ["<.>" code (#+ Parser)]]]
    [data
     ["." product]
     ["." text]
     [collection
      ["." list ("#\." functor)]]]
    [macro
     ["." code]]]])

(type: .public Declaration
  {#.doc (example "A declaration for either a constant or a function.")}
  {#name Text
   #arguments (List Text)})

(def: .public equivalence
  (Equivalence Declaration)
  ($_ product.equivalence
      text.equivalence
      (list.equivalence text.equivalence)
      ))

(def: .public parser
  {#.doc (example "A parser for declaration syntax."
                  "Such as:"
                  quux
                  (foo bar baz))}
  (Parser Declaration)
  (<>.either (<>.and <code>.local_identifier
                     (<>\in (list)))
             (<code>.form (<>.and <code>.local_identifier
                                  (<>.some <code>.local_identifier)))))

(def: .public (format value)
  (-> Declaration Code)
  (let [g!name (code.local_identifier (get@ #name value))]
    (case (get@ #arguments value)
      #.End
      g!name
      
      arguments
      (` ((~ g!name) (~+ (list\map code.local_identifier arguments)))))))
