... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    ["<>" projection]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" monad)]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection (.only Projection)]]]]])

(every .public Input
  (Record
   [#binding Code
    #type Code]))

(the .public equivalence
  (Equivalence Input)
  (all product.equivalence
       code.equivalence
       code.equivalence
       ))

(alias [=]
       ..equivalence)

(the .public injection
  (-> (List Input) Code)
  (|>> (list#each (function (_ value)
                    (list (its #binding value)
                          (its #type value))))
       list#conjoint
       code.tuple))

(the .public projection
  (Projection (List Input))
  (<| <code>.tuple
      <>.some
      (all <>.and
           <code>.any
           <code>.any
           )))
