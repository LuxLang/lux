(.module:
  [library
   [lux #*
    [abstract
     [equivalence (#+ Equivalence)]]
    [control
     ["<>" parser
      ["<.>" code (#+ Parser)]]]
    [data
     ["." product]]
    [macro
     ["." code]]]])

(type: .public Input
  {#.doc (example "The common typed-argument syntax used by many macros.")}
  {#binding Code
   #type Code})

(def: .public equivalence
  (Equivalence Input)
  ($_ product.equivalence
      code.equivalence
      code.equivalence
      ))

(def: .public (format value)
  (-> Input Code)
  (code.record
   (list [(value@ #binding value)
          (value@ #type value)])))

(def: .public parser
  {#.doc "Parser for the common typed-argument syntax used by many macros."}
  (Parser Input)
  (<code>.record
   ($_ <>.and
       <code>.any
       <code>.any
       )))
