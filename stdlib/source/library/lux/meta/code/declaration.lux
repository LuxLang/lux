... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Declaration)
   [abstract
    [equality (.only Equality)]
    ["<>" projection (.use "[1]:[0]" monad)]]
   [data
    ["[0]" product]
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]:[0]" functor)]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection (.only Projection)]]]]])

(every .public Declaration
  (Record
   [:name Text
    :arguments (List Text)]))

(the .public equality
  (Equality Declaration)
  (all product.equality
       text.equality
       (list.equality text.equality)
       ))

(alias [=]
       ..equality)

(the .public of_code
  (Projection Declaration)
  (<>.either (<>.and <code>.local
                     (<>:pure (list)))
             (<code>.form (<>.and <code>.local
                                  (<>.some <code>.local)))))

(the .public (as_code value)
  (-> Declaration
      Code)
  (let ['name (code.local (its :name value))]
    (when (its :arguments value)
      (list)
      'name
      
      arguments
      (` ((, 'name) (,* (list:each code.local arguments)))))))
