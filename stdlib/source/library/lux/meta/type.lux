... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except function as let variant tuple only except)
   [abstract
    [equivalence (.only Equivalence)]
    [format (.only Format)]
    ["[0]" monad (.only Monad do)]]
   [control
    ["?" projection (.use "[1]#[0]" monad)]
    ["[0]" maybe]
    ["[0]" try (.only Try)]
    ["[0]" function (.only)
     ["[0]" predicate (.only Predicate)]]]
   [data
    ["[0]" product]
    ["[0]" text (.use "[1]#[0]" monoid equivalence)]
    [collection
     ["[0]" array]
     ["[0]" list (.use "[1]#[0]" monad monoid mix)]]]
   [math
    [number
     ["n" nat (.use "[1]#[0]" decimal)]]]
   ["[0]" meta (.use "[1]#[0]" functor)
    ["[0]" location]
    ["[0]" symbol (.use "[1]#[0]" equivalence)]
    ["[0]" code (.only)
     ["?[1]" \\projection (.only Projection)]]
    ["[0]" macro (.only)
     ["[0]" syntax]
     ["[0]" expansion]]]]])

(with_template [<name> <tag>]
  [(the .public (<name> type)
     (-> Type
         [Nat Type])
     (loop (again [num_args 0
                   type type])
       (when type
         {<tag> env sub_type}
         (again (++ num_args) sub_type)

         _
         [num_args type])))]

  [flat_univ_q .#Universal]
  [flat_ex_q   .#Existential]
  )

(the .public (flat_function type)
  (-> Type
      [(List Type) Type])
  (when type
    {.#Function in out'}
    (.let [[ins out] (flat_function out')]
      [(list.partial in ins) out])

    _
    [(list) type]))

(the .public (flat_application type)
  (-> Type
      [Type (List Type)])
  (when type
    {.#Apply arg func'}
    (.let [[func args] (flat_application func')]
      [func (list#composite args (list arg))])

    _
    [type (list)]))

(with_template [<name> <tag>]
  [(the .public (<name> type)
     (-> Type
         (List Type))
     (when type
       {<tag> left right}
       (list.partial left (<name> right))

       _
       (list type)))]

  [flat_variant .#Sum]
  [flat_tuple   .#Product]
  )

(`` (the (injection symbol_format type)
      (-> (Format Text Symbol) Type
          Text)
      (when type
        {.#Nominal name params}
        (all text#composite
             "(Nominal "
             (text.enclosed' text.double_quote name)
             (|> params
                 (list#each (|>> (injection symbol_format) (text#composite " ")))
                 (list#mix (function.flipped text#composite) ""))
             ")")

        (,, (with_template [<tag> <open> <close> <flat>]
              [{<tag> _}
               (all text#composite <open>
                    (|> (<flat> type)
                        (list#each (injection symbol_format))
                        list.reversed
                        (list.interposed " ")
                        (list#mix text#composite ""))
                    <close>)]

              [.#Sum  "(Or " ")" flat_variant]
              [.#Product "["   "]" flat_tuple]))

        {.#Function input output}
        (.let [[ins out] (flat_function type)]
          (all text#composite  "(-> "
               (|> ins
                   (list#each (injection symbol_format))
                   list.reversed
                   (list.interposed " ")
                   (list#mix text#composite ""))
               " " (injection symbol_format out) ")"))

        {.#Parameter idx}
        (n#injection idx)

        {.#Var id}
        (all text#composite "-" (n#injection id))

        {.#Opaque id}
        (all text#composite "+" (n#injection id))

        {.#Apply param fun}
        (.let [[type_func type_args] (flat_application type)]
          (all text#composite  "(" (injection symbol_format type_func) " " (|> type_args (list#each (injection symbol_format)) list.reversed (list.interposed " ") (list#mix text#composite "")) ")"))

        (,, (with_template [<tag> <desc>]
              [{<tag> env body}
               (all text#composite "(" <desc> " {" (|> env (list#each (injection symbol_format)) (text.interposed " ")) "} " (injection symbol_format body) ")")]

              [.#Universal "All"]
              [.#Existential "Ex"]))

        {.#Named name type}
        (of symbol_format injection name)
        )))

(the .public absolute_injection
  (-> Type
      Text)
  (..injection symbol.absolute))

(the .public (relative_injection module)
  (-> Text Type
      Text)
  (..injection (symbol.relative module)))

... https://en.wikipedia.org/wiki/Lambda_calculus#%CE%B2-reduction
(`` (the (reduced env type)
      (-> (List Type) Type
          Type)
      (when type
        {.#Nominal name params}
        {.#Nominal name (list#each (reduced env) params)}
        
        (,, (with_template [<tag>]
              [{<tag> left right}
               {<tag> (reduced env left) (reduced env right)}]

              [.#Sum]
              [.#Product]
              [.#Function]
              [.#Apply]))
        
        (,, (with_template [<tag>]
              [{<tag> old_env def}
               (when old_env
                 {.#End}
                 {<tag> env def}

                 _
                 {<tag> (list#each (reduced env) old_env) def})]

              [.#Universal]
              [.#Existential]))
        
        {.#Parameter idx}
        (try.else (panic! (all text#composite
                               "Unknown type parameter" text.new_line
                               "      Index: " (n#injection idx) text.new_line
                               "Environment: " (|> env
                                                   list.enumeration
                                                   (list#each (.function (_ [index type])
                                                                (all text#composite
                                                                     (n#injection index)
                                                                     " " (..absolute_injection type))))
                                                   (text.interposed (text#composite text.new_line "             ")))))
                  (list.item idx env))
        
        _
        type
        )))

(`` (the .public equivalence
      (Equivalence Type)
      (implementation
       (the (= x y)
         (or (for .php
                  ... TODO: Remove this once JPHP is gone.
                  false
                  (same? x y))
             (when [x y]
               [{.#Nominal xname xparams} {.#Nominal yname yparams}]
               (and (text#= xname yname)
                    (n.= (list.size yparams) (list.size xparams))
                    (list#mix (.function (_ [x y] prev) (and prev (= x y)))
                              true
                              (list.zipped_2 xparams yparams)))

               (,, (with_template [<tag>]
                     [[{<tag> xid} {<tag> yid}]
                      (n.= yid xid)]

                     [.#Var]
                     [.#Opaque]
                     [.#Parameter]
                     ))

               (,, (with_template [<tag>]
                     [[{<tag> ll lr} {<tag> rl rr}]
                      (and (= ll rl)
                           (= lr rr))]

                     [.#Function]
                     [.#Apply]
                     [.#Sum]
                     [.#Product]
                     ))

               [{.#Named xname xtype} {.#Named yname ytype}]
               (and (symbol#= xname yname)
                    (= xtype ytype))

               (,, (with_template [<tag>]
                     [[{<tag> xenv xbody} {<tag> yenv ybody}]
                      (and (n.= (list.size yenv) (list.size xenv))
                           (= xbody ybody)
                           (list#mix (.function (_ [x y] prev) (and prev (= x y)))
                                     true
                                     (list.zipped_2 xenv yenv)))]

                     [.#Universal]
                     [.#Existential]
                     ))

               _
               false
               ))))))

(`` (the .public (applied params func)
      (-> (List Type) Type
          (Maybe Type))
      (when params
        {.#End}
        {.#Some func}

        {.#Item param params'}
        (when func
          (,, (with_template [<tag>]
                [{<tag> env body}
                 (|> body
                     (reduced (list.partial func param env))
                     (applied params'))]

                [.#Universal]
                [.#Existential]))

          {.#Apply A F}
          (applied (list.partial A params) F)

          {.#Named name unnamed}
          (applied params unnamed)
          
          _
          {.#None}))))

(`` (the .public (code type)
      (-> Type
          Code)
      (when type
        {.#Nominal name params}
        (` {.#Nominal (, (code.text name))
                      (.list (,* (list#each code params)))})

        (,, (with_template [<tag>]
              [{<tag> idx}
               (` {<tag> (, (code.nat idx))})]

              [.#Var]
              [.#Opaque]
              [.#Parameter]))

        (,, (with_template [<tag>]
              [{<tag> left right}
               (` {<tag> (, (code left))
                         (, (code right))})]

              [.#Sum]
              [.#Product]
              [.#Function]
              [.#Apply]))

        {.#Named name sub_type}
        (code.symbol name)

        (,, (with_template [<tag>]
              [{<tag> env body}
               (` {<tag> (.list (,* (list#each code env)))
                         (, (code body))})]

              [.#Universal]
              [.#Existential]))
        )))

(the .public (de_aliased type)
  (-> Type
      Type)
  (when type
    {.#Named _ {.#Named name type'}}
    (de_aliased {.#Named name type'})

    _
    type))

(the .public (anonymous type)
  (-> Type
      Type)
  (when type
    {.#Named name type'}
    (anonymous type')

    _
    type))

(with_template [<name> <base> <ctor>]
  [(the .public (<name> types)
     (-> (List Type)
         Type)
     (when types
       {.#End}
       <base>

       {.#Item type {.#End}}
       type

       {.#Item type types'}
       {<ctor> type (<name> types')}))]

  [variant Nothing .#Sum]
  [tuple   Any     .#Product]
  )

(the .public (function inputs output)
  (-> (List Type) Type
      Type)
  (when inputs
    {.#End}
    output

    {.#Item input inputs'}
    {.#Function input (function inputs' output)}))

(the .public (application params quant)
  (-> (List Type) Type
      Type)
  (when params
    {.#End}
    quant

    {.#Item param params'}
    (application params' {.#Apply param quant})))

(with_template [<name> <tag>]
  [(the .public (<name> size body)
     (-> Nat Type
         Type)
     (when size
       0 body
       _  (|> body (<name> (-- size)) {<tag> (list)})))]

  [univ_q .#Universal]
  [ex_q   .#Existential]
  )

(`` (the .public (quantified? type)
      (-> Type
          Bit)
      (when type
        {.#Named [module name] _type}
        (quantified? _type)

        {.#Apply A F}
        (|> (..applied (list A) F)
            (of maybe.monad each quantified?)
            (maybe.else false))

        (,, (with_template [<pattern>]
              [<pattern>
               true]

              [{.#Universal _}]
              [{.#Existential _}]))

        _
        false)))

(the .public (array depth element_type)
  (-> Nat Type
      Type)
  (when depth
    0 element_type
    _ (|> element_type
          (array (-- depth))
          (list)
          {.#Nominal array.nominal})))

(the .public (flat_array type)
  (-> Type
      [Nat Type])
  (expansion.let [<default> [0 type]]
    (when type
      {.#Nominal name (list element_type)}
      (if (text#= array.nominal name)
        (.let [[depth element_type] (flat_array element_type)]
          [(++ depth) element_type])
        <default>)

      _
      <default>)))

(the .public array?
  (-> Type
      Bit)
  (|>> ..flat_array
       product.left
       (n.> 0)))

(the new_secret_marker
  (syntax.macro (_ [])
    (macro.with_symbols ['_secret_marker_]
      (in (list '_secret_marker_)))))

(the secret_marker
  (`` (symbol (,, (new_secret_marker)))))

(the .public log!
  (syntax.macro (_ [input (?.or (?.and ?code.symbol
                                       (?.maybe (?.after (?code.this_symbol ..secret_marker) ?code.any)))
                                ?code.any)])
    (when input
      {.#Left [valueN valueC]}
      (do meta.monad
        [location meta.location
         valueT (meta.type valueN)
         .let [[@ _ _] location
               _ (.log!# (all text#composite
                              (of symbol.absolute injection (symbol ..log!)) " " (location.injection location) text.new_line
                              "Expression: " (when valueC
                                               {.#Some valueC}
                                               (code.absolute valueC)
                                               
                                               {.#None}
                                               (of symbol.absolute injection valueN))
                              text.new_line
                              "      Type: " (..relative_injection @ valueT)))]]
        (in (list (code.symbol valueN))))
      
      {.#Right valueC}
      (macro.with_symbols ['value]
        (in (list (` (.let [(, 'value) (, valueC)]
                       (..log! (, valueC) (, (code.symbol ..secret_marker)) (, 'value))))))))))

(the type_parameters
  (Projection (List Text))
  (?code.tuple (?.some ?code.local)))

(the .public as
  (syntax.macro (_ [type_vars type_parameters
                    input ?code.any
                    output ?code.any
                    value (?.maybe ?code.any)])
    (macro.with_symbols ['_]
      (.let [casterC (` (is (for_any ((, '_) (,* (list#each code.local type_vars)))
                              (-> (, input) (, output)))
                            (|>> as_expected)))]
        (when value
          {.#None}
          (in (list casterC))
          
          {.#Some value}
          (in (list (` ((, casterC) (, value))))))))))

(every Typed
  (Record
   [#type Code
    #expression Code]))

(the (typed lux)
  (-> Lux
      (Projection Typed))
  (do ?.monad
    [it ?code.any
     type_check (?.of_try (meta.value lux (expansion.complete it)))]
    (<| (?code.locally type_check)
        ?code.form
        (?.after (?code.this (` .is#)))
        (?.and ?code.any ?code.any))))

... TODO: Make sure the generated code always gets optimized away.
(the .public sharing
  (syntax.macro (_ lux [type_vars ..type_parameters
                        exemplar (..typed lux)
                        computation (..typed lux)])
    (macro.with_symbols ['_]
      (.let ['type (` (for_any ((, '_) (,* (list#each code.local type_vars)))
                        (-> (, (its #type exemplar))
                            (, (its #type computation)))))
             'share (` (is (, 'type)
                           (.function ((, '_) (, '_))
                             (, (its #expression computation)))))]
        (in (list (` ((, 'share) (, (its #expression exemplar))))))))))

(the .public by_example
  (syntax.macro (_ lux [type_vars ..type_parameters
                        exemplar (..typed lux)
                        extraction ?code.any])
    (in (list (` (.type_of (..sharing [(,* (list#each code.local type_vars))]
                             (is (, (its #type exemplar))
                                 (, (its #expression exemplar)))
                             (is (, extraction)
                                 ... The value of this expression will never be relevant, so it doesn't matter what it is.
                                 (.as .Nothing [])))))))))

(`` (the .public (replaced before after)
      (-> Type Type Type
          Type)
      (.function (again it)
        (if (of ..equivalence = before it)
          after
          (when it
            {.#Nominal name co_variant}
            {.#Nominal name (list#each again co_variant)}

            (,, (with_template [<tag>]
                  [{<tag> left right}
                   {<tag> (again left) (again right)}]

                  [.#Sum]
                  [.#Product]
                  [.#Function]
                  [.#Apply]))

            (,, (with_template [<tag>]
                  [{<tag> env body}
                   {<tag> (list#each again env) (again body)}]

                  [.#Universal]
                  [.#Existential]))

            (,, (with_template [<pattern>]
                  [<pattern>
                   it]

                  [{.#Parameter _}]
                  [{.#Var _}]
                  [{.#Opaque _}]
                  [{.#Named _}]))
            )))))

(the .public let
  (syntax.macro (_ [bindings (?code.tuple (?.some (?.and ?code.any ?code.any)))
                    bodyT ?code.any])
    (in (list (` (expansion.let [(,* (|> bindings
                                         (list#each (.function (_ [localT valueT])
                                                      (list localT (` (.these (, valueT))))))
                                         list#conjoint))]
                   (, bodyT)))))))

(the .public member
  (syntax.macro (_ [label ?code.global
                    parameters (?code.tuple (?.some ?code.any))])
    (do [! meta.monad]
      [full_label (meta.normal label)
       label (meta.or (meta.tag full_label)
                      (meta.slot full_label))
       parameters (monad.each ! (|>> (meta.eval Type)
                                     (of ! each (|>> (.as Type))))
                              parameters)
       .let [[flat context it] (when label
                                 {.#Left [context it]}
                                 [..flat_variant context it]
                                 
                                 {.#Right [context it]}
                                 [..flat_tuple context it])]
       here meta.current_module_name
       it (when (..applied parameters it)
            {.#Some it}
            (in it)

            {.#None}
            (meta.failure (all text#composite "Cannot apply type "
                               (relative_injection here it)
                               " to parameters: "
                               (|> parameters
                                   (list#each (relative_injection here))
                                   (text.interposed ", ")))))
       it (when context
            {.#None}
            (in it)

            {.#Some [lefts right? _]}
            (when (list.item (if right?
                               (++ lefts)
                               lefts)
                             (flat it))
              {try.#Success it}
              (in it)

              {try.#Failure _}
              (meta.failure (all text#composite "Invalid label "
                                 (of (symbol.relative here) injection full_label)
                                 " for type "
                                 (relative_injection here it)))))]
      (in (list (..code it))))))

(the .public cannot_specialize_function
  Error
  (location.with (location.here)
    "Cannot specialize function type."))

(the (specialized_function it parameters)
  (-> Symbol (List Code)
      (Meta [(List Type) Type]))
  (do [! meta.monad]
    [[it _] (meta.export it)
     parameters (monad.each ! (|>> (meta.eval Type)
                                   (of ! each (|>> (.as Type))))
                            parameters)
     it (when (..applied parameters it)
          {.#Some it}
          (in it)

          {.#None}
          (meta.failure ..cannot_specialize_function))]
    (in (..flat_function it))))

(the as_type
  (-> Type
      Code)
  (|>> ..code
       (,)
       (is Type)
       (`)))

(the .public parameters
  (syntax.macro (_ [function ?code.global
                    parameters (?code.tuple (?.some ?code.any))])
    (meta#each (|>> product.left
                    ..tuple
                    ..as_type
                    list)
               (specialized_function function parameters))))

(the .public return
  (syntax.macro (_ [function ?code.global
                    parameters (?code.tuple (?.some ?code.any))])
    (meta#each (|>> product.right
                    ..as_type
                    list)
               (specialized_function function parameters))))

(the .public slots_belong_to_different_types
  Error
  (location.with (location.here)
    "Slots belong to different types."))

(the (specialized_record parameters [head tail])
  (-> (List Code) [Symbol (List Symbol)]
      (Meta [(List Type) [Label (List Label)]]))
  (do [! meta.monad]
    [head (meta.slot head)
     tail (monad.each ! meta.slot tail)
     .let [general_type (product.right head)]
     _ (if (list.every? (|>> product.right (of ..equivalence = general_type)) tail)
         (in [])
         (meta.failure ..slots_belong_to_different_types))
     parameters (monad.each ! (|>> (meta.eval Type)
                                   (of ! each (|>> (.as Type))))
                            parameters)
     it (when (..applied parameters general_type)
          {.#Some it}
          (in it)

          {.#None}
          (meta.failure ..cannot_specialize_function))]
    (in [(..flat_tuple it) [head tail]])))

(the .public singleton_record
  Error
  (location.with (location.here)
    "Cannot produce a subset of a singleton record/tuple."))

(the (slot_index [it _])
  (-> Label
      (Try Nat))
  (when it
    {.#Some [index right? cohort]}
    {try.#Success (if right?
                    (++ index)
                    index)}

    {.#None}
    {try.#Failure ..singleton_record}))

(the .public only
  (syntax.macro (_ [parameters (?code.tuple (?.some ?code.any))
                    [head tail] (?code.tuple (?.and ?code.symbol (?.some ?code.symbol)))])
    (do [! meta.monad]
      [[specialized_type [head tail]] (specialized_record parameters [head tail])
       subset (meta.of_try
               (do [! try.monad]
                 [head (..slot_index head)
                  tail (monad.each ! ..slot_index tail)]
                 (|> (list.partial head tail)
                     (monad.each ! ((function.flipped list.item) specialized_type)))))]
      (in (list (as_type (..tuple subset)))))))

(the .public except
  (syntax.macro (_ [parameters (?code.tuple (?.some ?code.any))
                    [head tail] (?code.tuple (?.and ?code.symbol (?.some ?code.symbol)))])
    (do [! meta.monad]
      [[specialized_type [head tail]] (specialized_record parameters [head tail])
       excluded (meta.of_try
                 (do [! try.monad]
                   [head (..slot_index head)
                    tail (monad.each ! ..slot_index tail)]
                   (in (list.partial head tail))))
       .let [excluded? (is (Predicate Nat)
                           (.function (_ index)
                             (list.any? (n.= index) excluded)))
             included (|> (when (product.left head)
                            {.#Some [index exported? cohort]}
                            (list.size cohort)

                            {.#None}
                            1)
                          list.indices
                          (list.only (predicate.not excluded?)))]
       subset (meta.of_try
               (monad.each try.monad ((function.flipped list.item) specialized_type)
                           included))]
      (in (list (as_type (..tuple subset)))))))
