... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except with)
   [abstract
    [equivalence (.only Equivalence)]]]])

(the .public equivalence
  (Equivalence Location)
  (implementation
   (the (= expected actual)
     (and (.text_=# (its .#module expected) (its .#module actual))
          (.i64_=# (its .#line expected) (its .#line actual))
          (.i64_=# (its .#column expected) (its .#column actual))))))

(the .public dummy
  Location
  [.#module ""
   .#line 0
   .#column 0])

(the .public here
  (macro (_ tokens compiler)
    (when tokens
      {.#End}
      (let [location (its .#location compiler)]
        {.#Right [compiler
                  (list (` (.is .Location
                                [.#module (, [..dummy {.#Text (its .#module location)}])
                                 .#line (, [..dummy {.#Nat (its .#line location)}])
                                 .#column (, [..dummy {.#Nat (its .#column location)}])])))]})

      _
      {.#Left .wrong_syntax})))

(the .public (format it)
  (-> Location
      Text)
  (let [separator ","
        [file line column] it]
    (.text_composite# "@"
                      ((.in_module# .prelude .text#injection) file) separator
                      ((.in_module# .prelude .nat#injection) line) separator
                      ((.in_module# .prelude .nat#injection) column))))

(the \n
  (.int_char# +10))

(the .public (with location error)
  (-> Location Text
      Text)
  (.text_composite# (..format location) \n
                    error))
