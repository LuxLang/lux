... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.`` (.require
      [library
       [lux (.except)
        [abstract
         ["[0]" monad]]
        [control
         ["<>" projection (.use "[1]#[0]" monad)]]
        [data
         ["[0]" product]
         [collection
          ["[0]" list (.use "[1]#[0]" functor)]]]
        [meta
         ["[0]" code (.only)
          ["<c>" \\projection (.only Projection)]]
         [macro (.only with_symbols)
          ["[0]" syntax]
          ["[0]" template]]
         [compiler
          [target
           (.,, (.for .jvm
                      [jvm
                       ["_" bytecode (.only Bytecode)]]

                      ... else
                      [/]))]
          [language
           [lux
            ["[0]" phase]
            ["[0]" analysis (.only)
             ["<a>" \\projection]]
            ["[0]" synthesis (.only)
             ["<s>" \\projection]]
            ["[0]" translation]
            ["[0]" declaration]
            [phase
             [translation
              (.,, (.for .old
                         ["[0]" jvm
                          ["[1]" runtime]]

                         .jvm
                         ["[0]" jvm
                          ["[1]" runtime]]

                         ... else
                         [/]))
              ["[0]" js
               ["[1]" runtime]]
              ["[0]" lua
               ["[1]" runtime]]
              ["[0]" python
               ["[1]" runtime]]
              ["[0]" ruby
               ["[1]" runtime]]]]]]]]]]))

(template.with [<any> <end> <and> <value> <name> <extension_type> <handler_type>]
  [(the .public <name>
     (syntax.macro (_ [[handler phase archive inputs] (<c>.form (all <>.and
                                                                     <c>.local
                                                                     <c>.local
                                                                     <c>.local
                                                                     (<c>.tuple (<>.some <c>.any))))
                       body <c>.any])
       (let [g!handler (code.local handler)
             g!phase (code.local phase)
             g!archive (code.local archive)]
         (with_symbols [g!inputs g!error g!_]
           (in (list (` (<| (as <extension_type>)
                            (is <handler_type>)
                            (.function ((, g!handler) (, g!phase) (, g!archive) (, g!inputs))
                              (.when (<value>
                                      (monad.do <>.monad
                                        [(,* inputs)
                                         (, g!_) <end>]
                                        (.of <>.monad (,' in) (, body)))
                                      (, g!inputs))
                                {.#Right (, g!_)}
                                (, g!_)

                                {.#Left (, g!error)}
                                (phase.failure (, g!error)))
                              )))))))))]

  [<c>.any <c>.end <c>.and <c>.value analysis .Analysis analysis.Handler]
  [<a>.any <a>.end <a>.and <a>.value synthesis .Synthesis synthesis.Handler]
  [<s>.any <s>.end <s>.and <s>.value translation .Translation
   (for .jvm (translation.Handler jvm.Anchor jvm.Value jvm.Declaration)
        .js (translation.Handler js.Anchor js.Value js.Declaration)
        .lua (translation.Handler lua.Anchor lua.Value lua.Declaration)
        .python (translation.Handler python.Anchor python.Value python.Declaration)
        .ruby (translation.Handler ruby.Anchor ruby.Value ruby.Declaration))]
  [<c>.any <c>.end <c>.and <c>.value declaration .Declaration
   (for .jvm (declaration.Handler jvm.Anchor jvm.Value jvm.Declaration)
        .js (declaration.Handler js.Anchor js.Value js.Declaration)
        .lua (declaration.Handler lua.Anchor lua.Value lua.Declaration)
        .python (declaration.Handler python.Anchor python.Value python.Declaration)
        .ruby (declaration.Handler ruby.Anchor ruby.Value ruby.Declaration))]
  )
