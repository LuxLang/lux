(.module:
  {#.doc (.doc "Machinary for querying annotations on modules and definitions.")}
  [library
   [lux (#- nat int rev)
    [abstract
     ["." monad (#+ do)]]
    [data
     ["." maybe]
     ["." name ("#\." equivalence)]]]])

(type: .public Annotation
  Code)

(def: .public (value tag ann)
  (-> Name Annotation (Maybe Code))
  (case ann
    [_ (#.Record ann)]
    (loop [ann ann]
      (case ann
        (#.Item [key value] ann')
        (case key
          [_ (#.Tag tag')]
          (if (name\= tag tag')
            (#.Some value)
            (recur ann'))

          _
          (recur ann'))
        
        #.End
        #.None))

    _
    #.None))

(template [<name> <tag> <type>]
  [(def: .public (<name> tag ann)
     (-> Name Annotation (Maybe <type>))
     (case (..value tag ann)
       (#.Some [_ (<tag> value)])
       (#.Some value)

       _
       #.None))]

  [bit        #.Bit        Bit]
  [nat        #.Nat        Nat]
  [int        #.Int        Int]
  [rev        #.Rev        Rev]
  [frac       #.Frac       Frac]
  [text       #.Text       Text]
  [identifier #.Identifier Name]
  [tag        #.Tag        Name]
  [form       #.Form       (List Code)]
  [tuple      #.Tuple      (List Code)]
  [record     #.Record     (List [Code Code])]
  )

(def: .public documentation
  (-> Annotation (Maybe Text))
  (..text (name_of #.doc)))

(def: .public (flagged? flag)
  (-> Name Annotation Bit)
  (|>> (..bit flag) (maybe.else false)))

(template [<name> <tag>]
  [(def: .public <name>
     (-> Annotation Bit)
     (..flagged? (name_of <tag>)))]

  [implementation? #.implementation?]
  [recursive_type? #.type_rec?]
  [interface?      #.interface?]
  )

(def: (text_parser input)
  (-> Code (Maybe Text))
  (case input
    [_ (#.Text actual_value)]
    (#.Some actual_value)

    _
    #.None))

(template [<name> <tag>]
  [(def: .public (<name> ann)
     (-> Annotation (List Text))
     (<| (maybe.else (list))
         (do {! maybe.monad}
           [args (..tuple (name_of <tag>) ann)]
           (monad.map ! ..text_parser args))))]

  [function_arguments #.func_args]
  [type_arguments     #.type_args]
  )
