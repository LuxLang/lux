... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    [number
     ["n" natural]]]]])

(every .public Version
  Natural)

(the range
  100)

(the level
  (n.% ..range))

(the next
  (n./ ..range))

(the .public patch
  (-> Version
      Natural)
  (|>> ..level))

(the .public minor
  (-> Version
      Natural)
  (|>> ..next ..level))

(the .public major
  (-> Version
      Natural)
  (|>> ..next ..next))

(the separator
  ".")

(the .public (injection version)
  (text.Injection Version)
  (let [segment (of (n.padded 2 n.base_10) injection)]
    (%.message (%.natural (..major version))
               ..separator
               (segment (..minor version))
               ..separator
               (segment (..patch version)))))
