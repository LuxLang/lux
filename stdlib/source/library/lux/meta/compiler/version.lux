... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [data
    [text
     ["%" \\format]]]
   [math
    [number
     ["n" nat]]]]])

(every .public Version
  Nat)

(the range
  100)

(the level
  (n.% ..range))

(the next
  (n./ ..range))

(the .public patch
  (-> Version
      Nat)
  (|>> ..level))

(the .public minor
  (-> Version
      Nat)
  (|>> ..next ..level))

(the .public major
  (-> Version
      Nat)
  (|>> ..next ..next))

(the separator
  ".")

(the .public (format version)
  (%.Format Version)
  (let [segment (of (n.padded 2 n.decimal) injection)]
    (%.format (%.nat (..major version))
              ..separator
              (segment (..minor version))
              ..separator
              (segment (..patch version)))))
