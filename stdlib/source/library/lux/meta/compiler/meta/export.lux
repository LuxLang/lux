... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Source)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["[0]" try (.only Try) (.use "[1]#[0]" monad)]
    [concurrency
     ["[0]" async (.only Async) (.use "[1]#[0]" functor)]]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    ["[0]" binary
     ["[1]" \\injection]]
    [collection
     ["[0]" dictionary]
     ["[0]" sequence]]
    [format
     ["[0]" tar]]]
   [meta
    [compiler
     [meta
      [cli (.only Source Export)]
      ["[0]" io
       ["[1]" context]]]]]
   [world
    ["[0]" file]
    [time
     ["[0]" instant]]]]])

(the .public file
  "library.tar")

(the .public mode
  (all tar.and
       tar.read_by_owner tar.write_by_owner
       tar.read_by_group tar.write_by_group
       tar.read_by_other))

(the .public ownership
  tar.Ownership
  (let [commons (is tar.Owner
                    [tar.#name tar.anonymous
                     tar.#id tar.no_id])]
    [tar.#user commons
     tar.#group commons]))

(the .public (library fs sources)
  (-> (file.System Async) (List Source) (Async (Try tar.Tar)))
  (|> sources
      (io.listing fs)
      (async#each (|>> (try#each (|>> dictionary.entries
                                      (monad.each try.monad
                                                  (function (_ [path source_code])
                                                    (do try.monad
                                                      [path (|> path
                                                                (text.replaced (of fs separator) .module_separator)
                                                                tar.path)]
                                                      (try#each (|>> [path
                                                                      (instant.of_millis +0)
                                                                      ..mode
                                                                      ..ownership]
                                                                     {tar.#Normal})
                                                                (tar.content source_code)))))
                                      (try#each sequence.of_list)))
                       try#conjoint))))

(the .public (export fs [sources target])
  (-> (file.System Async) Export (Async (Try Any)))
  (do [! (try.with async.monad)]
    [tar (|> sources
             (..library fs)
             (of ! each (binary.value tar.format)))
     .let [/ (of fs separator)]]
    (of fs write (%.format target / ..file) tar)))
