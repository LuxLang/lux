... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Module Source Target
                #module #target #configuration)
   [control
    ["<>" projection]
    ["|" pipe]]
   [data
    ["[0]" text
     ["<[1]>" \\projection]]]
   [macro
    ["^" pattern]
    ["[0]" template]]
   [meta
    ["[0]" configuration (.only Configuration)]
    [compiler
     [meta
      [archive
       [module
        ["[0]" descriptor]]]]]]
   ["[0]" program
    ["<[1]>" \\projection (.only Projection)]]
   [world
    [file (.only Path)]]]]
 ["[0]" /
  ["[1][0]" compiler (.only Compiler)]])

(every .public Host_Dependency
  Path)

(every .public Library
  Path)

(every .public Source
  Path)

(every .public Target
  Path)

(every .public Module
  descriptor.Module)

(every .public Compilation
  (Record
   [#host_dependencies (List Host_Dependency)
    #libraries (List Library)
    #compilers (List Compiler)
    #sources (List Source)
    #target Target
    #module Module
    #program (Maybe Text)
    #configuration Configuration]))

(every .public Interpretation
  ..Compilation)

(every .public Export
  [(List Source) Target])

(every .public Service
  (Variant
   {#Compilation Compilation}
   {#Interpretation Interpretation}
   {#Export Export}))

(template.with [<name> <long> <type> <projection>]
  [(the <name>
     (Projection <type>)
     (<program>.named <long> <projection>))]

  [host_dependency_projection "--host_dependency" Host_Dependency <program>.any]
  [library_projection "--library" Library <program>.any]
  [compiler_projection "--compiler" Compiler (<text>.then /compiler.projection <program>.any)]
  [source_projection "--source" Source <program>.any]
  [target_projection "--target" Target <program>.any]
  [module_projection "--module" Module <program>.any]
  [configuration_projection "--configuration" Configuration (<text>.then configuration.projection <program>.any)]
  )

(the program_projection
  (Projection (Maybe Text))
  (<>.maybe (<program>.named "--program" <program>.any)))

(the .public service
  (Projection Service)
  (let [compilation (is (Projection Compilation)
                        (all <>.and
                             (<>.some ..host_dependency_projection)
                             (<>.some ..library_projection)
                             (<>.some ..compiler_projection)
                             (<>.some ..source_projection)
                             ..target_projection
                             ..module_projection
                             ..program_projection
                             (<>.else configuration.empty ..configuration_projection)))]
    (all <>.or
         (<>.after (<program>.this "build")
                   compilation)
         (<>.after (<program>.this "repl")
                   compilation)
         (<>.after (<program>.this "export")
                   (all <>.and
                        (<>.some ..source_projection)
                        ..target_projection))
         )))

(the .public target
  (-> Service Target)
  (|>> (|.when (^.or {#Compilation [host_dependencies libraries compilers sources target module]}
                     {#Interpretation [host_dependencies libraries compilers sources target module]}
                     {#Export [sources target]})
         target)))
