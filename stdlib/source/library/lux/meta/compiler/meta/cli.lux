... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Module Source Target
                #module #target #configuration)
   [abstract
    ["<>" projection]]
   [control
    ["|" pipe]]
   [data
    ["[0]" text
     ["<[1]>" \\projection]]]
   [macro
    ["^" pattern]
    ["[0]" template]]
   [meta
    ["[0]" configuration (.only Configuration)]
    [compiler
     [meta
      [archive
       [module
        ["[0]" descriptor]]]]]]
   ["[0]" program
    ["<[1]>" \\projection (.only Projection)]]
   [world
    [file (.only Path)]]]]
 ["[0]" /
  ["[1][0]" compiler (.only Compiler)]])

(every .public Host_Dependency
  Path)

(every .public Library
  Path)

(every .public Source
  Path)

(every .public Target
  Path)

(every .public Module
  descriptor.Module)

(every .public Compilation
  (Record
   [#host_dependencies (Stack Host_Dependency)
    #libraries (Stack Library)
    #compilers (Stack Compiler)
    #sources (Stack Source)
    #target Target
    #module Module
    #program (Maybe Text)
    #configuration Configuration]))

(every .public Interpretation
  ..Compilation)

(every .public Export
  [(Stack Source) Target])

(every .public Service
  (Variant
   {#Compilation Compilation}
   {#Interpretation Interpretation}
   {#Export Export}))

(template.with [<name> <long> <type> <projection>]
  [(the <name>
     (Projection <type>)
     (<program>.named <long> <projection>))]

  [host_dependency_of "--host_dependency" Host_Dependency <program>.any]
  [library_of "--library" Library <program>.any]
  [compiler_of "--compiler" Compiler (<text>.then /compiler.of_text <program>.any)]
  [source_of "--source" Source <program>.any]
  [target_of "--target" Target <program>.any]
  [module_of "--module" Module <program>.any]
  [configuration_of "--configuration" Configuration (<text>.then configuration.of_text <program>.any)]
  )

(the program_of
  (Projection (Maybe Text))
  (<>.maybe (<program>.named "--program" <program>.any)))

(the .public service
  (Projection Service)
  (let [compilation (is (Projection Compilation)
                        (all <>.and
                             (<>.some ..host_dependency_of)
                             (<>.some ..library_of)
                             (<>.some ..compiler_of)
                             (<>.some ..source_of)
                             ..target_of
                             ..module_of
                             ..program_of
                             (<>.else configuration.empty ..configuration_of)))]
    (all <>.or
         (<>.after (<program>.this "build")
                   compilation)
         (<>.after (<program>.this "repl")
                   compilation)
         (<>.after (<program>.this "export")
                   (all <>.and
                        (<>.some ..source_of)
                        ..target_of))
         )))

(the .public target
  (-> Service Target)
  (|>> (|.when (^.or {#Compilation [host_dependencies libraries compilers sources target module]}
                     {#Interpretation [host_dependencies libraries compilers sources target module]}
                     {#Export [sources target]})
         target)))
