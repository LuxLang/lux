... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only Monad do)]]
   [control
    ["[0]" pipe]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    [binary (.only Binary)]
    ["[0]" product]
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\format (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]
     ["[0]" dictionary (.only Dictionary)]]]
   [meta
    [compiler
     ["@" target]]]
   [world
    ["[0]" file]]]]
 ["[0]" // (.only)
  [//
   [context (.only Context)]
   [archive
    ["[0]" module]]]])

(exception.def .public (cannot_enable [archive @module error])
  (Exception [file.Path module.ID Text])
  (exception.report
   (list ["Archive" archive]
         ["Module ID" (%.nat @module)]
         ["Error" error])))

(def .public (path fs context @module)
  (All (_ !) (-> (file.System !) Context module.ID file.Path))
  (format (//.path fs context)
          (of fs separator)
          (%.nat @module)))

(def .public (enabled? fs context @module)
  (All (_ !) (-> (file.System !) Context module.ID (! Bit)))
  (of fs directory? (..path fs context @module)))

(def .public (enable! ! fs context @module)
  (All (_ !) (-> (Monad !) (file.System !) Context module.ID (! (Try Any))))
  (do !
    [.let [path (..path fs context @module)]
     module_exists? (of fs directory? path)]
    (if module_exists?
      (in {try.#Success []})
      (with_expansions [<failure> (exception.except ..cannot_enable [(//.path fs context)
                                                                     @module
                                                                     error])]
        (do !
          [? (//.enable! ! fs context)]
          (when ?
            {try.#Failure error}
            (in <failure>)
            
            success
            (|> path
                (of fs make_directory)
                (of ! each (|>> (pipe.when
                                  {try.#Failure error}
                                  <failure>

                                  success
                                  success))))))))))

(def file
  file.Path
  "descriptor")

(def .public (descriptor fs context @module)
  (All (_ !) (-> (file.System !) Context module.ID file.Path))
  (format (..path fs context @module)
          (of fs separator)
          ..file))

(def .public (cache! fs context @module content)
  (All (_ !) (-> (file.System !) Context module.ID Binary (! (Try Any))))
  (of fs write (..descriptor fs context @module) content))

(def .public (cache fs context @module)
  (All (_ !) (-> (file.System !) Context module.ID (! (Try Binary))))
  (of fs read (..descriptor fs context @module)))

(def .public (artifacts ! fs context @module)
  (All (_ !) (-> (Monad !) (file.System !) Context module.ID (! (Try (Dictionary Text Binary)))))
  (do [! (try.with !)]
    [files (of fs directory_files (..path fs context @module))
     pairs (|> files
               (list#each (function (_ file)
                            [(file.name fs file) file]))
               (list.only (|>> product.left (text#= ..file) not))
               (monad.each ! (function (_ [name path])
                               (|> path
                                   (of fs read)
                                   (of ! each (|>> [name]))))))]
    (in (dictionary.of_list text.hash (for @.old (as (List [Text Binary]) pairs)
                                           pairs)))))
