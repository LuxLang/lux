... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [control
    [try (.only Try)]
    [concurrency
     ["[0]" async (.only Async)]]]
   [data
    [binary (.only Binary)]
    [text
     ["%" \\injection]]]
   [world
    ["[0]" file]]]]
 ["[0]" //
  ["[1][0]" module]
  [//
   ["[0]" context (.only Context)]
   [archive
    ["[0]" module]
    ["[0]" artifact]]]])

(the .public (path fs context @module @artifact)
  (for_any (_ !)
    (-> (file.System !) Context module.ID artifact.ID
        file.Path))
  (let [/ (of fs separator)]
    (%.message (//module.path fs @module)
               / (%.natural @artifact)
               (its context.#artifact_extension context))))

(the .public (cache fs context @module @artifact)
  (for_any (_ !)
    (-> (file.System !) Context module.ID artifact.ID
        (! (Try Binary))))
  (of fs read (..path fs context @module @artifact)))

(the .public (cache! fs context @module @artifact content)
  (for_any (_ !)
    (-> (file.System !) Context module.ID artifact.ID Binary
        (! (Try Any))))
  (of fs write (..path fs context @module @artifact) content))
