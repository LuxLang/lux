... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Module)
   [abstract
    ["[0]" monad (.only Monad
                        do)]]
   [control
    ["<>" projection]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]
    [concurrency
     ["[0]" async (.only Async)]]]
   [data
    ["[0]" binary (.only Binary)
     ["<[1]>" \\projection]]
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" dictionary (.only Dictionary)]
     ["[0]" sequence]]
    [format
     ["[0]" tar]]]
   [meta
    [compiler
     [meta
      [cli (.only Library Module)]]]]
   [world
    ["[0]" file]]]])

(the Action
  (type (for_any (_ of)
          (Async (Try of)))))

(exception.the .public useless_tar_entry)

(exception.the .public (duplicate [library module])
  (Exception [Library Module])
  (exception.report
   (list ["Module" (%.text module)]
         ["Library" (%.text library)])))

(every .public Import
  (Dictionary file.Path Binary))

(the (import_library system library import)
  (-> (file.System Async) Library Import (Action Import))
  (let [! async.monad]
    (|> library
        (of system read)
        (of ! each (let [! try.monad]
                     (|>> (of ! each (<binary>.value tar.projection))
                          (of ! conjoint)
                          (of ! each (|>> sequence.list
                                          (monad.mix ! (function (_ entry import)
                                                         (when entry
                                                           {tar.#Normal [path instant mode ownership content]}
                                                           (let [path (tar.from_path path)]
                                                             (when (dictionary.has' path (tar.data content) import)
                                                               {try.#Failure error}
                                                               (exception.except ..duplicate [library path])

                                                               import'
                                                               import'))
                                                           
                                                           _
                                                           (exception.except ..useless_tar_entry [])))
                                                     import)))
                          (of ! conjoint)))))))

(the .public (import system libraries)
  (-> (file.System Async) (List Library) (Action Import))
  (monad.mix (is (Monad Action)
                 (try.with async.monad))
             (..import_library system)
             (dictionary.empty text.hash)
             libraries))
