(.require
 [library
  [lux (.except Definition #Definition)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    ["[0]" maybe]]
   [data
    ["[0]" product]
    ["[0]" text (.use "[1]#[0]" equivalence)]]
   [math
    [number
     ["[0]" nat]]]
   [meta
    [macro
     ["^" pattern]]]]]
 [/////
  [arity (.only Arity)]])

(type .public Definition
  [Text (Maybe [Arity [Nat Nat]])])

(def .public definition_equivalence
  (Equivalence Definition)
  (all product.equivalence
       text.equivalence
       (maybe.equivalence (all product.equivalence
                               nat.equivalence
                               nat.equivalence
                               nat.equivalence
                               ))
       ))

(type .public Category
  (Variant
   {#Anonymous}
   {#Definition Definition}
   {#Custom Text}))

(def .public equivalence
  (Equivalence Category)
  (implementation
   (def (= left right)
     (when [left right]
       [{#Anonymous} {#Anonymous}]
       true

       [{#Definition left} {#Definition right}]
       (of definition_equivalence = left right)
       
       [{#Custom left} {#Custom right}]
       (text#= left right)

       _
       false
       ))))
