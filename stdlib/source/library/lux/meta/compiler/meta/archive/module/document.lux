... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["<>" projection]]
   [control
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    [collection
     ["[0]" dictionary (.only Dictionary)]]
    ["[0]" binary
     [\\projection (.only Projection)]
     ["[1]" \\injection (.only Injection)]]]
   [type (.only sharing)
    ["[0]" nominal]]]]
 [///
  ["[0]" signature (.only Signature)]
  ["[0]" key (.only Key)]])

(exception.the .public (invalid_signature [expected actual])
  (Exception [Signature Signature])
  (exception.report
   (stack ["Expected" (signature.description expected)]
          ["Actual" (signature.description actual)])))

(nominal.every .public (Document d)
  (Record
   [#signature Signature
    #content d])

  (the .public (content key document)
    (for_any (_ d) (-> (Key d) (Document Any) (Try d)))
    (let [[document//signature document//content] (nominal.representation document)]
      (if (signature.= (key.signature key)
                       document//signature)
        {try.#Success (sharing [e]
                        (is (Key e)
                            key)
                        (is e
                            (as_expected document//content)))}
        (exception.except ..invalid_signature [(key.signature key)
                                               document//signature]))))

  (the .public (document key content)
    (for_any (_ d) (-> (Key d) d (Document d)))
    (nominal.abstraction
     [#signature (key.signature key)
      #content content]))

  (the .public (marked? key document)
    (for_any (_ d) (-> (Key d) (Document Any) (Try (Document d))))
    (do try.monad
      [_ (..content key document)]
      (in (as_expected document))))

  (the .public signature
    (-> (Document Any) Signature)
    (|>> nominal.representation
         (its #signature)))

  (the .public (as_binary content)
    (for_any (_ d)
      (-> (Injection d)
          (Injection (Document d))))
    (let [as_binary (all binary.and
                         signature.as_binary
                         content)]
      (|>> nominal.representation
           as_binary)))

  (the .public (of_binary key it)
    (for_any (_ d)
      (-> (Key d) (Projection d)
          (Projection (Document d))))
    (do <>.monad
      [actual signature.of_binary
       .let [expected (key.signature key)]
       _ (if (signature.= expected actual)
           (in [])
           (<>.of_try (exception.except ..invalid_signature [expected actual])))
       it it]
      (in (nominal.abstraction
           [actual it]))))
  )
