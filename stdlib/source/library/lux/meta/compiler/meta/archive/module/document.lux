(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["<>" parser]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    [collection
     ["[0]" dictionary (.only Dictionary)]]
    ["[0]" binary
     [\\parser (.only Parser)]
     ["[1]" \\format (.only Format)]]]
   [meta
    [type (.only sharing)
     ["[0]" nominal (.except def)]]]]]
 [///
  ["[0]" signature (.only Signature) (.use "[1]#[0]" equivalence)]
  ["[0]" key (.only Key)]])

(exception.def .public (invalid_signature [expected actual])
  (Exception [Signature Signature])
  (exception.report
   (list ["Expected" (signature.description expected)]
         ["Actual" (signature.description actual)])))

(nominal.def .public (Document d)
  (Record
   [#signature Signature
    #content d])

  (def .public (content key document)
    (All (_ d) (-> (Key d) (Document Any) (Try d)))
    (let [[document//signature document//content] (representation document)]
      (if (at signature.equivalence =
              (key.signature key)
              document//signature)
        {try.#Success (sharing [e]
                        (is (Key e)
                            key)
                        (is e
                            (as_expected document//content)))}
        (exception.except ..invalid_signature [(key.signature key)
                                               document//signature]))))

  (def .public (document key content)
    (All (_ d) (-> (Key d) d (Document d)))
    (abstraction [#signature (key.signature key)
                  #content content]))

  (def .public (marked? key document)
    (All (_ d) (-> (Key d) (Document Any) (Try (Document d))))
    (do try.monad
      [_ (..content key document)]
      (in (as_expected document))))

  (def .public signature
    (-> (Document Any) Signature)
    (|>> representation (the #signature)))

  (def .public (format content)
    (All (_ d) (-> (Format d) (Format (Document d))))
    (let [format (all binary.and
                      signature.format
                      content)]
      (|>> representation format)))

  (def .public (parser key it)
    (All (_ d) (-> (Key d) (Parser d) (Parser (Document d))))
    (do <>.monad
      [actual signature.parser
       .let [expected (key.signature key)]
       _ (if (signature#= expected actual)
           (in [])
           (<>.of_try (exception.except ..invalid_signature [expected actual])))
       it it]
      (in (abstraction [actual it]))))
  )
