... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except #version)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    ["<>" parser]]
   [data
    ["[0]" product]
    ["[0]" binary
     ["[1]" \\format (.only Format)]
     ["<[1]>" \\parser (.only Parser)]]
    ["[0]" text (.only)
     ["%" \\format]]]
   [math
    [number
     ["[0]" nat]]]
   [meta
    ["[0]" symbol]]]]
 [////
  ["[0]" version (.only Version)]])

(every .public Signature
  (Record
   [#name Symbol
    #version Version]))

(def .public equivalence
  (Equivalence Signature)
  (all product.equivalence
       symbol.equivalence
       nat.equivalence))

(def .public (description signature)
  (-> Signature Text)
  (%.format (%.symbol (its #name signature)) " " (version.format (its #version signature))))

(def .public format
  (Format Signature)
  (all binary.and
       (binary.and binary.text binary.text)
       binary.nat))

(def .public parser
  (Parser Signature)
  (all <>.and
       (<>.and <binary>.text <binary>.text)
       <binary>.nat))
