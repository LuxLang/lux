... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except #version)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    ["<>" projection]]
   [data
    ["[0]" product]
    ["[0]" binary
     ["[1]" \\injection (.only Injection)]
     ["<[1]>" \\projection (.only Projection)]]
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    [number
     ["[0]" natural]]]
   [meta
    ["[0]" symbol]]]]
 [////
  ["[0]" version (.only Version)]])

(every .public Signature
  (Record
   [#name Symbol
    #version Version]))

(the .public equivalence
  (Equivalence Signature)
  (all product.equivalence
       symbol.equivalence
       natural.equivalence))

(alias [=]
       ..equivalence)

(the .public (description signature)
  (-> Signature Text)
  (%.message (symbol.as_text (its #name signature))
             " "
             (version.injection (its #version signature))))

(the .public injection
  (Injection Signature)
  (all binary.and
       (all binary.and
            binary.text
            binary.text)
       binary.natural))

(the .public projection
  (Projection Signature)
  (all <>.and
       (<>.and <binary>.text <binary>.text)
       <binary>.natural))
