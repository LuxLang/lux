... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Module)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    ["<>" projection]]
   [data
    ["[0]" product]
    ["[0]" text]
    ["[0]" binary
     ["[0]" \\format (.only Format)]
     ["<[1]>" \\projection (.only Projection)]]
    [collection
     ["[0]" set (.only Set)]]]
   [math
    [number
     ["[0]" nat]]]
   [meta
    [macro
     ["^" pattern]]]
   [world
    [file (.only Path)]]]])

(every .public Module
  Text)

(the .public runtime
  Module
  "")

(every .public Descriptor
  (Record
   [#name Module
    #file Path
    #hash Nat
    #state Module_State
    #references (Set Module)]))

(the module_state_equivalence
  (Equivalence Module_State)
  (implementation
   (the (= left right)
     (when [left right]
       (^.with_template [<tag>]
         [[{<tag>} {<tag>}]
          true])
       ([.#Active]
        [.#Compiled]
        [.#Cached])

       _
       false))))

(the .public equivalence
  (Equivalence Descriptor)
  (all product.equivalence
       text.equivalence
       text.equivalence
       nat.equivalence
       ..module_state_equivalence
       set.equivalence
       ))

(the .public format
  (Format Descriptor)
  (all \\format.and
       \\format.text
       \\format.text
       \\format.nat
       \\format.any
       (\\format.set \\format.text)
       ))

(the .public projection
  (Projection Descriptor)
  (all <>.and
       <binary>.text
       <binary>.text
       <binary>.nat
       (of <>.monad in {.#Cached})
       (<binary>.set text.hash <binary>.text)
       ))
