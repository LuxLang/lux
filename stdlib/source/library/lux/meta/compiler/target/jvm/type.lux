... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Type Declaration)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [control
    ["[0]" maybe]]
   [data
    ["[0]" text (.only Injection)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    [number
     ["n" natural]]]
   [meta
    [type
     ["[0]" nominal]]
    [macro
     ["[0]" template]]]]]
 ["[0]" //
  [encoding
   ["[1][0]" name (.only External)]]]
 ["[0]" /
  [category (.only Void Value Return Method Primitive Object Class Array Var Parameter Declaration)]
  ["[1][0]" signature (.only Signature)]
  ["[1][0]" descriptor (.only Descriptor)]
  ["[1][0]" reflection (.only Reflection)]])

(nominal.every .public (Type category)
  [(Signature category)
   (Descriptor category)
   (Reflection category)]

  (every .public Argument
    [Text (Type Value)])

  (every .public (Typed a)
    [(Type Value) a])

  (every .public Constraint
    (Record
     [#name Text
      #super_class (Type Class)
      #super_interfaces (List (Type Class))]))
  
  (template.with [<name> <style>]
    [(the .public (<name> type)
       (for_any (_ category)
         (-> (Type category)
             (<style> category)))
       (let [[signature descriptor reflection] (nominal.representation type)]
         <name>))]

    [signature Signature]
    [descriptor Descriptor]
    )

  (the .public (reflection type)
    (for_any (_ category)
      (-> (Type (Value category))
          (Reflection (Value category))))
    (let [[signature descriptor reflection] (nominal.representation type)]
      reflection))

  (template.with [<category> <name> <signature> <descriptor> <reflection>]
    [(the .public <name>
       (Type <category>)
       (nominal.abstraction
        [<signature>
         <descriptor>
         <reflection>]))]

    [Void void /signature.void /descriptor.void /reflection.void]
    [Primitive boolean /signature.boolean /descriptor.boolean /reflection.boolean]
    [Primitive byte /signature.byte /descriptor.byte /reflection.byte]
    [Primitive short /signature.short /descriptor.short /reflection.short]
    [Primitive int /signature.int /descriptor.int /reflection.int]
    [Primitive long /signature.long /descriptor.long /reflection.long]
    [Primitive float /signature.float /descriptor.float /reflection.float]
    [Primitive double /signature.double /descriptor.double /reflection.double]
    [Primitive char /signature.char /descriptor.char /reflection.char]
    )

  (the .public (array type)
    (-> (Type Value)
        (Type Array))
    (nominal.abstraction
     [(/signature.array (..signature type))
      (/descriptor.array (..descriptor type))
      (/reflection.array (..reflection type))]))

  (the .public (class name parameters)
    (-> External (List (Type Parameter))
        (Type Class))
    (nominal.abstraction
     [(/signature.class (list#each ..signature parameters) name)
      (/descriptor.class name)
      (/reflection.class name)]))

  (the .public (declaration name variables)
    (-> External (List (Type Var))
        (Type Declaration))
    (nominal.abstraction
     [(/signature.declaration (list#each ..signature variables) name)
      (/descriptor.declaration name)
      (/reflection.declaration name)]))

  (the .public (as_class type)
    (-> (Type Declaration)
        (Type Class))
    (nominal.abstraction
     (let [[signature descriptor reflection] (nominal.representation type)]
       [(/signature.as_class signature)
        (/descriptor.as_class descriptor)
        (/reflection.as_class reflection)])))

  (the .public wildcard
    (Type Parameter)
    (nominal.abstraction
     [/signature.wildcard
      /descriptor.wildcard
      /reflection.wildcard]))

  (the .public (var name)
    (-> Text
        (Type Var))
    (nominal.abstraction
     [(/signature.var name)
      /descriptor.var
      /reflection.var]))

  (the .public (lower bound)
    (-> (Type Parameter)
        (Type Parameter))
    (nominal.abstraction
     (let [[signature descriptor reflection] (nominal.representation bound)]
       [(/signature.lower signature)
        (/descriptor.lower descriptor)
        (/reflection.lower reflection)])))

  (the .public (upper bound)
    (-> (Type Parameter)
        (Type Parameter))
    (nominal.abstraction
     (let [[signature descriptor reflection] (nominal.representation bound)]
       [(/signature.upper signature)
        (/descriptor.upper descriptor)
        (/reflection.upper reflection)])))

  (the .public (method [type_variables inputs output exceptions])
    (-> [(List (Type Var))
         (List (Type Value))
         (Type Return)
         (List (Type Class))]
        (Type Method))
    (nominal.abstraction
     [(/signature.method [(list#each ..signature type_variables)
                          (list#each ..signature inputs)
                          (..signature output)
                          (list#each ..signature exceptions)])
      (/descriptor.method [(list#each ..descriptor inputs)
                           (..descriptor output)])
      (as_expected ..void)]))

  (the .public equivalence
    (for_any (_ category)
      (Equivalence (Type category)))
    (implementation
     (the (= parameter subject)
       (of /signature.equivalence =
           (..signature parameter)
           (..signature subject)))))

  (alias [=]
         ..equivalence)

  (the .public hash
    (for_any (_ category)
      (Hash (Type category)))
    (implementation
     (the equivalence ..equivalence)
     (the hash (|>> ..signature (of /signature.hash hash)))))

  (the .public (primitive? type)
    (-> (Type Value)
        (Either (Type Object)
                (Type Primitive)))
    (if (`` (or (,, (template.with [<type>]
                      [(of ..equivalence = (is (Type Value) <type>) type)]
                      
                      [..boolean]
                      [..byte]
                      [..short]
                      [..int]
                      [..long]
                      [..float]
                      [..double]
                      [..char]))))
      (|> type (as (Type Primitive)) {.#Right})
      (|> type (as (Type Object)) {.#Left})))

  (the .public (void? type)
    (-> (Type Return)
        (Either (Type Value)
                (Type Void)))
    (if (`` (or (,, (template.with [<type>]
                      [(of ..equivalence = (is (Type Return) <type>) type)]
                      
                      [..void]))))
      (|> type (as (Type Void)) {.#Right})
      (|> type (as (Type Value)) {.#Left})))
  )

(the .public (class? type)
  (-> (Type Value)
      (Maybe External))
  (let [repr (|> type ..descriptor /descriptor.descriptor)]
    (if (and (text.starts_with? /descriptor.class_prefix repr)
             (text.ends_with? /descriptor.class_suffix repr))
      (let [prefix_size (text.size /descriptor.class_prefix)
            suffix_size (text.size /descriptor.class_suffix)
            name_size (|> (text.size repr)
                          (n.- prefix_size)
                          (n.- suffix_size))]
        (|> repr
            (text.clip prefix_size name_size)
            (of maybe.monad each (|>> //name.internal //name.external))))
      {.#None})))

(the .public injection
  (for_any (_ of)
    (Injection (Type of)))
  (|>> ..signature
       /signature.signature))
