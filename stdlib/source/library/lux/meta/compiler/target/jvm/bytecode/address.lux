... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [equivalence (.only Equivalence)]
    ["[0]" order (.only Order)]]
   [control
    ["[0]" try (.only Try)]]
   [data
    ["[0]" text]
    ["[0]" binary
     ["[1]" \\injection]]]
   [math
    [number
     ["n" natural]]]
   [meta
    [type
     ["[0]" nominal]]]]]
 [//
  ["[0]" jump]
  [//
   [encoding
    ["[0]" unsigned]
    ["[0]" signed (.only S4)]]]])

(the Representation unsigned.U2)
(the Distance unsigned.U2)

(nominal.every .public Address
  Representation

  (the .public value
    (-> Address
        Representation)
    (|>> nominal.representation))

  (the .public start
    Address
    (|> 0
        unsigned.u2
        try.trusted
        nominal.abstraction))

  (the .public (move distance)
    (-> Distance Address
        (Try Address))
    (|>> nominal.representation
         (unsigned.+/2 distance)
         (by try.functor each (|>> nominal.abstraction))))

  (the with_sign
    (-> Address
        (Try S4))
    (|>> nominal.representation
         unsigned.value
         .integer
         signed.s4))

  (the .public (jump from to)
    (-> Address Address
        (Try jump.Big))
    (do try.monad
      [from (with_sign from)
       to (with_sign to)]
      (signed.-/4 from to)))

  (the .public equivalence
    (Equivalence Address)
    (implementation
     (the (= expected actual)
       (by unsigned.equivalence =
           (nominal.representation expected)
           (nominal.representation actual)))))

  (alias [=]
         ..equivalence)

  (the .public order
    (Order Address)
    (implementation
     (the equivalence ..equivalence)
     (the (< expected actual)
       (n.< (|> expected nominal.representation unsigned.value)
            (|> actual nominal.representation unsigned.value)))))

  (order.definitions [] ..Address ..order)

  (the .public as_binary
    (binary.Injection Address)
    (|>> nominal.representation
         unsigned.injection/2))

  (the .public as_text
    (text.Injection Address)
    (|>> nominal.representation
         unsigned.value
         (by n.base_10 injection)))
  )
