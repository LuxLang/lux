... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Type static)
   [abstract
    [monad (.only do)]
    [equivalence (.only Equivalence)]]
   [control
    ["[0]" try (.only Try)]]
   [data
    ["[0]" product]
    [binary
     ["[0]" \\format (.only Format) (.use "[1]#[0]" monoid)]]]
   [math
    [number
     ["n" nat]]]]]
 ["[0]" /
  ["[1][0]" stack (.only Stack)]
  ["[1][0]" registry (.only Registry)]
  [////
   [type (.only Type)
    [category (.only Method)]]]])

(every .public Limit
  (Record
   [#stack Stack
    #registry Registry]))

(with_template [<name> <registry>]
  [(def .public (<name> type)
     (-> (Type Method) (Try Limit))
     (do try.monad
       [registry (<registry> type)]
       (in [#stack /stack.empty
            #registry registry])))]

  [static /registry.static]
  [virtual /registry.virtual]
  )

(def .public length
  (all n.+
       ... u2 max_stack;
       /stack.length
       ... u2 max_locals;
       /registry.length))

(def .public equivalence
  (Equivalence Limit)
  (all product.equivalence
       /stack.equivalence
       /registry.equivalence
       ))

(def .public (format limit)
  (Format Limit)
  (all \\format#composite
       (/stack.format (its #stack limit))
       (/registry.format (its #registry limit))
       ))
