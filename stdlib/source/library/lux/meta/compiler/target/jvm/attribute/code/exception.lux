... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" product]
    [binary
     ["[0]" \\injection (.only Injection)]]]
   [math
    [number
     ["n" nat]]]]]
 ["[0]" //
  ["//[1]" ///
   [constant (.only Class)]
   ["[1][0]" index (.only Index)]
   [bytecode
    ["[1][0]" address (.only Address)]]
   [encoding
    ["[1][0]" unsigned (.only U2)]]]])

(every .public Exception
  (Record
   [#start Address
    #end Address
    #handler Address
    #catch (Index Class)]))

(the .public equivalence
  (Equivalence Exception)
  (all product.equivalence
       ////address.equivalence
       ////address.equivalence
       ////address.equivalence
       ////index.equivalence
       ))

... https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-4.html#jvms-4.7.3
(the .public length
  Nat
  (all n.+
       ... u2 start_pc;
       ////unsigned.bytes/2
       ... u2 end_pc;
       ////unsigned.bytes/2
       ... u2 handler_pc;
       ////unsigned.bytes/2
       ... u2 catch_type;
       ////unsigned.bytes/2
       ))

(the .public format
  (Injection Exception)
  (all \\injection.and
       ////address.format
       ////address.format
       ////address.format
       ////index.format
       ))
