... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Type static has)
   [abstract
    [monad (.only do)]
    [monoid (.only Monoid)]]
   [control
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [meta
    [macro
     ["[0]" template]]]]]
 [/
  ["/[0]" limit (.only Limit)
   ["/[0]" stack (.only Stack)]
   ["/[0]" registry (.only Registry)]]
  [///
   [encoding
    [unsigned (.only U2)]]
   [type (.only Type)
    [category (.only Method)]]]])

(every .public Environment
  (Record
   [#limit Limit
    #stack (Maybe Stack)]))

(template.with [<name> <limit>]
  [(the .public (<name> type)
     (-> (Type Method) (Try Environment))
     (do try.monad
       [limit (<limit> type)]
       (in [#limit limit
            #stack {.#Some /stack.empty}])))]

  [static /limit.static]
  [virtual /limit.virtual]
  )

(every .public Condition
  (-> Environment (Try Environment)))

(the .public monoid
  (Monoid Condition)
  (implementation
   (the identity
     (|>> {try.#Success}))

   (the (composite left right)
     (function (_ environment)
       (do try.monad
         [environment (left environment)]
         (right environment))))))

(exception.the .public discontinuity)

(the .public (stack environment)
  (-> Environment (Try Stack))
  (when (its ..#stack environment)
    {.#Some stack}
    {try.#Success stack}

    {.#None}
    (exception.except ..discontinuity [])))

(the .public discontinue
  (-> Environment Environment)
  (.has ..#stack {.#None}))

(exception.the .public (mismatched_stacks [expected actual])
  (Exception [Stack Stack])
  (exception.report
   (list ["Expected" (/stack.as_text expected)]
         ["Actual" (/stack.as_text actual)])))

(the .public (continue expected environment)
  (-> Stack Environment (Try [Stack Environment]))
  (when (its ..#stack environment)
    {.#Some actual}
    (if (by /stack.equivalence = expected actual)
      {try.#Success [actual environment]}
      (exception.except ..mismatched_stacks [expected actual]))

    {.#None}
    {try.#Success [expected (.has ..#stack {.#Some expected} environment)]}))

(the .public (consumes amount)
  (-> U2 Condition)
  ... TODO: Revisit this definition once lenses/optics have been implemented,
  ... since it can probably be simplified with them.
  (function (_ environment)
    (do try.monad
      [previous (..stack environment)
       current (/stack.pop amount previous)]
      (in (.has ..#stack {.#Some current} environment)))))

(the .public (produces amount)
  (-> U2 Condition)
  (function (_ environment)
    (do try.monad
      [previous (..stack environment)
       current (/stack.push amount previous)
       .let [limit (|> environment
                       (its [..#limit /limit.#stack])
                       (/stack.max current))]]
      (in (|> environment
              (.has ..#stack {.#Some current})
              (.has [..#limit /limit.#stack] limit))))))

(the .public (has registry)
  (-> Registry Condition)
  (|>> (revised [..#limit /limit.#registry] (/registry.has registry))
       {try.#Success}))
