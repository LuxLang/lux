... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [data
    ["[0]" text (.only)
     ["%" \\format (.only format)]]]
   [meta
    [type
     ["[0]" nominal (.except def)]]]]])

(def .public internal_separator "/")
(def .public external_separator ".")

(type .public External
  Text)

(nominal.def .public Internal
  Text

  (def .public internal
    (-> External Internal)
    (|>> (text.replaced ..external_separator
                        ..internal_separator)
         abstraction))

  (def .public read
    (-> Internal Text)
    (|>> representation))

  (def .public external
    (-> Internal External)
    (|>> representation
         (text.replaced ..internal_separator
                        ..external_separator))))

(def .public safe
  (-> Text External)
  (|>> ..internal ..external))

(def .public (qualify package class)
  (-> Text External External)
  (format (..safe package) ..external_separator class))
