... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [meta
    [type
     ["[0]" nominal]]]]])

... https://en.wikipedia.org/wiki/Delimiter
(the .public internal_delimiter "/")
(the .public external_delimiter ".")

(every .public External
  Text)

(nominal.every .public Internal
  Text

  (the .public internal
    (-> External
        Internal)
    (|>> (text.replaced ..external_delimiter
                        ..internal_delimiter)
         nominal.abstraction))

  (the .public read
    (-> Internal
        Text)
    (|>> nominal.representation))

  (the .public external
    (-> Internal
        External)
    (|>> nominal.representation
         (text.replaced ..internal_delimiter
                        ..external_delimiter))))

(the .public safe
  (-> Text
      External)
  (|>> ..internal ..external))

(the .public (qualified package class)
  (-> Text External
      External)
  (%.message (..safe package)
             ..external_delimiter
             class))
