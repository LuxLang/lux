... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Tag)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    ["[0]" try]]
   [data
    [binary
     [\\injection (.only Injection)]]]
   [meta
    [type
     ["[0]" nominal]]]]]
 ["[0]" ///
  [encoding
   ["[1][0]" unsigned (.only U1) (.use "u1//[0]" equivalence)]]])

(nominal.every .public Tag
  U1

  (the .public equivalence
    (Equivalence Tag)
    (implementation
     (the (= expected actual)
       (u1//= (nominal.representation expected)
              (nominal.representation actual)))))

  (with_template [<code> <name>]
    [(the .public <name>
       Tag
       (|> <code>
           ///unsigned.u1
           try.trusted
           nominal.abstraction))]

    [01 utf8]
    [03 integer]
    [04 float]
    [05 long]
    [06 double]
    [07 class]
    [08 string]
    [09 field]
    [10 method]
    [11 interface_method]
    [12 name_and_type]
    [15 method_handle]
    [16 method_type]
    [18 invoke_dynamic]
    )

  (the .public format
    (Injection Tag)
    (|>> nominal.representation
         ///unsigned.format/1))
  )
