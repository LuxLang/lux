... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Type Declaration
                char)
   [abstract
    [monad (.only do)]]
   [control
    ["<>" projection (.use "[1]#[0]" monad)]
    ["[0]" try]
    ["[0]" function]]
   [data
    ["[0]" product]
    ["[0]" text
     ["%" \\injection]
     ["<[1]>" \\projection (.only Projection)]]
    [collection
     ["[0]" list]]]
   [meta
    [macro
     ["[0]" template]]]]]
 ["[0]" // (.only Type)
  [category (.only Void Value Return Method Primitive Object Class Array Var Parameter Declaration)]
  ["[1][0]" signature]
  ["[1][0]" descriptor]
  [//
   [encoding
    ["[1][0]" name (.only External)]]]])

(template.with [<category> <name> <signature> <type>]
  [(the .public <name>
     (Projection (Type <category>))
     (<>.after (<text>.this (//signature.signature <signature>))
               (<>#in <type>)))]

  [Void void //signature.void //.void]
  [Primitive boolean //signature.boolean //.boolean]
  [Primitive byte //signature.byte //.byte]
  [Primitive short //signature.short //.short]
  [Primitive int //signature.int //.int]
  [Primitive long //signature.long //.long]
  [Primitive float //signature.float //.float]
  [Primitive double //signature.double //.double]
  [Primitive char //signature.char //.char]
  [Parameter wildcard //signature.wildcard //.wildcard]
  )

(the .public primitive
  (Projection (Type Primitive))
  (all <>.either
       ..boolean
       ..byte
       ..short
       ..int
       ..long
       ..float
       ..double
       ..char
       ))

(the var/head
  (%.message "abcdefghijklmnopqrstuvwxyz"
             "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
             "_"))

(the var/tail
  (%.message var/head
             "0123456789$"))

(the class/set
  (%.message var/tail //name.internal_separator))

(template.with [<type> <name> <head> <tail> <adapter>]
  [(the .public <name>
     (Projection <type>)
     (of <>.functor each <adapter>
         (<text>.slice (<text>.and! (<text>.one_of! <head>)
                                    (<text>.some! (<text>.one_of! <tail>))))))]

  [External class_name class/set  class/set  (|>> //name.internal //name.external)]
  [Text     var_name   var/head   var/tail   function.identity]
  )

(the .public var'
  (Projection Text)
  (|> ..var_name
      (<>.after (<text>.this //signature.var_prefix))
      (<>.before (<text>.this //descriptor.class_suffix))))

(the .public var
  (Projection (Type Var))
  (<>#each //.var ..var'))

(the .public var?
  (-> (Type Value) (Maybe Text))
  (|>> //.signature
       //signature.signature
       (<text>.value ..var')
       try.maybe))

(the .public name
  (-> (Type Var) Text)
  (|>> //.signature
       //signature.signature
       (<text>.value ..var')
       try.trusted))

(template.with [<name> <prefix> <constructor>]
  [(the <name>
     (-> (Projection (Type Parameter)) (Projection (Type Parameter)))
     (|>> (<>.after (<text>.this <prefix>))
          (<>#each <constructor>)))]

  [lower //signature.lower_prefix //.lower]
  [upper //signature.upper_prefix //.upper]
  )

(the (class'' parameter)
  (-> (Projection (Type Parameter)) (Projection [External (List (Type Parameter))]))
  (|> (do <>.monad
        [name ..class_name
         parameters (|> (<>.some parameter)
                        (<>.after (<text>.this //signature.parameters_start))
                        (<>.before (<text>.this //signature.parameters_end))
                        (<>.else (list)))]
        (in [name parameters]))
      (<>.after (<text>.this //descriptor.class_prefix))
      (<>.before (<text>.this //descriptor.class_suffix))))

(the class'
  (-> (Projection (Type Parameter)) (Projection (Type Class)))
  (|>> ..class''
       (of <>.monad each (product.uncurried //.class))))

(the .public array'
  (-> (Projection (Type Value)) (Projection (Type Array)))
  (|>> (<>.after (<text>.this //descriptor.array_prefix))
       (<>#each //.array)))

(the (parameter' value)
  (-> (Projection (Type Value)) (Projection (Type Parameter)))
  (<>.rec
   (function (_ parameter)
     (let [class (..class' parameter)]
       (all <>.either
            ..var
            ..wildcard
            (..lower parameter)
            (..upper parameter)
            (..array' value)
            class
            )))))

(the .public value
  (Projection (Type Value))
  (<>.rec
   (function (_ value)
     (all <>.either
          ..primitive
          (..parameter' value)
          ))))

(the .public parameter
  (Projection (Type Parameter))
  (..parameter' ..value))

(the .public class
  (Projection (Type Class))
  (..class' ..parameter))

(template.with [<name> <prefix> <constructor>]
  [(the .public <name>
     (-> (Type Value) (Maybe (Type Parameter)))
     (|>> //.signature
          //signature.signature
          (<text>.value (<>.after (<text>.this <prefix>) ..parameter))
          try.maybe))]

  [lower? //signature.lower_prefix //.lower]
  [upper? //signature.upper_prefix //.upper]
  )

(the .public read_class
  (-> (Type Class) [External (List (Type Parameter))])
  (|>> //.signature
       //signature.signature
       (<text>.value (..class'' ..parameter))
       try.trusted))

(the .public array
  (Projection (Type Array))
  (..array' ..value))

(the .public object
  (Projection (Type Object))
  (all <>.either
       ..class
       ..array))

(the inputs
  (|> (<>.some ..value)
      (<>.after (<text>.this //signature.arguments_start))
      (<>.before (<text>.this //signature.arguments_end))))

(the .public return
  (Projection (Type Return))
  (<>.either ..void
             ..value))

(the exception
  (Projection (Type Class))
  (|> ..class
      (<>.after (<text>.this //signature.exception_prefix))))

(the .public var_declaration
  (Projection [(Type Var) (Type Class)])
  (do <>.monad
    [name ..var_name
     _ (<text>.this //signature.format_type_parameter_infix)
     type ..class]
    (in [(//.var name) type])))

(the .public method
  (-> (Type Method)
      [(List (Type Var))
       (List (Type Value))
       (Type Return)
       (List (Type Class))])
  (let [projection (is (Projection [(List (Type Var))
                                    (List (Type Value))
                                    (Type Return)
                                    (List (Type Class))])
                       (all <>.and
                            (|> (<>.some (<>#each product.left ..var_declaration))
                                (<>.after (<text>.this //signature.parameters_start))
                                (<>.before (<text>.this //signature.parameters_end))
                                (<>.else (list)))
                            ..inputs
                            ..return
                            (<>.some ..exception)))]
    (|>> //.signature
         //signature.signature
         (<text>.value projection)
         try.trusted)))

(template.with [<name> <category> <projection>]
  [(the .public <name>
     (-> (Type Value) (Maybe <category>))
     (|>> //.signature
          //signature.signature
          (<text>.value <projection>)
          try.maybe))]

  [array? (Type Value)
   (do <>.monad
     [_ (<text>.this //descriptor.array_prefix)]
     ..value)]
  [class? [External (List (Type Parameter))]
   (..class'' ..parameter)]

  [primitive? (Type Primitive) ..primitive]
  [wildcard? (Type Parameter) ..wildcard]
  [parameter? (Type Parameter) ..parameter]
  [object? (Type Object) ..object]
  )

(the .public declaration'
  (Projection [External (List (Type Var))])
  (|> (<>.and ..class_name
              (|> (<>.some ..var)
                  (<>.after (<text>.this //signature.parameters_start))
                  (<>.before (<text>.this //signature.parameters_end))
                  (<>.else (list))))
      (<>.after (<text>.this //descriptor.class_prefix))
      (<>.before (<text>.this //descriptor.class_suffix))))

(the .public declaration
  (-> (Type Declaration) [External (List (Type Var))])
  (|>> //.signature
       //signature.signature
       (<text>.value ..declaration')
       try.trusted))
