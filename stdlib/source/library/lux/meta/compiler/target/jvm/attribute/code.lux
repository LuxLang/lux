... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Code)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" product]
    ["[0]" binary (.only Binary)]
    ["[0]" binary
     ["[1]F" \\format (.only Format) (.use "[1]#[0]" monoid)]]
    [collection
     ["[0]" sequence (.only Sequence) (.use "[1]#[0]" functor mix)]]]
   [math
    [number
     ["n" nat]]]]]
 ["[0]" ///
  [bytecode
   [environment
    ["[1][0]" limit (.only Limit)]]]
  [encoding
   ["[1][0]" unsigned (.only U2)]]]
 ["[0]" /
  ["[1][0]" exception (.only Exception)]])

(every .public (Code Attribute)
  (Record
   [#limit Limit
    #code Binary
    #exception_table (Sequence Exception)
    #attributes (Sequence Attribute)]))

(def .public (length length code)
  (All (_ Attribute) (-> (-> Attribute Nat) (Code Attribute) Nat))
  (all n.+
       ... u2 max_stack;
       ... u2 max_locals;
       ///limit.length
       ... u4 code_length;
       ///unsigned.bytes/4
       ... u1 code[code_length];
       (binary.size (its #code code))
       ... u2 exception_table_length;
       ///unsigned.bytes/2
       ... exception_table[exception_table_length];
       (|> code
           (its #exception_table)
           sequence.size
           (n.* /exception.length))
       ... u2 attributes_count;
       ///unsigned.bytes/2
       ... attribute_info attributes[attributes_count];
       (|> code
           (its #attributes)
           (sequence#each length)
           (sequence#mix n.+ 0))))

(def .public (equivalence attribute_equivalence)
  (All (_ attribute)
    (-> (Equivalence attribute) (Equivalence (Code attribute))))
  (all product.equivalence
       ///limit.equivalence
       binary.equivalence
       (sequence.equivalence /exception.equivalence)
       (sequence.equivalence attribute_equivalence)
       ))

... https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-4.html#jvms-4.7.3
(def .public (format format code)
  (All (_ Attribute) (-> (Format Attribute) (Format (Code Attribute))))
  (all binaryF#composite
       ... u2 max_stack;
       ... u2 max_locals;
       (///limit.format (its #limit code))
       ... u4 code_length;
       ... u1 code[code_length];
       (binaryF.binary_32 (its #code code))
       ... u2 exception_table_length;
       ... exception_table[exception_table_length];
       ((binaryF.sequence_16 /exception.format) (its #exception_table code))
       ... u2 attributes_count;
       ... attribute_info attributes[attributes_count];
       ((binaryF.sequence_16 format) (its #attributes code))
       ))
