... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" equivalence (.only Equivalence)]]
   [data
    [binary
     [\\injection (.only Injection)]]]
   [meta
    [type
     ["[0]" nominal]]]]]
 ["[0]" //
  [encoding
   ["[1][0]" unsigned (.only U2)]]])

(the .public length
  //unsigned.bytes/2)

(nominal.every .public (Index of)
  U2

  (the .public index
    (for_any (_ of)
      (-> U2
          (Index of)))
    (|>> nominal.abstraction))

  (the .public value
    (-> (Index Any)
        U2)
    (|>> nominal.representation))

  (the .public equivalence
    (for_any (_ of)
      (Equivalence (Index of)))
    (of equivalence.functor each
        ..value
        //unsigned.equivalence))

  (alias [=]
         ..equivalence)

  (the .public injection
    (for_any (_ of)
      (Injection (Index of)))
    (|>> nominal.representation
         //unsigned.injection/2))
  )
