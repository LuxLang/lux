... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Type int char parameter)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["<>" parser (.only)]
    ["[0]" maybe]
    ["[0]" try]]
   [data
    ["[0]" text (.only)
     ["%" \\format (.only format)]
     ["<[1]>" \\parser (.only Parser)]]
    [collection
     ["[0]" dictionary (.only Dictionary)]]]]]
 ["[0]" // (.only Type)
  [category (.only Void Value Return Method Primitive Object Class Array Var Parameter)]
  ["[1][0]" descriptor]
  ["[1][0]" signature (.only Signature)]
  ["[1][0]" reflection]
  ["[1][0]" parser]])

(every .public Aliasing
  (Dictionary Text Text))

(the .public fresh
  Aliasing
  (dictionary.empty text.hash))

(the (var aliasing)
  (-> Aliasing (Parser (Type Var)))
  (do <>.monad
    [var //parser.var']
    (in (|> aliasing
            (dictionary.value var)
            (maybe.else var)
            //.var))))

(the (class parameter)
  (-> (Parser (Type Parameter)) (Parser (Type Class)))
  (|> (do <>.monad
        [name //parser.class_name
         parameters (|> (<>.some parameter)
                        (<>.after (<text>.this //signature.parameters_start))
                        (<>.before (<text>.this //signature.parameters_end))
                        (<>.else (list)))]
        (in (//.class name parameters)))
      (<>.after (<text>.this //descriptor.class_prefix))
      (<>.before (<text>.this //descriptor.class_suffix))))

(with_template [<name> <prefix> <bound> <constructor>]
  [(the <name>
     (-> (Parser (Type Class)) (Parser (Type Parameter)))
     (|>> (<>.after (<text>.this <prefix>))
          (of <>.monad each <bound>)))]

  [lower //signature.lower_prefix //.lower ..Lower]
  [upper //signature.upper_prefix //.upper ..Upper]
  )

(the (parameter aliasing)
  (-> Aliasing (Parser (Type Parameter)))
  (<>.rec
   (function (_ parameter)
     (let [class (..class parameter)]
       (all <>.either
            (..var aliasing)
            //parser.wildcard
            (..lower class)
            (..upper class)
            class
            )))))

(the (value aliasing)
  (-> Aliasing (Parser (Type Value)))
  (<>.rec
   (function (_ value)
     (all <>.either
          //parser.primitive
          (parameter aliasing)
          (//parser.array' value)
          ))))

(the (inputs aliasing)
  (-> Aliasing (Parser (List (Type Value))))
  (|> (<>.some (..value aliasing))
      (<>.after (<text>.this //signature.arguments_start))
      (<>.before (<text>.this //signature.arguments_end))))

(the (return aliasing)
  (-> Aliasing (Parser (Type Return)))
  (all <>.either
       //parser.void
       (..value aliasing)
       ))

(the (exception aliasing)
  (-> Aliasing (Parser (Type Class)))
  (|> (..class (..parameter aliasing))
      (<>.after (<text>.this //signature.exception_prefix))))

(the (bound aliasing)
  (-> Aliasing (Parser (Type Class)))
  (do <>.monad
    [_ (<text>.this ":")]
    (..class (..parameter aliasing))))

(the (bound_type_var aliasing)
  (-> Aliasing (Parser (Type Var)))
  (|> //parser.var_name
      (of <>.monad each //.var)
      (<>.before (<>.many (..bound aliasing)))))

(the .public (method aliasing)
  (-> Aliasing (-> (Type Method) (Type Method)))
  (|>> //.signature
       //signature.signature
       (<text>.value (do <>.monad
                       [type_variables (|> (<>.some (..bound_type_var aliasing))
                                           (<>.after (<text>.this //signature.parameters_start))
                                           (<>.before (<text>.this //signature.parameters_end))
                                           (<>.else (list)))
                        inputs (..inputs aliasing)
                        return (..return aliasing)
                        exceptions (<>.some (..exception aliasing))]
                       (in (//.method [type_variables inputs return exceptions]))))
       try.trusted))
