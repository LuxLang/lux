... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... https://docs.oracle.com/javase/specs/jvms/se7/html/jvms-4.html#jvms-4.7.12
(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" product]
    ["[0]" binary
     ["![1]" \\injection (.only Injection) (.use "[1]#[0]" monoid)]]
    [collection
     ["[0]" sequence (.only Sequence)]]]
   [math
    [number
     ["n" natural]]]]]
 [///
  [encoding
   ["[0]" unsigned (.only U2)]]])

(every .public Entry
  (Record
   [#start_program_counter U2
    #line_number U2]))

(the entry_length
  Natural
  (all n.+
       ... u2 start_pc;
       unsigned.bytes/2
       ... u2 line_number;
       unsigned.bytes/2
       ))

(the entry_equivalence
  (Equivalence Entry)
  (all product.equivalence
       unsigned.equivalence
       unsigned.equivalence
       ))

(the (entry_injection it)
  (Injection Entry)
  (all !binary#composite
       (unsigned.injection/2 (its #start_program_counter it))
       (unsigned.injection/2 (its #line_number it))
       ))

(every .public Line_Number_Table
  (Sequence Entry))

(the .public empty
  Line_Number_Table
  sequence.empty)

(the .public (length it)
  (-> Line_Number_Table
      Natural)
  (all n.+
       ... u2 line_number_table_length;
       unsigned.bytes/2
       ... line_number_table[line_number_table_length];
       (n.* entry_length (sequence.size it))
       ))

(the .public equivalence
  (Equivalence Line_Number_Table)
  (sequence.equivalence entry_equivalence))

(alias [=]
       ..equivalence)

(the .public injection
  (Injection Line_Number_Table)
  (!binary.sequence_16 entry_injection))
