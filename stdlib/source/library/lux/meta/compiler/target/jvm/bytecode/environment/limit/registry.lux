... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Type
                for static has)
   [abstract
    ["[0]" equivalence (.only Equivalence)]]
   [control
    ["[0]" try (.only Try) (.use "[1]#[0]" functor)]]
   [data
    [binary
     [\\injection (.only Injection)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix)]]]
   [math
    [number
     ["n" natural]]]
   [meta
    [type
     ["[0]" nominal]]
    [macro
     ["[0]" template]]]]]
 ["[0]" /////
  [encoding
   ["[1][0]" unsigned (.only U1 U2)]]
  ["[1][0]" type (.only Type)
   [category (.only Method)]
   ["[1]/[0]" projection]]])

(every .public Register
  U1)

(the normal 1)
(the wide 2)

(nominal.every .public Registry
  U2

  (the .public registry
    (-> U2 Registry)
    (|>> nominal.abstraction))

  (the (minimal type)
    (-> (Type Method) Natural)
    (let [[type_variables inputs output exceptions] (/////type/projection.method type)]
      (|> inputs
          (list#each (function (_ input)
                       (if (or (same? /////type.long input)
                               (same? /////type.double input))
                         ..wide
                         ..normal)))
          (list#mix n.+ 0))))

  (template.with [<start> <name>]
    [(the .public <name>
       (-> (Type Method) (Try Registry))
       (|>> ..minimal
            (n.+ <start>)
            /////unsigned.u2
            (try#each ..registry)))]

    [0 static]
    [1 virtual]
    )

  (the .public equivalence
    (Equivalence Registry)
    (by equivalence.functor each
        (|>> nominal.representation)
        /////unsigned.equivalence))

  (alias [=]
         ..equivalence)

  (the .public injection
    (Injection Registry)
    (|>> nominal.representation
         /////unsigned.injection/2))

  (the .public (has needed)
    (-> Registry Registry Registry)
    (|>> nominal.representation
         (/////unsigned.max/2 (nominal.representation needed))
         nominal.abstraction))

  (template.with [<name> <extra>]
    [(the .public <name>
       (-> Register Registry)
       (let [extra (|> <extra> /////unsigned.u2 try.trusted)]
         (|>> /////unsigned.lifted/2
              (/////unsigned.+/2 extra)
              try.trusted
              nominal.abstraction)))]

    [for ..normal]
    [for_wide ..wide]
    )
  )

(the .public length
  /////unsigned.bytes/2)
