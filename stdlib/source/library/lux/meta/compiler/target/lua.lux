... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Location Code Label
                int if function or and not let local comment its when)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    ["[0]" enum]]
   [control
    ["|" pipe]]
   [data
    ["[0]" text (.only)
     ["%" \\format (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix)]]]
   [math
    [number
     ["n" nat]
     ["i" int]
     ["d" dec]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\parser]]
    [macro
     [syntax (.only syntax)]
     ["[0]" template]]
    [type
     ["[0]" nominal]]]]])

... Added the carriage return for better Windows compatibility.
(the \n+
  Text
  (format text.carriage_return text.new_line))

(the nested
  (-> Text Text)
  (.let [nested_new_line (format text.new_line text.tab)]
    (|>> (format \n+)
         (text.replaced text.new_line nested_new_line))))

(the input_separator ", ")

(nominal.every .public (Code brand)
  Text

  (the .public equivalence
    (All (_ brand)
      (Equivalence (Code brand)))
    (implementation
     (the (= expected actual)
       (of text.equivalence = (nominal.representation expected) (nominal.representation actual)))))

  (the .public hash
    (All (_ brand) (Hash (Code brand)))
    (implementation
     (the equivalence ..equivalence)
     (the hash (|>> nominal.representation (of text.hash hash)))))

  (the .public manual
    (-> Text Code)
    (|>> nominal.abstraction))

  (the .public code
    (-> (Code Any) Text)
    (|>> nominal.representation))

  (with_template [<type> <super>+]
    [(with_expansions [<brand> (template.symbol [<type> "'"])]
       (these (nominal.every (<brand> brand) Any)
              (`` (every .public <type> (|> Any <brand> (,, (template.spliced <super>+)))))))]
    
    [Expression [Code]]
    [Computation [Expression' Code]]
    [Location [Computation' Expression' Code]]
    [Statement [Code]]
    )

  (with_template [<type> <super>+]
    [(with_expansions [<brand> (template.symbol [<type> "'"])]
       (these (nominal.every <brand> Any)
              (`` (every .public <type> (|> <brand> (,, (template.spliced <super>+)))))))]

    [Literal [Computation' Expression' Code]]
    [Var [Location' Computation' Expression' Code]]
    [Access [Location' Computation' Expression' Code]]
    [Label [Code]]
    )

  (the .public nil
    Literal
    (nominal.abstraction "nil"))

  (the .public boolean
    (-> Bit Literal)
    (|>> (|.when
           #0 "false"
           #1 "true")
         nominal.abstraction))

  (the .public int
    (-> Int Literal)
    ... Integers must be turned into hexadecimal to avoid quirks in how Lua parses integers.
    ... In particular, the number -9223372036854775808 will be incorrectly parsed as a float by Lua.
    (.let [to_hex (of n.hex injection)]
      (|>> .nat
           to_hex
           (format "0x")
           nominal.abstraction)))

  (the .public float
    (-> Dec Literal)
    (|>> (|.cond [(d.= d.positive_infinity)]
                 [(|.new "(1.0/0.0)" [])]
                 
                 [(d.= d.negative_infinity)]
                 [(|.new "(-1.0/0.0)" [])]
                 
                 [(d.= d.not_a_number)]
                 [(|.new "(0.0/0.0)" [])]

                 ... else
                 [%.dec (text.replaced "+" "")])
         nominal.abstraction))

  (the safe
    (-> Text Text)
    (`` (|>> (,, (with_template [<find> <replace>]
                   [(text.replaced <find> <replace>)]

                   ["\" "\\"]
                   [text.tab "\t"]
                   [text.vertical_tab "\v"]
                   [text.null "\0"]
                   [text.back_space "\b"]
                   [text.form_feed "\f"]
                   [text.new_line "\n"]
                   [text.carriage_return "\r"]
                   [text.double_quote (format "\" text.double_quote)]
                   ))
             )))

  (the .public string
    (-> Text Literal)
    (|>> ..safe
         (text.enclosed' text.double_quote)
         nominal.abstraction))

  (the .public multi
    (-> (List Expression) Expression)
    (|>> (list#each ..code)
         (text.interposed ..input_separator)
         nominal.abstraction))

  (the .public array
    (-> (List Expression) Literal)
    (|>> (list#each ..code)
         (text.interposed ..input_separator)
         (text.enclosed ["{" "}"])
         nominal.abstraction))

  (the .public table
    (-> (List [Text Expression]) Literal)
    (|>> (list#each (.function (_ [key value])
                      (format key " = " (nominal.representation value))))
         (text.interposed ..input_separator)
         (text.enclosed ["({" "})"])
         nominal.abstraction))

  (the .public (item idx array)
    (-> Expression Expression Access)
    (nominal.abstraction
     (format "(" (nominal.representation array) ")[" (nominal.representation idx) "]")))

  (the .public (its field table)
    (-> Text Expression Access)
    (nominal.abstraction
     (format (nominal.representation table) "." field)))

  (the .public length
    (-> Expression Computation)
    (|>> nominal.representation
         (text.enclosed ["#(" ")"])
         nominal.abstraction))

  (the .public (apply args func)
    (-> (List Expression) Expression Computation)
    (|> args
        (list#each ..code)
        (text.interposed ..input_separator)
        (text.enclosed ["(" ")"])
        (format (nominal.representation func))
        nominal.abstraction))

  (the .public (do method args table)
    (-> Text (List Expression) Expression Computation)
    (|> args
        (list#each ..code)
        (text.interposed ..input_separator)
        (text.enclosed ["(" ")"])
        (format (nominal.representation table) ":" method)
        nominal.abstraction))

  (with_template [<op> <name>]
    [(the .public (<name> parameter subject)
       (-> Expression Expression Expression)
       (nominal.abstraction
        (format "("
                (nominal.representation subject)
                " " <op> " "
                (nominal.representation parameter)
                ")")))]

    ["==" =]
    ["<"  <]
    ["<=" <=]
    [">"  >]
    [">=" >=]
    ["+"  +]
    ["-"  -]
    ["*"  *]
    ["^"  ^]
    ["/"  /]
    ["//" //]
    ["%"  %]
    [".." concat]

    ["or"  or]
    ["and" and]
    ["|"   bit_or]
    ["&"   bit_and]
    ["~"   bit_xor]

    ["<<" bit_shl]
    [">>" bit_shr]
    )

  (with_template [<name> <unary>]
    [(the .public (<name> subject)
       (-> Expression Expression)
       (nominal.abstraction
        (format "(" <unary> " " (nominal.representation subject) ")")))]

    [not "not"]
    [opposite "-"]
    )

  (with_template [<name> <type>]
    [(the .public <name>
       (-> Text <type>)
       (|>> nominal.abstraction))]

    [var Var]
    [label Label]
    )

  (the .public statement
    (-> Expression Statement)
    (|>> nominal.representation
         nominal.abstraction))

  (the .public (then pre! post!)
    (-> Statement Statement Statement)
    (nominal.abstraction
     (format (nominal.representation pre!)
             \n+
             (nominal.representation post!))))

  (the locations
    (-> (List Location) Text)
    (|>> (list#each ..code)
         (text.interposed ..input_separator)))

  (the .public (local vars)
    (-> (List Var) Statement)
    (nominal.abstraction
     (format "local " (..locations vars))))

  (the .public (set vars value)
    (-> (List Location) Expression Statement)
    (nominal.abstraction
     (format (..locations vars) " = " (nominal.representation value))))

  (the .public (let vars value)
    (-> (List Var) Expression Statement)
    (nominal.abstraction
     (format "local " (..locations vars) " = " (nominal.representation value))))

  (the .public (local/1 var value)
    (-> Var Expression Statement)
    (nominal.abstraction
     (format "local " (nominal.representation var) " = " (nominal.representation value))))

  (the .public (if test then! else!)
    (-> Expression Statement Statement Statement)
    (nominal.abstraction
     (format "if " (nominal.representation test)
             \n+ "then" (..nested (nominal.representation then!))
             \n+ "else" (..nested (nominal.representation else!))
             \n+ "end")))

  (the .public (when test then!)
    (-> Expression Statement Statement)
    (nominal.abstraction
     (format "if " (nominal.representation test)
             \n+ "then" (..nested (nominal.representation then!))
             \n+ "end")))

  (the .public (while test body!)
    (-> Expression Statement Statement)
    (nominal.abstraction
     (format "while " (nominal.representation test) " do"
             (..nested (nominal.representation body!))
             \n+ "end")))

  (the .public (repeat until body!)
    (-> Expression Statement Statement)
    (nominal.abstraction
     (format "repeat"
             (..nested (nominal.representation body!))
             \n+ "until " (nominal.representation until))))

  (the .public (for_in vars source body!)
    (-> (List Var) Expression Statement Statement)
    (nominal.abstraction
     (format "for " (|> vars
                        (list#each ..code)
                        (text.interposed ..input_separator))
             " in " (nominal.representation source) " do"
             (..nested (nominal.representation body!))
             \n+ "end")))

  (the .public (for_step var from to step body!)
    (-> Var Expression Expression Expression Statement
        Statement)
    (nominal.abstraction
     (format "for " (nominal.representation var)
             " = " (nominal.representation from)
             ..input_separator (nominal.representation to)
             ..input_separator (nominal.representation step) " do"
             (..nested (nominal.representation body!))
             \n+ "end")))

  (the .public (return value)
    (-> Expression Statement)
    (nominal.abstraction
     (format "return " (nominal.representation value))))

  (the .public (closure args body!)
    (-> (List Var) Statement Expression)
    (|> (format "function " (|> args
                                ..locations
                                (text.enclosed ["(" ")"]))
                (..nested (nominal.representation body!))
                \n+ "end")
        (text.enclosed ["(" ")"])
        nominal.abstraction))

  (with_template [<name> <code> <binding>]
    [(the .public (<name> name args body!)
       (-> <binding> (List Var) Statement Statement)
       (nominal.abstraction
        (format <code> " " (nominal.representation name)
                (|> args
                    ..locations
                    (text.enclosed ["(" ")"]))
                (..nested (nominal.representation body!))
                \n+ "end")))]

    [function "function" Location]
    [local_function "local function" Var]
    )

  (the .public break
    Statement
    (nominal.abstraction
     "break"))

  (the .public (set_label label)
    (-> Label Statement)
    (nominal.abstraction
     (format "::" (nominal.representation label) "::")))

  (the .public (go_to label)
    (-> Label Statement)
    (nominal.abstraction
     (format "goto " (nominal.representation label))))

  ... https://www.lua.org/pil/1.3.html
  (the .public (comment commentary on)
    (All (_ kind) (-> Text (Code kind) (Code kind)))
    (nominal.abstraction
     (format "-- "  commentary \n+ (nominal.representation on))))
  )

(the arity_inputs
  (syntax (_ [arity <code>.nat])
    (in (.when arity
          0 (.list)
          _ (|> (-- arity)
                (enum.range n.enum 0)
                (list#each (|>> %.nat code.local)))))))

(the arity_types
  (syntax (_ [arity <code>.nat])
    (in (list.repeated arity (` ..Expression)))))

(with_template [<arity> <function>+]
  [(with_expansions [<inputs> (arity_inputs <arity>)
                     <types> (arity_types <arity>)
                     <definitions> (template.spliced <function>+)]
     (with_template [<function>]
       [(`` (the .public ((,, (template.symbol [<function> "/" <arity>])) <inputs>)
              (-> <types> Computation)
              (..apply (.list <inputs>) (..var <function>))))]

       <definitions>))]

  [1
   [["error"]
    ["pcall"]
    ["print"]
    ["require"]
    ["type"]
    ["ipairs"]]]
  [2
   [["error"]]]
  )
