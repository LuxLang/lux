... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Location Code
                not or and list if int comment exec try its is when tuple)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    ["[0]" enum]]
   [control
    ["[0]" pipe]]
   [data
    ["[0]" text (.only)
     ["%" \\format (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix)]]]
   [math
    [number
     ["n" nat]
     ["f" frac]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\parser]]
    [macro
     [syntax (.only syntax)]
     ["[0]" template]]
    [type
     ["[0]" nominal]]]]])

(the input_separator
  ", ")

(the expression
  (-> Text Text)
  (text.enclosed ["(" ")"]))

... Added the carriage return for better Windows compatibility.
(the \n+
  Text
  (format text.carriage_return text.new_line))

(the nested
  (-> Text Text)
  (.let [nested_new_line (format text.new_line text.tab)]
    (|>> (format \n+)
         (text.replaced text.new_line nested_new_line))))

(nominal.every .public (Code brand)
  Text

  (the .public equivalence
    (All (_ brand) (Equivalence (Code brand)))
    (implementation
     (the (= reference subject)
       (of text.equivalence = (nominal.representation reference) (nominal.representation subject)))))

  (the .public hash
    (All (_ brand) (Hash (Code brand)))
    (implementation
     (the equivalence ..equivalence)
     (the hash (|>> nominal.representation (of text.hash hash)))))

  (the .public manual
    (-> Text Code)
    (|>> nominal.abstraction))

  (the .public code
    (-> (Code Any) Text)
    (|>> nominal.representation))

  (with_template [<type> <super>]
    [(with_expansions [<brand> (template.symbol [<type> "'"])]
       (these (`` (nominal.every (<brand> brand) Any))
              (`` (every .public (<type> brand)
                    (<super> (<brand> brand))))))]
    
    [Expression Code]
    [Computation Expression]
    [Location Computation]
    [Var Location]
    [Statement Code]
    )

  (with_template [<type> <super>]
    [(with_expansions [<brand> (template.symbol [<type> "'"])]
       (these (`` (nominal.every <brand> Any))
              (`` (every .public <type> (<super> <brand>)))))]

    [Literal Computation]
    [Access Location]
    [Loop Statement]
    )

  (with_template [<var> <brand>]
    [(nominal.every .public <brand> Any)

     (every .public <var> (Var <brand>))]

    [SVar Single]
    [PVar Poly]
    [KVar Keyword]
    )

  (the .public var
    (-> Text SVar)
    (|>> nominal.abstraction))

  (with_template [<name> <brand> <prefix>]
    [(the .public <name>
       (-> SVar (Var <brand>))
       (|>> nominal.representation
            (format <prefix>)
            nominal.abstraction))]

    [poly    Poly    "*"]
    [keyword Keyword "**"]
    )

  (the .public none
    Literal
    (nominal.abstraction
     "None"))

  (the .public bool
    (-> Bit Literal)
    (|>> (pipe.when
           #0 "False"
           #1 "True")
         nominal.abstraction))

  (the .public int
    (-> Int Literal)
    (|>> %.int
         nominal.abstraction))

  (the .public (long value)
    (-> Int Literal)
    (nominal.abstraction
     (format (%.int value) "L")))

  (the .public float
    (-> Frac Literal)
    (`` (|>> (pipe.cond (,, (with_template [<test> <python>]
                              [[<test>]
                               [(pipe.new (format "float(" text.double_quote <python> text.double_quote ")") [])]]

                              [(f.= f.positive_infinity) "inf"]
                              [(f.= f.negative_infinity) "-inf"]
                              [f.not_a_number? "nan"]
                              ))
                        
                        ... else
                        [%.frac])
             nominal.abstraction)))

  (the safe
    (-> Text Text)
    (`` (|>> (,, (with_template [<find> <replace>]
                   [(text.replaced <find> <replace>)]

                   ["\" "\\"]
                   [text.tab "\t"]
                   [text.vertical_tab "\v"]
                   [text.null "\0"]
                   [text.back_space "\b"]
                   [text.form_feed "\f"]
                   [text.new_line "\n"]
                   [text.carriage_return "\r"]
                   [text.double_quote (format "\" text.double_quote)]
                   ))
             )))

  (the .public string
    (-> Text Literal)
    (|>> ..safe
         (text.enclosed [text.double_quote text.double_quote])
         nominal.abstraction))

  (the .public unicode
    (-> Text Literal)
    (|>> ..string
         nominal.representation
         (format "u")
         nominal.abstraction))

  (the (composite_literal left_delimiter right_delimiter entry_serializer)
    (All (_ a)
      (-> Text Text (-> a Text)
          (-> (List a) Literal)))
    (function (_ entries)
      (<| nominal.abstraction
          ... ..expression
          (format left_delimiter
                  (|> entries
                      (list#each (|>> entry_serializer (text.suffix ..input_separator)))
                      text.together)
                  right_delimiter))))

  (with_template [<name> <pre> <post>]
    [(the .public <name>
       (-> (List (Expression Any)) Literal)
       (composite_literal <pre> <post> ..code))]

    [tuple "(" ")"]
    [list  "[" "]"]
    )

  (the .public (slice from to list)
    (-> (Expression Any) (Expression Any) (Expression Any) Access)
    (<| nominal.abstraction
        ... ..expression
        (format (nominal.representation list) "[" (nominal.representation from) ":" (nominal.representation to) "]")))

  (the .public (slice_from from list)
    (-> (Expression Any) (Expression Any) Access)
    (<| nominal.abstraction
        ... ..expression
        (format (nominal.representation list) "[" (nominal.representation from) ":]")))

  (the .public dict
    (-> (List [(Expression Any) (Expression Any)]) (Computation Any))
    (composite_literal "{" "}" (.function (_ [k v]) (format (nominal.representation k) " : " (nominal.representation v)))))

  (the .public (apply args func)
    (-> (List (Expression Any)) (Expression Any) (Computation Any))
    (<| nominal.abstraction
        ... ..expression
        (format (nominal.representation func)
                "("
                (|> args (list#each ..code) (text.interposed ..input_separator))
                ")")))

  (with_template [<name> <brand> <prefix>]
    [(the .public <name>
       (-> (Expression Any) (Expression Any))
       (|>> nominal.representation
            (format <prefix>)
            nominal.abstraction))]

    [splat_poly    Poly    "*"]
    [splat_keyword Keyword "**"]
    )

  (the .public (its name object)
    (-> Text (Expression Any) Access)
    (nominal.abstraction
     (format (nominal.representation object) "." name)))

  (the .public (do method args object)
    (-> Text (List (Expression Any)) (Expression Any) (Computation Any))
    (|> object
        (..its method)
        (..apply args)))

  (the .public (item idx array)
    (-> (Expression Any) (Expression Any) Access)
    (nominal.abstraction
     (format (nominal.representation array) "[" (nominal.representation idx) "]")))

  (the .public (? test then else)
    (-> (Expression Any) (Expression Any) (Expression Any) (Computation Any))
    (<| nominal.abstraction
        ..expression
        (format (nominal.representation then) " if " (nominal.representation test) " else " (nominal.representation else))))

  (with_template [<name> <op>]
    [(the .public (<name> param subject)
       (-> (Expression Any) (Expression Any) (Computation Any))
       (<| nominal.abstraction
           ..expression
           (format (nominal.representation subject) " " <op> " " (nominal.representation param))))]

    [is      "is"]
    [=       "=="]
    [<       "<"]
    [<=      "<="]
    [>       ">"]
    [>=      ">="]
    [+       "+"]
    [-       "-"]
    [*       "*"]
    [/       "/"]
    [//      "//"]
    [%       "%"]
    [**      "**"]
    [bit_or  "|"]
    [bit_and "&"]
    [bit_xor "^"]
    [bit_shl "<<"]
    [bit_shr ">>"]

    [or      "or"]
    [and     "and"]

    [in?     "in"]
    )

  (with_template [<name> <unary>]
    [(the .public (<name> subject)
       (-> (Expression Any) (Computation Any))
       (<| nominal.abstraction
           ... ..expression
           (format <unary> " " (nominal.representation subject))))]

    [not "not"]
    [opposite "-"]
    )

  (the .public (lambda arguments body)
    (-> (List (Var Any)) (Expression Any) (Computation Any))
    (<| nominal.abstraction
        ..expression
        (format "lambda " (|> arguments (list#each ..code) (text.interposed ..input_separator))
                ": " (nominal.representation body))))

  (the .public (set vars value)
    (-> (List (Location Any)) (Expression Any) (Statement Any))
    (nominal.abstraction
     (format (|> vars (list#each ..code) (text.interposed ..input_separator))
             " = "
             (nominal.representation value))))

  (the .public multi
    (-> (List (Expression Any)) (Expression Any))
    (|>> (list#each ..code)
         (text.interposed ..input_separator)
         nominal.abstraction))

  (the .public (delete where)
    (-> (Location Any) (Statement Any))
    (nominal.abstraction
     (format "del " (nominal.representation where))))

  (the .public (if test then! else!)
    (-> (Expression Any) (Statement Any) (Statement Any) (Statement Any))
    (nominal.abstraction
     (format "if " (nominal.representation test) ":"
             (..nested (nominal.representation then!))
             \n+ "else:"
             (..nested (nominal.representation else!)))))

  (the .public (when test then!)
    (-> (Expression Any) (Statement Any) (Statement Any))
    (nominal.abstraction
     (format "if " (nominal.representation test) ":"
             (..nested (nominal.representation then!)))))

  (the .public (then pre! post!)
    (-> (Statement Any) (Statement Any) (Statement Any))
    (nominal.abstraction
     (format (nominal.representation pre!)
             \n+
             (nominal.representation post!))))

  (with_template [<keyword> <0>]
    [(the .public <0>
       (Statement Any)
       (nominal.abstraction
        <keyword>))]

    ["break"    break]
    ["continue" continue]
    )

  (the .public (while test body! else!)
    (-> (Expression Any) (Statement Any) (Maybe (Statement Any)) Loop)
    (nominal.abstraction
     (format "while " (nominal.representation test) ":"
             (..nested (nominal.representation body!))
             (.when else!
               {.#Some else!}
               (format \n+ "else:"
                       (..nested (nominal.representation else!)))
               
               {.#None}
               ""))))

  (the .public (for_in var inputs body!)
    (-> SVar (Expression Any) (Statement Any) Loop)
    (nominal.abstraction
     (format "for " (nominal.representation var) " in " (nominal.representation inputs) ":"
             (..nested (nominal.representation body!)))))

  (the .public statement
    (-> (Expression Any) (Statement Any))
    (|>> nominal.transmutation))

  (the .public pass
    (Statement Any)
    (nominal.abstraction
     "pass"))

  (every .public Except
    (Record
     [#classes (List Text)
      #exception SVar
      #handler (Statement Any)]))
  
  (the .public (try body! excepts)
    (-> (Statement Any) (List Except) (Statement Any))
    (nominal.abstraction
     (format "try:"
             (..nested (nominal.representation body!))
             (|> excepts
                 (list#each (function (_ [classes exception catch!])
                              (format \n+ "except (" (text.interposed ..input_separator classes)
                                      ") as " (nominal.representation exception) ":"
                                      (..nested (nominal.representation catch!)))))
                 text.together))))

  (with_template [<name> <keyword> <pre>]
    [(the .public (<name> value)
       (-> (Expression Any) (Statement Any))
       (nominal.abstraction
        (format <keyword> (<pre> (nominal.representation value)))))]

    [raise  "raise "  |>]
    [return "return " |>]
    [print  "print"   ..expression]
    )

  (the .public (exec globals code)
    (-> (Maybe (Expression Any)) (Expression Any) (Statement Any))
    (let [extra (.when globals
                  {.#Some globals}
                  (.list globals)

                  {.#None}
                  (.list))]
      (nominal.abstraction
       (format "exec" (nominal.representation (..tuple (list.partial code extra)))))))

  (the .public (def name args body)
    (-> SVar (List (Ex (_ k) (Var k))) (Statement Any) (Statement Any))
    (nominal.abstraction
     (format "def " (nominal.representation name)
             "("
             (|> args (list#each ..code) (text.interposed ..input_separator))
             "):"
             (..nested (nominal.representation body)))))

  (the .public (import module_name)
    (-> Text (Statement Any))
    (nominal.abstraction
     (format "import " module_name)))

  (the .public (comment commentary on)
    (All (_ brand) (-> Text (Code brand) (Code brand)))
    (nominal.abstraction
     (format "# "  (text.replaced text.\n "\n" commentary) \n+
             (nominal.representation on))))
  )

(the arity_inputs
  (syntax (_ [arity <code>.nat])
    (in (.when arity
          0 (.list)
          _ (|> (-- arity)
                (enum.range n.enum 0)
                (list#each (|>> %.nat code.local)))))))

(the arity_types
  (syntax (_ [arity <code>.nat])
    (in (list.repeated arity (` (Expression Any))))))

(with_template [<arity> <function>+]
  [(with_expansions [<inputs> (arity_inputs <arity>)
                     <definitions> (template.spliced <function>+)]
     (with_template [<function>]
       [(`` (the .public ((,, (template.symbol [<function> "/" <arity>])) <inputs>)
              (-> (,, (arity_types <arity>)) (Computation Any))
              (..apply (.list <inputs>) (..var <function>))))]

       <definitions>))]

  [0
   [["locals"]
    ["globals"]]]

  [1
   [["str"]
    ["ord"]
    ["float"]
    ["int"]
    ["len"]
    ["chr"]
    ["unichr"]
    ["unicode"]
    ["repr"]
    ["__import__"]
    ["Exception"]]]

  [2
   []]

  [3
   []]
  )
