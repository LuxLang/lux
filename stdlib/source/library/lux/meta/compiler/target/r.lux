... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Code
                or and list if function cond not int when)
   [control
    ["[0]" pipe]
    ["[0]" function]
    ["[0]" maybe (.use "[1]#[0]" functor)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix)]]]
   [math
    [number
     ["d" dec]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro
     [syntax (.only syntax)]
     ["[0]" template]]
    [type
     ["[0]" nominal]]]]])

(nominal.every .public (Code kind)
  Text

  (with_template [<type> <super>+]
    [(with_expansions [<kind> (template.symbol [<type> "'"])]
       (these (nominal.every .public (<kind> kind) Any)
              (`` (every .public <type> (|> Any <kind> (,, (template.spliced <super>+)))))))]
    
    [Expression [Code]]
    )

  (with_template [<type> <super>+]
    [(with_expansions [<kind> (template.symbol [<type> "'"])]
       (these (nominal.every .public (<kind> kind) Any)
              (`` (every .public (<type> <brand>) (|> <brand> <kind> (,, (template.spliced <super>+)))))))]
    
    [Var [Expression' Code]]
    )

  (with_template [<var> <kind>]
    [(nominal.every .public <kind> Any)
     (every .public <var> (Var <kind>))]

    [SVar Single]
    [PVar Poly]
    )

  (the .public var
    (-> Text SVar)
    (|>> nominal.abstraction))
  
  (the .public var_args
    PVar
    (nominal.abstraction "..."))

  (the .public manual
    (-> Text Code)
    (|>> nominal.abstraction))
  
  (the .public code
    (-> (Code Any) Text)
    (|>> nominal.representation))

  (the (self_contained code)
    (-> Text Expression)
    (nominal.abstraction
     (%.format "(" code ")")))

  ... Added the carriage return for better Windows compatibility.
  (the \n+
    Text
    (%.format text.carriage_return text.new_line))

  (the nested_new_line
    (%.format text.new_line text.tab))

  (the nested
    (-> Text Text)
    (|>> (text.replaced text.new_line ..nested_new_line)
         (%.format text.carriage_return ..nested_new_line)))

  (the (_block expression)
    (-> Text Text)
    (%.format "{" (nested expression) \n+ "}"))

  (the .public (block expression)
    (-> Expression Expression)
    (nominal.abstraction
     (%.format "{"
               (..nested (nominal.representation expression))
               \n+ "}")))

  (with_template [<name> <r>]
    [(the .public <name>
       Expression
       (nominal.abstraction <r>))]

    [null "NULL"]
    [n/a "NA"]
    )

  (with_template [<name>]
    [(the .public <name> Expression n/a)]

    [not_available]
    [not_applicable]
    [no_answer]
    )

  (the .public bool
    (-> Bit Expression)
    (|>> (pipe.when
           #0 "FALSE"
           #1 "TRUE")
         nominal.abstraction))

  (the .public int
    (-> Int Expression)
    (|>> %.int nominal.abstraction))

  (the .public float
    (-> Dec Expression)
    (|>> (pipe.cond [(d.= d.positive_infinity)]
                    [(pipe.new "1.0/0.0" [])]
                    
                    [(d.= d.negative_infinity)]
                    [(pipe.new "-1.0/0.0" [])]
                    
                    [(d.= d.not_a_number)]
                    [(pipe.new "0.0/0.0" [])]
                    
                    ... else
                    [%.dec])
         ..self_contained))

  (the safe
    (-> Text Text)
    (`` (|>> (,, (with_template [<find> <replace>]
                   [(text.replaced <find> <replace>)]

                   ["\" "\\"]
                   ["|" "\|"]
                   [text.alarm "\a"]
                   [text.back_space "\b"]
                   [text.tab "\t"]
                   [text.new_line "\n"]
                   [text.carriage_return "\r"]
                   [text.double_quote (%.format "\" text.double_quote)]
                   ))
             )))

  (the .public string
    (-> Text Expression)
    (|>> ..safe %.text nominal.abstraction))

  (the .public (slice from to list)
    (-> Expression Expression Expression Expression)
    (..self_contained
     (%.format (nominal.representation list)
               "[" (nominal.representation from) ":" (nominal.representation to) "]")))

  (the .public (slice_from from list)
    (-> Expression Expression Expression)
    (..self_contained
     (%.format (nominal.representation list)
               "[-1"  ":-" (nominal.representation from) "]")))

  (the .public (apply args func)
    (-> (List Expression) Expression Expression)
    (let [func (nominal.representation func)
          spacing (|> " "
                      (list.repeated (text.size func))
                      text.together)]
      (nominal.abstraction
       (%.format func "("
                 (|> args
                     (list#each ..code)
                     (text.interposed (%.format "," \n+))
                     ..nested)
                 ")"))))

  (with_template [<name> <function>]
    [(the .public (<name> members)
       (-> (List Expression) Expression)
       (..apply members (..var <function>)))]

    [vector "c"]
    [list   "list"]
    )

  (the .public named_list
    (-> (List [Text Expression]) Expression)
    (|>> (list#each (.function (_ [key value])
                      (nominal.abstraction (%.format key "=" (nominal.representation value)))))
         ..list))

  (the .public (apply_kw args kw_args func)
    (-> (List Expression) (List [Text Expression]) Expression Expression)
    (..self_contained
     (%.format (nominal.representation func)
               (%.format "("
                         (text.interposed "," (list#each ..code args)) ","
                         (text.interposed "," (list#each (.function (_ [key val])
                                                           (%.format key "=" (nominal.representation val)))
                                                         kw_args))
                         ")"))))

  (the arity_inputs
    (syntax (_ [arity <code>.nat])
      (in (.when arity
            0 (.list)
            _ (|> arity
                  list.indices
                  (list#each (|>> %.nat code.local)))))))

  (the arity_types
    (syntax (_ [arity <code>.nat])
      (in (list.repeated arity (` ..Expression)))))

  (with_template [<arity> <function>+]
    [(with_expansions [<apply> (template.symbol ["apply/" <arity>])
                       <inputs> (arity_inputs <arity>)
                       <types> (arity_types <arity>)
                       <definitions> (template.spliced <function>+)]
       (these (the .public (<apply> function [<inputs>])
                (-> Expression [<types>] Expression)
                (..apply (.list <inputs>) function))

              (with_template [<function>]
                [(`` (the .public (,, (template.symbol [<function> "/" <arity>]))
                       (-> [<types>] Expression)
                       (<apply> (..var <function>))))]

                <definitions>)))]

    [0
     [["commandArgs"]]]
    [1
     [["intToUtf8"]]]
    [2
     [["paste"]]]
    )

  (the .public as::integer
    (-> Expression Expression)
    (..apply/1 (..var "as.integer")))

  (the .public (item idx list)
    (-> Expression Expression Expression)
    (..self_contained
     (%.format (nominal.representation list) "[[" (nominal.representation idx) "]]")))

  (the .public (if test then else)
    (-> Expression Expression Expression Expression)
    (nominal.abstraction
     (%.format "if(" (nominal.representation test) ")"
               " " (.._block (nominal.representation then))
               " else " (.._block (nominal.representation else)))))

  (the .public (when test then)
    (-> Expression Expression Expression)
    (nominal.abstraction
     (%.format "if(" (nominal.representation test) ") {"
               (.._block (nominal.representation then))
               \n+ "}")))

  (the .public (cond clauses else)
    (-> (List [Expression Expression]) Expression Expression)
    (list#mix (.function (_ [test then] next)
                (if test then next))
              else
              (list.reversed clauses)))

  (with_template [<name> <op>]
    [(the .public (<name> param subject)
       (-> Expression Expression Expression)
       (..self_contained
        (%.format (nominal.representation subject)
                  " " <op> " "
                  (nominal.representation param))))]

    [=       "=="]
    [<       "<"]
    [<=      "<="]
    [>       ">"]
    [>=      ">="]
    [+       "+"]
    [-       "-"]
    [*       "*"]
    [/       "/"]
    [%%      "%%"]
    [**      "**"]
    [or      "||"]
    [and     "&&"]
    )

  (with_template [<name> <func>]
    [(the .public (<name> param subject)
       (-> Expression Expression Expression)
       (..apply (.list subject param) (..var <func>)))]

    [bit_or   "bitwOr"]
    [bit_and  "bitwAnd"]
    [bit_xor  "bitwXor"]
    [bit_shl  "bitwShiftL"]
    [bit_ushr "bitwShiftR"]
    )

  (the .public (bit_not subject)
    (-> Expression Expression)
    (..apply (.list subject) (..var "bitwNot")))

  (with_template [<name> <op>]
    [(the .public <name>
       (-> Expression Expression)
       (|>> nominal.representation (%.format <op>) ..self_contained))]

    [not    "!"]
    [negate "-"]
    )
  
  (the .public (length list)
    (-> Expression Expression)
    (..apply (.list list) (..var "length")))

  (the .public (range from to)
    (-> Expression Expression Expression)
    (..self_contained
     (%.format (nominal.representation from) ":" (nominal.representation to))))

  (the .public (function inputs body)
    (-> (List (Ex (_ k) (Var k))) Expression Expression)
    (let [args (|> inputs (list#each ..code) (text.interposed ", "))]
      (..self_contained
       (%.format "function(" args ") "
                 (.._block (nominal.representation body))))))

  (the .public (try body warning error finally)
    (-> Expression (Maybe Expression) (Maybe Expression) (Maybe Expression) Expression)
    (let [optional (is (-> Text (Maybe Expression) (-> Text Text) Text)
                       (.function (_ parameter value preparation)
                         (|> value
                             (maybe#each (|>> nominal.representation preparation (%.format ", " parameter " = ")))
                             (maybe.else ""))))]
      (..self_contained
       (%.format "tryCatch("
                 (.._block (nominal.representation body))
                 (optional "warning" warning function.identity)
                 (optional "error" error function.identity)
                 (optional "finally" finally .._block)
                 ")"))))

  (the .public (while test body)
    (-> Expression Expression Expression)
    (..self_contained
     (%.format "while (" (nominal.representation test) ") "
               (.._block (nominal.representation body)))))

  (the .public (for_in var inputs body)
    (-> SVar Expression Expression Expression)
    (..self_contained
     (%.format "for (" (nominal.representation var) " in " (nominal.representation inputs) ")"
               (.._block (nominal.representation body)))))

  (with_template [<name> <keyword>]
    [(the .public (<name> message)
       (-> Expression Expression)
       (..apply (.list message) (..var <keyword>)))]

    [stop  "stop"]
    [print "print"]
    )

  (the .public (set! var value)
    (-> SVar Expression Expression)
    (..self_contained
     (%.format (nominal.representation var) " <- " (nominal.representation value))))

  (the .public (set_item! idx value list)
    (-> Expression Expression SVar Expression)
    (..self_contained
     (%.format (nominal.representation list) "[[" (nominal.representation idx) "]] <- " (nominal.representation value))))

  (the .public (then pre post)
    (-> Expression Expression Expression)
    (nominal.abstraction
     (%.format (nominal.representation pre)
               \n+
               (nominal.representation post))))
  )
