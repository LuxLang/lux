... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [control
    ["<>" parser]]
   [data
    ["[0]" binary
     ["_" \\format (.only Format)]
     ["<[1]>" \\parser (.only Parser)]]]
   [meta
    ["[0]" version]]]]
 ["[0]" /
  [analysis
   ["[0]" module]]
  [///
   [meta
    [archive
     ["[0]" signature]
     ["[0]" key (.only Key)]]]]])

... TODO: Remove #module_hash, #imports & #module_state ASAP.
... TODO: Not just from this parser, but from the lux.Module type.
(def .public format
  (Format .Module)
  (let [definition (is (Format Definition)
                       (all _.and _.type _.any))
        alias (is (Format Alias)
                  (_.and _.text _.text))
        global (is (Format Global)
                   (all _.or
                        definition
                        alias
                        definition))]
    (all _.and
         ... #module_hash
         _.nat
         ... #module_aliases
         (_.list alias)
         ... #definitions
         (_.list (_.and _.text (_.and _.bit global)))
         ... #imports
         (_.list _.text)
         ... #module_state
         _.any)))

(def .public parser
  (Parser .Module)
  (let [definition (is (Parser Definition)
                       (all <>.and
                            <binary>.type
                            <binary>.any))
        alias (is (Parser Alias)
                  (all <>.and
                       <binary>.text
                       <binary>.text))
        global (is (Parser Global)
                   (all <binary>.or
                        definition
                        alias
                        definition))]
    (all <>.and
         ... #module_hash
         <binary>.nat
         ... #module_aliases
         (<binary>.list alias)
         ... #definitions
         (<binary>.list (<>.and <binary>.text (<>.and <binary>.bit global)))
         ... #imports
         (<binary>.list <binary>.text)
         ... #module_state
         (of <>.monad in {.#Cached}))))

(def .public key
  (Key .Module)
  (key.key [signature.#name (symbol ..compiler)
            signature.#version version.latest]
           (module.empty 0)))
