(.require
 [library
  [lux (.except Tuple Variant)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text (.only)
     ["%" \\format (.only Format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    [number
     ["n" nat]]]]])

(type .public (Variant a)
  (Record
   [#lefts Nat
    #right? Bit
    #value a]))

(type .public (Tuple a)
  (List a))

(type .public (Complex a)
  (.Variant
   {#Variant (Variant a)}
   {#Tuple (Tuple a)}))

(type .public Tag
  Nat)

(def .public (tag right? lefts)
  (-> Bit Nat Tag)
  (if right?
    (++ lefts)
    lefts))

(def .public (lefts right? tag)
  (-> Bit Tag Nat)
  (if right?
    (-- tag)
    tag))

(def .public (choice multiplicity pick)
  (-> Nat Tag [Nat Bit])
  (let [right? (n.= (-- multiplicity) pick)]
    [(..lefts right? pick)
     right?]))

(def .public (equivalence (open "/#[0]"))
  (All (_ a) (-> (Equivalence a) (Equivalence (Complex a))))
  (implementation
   (def (= reference sample)
     (case [reference sample]
       [{#Variant [reference_lefts reference_right? reference_value]}
        {#Variant [sample_lefts sample_right? sample_value]}]
       (and (n.= reference_lefts sample_lefts)
            (bit#= reference_right? sample_right?)
            (/#= reference_value sample_value))
       
       [{#Tuple reference} {#Tuple sample}]
       (at (list.equivalence /#=) = reference sample)

       _
       false))))

(def .public (hash super)
  (All (_ a) (-> (Hash a) (Hash (Complex a))))
  (implementation
   (def equivalence
     (..equivalence (at super equivalence)))
   
   (def (hash value)
     (case value
       {#Variant [lefts right? value]}
       (all n.* 2
            (at n.hash hash lefts)
            (at bit.hash hash right?)
            (at super hash value))
       
       {#Tuple members}
       (all n.* 3
            (at (list.hash super) hash members))
       ))))

(def .public (format %it it)
  (All (_ a) (-> (Format a) (Format (Complex a))))
  (case it
    {#Variant [lefts right? it]}
    (%.format "{" (%.nat lefts) " " (%.bit right?) " " (%it it) "}")
    
    {#Tuple it}
    (|> it
        (list#each %it)
        (text.interposed " ")
        (text.enclosed ["[" "]"]))))
