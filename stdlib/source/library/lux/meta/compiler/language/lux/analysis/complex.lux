... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Tag Tuple Variant
                #Variant #Tuple)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text (.only)
     ["%" \\format (.only Format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    [number
     ["n" nat]]]]])

(every .public (Variant a)
  (Record
   [#lefts Nat
    #right? Bit
    #value a]))

(every .public (Tuple a)
  (List a))

(every .public (Complex a)
  (.Variant
   {#Variant (Variant a)}
   {#Tuple (Tuple a)}))

(every .public Tag
  Nat)

(def .public (tag right? lefts)
  (-> Bit Nat Tag)
  (if right?
    (++ lefts)
    lefts))

(def .public (lefts right? tag)
  (-> Bit Tag Nat)
  (if right?
    (-- tag)
    tag))

(def .public (choice multiplicity pick)
  (-> Nat Tag [Nat Bit])
  (let [right? (n.= (-- multiplicity) pick)]
    [(..lefts right? pick)
     right?]))

(def .public (equivalence (open "/#[0]"))
  (All (_ a) (-> (Equivalence a) (Equivalence (Complex a))))
  (implementation
   (def (= reference sample)
     (when [reference sample]
       [{#Variant [reference_lefts reference_right? reference_value]}
        {#Variant [sample_lefts sample_right? sample_value]}]
       (and (n.= reference_lefts sample_lefts)
            (bit#= reference_right? sample_right?)
            (/#= reference_value sample_value))
       
       [{#Tuple reference} {#Tuple sample}]
       (of (list.equivalence /#=) = reference sample)

       _
       false))))

(def .public (hash super)
  (All (_ a) (-> (Hash a) (Hash (Complex a))))
  (implementation
   (def equivalence
     (..equivalence (of super equivalence)))
   
   (def (hash value)
     (when value
       {#Variant [lefts right? value]}
       (all n.* 2
            (of n.hash hash lefts)
            (of bit.hash hash right?)
            (of super hash value))
       
       {#Tuple members}
       (all n.* 3
            (of (list.hash super) hash members))
       ))))

(def .public (format %it it)
  (All (_ a) (-> (Format a) (Format (Complex a))))
  (when it
    {#Variant [lefts right? it]}
    (%.format "{" (%.nat lefts) " " (%.bit right?) " " (%it it) "}")
    
    {#Tuple it}
    (|> it
        (list#each %it)
        (text.interposed " ")
        (text.enclosed ["[" "]"]))))
