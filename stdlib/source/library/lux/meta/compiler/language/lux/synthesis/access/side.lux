... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [data
    ["[0]" product]
    ["[0]" bit]
    [text
     ["%" \\format]]]
   [math
    [number
     ["[0]" nat]]]]])

(type .public Side
  (Record
   [#lefts Nat
    #right? Bit]))

(def .public (format it)
  (%.Format Side)
  (%.format "{" (%.nat (the #lefts it)) " " (%.bit (the #right? it)) "}"))

(def .public hash
  (Hash Side)
  (all product.hash
       nat.hash
       bit.hash
       ))

(def .public equivalence
  (Equivalence Side)
  (of ..hash equivalence))
