(.require
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["[0]" function]]
   [data
    ["[0]" product]
    [number
     ["f" frac]]
    [collection
     ["[0]" dictionary]]]
   [meta
    [target
     ["_" common_lisp (.only Expression)]]]]]
 ["[0]" ///
  ["[1][0]" runtime (.only Operation Phase Handler Bundle)]
  ["[1][0]" primitive]
  [//
   [extension (.only Nullary Unary Binary Trinary
                     nullary unary binary trinary)]
   [//
    [extension
     ["[0]" bundle]]]]])

(def lux_procs
  Bundle
  (|> bundle.empty
      (bundle.install "is" (binary (product.uncurried _.eq)))
      (bundle.install "try" (unary ///runtime.lux//try))))

(def (i64//left_shifted [paramG subjectG])
  (Binary (Expression Any))
  (_.ash (_.rem (_.int +64) paramG) subjectG))

(def (i64//arithmetic_right_shifted [paramG subjectG])
  (Binary (Expression Any))
  (_.ash (|> paramG (_.rem (_.int +64)) (_.* (_.int -1)))
         subjectG))

(def (i64//logic_right_shifted [paramG subjectG])
  (Binary (Expression Any))
  (///runtime.i64//logic_right_shifted (_.rem (_.int +64) paramG) subjectG))

(def i64_procs
  Bundle
  (<| (bundle.prefix "i64")
      (|> bundle.empty
          (bundle.install "and" (binary (product.uncurried _.logand)))
          (bundle.install "or" (binary (product.uncurried _.logior)))
          (bundle.install "xor" (binary (product.uncurried _.logxor)))
          (bundle.install "left-shift" (binary i64//left_shifted))
          (bundle.install "logical-right-shift" (binary i64//logic_right_shifted))
          (bundle.install "arithmetic-right-shift" (binary i64//arithmetic_right_shifted))
          (bundle.install "=" (binary (product.uncurried _.=)))
          (bundle.install "<" (binary (product.uncurried _.<)))
          (bundle.install "+" (binary (product.uncurried _.+)))
          (bundle.install "-" (binary (product.uncurried _.-)))
          (bundle.install "*" (binary (product.uncurried _.*)))
          (bundle.install "/" (binary (product.uncurried _.floor)))
          (bundle.install "%" (binary (product.uncurried _.rem)))
          (bundle.install "f64" (unary (function (_ value)
                                         (_.coerce/2 [value (_.symbol "double-float")]))))
          (bundle.install "char" (unary (|>> _.code_char/1 _.string/1)))
          )))

(def f64_procs
  Bundle
  (<| (bundle.prefix "f64")
      (|> bundle.empty
          (bundle.install "+" (binary (product.uncurried _.+)))
          (bundle.install "-" (binary (product.uncurried _.-)))
          (bundle.install "*" (binary (product.uncurried _.*)))
          (bundle.install "/" (binary (product.uncurried _./)))
          (bundle.install "%" (binary (product.uncurried _.mod)))
          (bundle.install "=" (binary (product.uncurried _.=)))
          (bundle.install "<" (binary (product.uncurried _.<)))
          (bundle.install "i64" (unary _.floor/1))
          (bundle.install "encode" (unary _.write_to_string/1))
          (bundle.install "decode" (unary (let [@temp (_.var "temp")]
                                            (function (_ input)
                                              (_.let (list [@temp (_.read_from_string/1 input)])
                                                (_.if (_.equal (_.symbol "DOUBLE-FLOAT")
                                                               (_.type_of/1 @temp))
                                                  (///runtime.some @temp)
                                                  ///runtime.none)))))))))

(def (text//< [paramG subjectG])
  (Binary (Expression Any))
  (|> (_.string< paramG subjectG)
      _.null/1
      _.not/1))

(def (text//clip [paramO extraO subjectO])
  (Trinary (Expression Any))
  (///runtime.text//clip subjectO paramO extraO))

(def (text//index [startO partO textO])
  (Trinary (Expression Any))
  (///runtime.text//index textO partO startO))

(def text_procs
  Bundle
  (<| (bundle.prefix "text")
      (|> bundle.empty
          (bundle.install "=" (binary (product.uncurried _.string=)))
          (bundle.install "<" (binary text//<))
          (bundle.install "concat" (binary _.concatenate/2|string))
          (bundle.install "index" (trinary text//index))
          (bundle.install "size" (unary _.length/1))
          (bundle.install "char" (binary (|>> _.char/2 _.char_int/1)))
          (bundle.install "clip" (trinary text//clip))
          )))

(def (void code)
  (-> (Expression Any) (Expression Any))
  (all _.progn
       code
       ///runtime.unit))

(def io_procs
  Bundle
  (<| (bundle.prefix "io")
      (|> bundle.empty
          (bundle.install "log" (unary (|>> _.print/1 ..void)))
          (bundle.install "error" (unary _.error/1))
          )))

(def .public bundle
  Bundle
  (<| (bundle.prefix "lux")
      (|> lux_procs
          (dictionary.composite i64_procs)
          (dictionary.composite f64_procs)
          (dictionary.composite text_procs)
          (dictionary.composite io_procs)
          )))
