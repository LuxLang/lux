(.require
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["<>" parser]
    ["|" pipe]
    ["[0]" function]
    ["[0]" try]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\format (.only format)]]
    [collection
     ["[0]" dictionary]
     ["[0]" list (.use "[1]#[0]" functor mix)]]]
   [math
    [number
     ["f" frac]]]
   [meta
    [macro
     ["^" pattern]]
    [target
     ["_" python (.only Expression Statement)]]
    [compiler
     [meta
      [archive (.only Archive)]]]]]]
 [/////
  ["[0]" extension]
  [translation
   ["[0]" reference]
   [extension (.only Nullary Unary Binary Trinary Variadic
                     nullary unary binary trinary variadic)]
   ["//" python
    ["[1][0]" runtime (.only Operation Phase Phase! Handler Bundle Translator)]
    ["[1][0]" primitive]
    ["[1][0]" structure]
    ["[1][0]" reference]
    ["[1][0]" function]
    ["[1][0]" when]
    ["[1][0]" loop]]]
  [//
   [analysis (.only)]
   ["[0]" translation]
   ["[0]" synthesis (.only %synthesis)
    ["<[1]>" \\parser (.only Parser)]]
   [///
    ["[0]" phase (.use "[1]#[0]" monad)]]]])

(def .public (custom [parser handler])
  (All (_ s)
    (-> [(Parser s)
         (-> Phase Archive s (Operation (Expression Any)))]
        Handler))
  (function (_ phase archive input)
    (when (<synthesis>.result parser input)
      {try.#Success input'}
      (handler phase archive input')

      {try.#Failure error}
      (phase.failure error))))

(def .public (statement expression archive synthesis)
  Phase!
  (when synthesis
    ... TODO: Get rid of this ASAP
    [@ {synthesis.#Extension [.prelude "when_char#|translation"] parameters}]
    (do phase.monad
      [body (expression archive synthesis)]
      (in (as (Statement Any) body)))
    
    (^.with_template [<tag>]
      [(<tag> @ value)
       (phase#each _.return (expression archive synthesis))])
    ([synthesis.bit]
     [synthesis.i64]
     [synthesis.f64]
     [synthesis.text]
     [synthesis.variant]
     [synthesis.tuple]
     [synthesis.branch/get]
     [synthesis.function/apply])

    (^.with_template [<tag>]
      [[@ {<tag> value}]
       (phase#each _.return (expression archive synthesis))])
    ([synthesis.#Reference]
     [synthesis.#Extension])

    (synthesis.branch/when @ when)
    (//when.when! false statement expression archive when)

    (^.with_template [<tag> <translator>]
      [(<tag> @ value)
       (<translator> statement expression archive value)])
    ([synthesis.branch/exec //when.exec!]
     [synthesis.branch/let //when.let!]
     [synthesis.branch/if //when.if!]
     [synthesis.loop/scope //loop.scope!]
     [synthesis.loop/again //loop.again!])

    (synthesis.function/abstraction @ abstraction)
    (phase#each _.return (//function.function statement expression archive abstraction))
    ))

... TODO: Get rid of this ASAP
(def lux::syntax_char_case!
  (..custom [(all <>.and
                  <synthesis>.any
                  <synthesis>.any
                  (<>.some (<synthesis>.tuple (all <>.and
                                                   (<synthesis>.tuple (<>.many <synthesis>.i64))
                                                   <synthesis>.any))))
             (function (_ phase archive [input else conditionals])
               (do [! phase.monad]
                 [inputG (phase archive input)
                  else! (..statement phase archive else)
                  @input (of ! each _.var (translation.symbol "input"))
                  conditionals! (is (Operation (List [(Expression Any)
                                                      (Statement Any)]))
                                    (monad.each ! (function (_ [chars branch])
                                                    (do !
                                                      [branch! (..statement phase archive branch)]
                                                      (in [(|> chars
                                                               (list#each (|>> .int _.int (_.= @input)))
                                                               (list#mix (function (_ clause total)
                                                                           (if (same? _.none total)
                                                                             clause
                                                                             (_.or clause total)))
                                                                         _.none))
                                                           branch!])))
                                                conditionals))
                  ... .let [dependencies (//case.dependencies (list#mix (function (_ right left)
                  ...                                                      (synthesis.path/seq left right))
                  ...                                                    (synthesis.path/then input)
                  ...                                                    {.#Item (synthesis.path/then else)
                  ...                                                     (list#each (|>> product.right
                  ...                                                                     synthesis.path/then)
                  ...                                                                conditionals)}))
                  ...       @closure (_.var (reference.artifact artifact_id))
                  ...       closure (_.def @closure dependencies
                  ...                      (all _.then
                  ...                          (_.set (list @input) inputG)
                  ...                          (list#mix (function (_ [test then!] else!)
                  ...                                       (_.if test then! else!))
                  ...                                     else!
                  ...                                     conditionals!)))]
                  ... _ (translation.execute! closure)
                  ... _ (translation.save! (product.right artifact_id) {.#None} closure)
                  ]
                 ... (in (_.apply @closure dependencies))
                 (in (<| (as (Expression Any))
                         (is (Statement Any))
                         (all _.then
                              (_.set (list @input) inputG)
                              (list#mix (function (_ [test then!] else!)
                                          (_.if test then! else!))
                                        else!
                                        conditionals!))))))]))

(def with_basic_extensions
  (-> Bundle Bundle)
  (|>> (dictionary.has "when_char#|translation" lux::syntax_char_case!)
       (dictionary.has "is?#|translation" (binary (product.uncurried _.is)))
       (dictionary.has "try#|translation" (unary //runtime.lux::try))))

(def (capped operation parameter subject)
  (-> (-> (Expression Any) (Expression Any) (Expression Any))
      (-> (Expression Any) (Expression Any) (Expression Any)))
  (//runtime.i64::64 (operation parameter subject)))

(def with_i64_extensions
  (-> Bundle Bundle)
  (|>> (dictionary.has "i64_and#|translation" (binary (product.uncurried //runtime.i64::and)))
       (dictionary.has "i64_or#|translation" (binary (product.uncurried //runtime.i64::or)))
       (dictionary.has "i64_xor#|translation" (binary (product.uncurried //runtime.i64::xor)))
       (dictionary.has "i64_left#|translation" (binary (product.uncurried //runtime.i64::left_shifted)))
       (dictionary.has "i64_right#|translation" (binary (product.uncurried //runtime.i64::right_shifted)))

       (dictionary.has "i64_=#|translation" (binary (product.uncurried _.=)))
       (dictionary.has "i64_+#|translation" (binary (product.uncurried (..capped _.+))))
       (dictionary.has "i64_-#|translation" (binary (product.uncurried (..capped _.-))))
       ))

(def with_int_extensions
  (-> Bundle Bundle)
  (|>> (dictionary.has "int_<#|translation" (binary (product.uncurried _.<)))
       (dictionary.has "int_*#|translation" (binary (product.uncurried (..capped _.*))))
       (dictionary.has "int_/#|translation" (binary (product.uncurried //runtime.i64#/)))
       (dictionary.has "int_%#|translation" (binary (product.uncurried //runtime.i64::remainder)))
       
       (dictionary.has "int_f64#|translation" (unary _.float/1))
       (dictionary.has "int_char#|translation" (unary //runtime.i64::char))
       ))

(def with_frac_extensions
  (-> Bundle Bundle)
  (|>> (dictionary.has "f64_+#|translation" (binary (product.uncurried _.+)))
       (dictionary.has "f64_-#|translation" (binary (product.uncurried _.-)))
       (dictionary.has "f64_*#|translation" (binary (product.uncurried _.*)))
       (dictionary.has "f64_/#|translation" (binary (product.uncurried //runtime.f64::/)))
       (dictionary.has "f64_%#|translation" (binary (function (_ [parameter subject])
                                                      (|> (_.__import__/1 (_.unicode "math"))
                                                          (_.do "fmod" (list subject parameter))))))
       (dictionary.has "f64_=#|translation" (binary (product.uncurried _.=)))
       (dictionary.has "f64_<#|translation" (binary (product.uncurried _.<)))
       (dictionary.has "f64_int#|translation" (unary _.int/1))
       (dictionary.has "f64_encoded#|translation" (unary _.repr/1))
       (dictionary.has "f64_decoded#|translation" (unary //runtime.f64::decode))))

(def (text::clip [paramO extraO subjectO])
  (Trinary (Expression Any))
  (//runtime.text::clip paramO extraO subjectO))

(def (text::index [startO partO textO])
  (Trinary (Expression Any))
  (//runtime.text::index startO partO textO))

(def text::composite
  (Variadic (Expression Any))
  (|>> (|.when (list)
         (_.string "")

         (list single)
         single

         (list left right)
         (|> left (_.+ right))

         parts
         (_.do "join" (list (_.list parts)) (_.string "")))))

(def with_text_extensions
  (-> Bundle Bundle)
  (|>> (dictionary.has "text_=#|translation" (binary (product.uncurried _.=)))
       (dictionary.has "text_<#|translation" (binary (product.uncurried _.<)))
       (dictionary.has "text_composite#|translation" (variadic ..text::composite))
       (dictionary.has "text_index#|translation" (trinary ..text::index))
       (dictionary.has "text_size#|translation" (unary _.len/1))
       (dictionary.has "text_char#|translation" (binary (product.uncurried //runtime.text::char)))
       (dictionary.has "text_clip#|translation" (trinary ..text::clip))
       ))

(def with_io_extensions
  (-> Bundle Bundle)
  (|>> (dictionary.has "log!#|translation" (unary //runtime.io::log!))
       (dictionary.has "error#|translation" (unary //runtime.io::throw!))))

(def .public bundle
  Bundle
  (|> extension.empty
      with_basic_extensions
      with_i64_extensions
      with_int_extensions
      with_frac_extensions
      with_text_extensions
      with_io_extensions
      ))
