... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Label
                function)
   [abstract
    ["[0]" monad (.only do)]]
   [data
    ["[0]" product]
    [text
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" monoid functor mix)]]]
   [math
    [number
     ["n" natural]]]
   [meta
    [compiler
     [target
      ["_" lua (.only Var Expression Label Statement)]]]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Phase! Translator)]
  ["[1][0]" reference]
  ["[1][0]" when]
  ["/[1]" //
   ["[1][0]" reference]
   ["//[1]" ///
    [analysis (.only Abstraction Reification)]
    ["[0]" phase (.use "[1]#[0]" monad)]
    ["[0]" synthesis (.only Apply)]
    ["[0]" translation]
    ["//[1]" ///
     [arity (.only Arity)]
     [meta
      [archive
       ["[0]" unit]]
      ["[0]" cache
       [dependency
        ["[1]" artifact]]]]
     [reference
      [register (.only Register)]]]]]])

(the capture
  (-> Register Var)
  (|>> (///reference.foreign //reference.system) as_expected))

(the (procedure_id abstraction_id)
  (-> unit.ID
      unit.ID)
  [unit.#module (unit.module abstraction_id)
   unit.#artifact (++ (unit.artifact abstraction_id))])

(the (procedure environment name arity @scope body)
  (-> (List //runtime.Value) unit.ID Arity Label Statement
      Statement)
  (_.function (_.var (///reference.artifact name))
    (all list#composite
         (|> (list.enumeration environment)
             (list#each (|>> product.left ..capture)))
         (list (//when.register 0))
         (list#each (|>> ++ //when.register)
                    (list.indices arity))
         )
    (all _.then
         (_.set_label @scope)
         body)))

(the (arbitrary_reification' expression archive [abstraction parameters])
  (Translator [//runtime.Value (List synthesis.Term)])
  (do [! phase.monad]
    [parameters (monad.each ! (expression archive) parameters)]
    (in (_.apply parameters abstraction))))

(the (arbitrary_reification expression archive [abstraction parameters])
  (Translator Apply)
  (do phase.monad
    [abstraction (expression archive abstraction)]
    (arbitrary_reification' expression archive [abstraction parameters])))

(the (exact_reification expression archive [abstraction arity parameters])
  (Translator [unit.ID Arity (List synthesis.Term)])
  (do [! phase.monad]
    [parameters (monad.each ! (expression archive) parameters)]
    (in (_.apply {.#Item (_.var (///reference.artifact abstraction))
                         parameters}
                 (_.var (///reference.artifact (..procedure_id abstraction)))))))

(the (successive_reification expression archive [abstraction arity parameters])
  (Translator [unit.ID Arity (List synthesis.Term)])
  (do [! phase.monad]
    [first (exact_reification expression archive [abstraction arity (list.first arity parameters)])]
    (arbitrary_reification' expression archive [first (list.after arity parameters)])))

(the .public (apply expression archive [abstraction parameters])
  (Translator Apply)
  (when abstraction
    (synthesis.constant @ $abstraction)
    (do [! phase.monad]
      [[@definition |abstraction|] (translation.definition archive $abstraction)
       .let [actual_arity (list.size parameters)]]
      (when |abstraction|
        {.#Some [_ {.#Some [expected_arity @abstraction]}]}
        (if (n.= expected_arity actual_arity)
          (exact_reification expression archive [@abstraction expected_arity parameters])

          (n.< expected_arity actual_arity)
          (arbitrary_reification expression archive [abstraction parameters])
          
          ... (n.> expected_arity actual_arity)
          (successive_reification expression archive [@abstraction expected_arity parameters]))
        
        _
        (arbitrary_reification expression archive [abstraction parameters])))

    _
    (arbitrary_reification expression archive [abstraction parameters])))

(the (with_closure inits @self @args body!)
  (-> (List Expression) Var (List Var) Statement [Statement Expression])
  (when inits
    {.#End}
    [(_.function @self @args body!)
     @self]

    _
    (let [@inits (|> (list.enumeration inits)
                     (list#each (|>> product.left ..capture)))]
      [(_.function @self @inits
         (all _.then
              (_.local_function @self @args body!)
              (_.return @self)))
       (_.apply inits @self)])))

(the input
  (|>> ++ //when.register))

(the (@scope abstraction_id)
  (-> unit.ID Label)
  (_.label (%.message (///reference.artifact abstraction_id) "_scope")))

(the .public (function statement expression archive [environment arity bodyS])
  (-> Phase! (Translator (Abstraction synthesis.Term)))
  (do [! phase.monad]
    [dependencies (cache.dependencies archive bodyS)
     [abstraction_id procedure_id body!] (<| (translation.with_new_context archive dependencies)
                                             (translation.with_new_context archive dependencies)
                                             (do !
                                               [@scope (of ! each ..@scope
                                                           (translation.context archive))]
                                               (translation.with_anchor [1 @scope]
                                                 (statement expression archive bodyS))))
     environment (monad.each ! (expression archive) environment)
     .let [@curried (_.var "curried")
           arityO (|> arity .integer _.int)
           @num_args (_.var "num_args")
           @scope (..@scope procedure_id)
           @self (_.var (///reference.artifact abstraction_id))
           initialize_self! (_.local/1 (//when.register 0) @self)
           initialize! (list#mix (.function (_ post pre!)
                                   (all _.then
                                        pre!
                                        (_.local/1 (..input post) (_.item (|> post ++ .integer _.int) @curried))))
                                 initialize_self!
                                 (list.indices arity))
           pack (|>> (list) _.array)
           unpack (is (-> Expression Expression)
                      (.function (_ it)
                        (_.apply (list it) (_.var "table.unpack"))))
           @var_args (_.var "...")]
     _ (when environment
         (list)
         (do !
           [.let [procedure_definition (..procedure environment procedure_id arity @scope body!)]
            _ (translation.execute! procedure_definition)]
           (translation.save! (unit.artifact procedure_id) {.#None} procedure_definition))

         else
         (in []))
     .let [[definition instantiation] (with_closure environment @self (list @var_args)
                                        (all _.then
                                             (_.local/1 @curried (pack @var_args))
                                             (_.local/1 @num_args (_.length @curried))
                                             (<| (_.if (|> @num_args (_.= arityO))
                                                   (all _.then
                                                        initialize!
                                                        (_.set_label @scope)
                                                        body!))
                                                 (_.if (|> @num_args (_.< arityO))
                                                   (_.return (_.closure (list @var_args)
                                                                        (let [@extra_args (_.var "extra_args")]
                                                                          (all _.then
                                                                               (_.local/1 @extra_args (pack @var_args))
                                                                               (_.return (_.apply (list (unpack (_.apply (list @extra_args
                                                                                                                               (_.int +1)
                                                                                                                               (_.length @extra_args)
                                                                                                                               (_.+ (_.int +1) @num_args)
                                                                                                                               (_.apply (list @curried
                                                                                                                                              (_.int +1)
                                                                                                                                              @num_args
                                                                                                                                              (_.int +1)
                                                                                                                                              (_.array (list)))
                                                                                                                                        (_.var "table.move")))
                                                                                                                         (_.var "table.move"))))
                                                                                                  @self)))))))
                                                 ... (|> @num_args (_.> arityO))
                                                 (let [arity_inputs (_.apply (list @curried
                                                                                   (_.int +1)
                                                                                   arityO
                                                                                   (_.int +1)
                                                                                   (_.array (list)))
                                                                             (_.var "table.move"))
                                                       extra_inputs (_.apply (list @curried
                                                                                   (_.+ (_.int +1) arityO)
                                                                                   @num_args
                                                                                   (_.int +1)
                                                                                   (_.array (list)))
                                                                             (_.var "table.move"))]
                                                   (_.return (|> @self
                                                                 (_.apply (list (unpack arity_inputs)))
                                                                 (_.apply (list (unpack extra_inputs)))))))
                                             ))]
     _ (translation.execute! definition)
     _ (translation.save! (product.right abstraction_id) {.#None} definition)]
    (in instantiation)))
