(.require
 [library
  [lux (.except #Bit #Text)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [control
    ["[0]" pipe]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\format]]]
   [math
    [number
     ["[0]" i64 (.use "[1]#[0]" equivalence)]
     ["n" nat]
     ["i" int]
     ["f" frac]]]
   [meta
    [macro
     ["^" pattern]]]]])

(type .public Simple
  (Variant
   {#Bit Bit}
   {#I64 I64}
   {#F64 Frac}
   {#Text Text}))

(def .public (format it)
  (%.Format Simple)
  (when it
    (^.with_template [<pattern> <format>]
      [{<pattern> value}
       (<format> value)])
    ([#Bit  %.bit]
     [#F64  %.frac]
     [#Text %.text])
    
    {#I64 value}
    (%.int (.int value))))

(def .public equivalence
  (Equivalence Simple)
  (implementation
   (def (= reference sample)
     (when [reference sample]
       (^.with_template [<tag> <eq> <format>]
         [[{<tag> reference'} {<tag> sample'}]
          (<eq> reference' sample')])
       ([#Bit  bit#=  %.bit]
        [#F64  f.=    %.frac]
        [#Text text#= %.text])

       [{#I64 reference'} {#I64 sample'}]
       (i64#= reference' sample')

       _
       false))))

(def .public hash
  (Hash Simple)
  (implementation
   (def equivalence ..equivalence)

   (def hash
     (|>> (pipe.when
            (^.with_template [<factor> <tag> <hash>]
              [{<tag> value'}
               (n.* <factor>  (of <hash> hash value'))])
            ([2 #Bit  bit.hash]
             [3 #F64  f.hash]
             [5 #Text text.hash]
             [7 #I64  i64.hash]))))))
