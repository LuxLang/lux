... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["[0]" function]
    ["[0]" try]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list]]]
   [math
    [number
     ["n" natural]]]
   ["[0]" meta (.only)
    ["[0]" module]
    ["[0]" symbol]
    [macro
     ["^" pattern]]
    ["[0]" type (.only)
     ["[0]" check (.only Check)]]]]]
 ["/" // (.only Operation)
  [//
   ["[0]" phase]]])

(the .public (check action)
  (for_any (_ a) (-> (Check a) (Operation a)))
  (function (_ state)
    (when (action (its .#type_context state))
      {try.#Success [context' output]}
      {try.#Success [(has .#type_context context' state)
                     output]}

      {try.#Failure error}
      ((/.failure error) state))))

(the prefix
  (%.message (symbol.as_text (symbol ..type)) "#"))

(the .public (existential? type)
  (-> Type Bit)
  (when type
    {.#Nominal actual {.#End}}
    (text.starts_with? ..prefix actual)

    _
    false))

(the (existential' module id)
  (-> Text Natural Type)
  {.#Nominal (%.message ..prefix module "#" (%.natural id)) (list)})

(the .public existential
  (Operation Type)
  (do phase.monad
    [[module _] module.current
     id meta.seed]
    (in (..existential' module id))))

(the .public (expecting expected)
  (for_any (_ a) (-> Type (Operation a) (Operation a)))
  (phase.localized (its .#expected) (has .#expected)
                   (function.constant {.#Some expected})))

(the .public fresh
  (for_any (_ a) (-> (Operation a) (Operation a)))
  (phase.localized (its .#type_context) (has .#type_context)
                   (function.constant check.fresh_context)))

(the .public (inference actualT)
  (-> Type (Operation Any))
  (do phase.monad
    [[module _] module.current
     expectedT meta.expected_type]
    (..check (check.check expectedT actualT)
             ... (do [! check.monad]
             ...   [pre check.context
             ...    it (check.check expectedT actualT)
             ...    post check.context
             ...    .let [pre#var_counter (its .#var_counter pre)]]
             ...   (if (n.< (its .#var_counter post)
             ...            pre#var_counter)
             ...     (do !
             ...       [.let [new! (is (-> [Natural (Maybe Type)] (Maybe Natural))
             ...                      (function (_ [id _])
             ...                        (if (n.< id pre#var_counter)
             ...                          {.#Some id}
             ...                          {.#None})))
             ...              new_vars (|> post
             ...                           (its .#var_bindings)
             ...                           (list.all new!))]
             ...        _ (monad.each ! (function (_ @new)
             ...                          (do !
             ...                            [:new: (check.try (check.identity new_vars @new))]
             ...                            (when :new:
             ...                              {try.#Success :new:}
             ...                              (in :new:)
             
             ...                              {try.#Failure error}
             ...                              (do !
             ...                                [[id _] check.existential
             ...                                 .let [:new: (..existential' module id)]
             ...                                 _ (check.bind :new: @new)]
             ...                                (in :new:)))))
             ...                      new_vars)
             ...        expectedT' (check.clean new_vars expectedT)
             ...        _ (check.with pre)]
             ...       (check.check expectedT' actualT))
             ...     (in it)))
             )))

(the .public (with_var it)
  (for_any (_ a)
    (-> (-> [type.Variable Type] (Operation a))
        (Operation a)))
  (do phase.monad
    [@it,:it: (..check check.var)
     it (it @it,:it:)
     .let [[@it :it:] @it,:it:]
     _ (..check (check.forget! @it))]
    (in it)))

(the .public (inferring action)
  (for_any (_ a)
    (-> (Operation a)
        (Operation [Type a])))
  (<| ..with_var
      (function (_ [@it :it:]))
      (do phase.monad
        [it (..expecting :it: action)
         :it: (..check (check.identity (list) @it))]
        (in [:it: it]))))
