... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Tuple Variant Pattern
                #Function #Apply
                natural integer revolution when local except variant tuple)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    [monad (.only do)]]
   [control
    ["[0]" function]
    ["[0]" maybe]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix)]]]
   [math
    [number
     ["n" natural]
     ["i" integer]
     ["r" revolution]]]
   [meta
    ["[0]" location]
    ["[0]" configuration (.only Configuration)]
    ["[0]" symbol]
    ["[0]" type]
    ["[0]" code
     ["<[1]>" \\projection]]
    [macro
     ["[0]" syntax]
     ["[0]" expansion]
     ["[0]" template]]]]]
 ["[0]" /
  ["[1][0]" simple (.only Simple)]
  ["[1][0]" complex (.only Tuple Variant Complex)]
  ["[1][0]" pattern (.only Pattern)]
  [//
   ["[0]" phase (.only)
    ["[0]" extension (.only Extension)]]
   [///
    [arity (.only Arity)]
    ["[0]" version (.only Version)]
    ["[0]" reference (.only Reference)
     ["[0]" variable (.only Variable)]]
    [meta
     [archive (.only Archive)]]]]])

(every .public (Branch' e)
  (Record
   [#when Pattern
    #then e]))

(every .public (Match' e)
  [(Branch' e) (List (Branch' e))])

(every .public (Environment a)
  (List a))

(expansion.let [@ ($ (Term' $))]
  (every .public (Term' $)
    (.Variant
     {#Simple Simple}
     {#Structure (Complex @)}
     {#Reference Reference}
     {#When @ (Match' @)}
     {#Function (Environment @) @}
     {#Apply @ @}
     {#Extension (Extension @)})))

(every .public Term
  (Ann Location
       (Term' (Ann Location))))

(every .public Branch
  (Branch' Term))

(every .public Match
  (Match' Term))

(the (branch_equivalence equivalence)
  (-> (Equivalence Term)
      (Equivalence Branch))
  (implementation
   (the (= [expected_pattern expected_body] [actual_pattern actual_body])
     (and (of /pattern.equivalence = expected_pattern actual_pattern)
          (of equivalence = expected_body actual_body)))))

(the .public equivalence
  (Equivalence Term)
  (implementation
   (the (= [_ expected] [_ actual])
     (.when [expected actual]
       [{#Simple expected} {#Simple actual}]
       (of /simple.equivalence = expected actual)

       [{#Structure expected} {#Structure actual}]
       (of (/complex.equivalence =) = expected actual)

       [{#Reference expected} {#Reference actual}]
       (of reference.equivalence = expected actual)

       [{#When [expected_analysis expected_match]}
        {#When [actual_analysis actual_match]}]
       (and (= expected_analysis actual_analysis)
            (of (list.equivalence (branch_equivalence =)) = {.#Item expected_match} {.#Item actual_match}))

       [{#Function [expected_environment expected_analysis]}
        {#Function [actual_environment actual_analysis]}]
       (and (= expected_analysis actual_analysis)
            (of (list.equivalence =) = expected_environment actual_environment))

       [{#Apply [expected_input expected_abstraction]}
        {#Apply [actual_input actual_abstraction]}]
       (and (= expected_input actual_input)
            (= expected_abstraction actual_abstraction))

       [{#Extension expected} {#Extension actual}]
       (of (extension.equivalence =) = expected actual)

       _
       false))))

(alias [=]
       ..equivalence)

(template.with [<name> <tag>]
  [(the .public <name>
     (template.macro (<name> location content)
       [[location {<tag> content}]]))]

  [when ..#When]
  )

(the .public unit
  (template.macro (unit location)
    [[location {..#Simple {/simple.#Unit}}]]))

(template.with [<name> <tag>]
  [(the .public <name>
     (template.macro (<name> location value)
       [[location {..#Simple {<tag> value}}]]))]

  [bit /simple.#Bit]
  [natural /simple.#Natural]
  [integer /simple.#Integer]
  [revolution /simple.#Revolution]
  [decimal /simple.#Decimal]
  [text /simple.#Text]
  )

(every .public (Abstraction c)
  [(Environment c) Arity c])

(every .public (Reification c)
  [c (List c)])

(the .public no_op
  (template.macro (no_op @ value)
    [(|> 1
         {variable.#Local}
         {reference.#Variable}
         {..#Reference} [@]
         {..#Function (list)} [@]
         {..#Apply value} [@])]))

(the .public (reified [[@ abstraction] inputs])
  (-> (Reification Term)
      Term)
  (list#mix (function (_ input abstraction')
              [@ {#Apply input abstraction'}])
            [@ abstraction]
            inputs))

(the .public (reification analysis)
  (-> Term
      (Reification Term))
  (loop (again [[@ abstraction] analysis
                inputs (is (List Term)
                           (list))])
    (.when abstraction
      {#Apply input next}
      (again next {.#Item input inputs})

      _
      [[@ abstraction] inputs])))

(template.with [<name> <tag>]
  [(the .public <name>
     (syntax.macro (_ [location <code>.any
                       content <code>.any])
       (in (list (` (.<| [(, location)]
                         {..#Reference}
                         <tag>
                         (, content)))))))]

  [variable {reference.#Variable}]
  [constant {reference.#Constant}]

  [local    reference.local]
  [foreign  reference.foreign]
  )

(template.with [<name> <tag>]
  [(the .public <name>
     (template.macro (<name> location content)
       [(.<| [location]
             {..#Structure}
             {<tag>}
             content)]))]

  [variant /complex.#Variant]
  [tuple   /complex.#Tuple]
  )

(the .public (injection [@ analysis])
  (text.Injection Term)
  (.when analysis
    {#Simple it}
    (/simple.injection it)
    
    {#Structure it}
    (/complex.injection injection it)
    
    {#Reference it}
    (reference.injection it)
    
    {#When analysis match}
    (%.message "({"
               (|> {.#Item match}
                   (list#each (function (_ [when then])
                                (%.message (/pattern.injection when) " " (injection then))))
                   (text.interposed " "))
               "} "
               (injection analysis)
               ")")
    
    {#Function environment body}
    (|> (injection body)
        (%.message " ")
        (%.message (|> environment
                       (list#each injection)
                       (text.interposed " ")
                       (text.enclosed ["[" "]"])))
        (text.enclosed ["(" ")"]))
    
    {#Apply _}
    (|> [@ analysis]
        ..reification
        {.#Item}
        (list#each injection)
        (text.interposed " ")
        (text.enclosed ["(" ")"]))
    
    {#Extension name parameters}
    (|> parameters
        (list#each injection)
        (text.interposed " ")
        (%.message (symbol.as_text name) " ")
        (text.enclosed ["(" ")"]))))

(every .public State
  Lux)

(every .public Operation
  (phase.Operation State))

(every .public Phase
  (phase.Phase State Code Term))

(template.with [<special> <general>]
  [(every .public <special>
     (<general> State Code Term))]

  [Handler   extension.Handler]
  [Bundle    extension.Bundle]
  [Extender  extension.Extender]
  )

(the .public (with_source_code source action)
  (for_any (_ of)
    (-> Source (Operation of)
        (Operation of)))
  (function (_ state)
    (let [old_source (its .#source state)]
      (.when (action (has .#source source state))
        {try.#Success [state' output]}
        {try.#Success [(has .#source old_source state')
                       output]}

        failure
        failure))))

(the .public (with_current_module name)
  (for_any (_ of)
    (-> Text (Operation of)
        (Operation of)))
  (phase.localized (its .#current_module)
                   (has .#current_module)
                   (function.constant {.#Some name})))

(the .public (with_location location action)
  (for_any (_ of)
    (-> Location (Operation of)
        (Operation of)))
  (if (text.= "" (product.left location))
    action
    (function (_ state)
      (let [old_location (its .#location state)]
        (.when (action (has .#location location state))
          {try.#Success [state' output]}
          {try.#Success [(has .#location old_location state')
                         output]}

          failure
          failure)))))

(the (located location error)
  (-> Location Text
      Text)
  (%.message (location.as_text location) text.new_line
             error))

(the .public (failure error)
  (-> Text
      Operation)
  (function (_ state)
    {try.#Failure (located (its .#location state) error)}))

(the .public (of_try it)
  (for_any (_ of)
    (-> (Try of)
        (Operation of)))
  (function (_ state)
    (.when it
      {try.#Failure error}
      {try.#Failure (located (its .#location state) error)}

      {try.#Success it}
      {try.#Success [state it]})))

(the .public (except exception parameters)
  (for_any (_ ex)
    (-> (Exception ex) ex
        Operation))
  (..failure (exception.error exception parameters)))

(the .public (assertion exception parameters condition)
  (for_any (_ ex)
    (-> (Exception ex) ex Bit
        (Operation Any)))
  (if condition
    (of phase.monad in [])
    (..except exception parameters)))

(the .public (with_exception exception message action)
  (for_any (_ ex of)
    (-> (Exception ex) ex (Operation of)
        (Operation of)))
  (function (_ state)
    (.when (exception.with exception message
             (action state))
      {try.#Failure error}
      {try.#Failure (located (its .#location state) error)}

      success
      success)))

(the .public (set_state state)
  (-> .Lux
      (Operation Any))
  (function (_ _)
    {try.#Success [state []]}))

(template.with [<name> <type> <field> <value>]
  [(the .public (<name> value)
     (-> <type>
         (Operation Any))
     (phase.update (has <field> <value>)))]

  [set_source_code    Source   .#source         value]
  [set_current_module Text     .#current_module {.#Some value}]
  [set_location       Location .#location       value]
  )

(the .public (location file)
  (-> Text
      Location)
  [file 1 0])

(the .public (source file code)
  (-> Text Text
      Source)
  [(location file) 0 code])

(the dummy_source
  Source
  [location.dummy 0 ""])

(the type_context
  Type_Context
  [.#ex_counter 0
   .#var_counter 0
   .#var_bindings (list)])

(the .public (info version host configuration)
  (-> Version Text Configuration
      Info)
  [.#target host
   .#version (version.injection version)
   .#mode {.#Build}
   .#configuration configuration])

(the .public (state info)
  (-> Info
      Lux)
  [.#info            info
   .#source          ..dummy_source
   .#location        location.dummy
   .#current_module  {.#None}
   .#modules         (list)
   .#scopes          (list)
   .#type_context    ..type_context
   .#expected        {.#None}
   .#seed            0
   .#scope_type_vars (list)
   .#extensions      []
   .#eval            (as (-> Type Code (Meta Any)) [])
   .#host            []])

(the .public (delegated extender analysis archive extension parameters)
  (-> Extender Phase Archive Symbol (List Code)
      (Operation Term))
  (do phase.monad
    [lux phase.state]
    (extension.application extender
                           lux analysis archive
                           .Analysis extension parameters
                           (|>>)
                           (function (_ _) {.#None}))))
