... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except symbol
                when exec let if
                or and)
   [abstract
    ["[0]" monad (.only do)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix monoid)]
     ["[0]" set]]]
   [math
    [number
     ["n" natural]
     ["i" integer]]]
   [meta
    [macro
     ["[0]" template]]
    [compiler
     [target
      ["_" ruby (.only Expression LVar Statement)]]]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Translator Phase! Translator!)]
  ["[1][0]" reference]
  ["[1][0]" primitive]
  ["/[1]" //
   ["[1][0]" reference]
   ["/[1]" //
    [synthesis
     ["[0]" when]]
    ["/[1]" //
     ["[0]" phase (.use "[1]#[0]" monad)]
     ["[1][0]" translation]
     ["[0]" synthesis (.only Path)
      [access
       ["[0]" member (.only Member)]]]
     ["//[1]" ///
      [reference
       [register (.only Register)]]
      [meta
       [archive (.only Archive)]]]]]]])

(the symbol
  (-> Text
      (Operation LVar))
  (|>> /////translation.symbol
       (phase#each _.local)))

(the .public register
  (-> Register LVar)
  (|>> (///reference.local //reference.system) as_expected))

(the .public capture
  (-> Register LVar)
  (|>> (///reference.foreign //reference.system) as_expected))

(the .public (exec next archive it)
  (Translator (synthesis.Exec synthesis.Term))
  (do [! phase.monad]
    [.let [[tail after] (synthesis.flat_exec (its synthesis.#after it))]
     all_before (monad.each ! (next archive)
                            (list.partial (its synthesis.#before it) tail))
     after (next archive after)]
    (in (|> (_.array (list (_.array all_before)
                           after))
            (_.item (_.int +1))))))

(the .public (exec! statement expression archive [this that])
  (Translator! (synthesis.Exec synthesis.Term))
  (do phase.monad
    [this (expression archive this)
     that (statement expression archive that)]
    (in (all _.then
             (_.; this)
             that
             ))))

... TODO: Find some way to do 'let' without paying the price of the closure.
(the .public (let next archive [head_binding head_value body])
  (Translator synthesis.Let)
  (do [! phase.monad]
    [.let [[tail body] (synthesis.flat_let body)]
     bindings (monad.each ! (function (_ [binding value])
                              (phase#each (|>> (_.set (list (..register binding)))
                                               _.;)
                                          (next archive value)))
                          (list.partial [head_binding head_value]
                                        tail))
     body (next archive body)]
    ... (in (|> (_.array (list#composite bindings
    ...                                  (list body)))
    ...         (_.item (_.int (.integer (list.size bindings))))))
    (in (<| (_.apply_lambda (list))
            (_.lambda {.#None})
            [(list)
             (list#mix _.then
                       (_.return body)
                       (list.reversed bindings))]))
    ))

(the .public (let! statement expression archive [register valueS bodyS])
  (Translator! synthesis.Let)
  (do phase.monad
    [valueO (expression archive valueS)
     bodyO (statement expression archive bodyS)]
    (in (all _.then
             (_.; (_.set (list (..register register)) valueO))
             bodyO))))

(the .public (if expression archive [testS thenS elseS])
  (Translator synthesis.If)
  (do phase.monad
    [testO (expression archive testS)
     thenO (expression archive thenS)
     elseO (expression archive elseS)]
    (in (_.? testO thenO elseO))))

(the .public (if! statement expression archive [testS thenS elseS])
  (Translator! synthesis.If)
  (do phase.monad
    [test! (expression archive testS)
     then! (statement expression archive thenS)
     else! (statement expression archive elseS)]
    (in (_.if test!
          then!
          else!))))

(the (projection it)
  (.if (its member.#right? it)
    (//runtime.tuple//right (_.int (.integer (its member.#lefts it))))
    (//runtime.tuple//left (_.int (.integer (its member.#lefts it))))))

(the .public (get expression archive [pathP valueS])
  (Translator [(List Member) synthesis.Term])
  (do phase.monad
    [valueO (expression archive valueS)]
    (in (list#mix (function (_ member source)
                    ((projection member) source))
                  valueO
                  pathP))))

(the @savepoint (_.local "lux_pm_savepoint"))
(the @cursor (_.local "lux_pm_cursor"))
(the @temp (_.local "lux_pm_temp"))

(the (push! value)
  (-> Expression Statement)
  (_.; (|> @cursor (_.do "push" (list value) {.#None}))))

(the peek_and_pop
  Expression
  (|> @cursor (_.do "pop" (list) {.#None})))

(the pop!
  Statement
  (_.; ..peek_and_pop))

(the peek
  Expression
  (_.item (_.int -1) @cursor))

(the save!
  Statement
  (.let [cursor (_.array_range (_.int +0) (_.int -1) @cursor)]
    (_.; (|> @savepoint (_.do "push" (list cursor) {.#None})))))

(the restore!
  Statement
  (_.; (_.set (list @cursor) (|> @savepoint (_.do "pop" (list) {.#None})))))

(the fail! _.break)

(the (multi_pop! pops)
  (-> Natural Statement)
  (_.; (_.do "slice!"
         (list (_.int (i.* -1 (.integer pops)))
               (_.int (.integer pops)))
         {.#None}
         @cursor)))

(template.with [<name> <flag>]
  [(the (<name> simple? idx)
     (-> Bit Natural Statement)
     (all _.then
          (_.; (_.set (list @temp) (//runtime.sum//get ..peek <flag>
                                                       (|> idx .integer _.int))))
          (.if simple?
            (_.when (_.= _.nil @temp)
              fail!)
            (_.if (_.= _.nil @temp)
              fail!
              (..push! @temp)))))]

  [left_choice  _.nil]
  [right_choice //runtime.unit]
  )

(the (with_looping in_closure? 'once 'continue? body!)
  (-> Bit LVar LVar Statement Statement)
  (.if in_closure?
    (all _.then
         (_.while (_.bool true)
                  body!))
    (all _.then
         (_.; (_.set (list 'once) (_.bool true)))
         (_.; (_.set (list 'continue?) (_.bool false)))
         (<| (_.while (_.bool true))
             (_.if 'once
               (all _.then
                    (_.; (_.set (list 'once) (_.bool false)))
                    body!)
               (all _.then
                    (_.; (_.set (list 'continue?) (_.bool true)))
                    _.break)))
         (_.when 'continue?
           _.next))))

(the (alternation in_closure? 'once 'continue? pre! post!)
  (-> Bit LVar LVar Statement Statement Statement)
  (all _.then
       (with_looping in_closure? 'once 'continue?
         (all _.then
              ..save!
              pre!))
       ..restore!
       post!))

(the (pattern_matching' in_closure? statement expression archive)
  (-> Bit (Translator! Path))
  (function (again pathP)
    (`` (.when pathP
          {synthesis.#Then bodyS}
          (statement expression archive bodyS)

          {synthesis.#Pop}
          (phase#in ..pop!)

          {synthesis.#Bind register}
          (phase#in (_.; (_.set (list (..register register)) ..peek)))

          {synthesis.#Bit_Fork when thenP elseP}
          (do [! phase.monad]
            [then! (again thenP)
             else! (.when elseP
                     {.#Some elseP}
                     (again elseP)

                     {.#None}
                     (in ..fail!))]
            (in (.if when
                  (_.if ..peek
                    then!
                    else!)
                  (_.if ..peek
                    else!
                    then!))))

          (,, (template.with [<tag> <injection>]
                [{<tag> item}
                 (do [! phase.monad]
                   [clauses (monad.each ! (function (_ [when then])
                                            (by ! each (|>> [(<injection> when)])
                                                (again then)))
                                        {.#Item item})]
                   (in (_.case ..peek (list.reversed clauses) ..fail!)))]

                ... [synthesis.#I64_Fork //primitive.i64]
                [synthesis.#F64_Fork //primitive.f64]
                [synthesis.#Text_Fork //primitive.text]))

          {synthesis.#I64_Fork item}
          (do [! phase.monad]
            ['input (..symbol "input")
             clauses (monad.each ! (function (_ [when then])
                                     (by ! each (|>> [(//primitive.i64 when)])
                                         (again then)))
                                 {.#Item item})]
            (in (all _.then
                     (_.; (_.set (list 'input) ..peek))
                     (list#mix (function (_ [when then] else)
                                 (_.if (_.= when 'input)
                                   then
                                   else))
                               ..fail!
                               (list.reversed clauses)))))

          (,, (template.with [<complex> <simple> <choice>]
                [(<complex> idx)
                 (phase#in (<choice> false idx))

                 (<simple> idx nextP)
                 (|> nextP
                     again
                     (phase#each (_.then (<choice> true idx))))]

                [synthesis.side/left  synthesis.simple_left_side  ..left_choice]
                [synthesis.side/right synthesis.simple_right_side ..right_choice]))

          (synthesis.member/left 0)
          (phase#in (|> ..peek (_.item (_.int +0)) ..push!))
          
          (,, (template.with [<pm> <getter>]
                [(<pm> lefts)
                 (phase#in (|> ..peek (<getter> (_.int (.integer lefts))) ..push!))]

                [synthesis.member/left  //runtime.tuple//left]
                [synthesis.member/right //runtime.tuple//right]))

          (synthesis.!bind_top register thenP)
          (do phase.monad
            [then! (again thenP)]
            (phase#in (all _.then
                           (_.; (_.set (list (..register register)) ..peek_and_pop))
                           then!)))

          (synthesis.!multi_pop nextP)
          (.let [[extra_pops nextP'] (when.count_pops nextP)]
            (do phase.monad
              [next! (again nextP')]
              (phase#in (all _.then
                             (..multi_pop! (n.+ 2 extra_pops))
                             next!))))

          (synthesis.path/seq preP postP)
          (do phase.monad
            [pre! (again preP)
             post! (again postP)]
            (in (all _.then
                     pre!
                     post!)))

          (synthesis.path/alt preP postP)
          (do phase.monad
            [pre! (again preP)
             post! (again postP)
             'once (..symbol "once")
             'continue? (..symbol "continue")]
            (in (..alternation in_closure? 'once 'continue? pre! post!)))))))

(the (pattern_matching in_closure? statement expression archive pathP)
  (-> Bit (Translator! Path))
  (do phase.monad
    [pattern_matching! (pattern_matching' in_closure? statement expression archive pathP)
     'once (..symbol "once")
     'continue? (..symbol "continue")]
    (in (all _.then
             (..with_looping in_closure? 'once 'continue?
               pattern_matching!)
             (_.; (_.raise (_.string when.pattern_matching_error)))))))

(the .public (when! in_closure? statement expression archive [valueS pathP])
  (-> Bit (Translator! [synthesis.Term Path]))
  (do phase.monad
    [stack_init (expression archive valueS)
     pattern_matching! (pattern_matching in_closure? statement expression archive pathP)]
    (in (all _.then
             (_.; (_.set (list @cursor) (_.array (list stack_init))))
             (_.; (_.set (list @savepoint) (_.array (list))))
             pattern_matching!
             ))))

(the .public (when statement expression archive when)
  (-> Phase! (Translator [synthesis.Term Path]))
  (|> when
      (when! true statement expression archive)
      (by phase.monad each
          (|>> [(list)] (_.lambda {.#None})
               (_.apply_lambda (list))))))

(the .public (multi_let! statement expression archive [input bindings body])
  (Translator! synthesis.Multi_Let)
  (do phase.monad
    [input (expression archive input)
     body (statement expression archive body)
     'tuple (..symbol "tuple")]
    (in (all _.then
             (list#mix (function (_ [register member] left)
                         (all _.then
                              left
                              (_.; (_.set (list (..register register)) (..projection member 'tuple)))))
                       (_.; (_.set (list 'tuple) input))
                       bindings)
             body))))

(the .public (multi_let statement expression archive)
  (-> Phase!
      (Translator synthesis.Multi_Let))
  (|>> (multi_let! statement expression archive)
       (by phase.monad each
           (|>> [(list)] (_.lambda {.#None})
                (_.apply_lambda (list))))))

(template.with [,lux ,host]
  [(the .public (,lux expression archive [parameter subject])
     (Translator [synthesis.Term synthesis.Term])
     (do phase.monad
       [parameter (expression archive parameter)
        subject (expression archive subject)]
       (in (,host parameter subject))))]

  [or _.or]
  [and _.and]
  )

(template.with [,expression ,statement ,type ,injection]
  [(the .public (,statement statement expression archive [input clauses else])
     (Translator! [synthesis.Term (List [,type synthesis.Term]) synthesis.Term])
     (do [! phase.monad]
       [input (expression archive input)
        clauses (monad.each ! (function (_ [when then])
                                (by ! each (|>> [(,injection when)]) (statement expression archive then)))
                            clauses)
        else (statement expression archive else)]
       (in (_.case input (list.reversed clauses) else))))

   (the .public (,expression statement expression archive)
     (-> Phase!
         (Translator [synthesis.Term (List [,type synthesis.Term]) synthesis.Term]))
     (|>> (,statement statement expression archive)
          (by phase.monad each
              (|>> [(list)] (_.lambda {.#None})
                   (_.apply_lambda (list))))))]

  ... [when_i64 when_i64! I64 //primitive.i64]
  [when_f64 when_f64! F64 //primitive.f64]
  [when_text when_text! Text //primitive.text]
  )

(the .public (when_i64! statement expression archive [input clauses else])
  (Translator! [synthesis.Term (List [I64 synthesis.Term]) synthesis.Term])
  (do [! phase.monad]
    [input (expression archive input)
     'input (..symbol "input")
     clauses (monad.each ! (function (_ [when then])
                             (by ! each (|>> [(//primitive.i64 when)])
                                 (statement expression archive then)))
                         clauses)
     else (statement expression archive else)]
    (in (all _.then
             (_.; (_.set (list 'input) input))
             (list#mix (function (_ [when then] else)
                         (_.if (_.= when 'input)
                           then
                           else))
                       else
                       (list.reversed clauses))))))

(the .public (when_i64 statement expression archive)
  (-> Phase!
      (Translator [synthesis.Term (List [I64 synthesis.Term]) synthesis.Term]))
  (|>> (when_i64! statement expression archive)
       (by phase.monad each
           (|>> [(list)] (_.lambda {.#None})
                (_.apply_lambda (list))))))
