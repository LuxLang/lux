(.require
 [library
  [lux (.except Analysis function)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" maybe]
    ["[0]" try]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\format (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" monoid monad)]]]
   [math
    [number
     ["n" nat]]]
   ["[0]" meta (.only)
    ["[0]" type (.only)
     ["[0]" check]]]]]
 [////
  ["[0]" phase (.use "[1]#[0]" functor)]
  ["/" analysis (.only Analysis Operation Phase)
   ["[1][0]" type]
   ["[1][0]" inference]
   ["[1][0]" scope]]
  [///
   [reference (.only)
    [variable (.only)]]]])

(exception.def .public (cannot_analyse [expected function argument body])
  (Exception [Type Text Text Code])
  (exception.report
   (list ["Type" (%.type expected)]
         ["Function" function]
         ["Argument" argument]
         ["Body" (%.code body)])))

(exception.def .public (cannot_apply [:function: functionC arguments])
  (Exception [Type Code (List Code)])
  (exception.report
   (list ["Function type" (%.type :function:)]
         ["Function" (%.code functionC)]
         ["Arguments" (|> arguments
                          list.enumeration
                          (list#each (.function (_ [idx argC])
                                       (format (%.nat idx) " " (%.code argC))))
                          (text.interposed text.new_line))])))

(def .public (function analyse function_name arg_name archive body)
  (-> Phase Text Text Phase)
  (do [! phase.monad]
    [expectedT meta.expected_type]
    (loop (again [expectedT expectedT])
      (/.with_exception ..cannot_analyse [expectedT function_name arg_name body]
        (when expectedT
          {.#Function :input: :output:}
          (<| (do !
                [@ meta.location])
              (of ! each (.function (_ [scope bodyA])
                           [@ {/.#Function (list#each (|>> (/.variable @))
                                                      (/scope.environment scope))
                                           bodyA}]))
              /scope.with
              ... Functions have access not only to their argument, but
              ... also to themselves, through a local variable.
              (/scope.with_local [function_name expectedT])
              (/scope.with_local [arg_name :input:])
              (/type.expecting :output:)
              (analyse archive body))
          
          {.#Named name :anonymous:}
          (again :anonymous:)

          {.#Apply argT funT}
          (when (type.applied (list argT) funT)
            {.#Some value}
            (again value)

            {.#None}
            (/.failure (exception.error ..cannot_analyse [expectedT function_name arg_name body])))

          {.#UnivQ _}
          (do !
            [[@instance :instance:] (/type.check check.existential)]
            (again (maybe.trusted (type.applied (list :instance:) expectedT))))

          {.#ExQ _}
          (<| /type.with_var
              (.function (_ [@instance :instance:]))
              (again (maybe.trusted (type.applied (list :instance:) expectedT))))
          
          {.#Var id}
          (do !
            [?expectedT' (/type.check (check.peek id))]
            (when ?expectedT'
              {.#Some expectedT'}
              (again expectedT')

              ... Inference
              _
              (<| /type.with_var
                  (.function (_ [@input :input:]))
                  /type.with_var
                  (.function (_ [@output :output:]))
                  (do !
                    [functionA (again {.#Function :input: :output:})])
                  /type.check
                  (do check.monad
                    [:output: (check.identity (list) @output)
                     ?:input: (check.try (check.identity (list @output) @input))
                     ? (check.linked? @input @output)
                     _ (<| (check.check expectedT)
                           (when ?:input:
                             {try.#Success :input:}
                             {.#Function :input: (if ?
                                                   :input:
                                                   :output:)}

                             {try.#Failure _}
                             (|> (if ?
                                   :input:
                                   :output:)
                                 {.#Function :input:}
                                 (/inference.quantified @input 1)
                                 {.#UnivQ (list)})))]
                    (in functionA)))))

          _
          (/.failure "")
          )))))

(def .public (apply analyse argsC+ :function: functionA archive functionC)
  (-> Phase (List Code) Type Analysis Phase)
  (|> (/inference.general archive analyse :function: argsC+)
      (phase#each (|>> product.right [functionA] /.reified))
      (/.with_exception ..cannot_apply [:function: functionC argsC+])))
