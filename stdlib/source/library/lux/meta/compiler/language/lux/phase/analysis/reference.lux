... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   ["[0]" meta (.only)
    ["[0]" module]
    ["[0]" code]]]]
 [////
  ["[0]" phase]
  ["/" analysis (.only Operation Phase Extender)
   ["[0]" scope]]
  [///
   [meta
    [archive (.only Archive)]]]])

(the .public (reference extender analysis archive quoted_module it)
  (-> Extender Phase Archive Text Symbol
      (Operation /.Term))
  (when it
    ["" short]
    (do [! phase.monad]
      [?var (scope.variable short)]
      (when ?var
        {.#Some _}
        (/.delegated extender analysis archive (symbol .local#) (list (code.symbol it)))

        {.#None}
        (do !
          [[this_module _] module.current]
          (/.delegated extender analysis archive (symbol .global#) (list (code.text quoted_module) (code.symbol [this_module short]))))))

    _
    (/.delegated extender analysis archive (symbol .global#) (list (code.text quoted_module) (code.symbol it)))))
