... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Label Scope)
   [abstract
    ["[0]" monad (.only do)]]
   [data
    ["[0]" product]
    [text
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]
     ["[0]" set]]]
   [math
    [number
     ["n" natural]]]
   [meta
    [compiler
     [target
      ["_" lua (.only Var Expression Label Statement)]]]]]]
 ["[0]" //
  [runtime (.only Operation Phase Phase! Translator Translator!)]
  ["[1][0]" when]
  ["/[1]" //
   ["[1][0]" reference]
   ["//[1]" ///
    ["[0]" phase]
    ["[0]" synthesis (.only Scope)]
    ["[1][0]" translation]
    ["//[1]" ///
     [meta
      [archive (.only Archive)]
      ["[0]" cache
       [dependency
        ["[1]" artifact]]]]
     [reference
      [register (.only Register)]]]]]])

(the @scope
  (-> Natural Label)
  (|>> %.natural (%.message "scope") _.label))

(the (setup initial? offset bindings as_expression? body)
  (-> Bit Register (List Expression) Bit Statement Statement)
  (let [variables (|> bindings
                      list.enumeration
                      (list#each (|>> product.left (n.+ offset) //when.register)))]
    (if as_expression?
      body
      (all _.then
           (if initial?
             (_.let variables (_.multi bindings))
             (_.set variables (_.multi bindings)))
           body))))

(the .public (scope! statement expression archive as_expression? [start initsS+ bodyS])
  ... (Translator! (Scope synthesis.Term))
  (-> Phase! Phase Archive Bit (Scope synthesis.Term)
      (Operation [(List Expression) Statement]))
  (when initsS+
    ... function/false/non-independent loop
    {.#End}
    (|> bodyS
        (statement expression archive)
        (of phase.monad each (|>> [(list)])))

    ... true loop
    _
    (do [! phase.monad]
      [@scope (of ! each ..@scope /////translation.next)
       initsO+ (monad.each ! (expression archive) initsS+)
       body! (/////translation.with_anchor [start @scope]
               (statement expression archive bodyS))]
      (in [initsO+
           (..setup true start initsO+ as_expression?
                    (all _.then
                         (_.set_label @scope)
                         body!))]))))

(the .public (scope statement expression archive [start initsS+ bodyS])
  (-> Phase! (Translator (Scope synthesis.Term)))
  (when initsS+
    ... function/false/non-independent loop
    {.#End}
    (expression archive bodyS)

    ... true loop
    _
    (do [! phase.monad]
      [dependencies (cache.dependencies archive bodyS)
       [[artifact_module artifact_id] [initsO+ scope!]] (/////translation.with_new_context archive dependencies
                                                          (scope! statement expression archive true [start initsS+ bodyS]))
       .let [@loop (_.var (///reference.artifact [artifact_module artifact_id]))
             locals (|> initsO+
                        list.enumeration
                        (list#each (|>> product.left (n.+ start) //when.register)))
             [declaration instantiation] (is [Statement Expression]
                                             (when (|> (synthesis.path/then bodyS)
                                                       //when.dependencies
                                                       (set.of_list _.hash)
                                                       (set.difference (set.of_list _.hash locals))
                                                       set.list)
                                               {.#End}
                                               [(_.function @loop locals
                                                  scope!)
                                                @loop]

                                               foreigns
                                               (let [@context (_.var (%.message (_.code @loop) "_context"))]
                                                 [(_.function @context foreigns
                                                    (all _.then
                                                         (<| (_.local_function @loop locals)
                                                             scope!)
                                                         (_.return @loop)
                                                         ))
                                                  (_.apply foreigns @context)])))]
       _ (/////translation.execute! declaration)
       _ (/////translation.save! artifact_id {.#None} declaration)]
      (in (_.apply initsO+ instantiation)))))

(the .public (again! statement expression archive it)
  (Translator! (List synthesis.Term))
  (do [! phase.monad]
    [[offset @scope] /////translation.anchor
     it (monad.each ! (expression archive) it)]
    (in (..setup false offset it false (_.go_to @scope)))))
