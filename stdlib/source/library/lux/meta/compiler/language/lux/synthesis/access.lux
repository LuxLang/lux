... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]]
   [data
    ["[0]" sum]
    [text
     ["%" \\format (.only Format)]]]]]
 ["[0]" /
  ["[1][0]" side (.only Side)]
  ["[1][0]" member (.only Member)]])

(every .public Access
  (Variant
   {#Side Side}
   {#Member Member}))

(the .public (format it)
  (Format Access)
  (when it
    {#Side it}
    (/side.format it)
    
    {#Member it}
    (/member.format it)))

(the .public hash
  (Hash Access)
  (all sum.hash
       /side.hash
       /member.hash
       ))

(the .public equivalence
  (Equivalence Access)
  (of ..hash equivalence))
