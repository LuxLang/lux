... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Variant Tuple)
   [abstract
    ["[0]" monad (.only do)]]
   [meta
    [compiler
     [target
      ["_" python (.only Expression)]]]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Translator)]
  ["[1][0]" primitive]
  ["///[1]" ////
   ["[0]" phase (.use "[1]#[0]" monad)]
   ["[0]" synthesis]
   [analysis
    [complex (.only Variant Tuple)]]]])

(def .public (tuple translate archive elemsS+)
  (Translator (Tuple synthesis.Term))
  (when elemsS+
    {.#End}
    (phase#in (//primitive.text synthesis.unit))

    {.#Item singletonS {.#End}}
    (translate archive singletonS)

    _
    (|> elemsS+
        (monad.each phase.monad (translate archive))
        (phase#each _.list))))

(def .public (variant translate archive [lefts right? valueS])
  (Translator (Variant synthesis.Term))
  (phase#each (//runtime.variant lefts right?)
              (translate archive valueS)))
