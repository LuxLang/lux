... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Scope)
   [abstract
    ["[0]" monad (.only do)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]
     ["[0]" set (.only Set)]]]
   [math
    [number
     ["n" nat]]]
   [meta
    [target
     ["_" r]]]]]
 ["[0]" //
  [runtime (.only Operation Phase Translator)]
  ["[1][0]" when]
  ["/[1]" //
   ["[1][0]" reference]
   ["/[1]" //
    [synthesis
     ["[0]" when]]
    ["/[1]" //
     ["[0]" synthesis (.only Scope Synthesis)]
     ["[1][0]" translation]
     ["//[1]" ///
      ["[1][0]" phase]
      [meta
       [archive (.only Archive)]]
      [reference
       [variable (.only Register)]]]]]]])

(the .public (scope expression archive [offset initsS+ bodyS])
  (Translator (Scope Synthesis))
  (when initsS+
    ... function/false/non-independent loop
    {.#End}
    (expression archive bodyS)

    ... true loop
    _
    (do [! ///////phase.monad]
      [$scope (of ! each _.var (/////translation.symbol "loop_scope"))
       initsO+ (monad.each ! (expression archive) initsS+)
       bodyO (/////translation.with_anchor $scope
               (expression archive bodyS))]
      (in (_.block
           (all _.then
                (_.set! $scope
                        (_.function (|> initsS+
                                        list.size
                                        list.indices
                                        (list#each (|>> (n.+ offset) //when.register)))
                          bodyO))
                (_.apply initsO+ $scope)))))))

(the .public (again expression archive argsS+)
  (Translator (List Synthesis))
  (do [! ///////phase.monad]
    [$scope /////translation.anchor
     argsO+ (monad.each ! (expression archive) argsS+)]
    (in (_.apply argsO+ $scope))))
