... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Definition)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]]
   [data
    [binary
     ["[0]" \\format]]
    [collection
     ["[0]" sequence]]]
   [meta
    [compiler
     [target
      [jvm
       ["_" bytecode (.only Bytecode)]
       ["[0]" modifier (.only Modifier) (.use "[1]#[0]" monoid)]
       ["[0]" method (.only Method)]
       ["[0]" version]
       ["[0]" class (.only Class)]
       [encoding
        ["[0]" name]]
       ["[0]" type (.only)
        ["[0]" reflection]]]]]
    [compiler
     [language
      [lux
       [program (.only Program)]]]
     [meta
      [archive
       ["[0]" unit]]]]]]]
 ["[0]" // (.only)
  ["[1][0]" runtime (.only Definition)]
  ["[1][0]" function/abstract]])

(def ^Object
  (type.class "java.lang.Object" (list)))

(def ^String
  (type.class "java.lang.String" (list)))

(def ^Args
  (type.array ^String))

(def main::type
  (type.method [(list) (list ..^Args) type.void (list)]))

(def main::modifier
  (Modifier Method)
  (all modifier#composite
       method.public
       method.static
       method.strict
       ))

(def program::modifier
  (Modifier Class)
  (all modifier#composite
       class.public
       class.final
       ))

(def list:end
  //runtime.none_injection)

(def amount_of_inputs
  (Bytecode Any)
  (all _.composite
       _.aload_0
       _.arraylength))

(def decrease
  (Bytecode Any)
  (all _.composite
       _.iconst_1
       _.isub))

(def head
  (Bytecode Any)
  (all _.composite
       _.dup
       _.aload_0
       _.swap
       _.aaload
       _.swap
       _.dup_x2
       _.pop))

(def pair
  (Bytecode Any)
  (let [empty_pair (all _.composite
                        _.iconst_2
                        (_.anewarray ^Object)
                        )
        set_side! (is (-> (Bytecode Any) (Bytecode Any))
                      (function (_ index)
                        (all _.composite
                             ... ?P
                             _.dup_x1  ... P?P
                             _.swap    ... PP?
                             index     ... PP?I
                             _.swap    ... PPI?
                             _.aastore ... P
                             )))]
    (all _.composite
         ... RL
         empty_pair ... RLP
         (set_side! _.iconst_0) ... RP
         (set_side! _.iconst_1) ... P
         )))

(def list:item //runtime.right_injection)

(def input_list
  (Bytecode Any)
  (do _.monad
    [@loop _.new_label
     @end _.new_label]
    (all _.composite
         ..list:end
         ..amount_of_inputs
         (_.set_label @loop)
         ..decrease
         _.dup
         (_.iflt @end)
         ..head
         ..pair
         ..list:item
         _.swap
         (_.goto @loop)
         (_.set_label @end)
         _.pop)))

(def feed_inputs
  //runtime.apply)

(def run_io
  (Bytecode Any)
  (all _.composite
       (_.checkcast //function/abstract.class)
       //runtime.unit
       //runtime.apply))

(def .public (program artifact_name context program)
  (-> (-> unit.ID Text)
      (Program (Bytecode Any) Definition))
  (let [super_class (|> ..^Object type.reflection reflection.reflection name.internal)
        main (method.method ..main::modifier "main"
               false ..main::type
               (list)
               {.#Some (all _.composite
                            program
                            ..input_list
                            ..feed_inputs
                            ..run_io
                            _.return)})
        class (artifact_name context)]
    [class
     (<| (\\format.value class.format)
         try.trusted
         (class.class version.v6_0
           ..program::modifier
           (name.internal class)
           {.#None}
           super_class
           (list)
           (list)
           (list main)
           (list)))]))
