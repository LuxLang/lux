(.require
 [library
  [lux (.except Module #module #imports)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" monoid)]]]]]
 [//
  ["[0]" analysis]
  ["[0]" synthesis]
  ["[0]" translation]
  [phase
   ["[0]" extension]]
  [///
   ["[0]" phase]
   [meta
    [archive
     [module
      [descriptor (.only Module)]]]]]])

(type .public (Component state phase)
  (Record
   [#state state
    #phase phase]))

(type .public (State anchor expression declaration)
  (Record
   [#analysis (Component analysis.State
                         analysis.Phase)
    #synthesis (Component synthesis.State
                          (-> Lux synthesis.Phase))
    #translation (Component (translation.State anchor expression declaration)
                            (-> Lux (translation.Phase anchor expression declaration)))]))

(type .public Import
  (Record
   [#module Module
    #alias Text]))

(type .public Requirements
  (Record
   [#imports (List Import)
    #referrals (List Code)]))

(def .public no_requirements
  Requirements
  [#imports (list)
   #referrals (list)])

(def .public (merge_requirements left right)
  (-> Requirements Requirements Requirements)
  [#imports (list#composite (the #imports left) (the #imports right))
   #referrals (list#composite (the #referrals left) (the #referrals right))])

(type .public (Operation anchor expression declaration)
  (phase.Operation (State anchor expression declaration)))

(type .public (Phase anchor expression declaration)
  (phase.Phase (State anchor expression declaration) Code Requirements))

(with_template [<special> <general>]
  [(type .public (<special> anchor expression declaration)
     (<general> (..State anchor expression declaration) Code Requirements))]

  [Handler   extension.Handler]
  [Bundle    extension.Bundle]
  [Extender  extension.Extender]
  )

(with_template [<name> <component> <phase>]
  [(def .public <name>
     (All (_ anchor expression declaration)
       (Operation anchor expression declaration <phase>))
     (function (_ state)
       {try.#Success [state (the [<component> ..#phase] state)]}))]

  [analysis   ..#analysis   analysis.Phase]
  [synthesis  ..#synthesis  (-> Lux synthesis.Phase)]
  [translation ..#translation (-> Lux (translation.Phase anchor expression declaration))]
  )

(with_template [<name> <component> <operation>]
  [(def .public <name>
     (All (_ anchor expression declaration output)
       (-> (<operation> output)
           (Operation anchor expression declaration output)))
     (phase.sub [(the [<component> ..#state])
                 (has [<component> ..#state])]))]

  [lifted_analysis   ..#analysis   analysis.Operation]
  [lifted_synthesis  ..#synthesis  synthesis.Operation]
  [lifted_translation ..#translation (translation.Operation anchor expression declaration)]
  )

(def .public (set_current_module module)
  (All (_ anchor expression declaration)
    (-> Module (Operation anchor expression declaration Any)))
  (do phase.monad
    [_ (..lifted_analysis (analysis.set_current_module module))]
    (..lifted_translation (translation.enter_module module))))
