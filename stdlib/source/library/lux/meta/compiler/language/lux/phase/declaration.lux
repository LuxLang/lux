... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception (.only Exception)]]
   [data
    [text
     ["%" \\format (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" mix monoid)]]]
   ["[0]" meta (.only)
    [type (.only sharing)
     ["[0]" check]]]]]
 ["[0]" //
  ["[0]" extension]
  ["/[1]" //
   ["/" declaration (.only Operation Phase Handler Extender)]
   ["[0]" phase (.use "[1]#[0]" monad)]
   ["[0]" analysis (.only)
    ["[0]" evaluation]
    ["[1]/[0]" macro (.only Expander)]
    ["[1]/[0]" type]]
   [///
    [reference (.only)
     [variable (.only)]]
    [meta
     [archive (.only Archive)]]]]])

(exception.def .public (not_a_declaration code)
  (Exception Code)
  (exception.report
   (list ["Declaration" (%.code code)])))

(exception.def .public (invalid_macro_call code)
  (Exception Code)
  (exception.report
   (list ["Code" (%.code code)])))

(type Eval
  (-> Type Code (Meta Any)))

(def (requiring phase archive expansion)
  (All (_ anchor expression declaration)
    (-> (Phase anchor expression declaration) Archive (List Code)
        (Operation anchor expression declaration /.Requirements)))
  (when expansion
    {.#End}
    (phase#in /.no_requirements)
    
    {.#Item head tail}
    (do phase.monad
      [head' (phase archive head)
       tail' (requiring phase archive tail)]
      (in (/.merge_requirements head' tail')))))

(type Outcome
  (Variant
   {#More (List Code)}
   {#Done /.Requirements}))

(def (macro_or_extension analysis archive whole_term function_term)
  (All (_ anchor expression declaration)
    (-> analysis.Phase Archive Code Code (Operation anchor expression declaration [Bit Symbol])))
  (when function_term
    [_ {.#Symbol it}]
    (phase#in [false it])

    function_term
    (do phase.monad
      [[type analysis] (/.of_analysis
                        (analysis/type.inferring
                         (analysis archive function_term)))]
      (when analysis
        (analysis.constant @ definition)
        (if (or (check.subsumes? .Macro type)
                (check.subsumes? .Declaration type))
          (in [true definition])
          (phase.except ..not_a_declaration [whole_term]))

        _
        (phase.except ..not_a_declaration [whole_term])))))

(with_expansions [<lux_def_module> (these [|form_location| {.#Form (list.partial [|text_location| {.#Symbol [..prelude "module#"]}] annotations)}])]
  (def .public (phase wrapper extender expander)
    (All (_ anchor expression declaration)
      (-> phase.Wrapper (Extender anchor expression declaration) Expander (Phase anchor expression declaration)))
    (function (again archive code)
      (do [! phase.monad]
        [state phase.state
         .let [analysis (the [/.#analysis /.#phase] state)
               compiler_eval ((evaluation.evaluator analysis
                                                    [(the [/.#synthesis /.#state] state)
                                                     (the [/.#synthesis /.#phase] state)]
                                                    [(the [/.#translation /.#state] state)
                                                     (the [/.#translation /.#phase] state)])
                              archive)
               extension_eval (as Eval (wrapper (as_expected compiler_eval)))]
         _ (phase.with (has [/.#analysis /.#state .#eval] extension_eval state))]
        (when code
          [_ {.#Form (list.partial term inputs)}]
          (do !
            [[validated? macro|extension] (macro_or_extension analysis archive code term)
             expansion|requirements (extension.application extender
                                                           (the [/.#analysis /.#state] state) again archive
                                                           .Declaration validated? macro|extension inputs
                                                           (|>> {#Done})
                                                           (function (_ _)
                                                             {.#Some (do !
                                                                       [macro (/.of_analysis (meta.macro macro|extension))]
                                                                       (when macro
                                                                         {.#Some macro}
                                                                         (/.of_analysis
                                                                          (do !
                                                                            [expansion (analysis/macro.expansion expander macro|extension macro inputs)]
                                                                            (in {#More expansion})))
                                                                         
                                                                         {.#None}
                                                                         (phase.except ..invalid_macro_call [code])))}))]
            (when expansion|requirements
              {.#Left expansion}
              (when expansion
                (list.partial <lux_def_module> referrals)
                (|> (again archive <lux_def_module>)
                    (of ! each (revised /.#referrals (list#composite referrals))))

                _
                (..requiring again archive expansion))

              {.#Right requirements}
              (in requirements)))

          _
          (phase.except ..not_a_declaration [code]))))))
