... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]#[0]" mix monoid)]]]
   [macro
    ["^" pattern]
    ["[0]" expansion]]
   [type (.only sharing)
    ["[0]" check]]
   ["[0]" meta (.only)
    ["[0]" module]
    ["[0]" binding]
    ["[0]" code]]]]
 ["[0]" // (.use "[1]#[0]" monad)
  ["[0]" extension]
  ["/[1]" //
   ["/" declaration (.only Operation Phase Handler Extender)]
   ["[0]" analysis (.only)
    ["[0]" evaluation]
    ["[1]/[0]" macro (.only Expander)]
    ["[1]/[0]" type]]
   [///
    [reference (.only)
     [variable (.only)]]
    [meta
     [archive (.only Archive)]]]]])

(exception.the .public (not_a_declaration [module code])
  (Exception [Text Code])
  (exception.report
   (list ["Declaration" (code.relative module code)])))

(every Eval
  (-> Type Code (Meta Any)))

(the (requiring phase archive expansion)
  (for_any (_ anchor expression declaration)
    (-> (Phase anchor expression declaration) Archive (List Code)
        (Operation anchor expression declaration /.Requirements)))
  (when expansion
    {.#End}
    (//#in /.no_requirements)
    
    {.#Item head tail}
    (do //.monad
      [head' (phase archive head)
       tail' (requiring phase archive tail)]
      (in (/.merge_requirements head' tail')))))

(every Outcome
  (Variant
   {#More (List Code)}
   {#Done /.Requirements}))

(the (macro_or_extension analysis archive whole_term function_term)
  (for_any (_ anchor expression declaration)
    (-> analysis.Phase Archive Code Code
        (Operation anchor expression declaration Name)))
  (do //.monad
    [[here _] (/.of_analysis module.current)
     attempt (/.of_analysis
              (all //.or
                   (<| (analysis/type.expecting .Macro)
                       (analysis archive function_term))
                   (<| //.try
                       (analysis/type.expecting .Declaration)
                       (analysis archive function_term))))]
    (when attempt
      (^.or {#0 analysis}
            {#1 {try.#Success analysis}})
      (when analysis
        (analysis.constant @ definition)
        (in definition)

        _
        (//.except ..not_a_declaration [here function_term]))

      {#1 {try.#Failure error}}
      (if (text.contains? (its exception.#label extension.defaults_cannot_be_referenced)
                          error)
        (when function_term
          [_ {.#Name definition}]
          (in definition)

          _
          (//.except ..not_a_declaration [here function_term]))
        (//.except ..not_a_declaration [here function_term])))))

(expansion.let [<lux_def_module> (these [|form_provenance| {.#Form (list.partial [|text_provenance| {.#Name [..prelude "module#"]}] annotations)}])]
  (the .public (phase wrapper extender expander)
    (for_any (_ anchor expression declaration)
      (-> //.Wrapper (Extender anchor expression declaration) Expander
          (Phase anchor expression declaration)))
    (function (again archive code)
      (do [! //.monad]
        [state //.state
         .let [analysis (its [/.#analysis /.#phase] state)
               compiler_eval ((evaluation.evaluator analysis
                                                    [(its [/.#synthesis /.#state] state)
                                                     (its [/.#synthesis /.#phase] state)]
                                                    [(its [/.#translation /.#state] state)
                                                     (its [/.#translation /.#phase] state)])
                              archive)
               extension_eval (as Eval (wrapper (as_expected compiler_eval)))]
         _ (//.with (has [/.#analysis /.#state .#eval] extension_eval state))]
        (when code
          [_ {.#Form (list.partial term inputs)}]
          (do !
            [macro|extension (macro_or_extension analysis archive code term)
             expansion|requirements (extension.reification extender
                                                           (its [/.#analysis /.#state] state) again archive
                                                           .Declaration macro|extension inputs
                                                           (|>> {#Done})
                                                           (function (_ _)
                                                             {.#Some (/.of_analysis
                                                                      (do !
                                                                        [macro (binding.macro macro|extension)
                                                                         expansion (analysis/macro.expansion expander macro|extension macro inputs)]
                                                                        (in {#More expansion})))}))]
            (when expansion|requirements
              {.#Left expansion}
              (when expansion
                (list.partial <lux_def_module> referrals)
                (|> (again archive <lux_def_module>)
                    (by ! each (revised /.#referrals (list#composite referrals))))

                _
                (..requiring again archive expansion))

              {.#Right requirements}
              (in requirements)))

          _
          (do !
            [[here _] (/.of_analysis module.current)]
            (//.except ..not_a_declaration [here code])))))))
