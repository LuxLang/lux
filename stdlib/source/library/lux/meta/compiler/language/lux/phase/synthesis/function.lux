(.require
 [library
  [lux (.except Analysis Synthesis)
   [abstract
    ["[0]" monad (.only do)]
    ["[0]" enum]]
   [control
    ["[0]" pipe]
    ["[0]" maybe (.use "[1]#[0]" functor)]
    ["[0]" exception (.only Exception)]]
   [data
    [text
     ["%" \\format (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor monoid)]]]
   [math
    [number
     ["n" nat]]]
   [meta
    [macro
     ["^" pattern]]]]]
 ["[0]" //
  ["[1][0]" loop (.only Transform)]
  ["//[1]" ///
   ["[1][0]" analysis (.only Environment Analysis)
    ["[1]/[0]" complex]]
   ["/" synthesis (.only Path Abstraction Synthesis Operation Phase)]
   [///
    [arity (.only Arity)]
    ["[0]" phase (.use "[1]#[0]" monad)]
    ["[1][0]" reference (.only)
     ["[1]/[0]" variable (.only Register Variable)]]]]])

(exception.def .public (cannot_find_foreign_variable_in_environment [foreign environment])
  (Exception [Register (Environment Synthesis)])
  (exception.report
   (list ["Foreign" (%.nat foreign)]
         ["Environment" (exception.listing /.%synthesis environment)])))

(def arity_arguments
  (-> Arity (List Synthesis))
  (|>> --
       (enum.range n.enum 1)
       (list#each (|>> /.variable/local))))

(def .public self_reference
  (template (self_reference)
    [(/.variable/local 0)]))

(def (expanded_nested_self_reference arity)
  (-> Arity Synthesis)
  (/.function/apply [(..self_reference) (arity_arguments arity)]))

(def .public (apply phase)
  (-> Phase Phase)
  (function (_ archive exprA)
    (let [[funcA argsA] (////analysis.reification exprA)]
      (do [! phase.monad]
        [funcS (phase archive funcA)
         argsS (monad.each ! (phase archive) argsA)]
        (with_expansions [<apply> (these (/.function/apply [funcS argsS]))]
          (when funcS
            (/.function/abstraction functionS)
            (if (n.= (the /.#arity functionS)
                     (list.size argsS))
              (do !
                [locals /.locals]
                (in (|> functionS
                        (//loop.optimization true locals argsS)
                        (maybe#each (is (-> [Nat (List Synthesis) Synthesis] Synthesis)
                                        (function (_ [start inits iteration])
                                          (when iteration
                                            (/.loop/scope [start' inits' output])
                                            (if (and (n.= start start')
                                                     (list.empty? inits'))
                                              (/.loop/scope [start inits output])
                                              (/.loop/scope [start inits iteration]))

                                            _
                                            (/.loop/scope [start inits iteration])))))
                        (maybe.else <apply>))))
              (in <apply>))

            (/.function/apply [funcS' argsS'])
            (in (/.function/apply [funcS' (list#composite argsS' argsS)]))

            _
            (in <apply>)))))))

(def (find_foreign environment register)
  (-> (Environment Synthesis) Register (Operation Synthesis))
  (when (list.item register environment)
    {.#Some aliased}
    (phase#in aliased)

    {.#None}
    (phase.except ..cannot_find_foreign_variable_in_environment [register environment])))

(def (grow_path grow path)
  (-> (-> Synthesis (Operation Synthesis)) Path (Operation Path))
  (when path
    {/.#Bind register}
    (phase#in {/.#Bind (++ register)})

    (^.with_template [<tag>]
      [{<tag> left right}
       (do phase.monad
         [left' (grow_path grow left)
          right' (grow_path grow right)]
         (in {<tag> left' right'}))])
    ([/.#Alt] [/.#Seq])

    {/.#Bit_Fork test then else}
    (do [! phase.monad]
      [then (grow_path grow then)
       else (when else
              {.#Some else}
              (at ! each (|>> {.#Some}) (grow_path grow else))

              {.#None}
              (in {.#None}))]
      (in {/.#Bit_Fork test then else}))
    
    (^.with_template [<tag>]
      [{<tag> [[test then] elses]}
       (do [! phase.monad]
         [then (grow_path grow then)
          elses (monad.each ! (function (_ [else_test else_then])
                                (do !
                                  [else_then (grow_path grow else_then)]
                                  (in [else_test else_then])))
                            elses)]
         (in {<tag> [[test then] elses]}))])
    ([/.#I64_Fork]
     [/.#F64_Fork]
     [/.#Text_Fork])
    
    {/.#Then thenS}
    (|> thenS
        grow
        (phase#each (|>> {/.#Then})))

    _
    (phase#in path)))

(def (grow environment expression)
  (-> (Environment Synthesis) Synthesis (Operation Synthesis))
  (when expression
    {/.#Structure structure}
    (when structure
      {////analysis/complex.#Variant [lefts right? subS]}
      (|> subS
          (grow environment)
          (phase#each (|>> [lefts right?] /.variant)))
      
      {////analysis/complex.#Tuple membersS+}
      (|> membersS+
          (monad.each phase.monad (grow environment))
          (phase#each (|>> /.tuple))))

    (..self_reference)
    (phase#in (/.function/apply [expression (list (/.variable/local 1))]))
    
    {/.#Reference reference}
    (when reference
      {////reference.#Variable variable}
      (when variable
        {////reference/variable.#Local register}
        (phase#in (/.variable/local (++ register)))
        
        {////reference/variable.#Foreign register}
        (..find_foreign environment register))
      
      {////reference.#Constant constant}
      (phase#in expression))
    
    {/.#Control control}
    (when control
      {/.#Branch branch}
      (when branch
        {/.#Exec [this that]}
        (do phase.monad
          [this (grow environment this)
           that (grow environment that)]
          (in (/.branch/exec [this that])))
        
        {/.#Let [inputS register bodyS]}
        (do phase.monad
          [inputS' (grow environment inputS)
           bodyS' (grow environment bodyS)]
          (in (/.branch/let [inputS' (++ register) bodyS'])))
        
        {/.#If [testS thenS elseS]}
        (do phase.monad
          [testS' (grow environment testS)
           thenS' (grow environment thenS)
           elseS' (grow environment elseS)]
          (in (/.branch/if [testS' thenS' elseS'])))

        {/.#Get members inputS}
        (do phase.monad
          [inputS' (grow environment inputS)]
          (in (/.branch/get [members inputS'])))
        
        {/.#When [inputS pathS]}
        (do phase.monad
          [inputS' (grow environment inputS)
           pathS' (grow_path (grow environment) pathS)]
          (in (/.branch/when [inputS' pathS']))))
      
      {/.#Loop loop}
      (when loop
        {/.#Scope [start initsS+ iterationS]}
        (do [! phase.monad]
          [initsS+' (monad.each ! (grow environment) initsS+)
           iterationS' (grow environment iterationS)]
          (in (/.loop/scope [(++ start) initsS+' iterationS'])))
        
        {/.#Again argumentsS+}
        (|> argumentsS+
            (monad.each phase.monad (grow environment))
            (phase#each (|>> /.loop/again))))
      
      {/.#Function function}
      (when function
        {/.#Abstraction [_env _arity _body]}
        (do [! phase.monad]
          [_env' (monad.each !
                             (|>> (pipe.when
                                    {/.#Reference {////reference.#Variable {////reference/variable.#Foreign register}}}
                                    (..find_foreign environment register)

                                    captured
                                    (grow environment captured)))
                             _env)]
          (in (/.function/abstraction [_env' _arity _body])))
        
        {/.#Apply funcS argsS+}
        (do [! phase.monad]
          [funcS (grow environment funcS)
           argsS+ (monad.each ! (grow environment) argsS+)]
          (in (/.function/apply (when funcS
                                  (/.function/apply [(..self_reference) pre_argsS+])
                                  [(..self_reference)
                                   (list#composite pre_argsS+ argsS+)]

                                  _
                                  [funcS
                                   argsS+]))))))
    
    {/.#Extension name argumentsS+}
    (|> argumentsS+
        (monad.each phase.monad (grow environment))
        (phase#each (|>> {/.#Extension name})))

    {/.#Simple _}
    (phase#in expression)))

(def .public (abstraction phase environment archive bodyA)
  (-> Phase (Environment Analysis) Phase)
  (do [! phase.monad]
    [environment (monad.each ! (phase archive) environment)
     bodyS (/.with_currying? true
             (/.with_locals 2
               (phase archive bodyA)))
     abstraction (is (Operation Abstraction)
                     (when bodyS
                       (/.function/abstraction [env' down_arity' bodyS'])
                       (|> bodyS'
                           (grow env')
                           (at ! each (function (_ body)
                                        [/.#environment environment
                                         /.#arity (++ down_arity')
                                         /.#body body])))
                       
                       _
                       (in [/.#environment environment
                            /.#arity 1
                            /.#body bodyS])))
     currying? /.currying?]
    (in (/.function/abstraction
         (if currying?
           abstraction
           (when (//loop.optimization false 1 (list) abstraction)
             {.#Some [startL initsL bodyL]}
             [/.#environment environment
              /.#arity (the /.#arity abstraction)
              /.#body (/.loop/scope [startL initsL bodyL])]
             
             {.#None}
             abstraction))))))
