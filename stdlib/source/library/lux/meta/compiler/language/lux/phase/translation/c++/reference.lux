... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except local)
   [abstract
    [monad (.only do)]]
   [control
    ["|" pipe]]
   [data
    ["[0]" product]
    [text
     ["%" \\injection]]]
   [macro
    ["[0]" template]]
   [meta
    [compiler
     [target
      ["_" c++]]]]]]
 [//
  ["/" runtime (.only Operation)]
  [//
   ["[0]" reference]
   [///
    ["[0]" phase (.use "[1]#[0]" monad)]
    ["[0]" translation]
    [///
     [reference
      [register (.only Register)]
      ["[0]" variable (.only Variable)]]
     [meta
      [archive (.only Archive)]]]]]])

(template.with [<prefix> <name>]
  [(the .public <name>
     (-> Register
         _.Local)
     (|>> %.nat
          (text <prefix>)
          _.local))]

  ["l" local]
  ["f" foreign]
  ["p" partial]
  )

(the .public this
  _.Local
  (..local 0))

(the .public variable
  (-> Variable
      _.Local)
  (|>> (|.when
         {variable.#Local it}
         (..local it)
         
         {variable.#Foreign it}
         (..foreign it))))

(the .public (constant archive it)
  (-> Archive Name
      (Operation _.Address))
  (phase#each (|>> product.left
                   reference.artifact
                   _.local)
              (translation.definition archive it)))
