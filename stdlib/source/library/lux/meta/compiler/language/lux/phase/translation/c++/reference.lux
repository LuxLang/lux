... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except local)
   [abstract
    [monad (.only do)]]
   [control
    ["|" pipe]]
   [data
    ["[0]" product]
    [text
     ["%" \\format]]]
   [meta
    [compiler
     [target
      ["_" c++]]]]]]
 [//
  ["/" runtime (.only Operation)]
  [//
   ["[0]" reference]
   [///
    ["[0]" phase (.use "[1]#[0]" monad)]
    ["[0]" translation]
    [///
     [reference
      ["[0]" variable (.only Register Variable)]]
     [meta
      [archive (.only Archive)]]]]]])

(with_template [<prefix> <name>]
  [(def .public <name>
     (-> Register
         /.Value)
     (|>> %.nat
          (%.format <prefix>)
          _.local))]

  ["l" local]
  ["f" foreign]
  ["p" partial]
  )

(def .public this
  /.Value
  (..local 0))

(def .public variable
  (-> Variable
      (Operation /.Value))
  (|>> (|.when
         {variable.#Local it}
         (..local it)
         
         {variable.#Foreign it}
         (..foreign it))
       phase#in))

(def .public (constant archive it)
  (-> Archive Symbol
      (Operation /.Value))
  (phase#each (|>> product.left
                   reference.artifact
                   _.local)
              (translation.definition archive it)))
