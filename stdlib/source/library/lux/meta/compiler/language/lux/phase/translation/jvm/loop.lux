(.require
 [library
  [lux (.except Synthesis)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["[0]" function]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    [number
     ["n" nat]]]
   [meta
    [target
     [jvm
      ["_" bytecode (.only Bytecode) (.use "[1]#[0]" monad)]]]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Translator)]
  ["[1][0]" value]
  [////
   ["[0]" synthesis (.only Path Synthesis)]
   ["[0]" translation]
   [///
    ["[0]" phase]
    [reference
     [variable (.only Register)]]]]])

(def (invariant? register changeS)
  (-> Register Synthesis Bit)
  (when changeS
    (synthesis.variable/local @ var)
    (n.= register var)

    _
    false))

(def no_op
  (_#in []))

(def .public (again translate archive updatesS)
  (Translator (List Synthesis))
  (do [! phase.monad]
    [[@begin offset] translation.anchor
     updatesG (|> updatesS
                  list.enumeration
                  (list#each (function (_ [index updateS])
                               [(n.+ offset index) updateS]))
                  (monad.each ! (function (_ [register updateS])
                                  (if (invariant? register updateS)
                                    (in [..no_op
                                         ..no_op])
                                    (do !
                                      [fetchG (translate archive updateS)
                                       .let [storeG (_.astore register)]]
                                      (in [fetchG storeG]))))))]
    (in (all _.composite
             ... It may look weird that first I fetch all the values separately,
             ... and then I store them all.
             ... It must be done that way in order to avoid a potential bug.
             ... Let's say that you'll recur with 2 expressions: X and Y.
             ... If Y depends on the value of X, and you don't perform fetches
             ... and stores separately, then by the time Y is evaluated, it
             ... will refer to the new value of X, instead of the old value, as
             ... should be the case.
             (|> updatesG
                 (list#each product.left)
                 (monad.all _.monad))
             (|> updatesG
                 list.reversed
                 (list#each product.right)
                 (monad.all _.monad))
             (_.goto @begin)))))

(def .public (scope translate archive [offset initsS+ iterationS])
  (Translator [Nat (List Synthesis) Synthesis])
  (do [! phase.monad]
    [@begin //runtime.forge_label
     initsI+ (monad.each ! (translate archive) initsS+)
     iterationG (translation.with_anchor [@begin offset]
                  (translate archive iterationS))
     .let [initializationG (list#each (function (_ [index initG])
                                        [initG (_.astore (n.+ offset index))])
                                      (list.enumeration initsI+))]]
    (in (all _.composite
             (|> initializationG
                 (list#each product.left)
                 (monad.all _.monad))
             (|> initializationG
                 list.reversed
                 (list#each product.right)
                 (monad.all _.monad))
             (_.set_label @begin)
             iterationG))))
