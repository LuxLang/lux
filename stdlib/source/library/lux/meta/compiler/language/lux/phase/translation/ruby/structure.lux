... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Variant Tuple
                variant tuple)
   [abstract
    ["[0]" monad (.only do)]]
   [meta
    [compiler
     [target
      ["_" ruby]]]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Expression)]
  ["[1][0]" primitive]
  ["///[1]" ////
   ["[0]" phase (.use "[1]#[0]" monad)]
   ["[0]" synthesis]
   [analysis
    [complex (.only Variant Tuple)]]]])

(the .public (variant translate archive [lefts right? valueS])
  (Expression (Variant synthesis.Term))
  (phase#each (//runtime.variant lefts right?)
              (translate archive valueS)))

(the .public (tuple translate archive elemsS+)
  (Expression (Tuple synthesis.Term))
  (when elemsS+
    {.#Empty}
    (phase#in (//primitive.text synthesis.unit))

    {.#Top singletonS {.#Empty}}
    (translate archive singletonS)

    _
    (|> elemsS+
        (monad.each phase.monad (translate archive))
        (phase#each _.array))))
