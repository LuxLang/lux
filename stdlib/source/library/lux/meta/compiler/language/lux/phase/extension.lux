... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except global)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    [monad (.only do)]]
   [control
    ["[0]" maybe]
    ["[0]" try]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" product]
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\format (.only Format)]]
    [collection
     ["[0]" list (.only)
      ["[0]" property]]
     ["[0]" dictionary (.only Dictionary)]]]
   ["[0]" meta (.only)
    ["[0]" symbol]
    [type (.only sharing)
     ["[0]" check]]]]]
 [///
  ["[0]" phase (.only Operation Phase) (.use "[1]#[0]" functor)]
  [///
   [meta
    [archive (.only Archive)]]]])

(every .public (Extension of)
  (Record
   [#name Symbol
    #parameters (List of)]))

(the .public equivalence
  (All (_ of)
    (-> (Equivalence of)
        (Equivalence (Extension of))))
  (|>> list.equivalence
       (product.equivalence symbol.equivalence)))

(the .public hash
  (All (_ of)
    (-> (Hash of)
        (Hash (Extension of))))
  (|>> list.hash
       (product.hash symbol.hash)))

(every .public (Handler state input output)
  (-> (Phase state input output)
      (Phase state (List input) output)))

(every .public (Bundle state input output)
  (Dictionary Text (Handler state input output)))

(the .public empty
  Bundle
  (dictionary.empty text.hash))

(every .public (Extender state input output)
  (-> Any
      (Handler state input output)))

(exception.the .public (invalid [global expected actual])
  (Exception [Symbol Type Type])
  (exception.report
   (list ["Definition" (%.symbol global)]
         ["Expected" (%.type expected)]
         ["Actual" (%.type actual)])))

(exception.the .public (error error)
  (Exception Text)
  error)

(every Value
  (Variant
   {#Normal Any}
   {#Special Any}))

(the (global validated_global? name)
  (-> Bit Symbol (Meta Global))
  (do meta.monad
    [name (meta.normal name)
     current_module_name meta.current_module_name
     lux meta.compiler_state]
    (loop (again [[normal_module normal_short] name])
      (when (is (Maybe [Bit Global])
                (do maybe.monad
                  [(open "/[0]") (|> lux
                                     (its .#modules)
                                     (property.value normal_module))]
                  (property.value normal_short /#definitions)))
        {.#Some [exported? it]}
        (when it
          {.#Definition [type value]}
          (if (or validated_global?
                  exported?
                  (text#= current_module_name normal_module))
            (in it)
            (meta.failure (%.format "Global is not an export: " (%.symbol name))))

          {.#Default [type value]}
          (if (or validated_global?
                  exported?
                  (text#= current_module_name normal_module))
            (in it)
            (meta.failure (%.format "Global is not an export: " (%.symbol name))))

          {.#Alias de_aliased}
          (again de_aliased))

        {.#None it}
        (meta.failure (%.format "Unknown global: " (%.symbol name)))))))

(the (global_value validated_global? name)
  (-> Bit Symbol (Meta [Type Value]))
  (do meta.monad
    [global (..global validated_global? name)]
    (when global
      {.#Definition [type value]}
      (in [type {#Normal value}])

      {.#Default [type value]}
      (in [type {#Special value}])
      
      {.#Alias _}
      (undefined))))

(the (global_extension expected_type validated_global? name)
  (-> Type Bit Symbol (Meta Value))
  (do meta.monad
    [[actual_type value] (global_value validated_global? name)]
    (if (check.subsumed? expected_type actual_type)
      (in value)
      (meta.failure (exception.error ..invalid [name expected_type actual_type])))))

(the .public (application extender
                          lux phase archive
                          expected_type validated_global? global parameters
                          when_valid
                          when_invalid)
  (All (_ state input raw_output processed_output)
    (-> (Extender state input raw_output)
        Lux (Phase state input raw_output) Archive
        Type Bit Symbol (List input)
        (-> raw_output processed_output)
        (-> Text (Maybe (Operation state processed_output)))
        (Operation state processed_output)))
  (when (|> (do [! meta.monad]
              [value (global_extension expected_type validated_global? global)]
              (in ((when value
                     {#Normal definition}
                     (extender definition)

                     {#Special default}
                     (sharing [state input raw_output]
                       (is (Extender state input raw_output)
                           extender)
                       (is (Handler state input raw_output)
                           (as_expected default))))
                   phase archive parameters)))
            (meta.value lux))
    {try.#Success operation}
    (phase#each when_valid operation)

    {try.#Failure error}
    (when (when_invalid error)
      {.#Some when_invalid}
      when_invalid

      {.#None}
      (phase.except ..error [error]))))
