... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except nat int rev)
   ["[0]" meta]
   [abstract
    [monad (.only do)]]]]
 [////
  ["[0]" phase]
  ["/" analysis (.only Operation)
   ["[1][0]" simple]
   ["[1][0]" type]]])

(with_template [<name> <type> <tag>]
  [(the .public (<name> value)
     (-> <type>
         (Operation /.Term))
     (do phase.monad
       [_ (/type.inference <type>)
        @ meta.location]
       (in [@ {/.#Simple {<tag> value}}])))]

  [bit  .Bit  /simple.#Bit]
  [nat  .Nat  /simple.#Nat]
  [int  .Int  /simple.#Int]
  [rev  .Rev  /simple.#Rev]
  [dec  .Dec  /simple.#Dec]
  [text .Text /simple.#Text]
  )

(the .public unit
  (Operation /.Term)
  (do phase.monad
    [_ (/type.inference .Any)
     @ meta.location]
    (in [@ {/.#Simple {/simple.#Unit}}])))
