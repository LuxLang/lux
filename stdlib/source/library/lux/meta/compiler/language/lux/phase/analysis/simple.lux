... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Analysis nat int rev)
   ["[0]" meta]
   [abstract
    [monad (.only do)]]]]
 [////
  ["[0]" phase]
  ["/" analysis (.only Analysis Operation)
   ["[1][0]" simple]
   ["[1][0]" type]]])

(with_template [<name> <type> <tag>]
  [(def .public (<name> value)
     (-> <type>
         (Operation Analysis))
     (do phase.monad
       [_ (/type.inference <type>)
        @ meta.location]
       (in [@ {/.#Simple {<tag> value}}])))]

  [bit  .Bit  /simple.#Bit]
  [nat  .Nat  /simple.#Nat]
  [int  .Int  /simple.#Int]
  [rev  .Rev  /simple.#Rev]
  [frac .Frac /simple.#Frac]
  [text .Text /simple.#Text]
  )

(def .public unit
  (Operation Analysis)
  (do phase.monad
    [_ (/type.inference .Any)
     @ meta.location]
    (in [@ {/.#Simple {/simple.#Unit}}])))
