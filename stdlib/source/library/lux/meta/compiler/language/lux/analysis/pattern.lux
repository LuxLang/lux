... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Pattern
                natural integer rev
                variant tuple)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    [number
     ["n" natural]]]
   [meta
    [macro
     ["[0]" template]]]]]
 ["[0]" //
  ["[1][0]" simple (.only Simple)]
  ["[1][0]" complex (.only Complex)]
  [////
   [reference
    [register (.only Register)]
    ["[1][0]" variable]]]])

(every .public Pattern
  (Rec Pattern
    (.Variant
     {#Simple Simple}
     {#Complex (Complex Pattern)}
     {#Bind Register})))

(the .public equivalence
  (Equivalence Pattern)
  (implementation
   (the (= expected actual)
     (when [expected actual]
       [{#Simple expected} {#Simple actual}]
       (of //simple.equivalence = expected actual)
       
       [{#Complex expected} {#Complex actual}]
       (of (//complex.equivalence =) = expected actual)

       [{#Bind expected} {#Bind actual}]
       (n.= expected actual)

       _
       false))))

(alias [=]
       ..equivalence)

(the .public (injection it)
  (text.Injection Pattern)
  (when it
    {#Simple it}
    (//simple.injection it)
    
    {#Complex it}
    (//complex.injection injection it)
    
    {#Bind it}
    (//variable.injection {//variable.#Local it})))

(template.with [<name> <tag>]
  [(the .public <name>
     (template.macro (<name> content)
       [(.<| {..#Complex}
             <tag>
             content)]))]

  [variant {//complex.#Variant}]
  [tuple   {//complex.#Tuple}]
  )

(the .public unit
  (template.macro (unit)
    [{..#Simple {//simple.#Unit}}]))

(template.with [<name> <tag>]
  [(the .public <name>
     (template.macro (<name> content)
       [{..#Simple {<tag> content}}]))]
  
  [bit //simple.#Bit]
  [natural //simple.#Natural]
  [integer //simple.#Integer]
  [rev //simple.#Rev]
  [decimal //simple.#Decimal]
  [text //simple.#Text]
  )

(the .public bind
  (template.macro (bind register)
    [{..#Bind register}]))
