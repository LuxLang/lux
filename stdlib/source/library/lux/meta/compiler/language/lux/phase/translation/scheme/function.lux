... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except function)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    pipe]
   [data
    ["[0]" product]
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [meta
    [target
     ["_" scheme (.only Expression Computation Var)]]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Translator)]
  ["[1][0]" reference]
  ["[1][0]" when]
  ["/[1]" //
   ["[1][0]" reference]
   ["//[1]" ///
    [analysis (.only Variant Tuple Abstraction Application Analysis)]
    [synthesis (.only Synthesis)]
    ["[1][0]" translation (.only Context)]
    ["//[1]" ///
     [arity (.only Arity)]
     ["[1][0]" phase (.use "[1]#[0]" monad)]
     [reference
      [variable (.only Register Variable)]]]]]])

(the .public (apply expression archive [functionS argsS+])
  (Translator (Application Synthesis))
  (do [! ///////phase.monad]
    [functionO (expression archive functionS)
     argsO+ (monad.each ! (expression archive) argsS+)]
    (in (_.apply argsO+ functionO))))

(the capture
  (-> Register Var)
  (|>> (///reference.foreign //reference.system) as_expected))

(the (with_closure inits function_definition)
  (-> (List Expression) Computation (Operation Computation))
  (///////phase#in
   (when inits
     {.#End}
     function_definition

     _
     (|> function_definition
         (_.lambda [(|> (list.enumeration inits)
                        (list#each (|>> product.left ..capture)))
                    {.#None}])
         (_.apply inits)))))

(the @curried (_.var "curried"))
(the @missing (_.var "missing"))

(the input
  (|>> ++ //when.register))

(the .public (function expression archive [environment arity bodyS])
  (Translator (Abstraction Synthesis))
  (do [! ///////phase.monad]
    [[function_name bodyO] (/////translation.with_new_context archive
                             (do !
                               [@self (of ! each (|>> ///reference.artifact _.var)
                                          (/////translation.context archive))]
                               (/////translation.with_anchor @self
                                 (expression archive bodyS))))
     closureO+ (monad.each ! (expression archive) environment)
     .let [arityO (|> arity .int _.int)
           apply_poly (.function (_ args func)
                        (_.apply/2 (_.var "apply") func args))
           @num_args (_.var "num_args")
           @self (_.var (///reference.artifact function_name))]]
    (with_closure closureO+
      (_.letrec (list [@self (_.lambda [(list) {.#Some @curried}]
                                       (_.let (list [@num_args (_.length/1 @curried)])
                                         (<| (_.if (|> @num_args (_.=/2 arityO))
                                               (<| (_.let (list [(//when.register 0) @self]))
                                                   (_.let_values (list [[(|> (list.indices arity)
                                                                             (list#each ..input))
                                                                         {.#None}]
                                                                        (_.apply/2 (_.var "apply") (_.var "values") @curried)]))
                                                   bodyO))
                                             (_.if (|> @num_args (_.>/2 arityO))
                                               (let [arity_args (//runtime.slice (_.int +0) arityO @curried)
                                                     output_func_args (//runtime.slice arityO
                                                                                       (|> @num_args (_.-/2 arityO))
                                                                                       @curried)]
                                                 (_.begin (list (|> @self
                                                                    (apply_poly arity_args)
                                                                    (apply_poly output_func_args))))))
                                             ... (|> @num_args (_.</2 arityO))
                                             (_.lambda [(list) {.#Some @missing}]
                                                       (|> @self
                                                           (apply_poly (_.append/2 @curried @missing)))))
                                         ))])
                @self))))
