... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Type Label
                type name)
   [data
    [collection
     ["[0]" list]]]
   [math
    [number
     ["n" natural]]]
   [meta
    [compiler
     [target
      [jvm
       ["_" bytecode (.only Label Bytecode)]
       ["[0]" modifier (.only Modifier) (.use "[1]#[0]" monoid)]
       ["[0]" method (.only Method)]
       [constant
        [pool (.only Resource)]]
       ["[0]" type (.only Type)
        ["[0]" category (.only Class)]]]]]]]]
 ["[0]" // (.only)
  ["//[1]" ///
   ["[0]" runtime]
   ["[1][0]" type]
   [//////
    [arity (.only Arity)]
    [meta
     [archive
      ["[0]" unit]]]]]])

(the .public name "impl")

(the .public (type :it: arity)
  (-> (Type Class) Arity
      (Type category.Method))
  (type.method [(list)
                (list.partial :it: (list.repeated arity ////type.value))
                ////type.value
                (list)]))

(the .public modifier
  (all modifier#composite
       method.static
       //.modifier
       ))

(the .public (method :it: arity @begin body)
  (-> (Type Class) Arity Label (Bytecode Any)
      (Resource Method))
  (method.method ..modifier
    ..name
    false (..type :it: arity)
    (list)
    {.#Some (all _.composite
                 (_.set_label @begin)
                 body
                 (_.when_continuous _.areturn)
                 )}))

(the .public (call :it: arity)
  (-> (Type Class) Arity
      (Bytecode Any))
  (_.invokestatic :it: ..name (..type :it: arity)))
