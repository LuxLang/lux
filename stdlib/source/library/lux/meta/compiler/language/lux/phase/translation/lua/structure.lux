(.require
 [library
  [lux (.except Tuple Variant Synthesis)
   [abstract
    ["[0]" monad (.only do)]]
   [meta
    [target
     ["_" lua (.only Expression)]]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Translator)]
  ["[1][0]" primitive]
  ["///[1]" ////
   ["[0]" phase (.use "[1]#[0]" monad)]
   ["[1][0]" synthesis (.only Synthesis)]
   [analysis
    [complex (.only Variant Tuple)]]]])

(def .public (tuple phase archive elemsS+)
  (Translator (Tuple Synthesis))
  (when elemsS+
    {.#End}
    (phase#in (//primitive.text /////synthesis.unit))

    {.#Item singletonS {.#End}}
    (phase archive singletonS)

    _
    (|> elemsS+
        (monad.each phase.monad (phase archive))
        (phase#each _.array))))

(def .public (variant phase archive [lefts right? valueS])
  (Translator (Variant Synthesis))
  (phase#each (//runtime.variant lefts right?)
              (phase archive valueS)))
