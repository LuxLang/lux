... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Tuple Variant)
   [abstract
    ["[0]" monad (.only do)]]
   [meta
    [compiler
     [target
      ["_" lua (.only Expression)]]]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Translator)]
  ["[1][0]" primitive]
  ["///[1]" ////
   ["[0]" phase (.use "[1]#[0]" monad)]
   ["[0]" synthesis]
   [analysis
    [complex (.only Variant Tuple)]]]])

(the .public (tuple phase archive elemsS+)
  (Translator (Tuple synthesis.Term))
  (when elemsS+
    {.#End}
    (phase#in (//primitive.text synthesis.unit))

    {.#Item singletonS {.#End}}
    (phase archive singletonS)

    _
    (|> elemsS+
        (monad.each phase.monad (phase archive))
        (phase#each _.array))))

(the .public (variant phase archive [lefts right? valueS])
  (Translator (Variant synthesis.Term))
  (phase#each (//runtime.variant lefts right?)
              (phase archive valueS)))
