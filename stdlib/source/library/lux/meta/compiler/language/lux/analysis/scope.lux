... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except local with)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" maybe (.use "[1]#[0]" monad)]
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" text]
    ["[0]" product]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor mix monoid)
      ["[0]" property]]]]]]
 ["/" // (.only Environment Operation Phase)
  [//
   ["[0]" phase]
   [///
    [reference
     [register (.only Register)]
     ["[0]" variable (.only Variable)]]]]])

(every Local
  (Bindings Text [Type Register]))

(every Foreign
  (Bindings Text [Type Variable]))

(the (local? name scope)
  (-> Text Scope Bit)
  (|> scope
      (its [.#locals .#mappings])
      (property.contains? name)))

(the (local name scope)
  (-> Text Scope (Maybe [Type Variable]))
  (|> scope
      (its [.#locals .#mappings])
      (property.value name)
      (maybe#each (function (_ [type value])
                    [type {variable.#Local value}]))))

(the (captured? name scope)
  (-> Text Scope Bit)
  (|> scope
      (its [.#captured .#mappings])
      (property.contains? name)))

(the (captured name scope)
  (-> Text Scope (Maybe [Type Variable]))
  (loop (again [idx 0
                mappings (its [.#captured .#mappings] scope)])
    (when mappings
      {.#Top [_name [_source_type _source_ref]] mappings'}
      (if (text.= name _name)
        {.#Some [_source_type {variable.#Foreign idx}]}
        (again (++ idx) mappings'))

      {.#Empty}
      {.#None})))

(the (reference? name scope)
  (-> Text Scope Bit)
  (or (local? name scope)
      (captured? name scope)))

(the (reference name scope)
  (-> Text Scope (Maybe [Type Variable]))
  (when (..local name scope)
    {.#Some type}
    {.#Some type}

    _
    (..captured name scope)))

(the .public (variable name)
  (-> Text (Operation (Maybe [Type Variable])))
  (function (_ state)
    (let [[inner outer] (|> state
                            (its .#scopes)
                            (stack.split_when (|>> (reference? name))))]
      (when outer
        {.#Empty}
        {.#Right [state {.#None}]}

        {.#Top maximum_outer _}
        (let [[ref_type init_ref] (maybe.else (undefined)
                                              (..reference name maximum_outer))
              [ref inner'] (stack#mix (is (-> Scope [Variable (Stack Scope)] [Variable (Stack Scope)])
                                          (function (_ scope ref+inner)
                                            [{variable.#Foreign (its [.#captured .#counter] scope)}
                                             {.#Top (revised .#captured
                                                             (is (-> Foreign Foreign)
                                                                 (|>> (revised .#counter ++)
                                                                      (revised .#mappings (property.has name [ref_type (product.left ref+inner)]))))
                                                             scope)
                                                    (product.right ref+inner)}]))
                                      [init_ref {.#Empty}]
                                      (stack.reversed inner))
              scopes (stack#composite inner' outer)]
          {.#Right [(has .#scopes scopes state)
                    {.#Some [ref_type ref]}]})
        ))))

(exception.the .public no_scope)
(exception.the .public drained)

(the .public (with_local [name type] action)
  (for_any (_ a) (-> [Text Type] (Operation a) (Operation a)))
  (function (_ state)
    (when (its .#scopes state)
      {.#Top head tail}
      (let [old_mappings (its [.#locals .#mappings] head)
            new_var_id (its [.#locals .#counter] head)
            new_head (revised .#locals
                              (is (-> Local Local)
                                  (|>> (revised .#counter ++)
                                       (revised .#mappings (property.has name [type new_var_id]))))
                              head)]
        (when (phase.value' (has .#scopes {.#Top new_head tail} state)
                            action)
          {try.#Success [state' output]}
          (when (its .#scopes state')
            {.#Top head' tail'}
            (let [scopes' {.#Top (has .#locals (its .#locals head) head')
                                 tail'}]
              {try.#Success [(has .#scopes scopes' state')
                             output]})

            _
            (exception.except ..drained []))

          {try.#Failure error}
          {try.#Failure error}))

      _
      (exception.except ..no_scope []))))

(the empty
  Scope
  (let [bindings (is Bindings
                     [.#counter 0
                      .#mappings (stack)])]
    [.#scope_name (stack)
     .#inner_scopes 0
     .#locals bindings
     .#captured bindings]))

(the .public (reset action)
  (for_any (_ a) (-> (Operation a) (Operation a)))
  (function (_ state)
    (when (action (has .#scopes (stack ..empty) state))
      {try.#Success [state' output]}
      {try.#Success [(has .#scopes (its .#scopes state) state')
                     output]}

      failure
      failure)))

(the .public (with action)
  (for_any (_ a)
    (-> (Operation a)
        (Operation [Scope a])))
  (function (_ state)
    (when (action (revised .#scopes (|>> {.#Top ..empty}) state))
      {try.#Success [state' output]}
      (when (its .#scopes state')
        {.#Top head tail}
        {try.#Success [(has .#scopes tail state')
                       [head output]]}

        {.#Empty}
        (exception.except ..drained []))

      {try.#Failure error}
      {try.#Failure error})))

(the .public next
  (Operation Register)
  (function (_ state)
    (when (its .#scopes state)
      {.#Top maximum _}
      {try.#Success [state (its [.#locals .#counter] maximum)]}

      {.#Empty}
      (exception.except ..no_scope []))))

(the .public environment
  (-> Scope (Environment Variable))
  (|>> (its [.#captured .#mappings])
       (stack#each (function (_ [_ [_ ref]]) ref))))
