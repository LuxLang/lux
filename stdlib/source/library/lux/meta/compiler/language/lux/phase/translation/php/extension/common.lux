... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["[0]" function]]
   [data
    ["[0]" product]
    ["[0]" text]
    [collection
     ["[0]" dictionary]]]
   [meta
    [target
     ["_" php (.only Expression)]]]]]
 ["[0]" ///
  ["[1][0]" runtime (.only Operation Phase Handler Bundle)]
  ["[1][0]" primitive]
  [//
   [extension (.only Nullary Unary Binary Trinary
                     nullary unary binary trinary)]
   [//
    [extension
     ["[0]" bundle]]]]])

(the lux_procs
  Bundle
  (|> bundle.empty
      (bundle.install "is" (binary (product.uncurried _.=)))
      (bundle.install "try" (unary ///runtime.lux//try))))

(the i64_procs
  Bundle
  (<| (bundle.prefix "i64")
      (|> bundle.empty
          (bundle.install "and" (binary (product.uncurried _.bit_and)))
          (bundle.install "or" (binary (product.uncurried _.bit_or)))
          (bundle.install "xor" (binary (product.uncurried _.bit_xor)))
          (bundle.install "left-shift" (binary (product.uncurried _.bit_shl)))
          (bundle.install "logical-right-shift" (binary (product.uncurried ///runtime.i64//logic_right_shift)))
          (bundle.install "arithmetic-right-shift" (binary (product.uncurried _.bit_shr)))
          (bundle.install "=" (binary (product.uncurried _.=)))
          (bundle.install "+" (binary (product.uncurried _.+)))
          (bundle.install "-" (binary (product.uncurried _.-)))
          )))

(the integer_procs
  Bundle
  (<| (bundle.prefix "int")
      (|> bundle.empty
          (bundle.install "<" (binary (product.uncurried _.<)))
          (bundle.install "*" (binary (product.uncurried _.*)))
          (bundle.install "/" (binary (product.uncurried _./)))
          (bundle.install "%" (binary (product.uncurried _.%)))
          (bundle.install "dec" (unary _.floatval/1))
          (bundle.install "char" (unary _.chr/1)))))

(the decimal_procs
  Bundle
  (<| (bundle.prefix "dec")
      (|> bundle.empty
          (bundle.install "+" (binary (product.uncurried _.+)))
          (bundle.install "-" (binary (product.uncurried _.-)))
          (bundle.install "*" (binary (product.uncurried _.*)))
          (bundle.install "/" (binary (product.uncurried _./)))
          (bundle.install "%" (binary (product.uncurried _.%)))
          (bundle.install "=" (binary (product.uncurried _.=)))
          (bundle.install "<" (binary (product.uncurried _.<)))
          (bundle.install "int" (unary _.intval/1))
          (bundle.install "encode" (unary _.strval/1))
          (bundle.install "decode" (unary (|>> _.floatval/1 ///runtime.some)))
          )))

(the (text//index [startO partO textO])
  (Trinary (Expression Any))
  (///runtime.text//index textO partO startO))

(the text_procs
  Bundle
  (<| (bundle.prefix "text")
      (|> bundle.empty
          (bundle.install "=" (binary (product.uncurried _.=)))
          (bundle.install "<" (binary (product.uncurried _.<)))
          (bundle.install "concat" (binary (product.uncurried _.concat)))
          (bundle.install "index" (trinary text//index))
          (bundle.install "size" (unary _.strlen/1))
          (bundle.install "char" (binary (function (text//char [text idx])
                                           (|> text (_.item idx) _.ord/1))))
          (bundle.install "clip" (trinary (function (text//clip [from to text])
                                            (_.substr/3 [text from (_.- from to)]))))
          )))

(the io_procs
  Bundle
  (<| (bundle.prefix "io")
      (|> bundle.empty
          (bundle.install "log" (unary (|>> (_.concat (_.string text.new_line)) _.print/1)))
          (bundle.install "error" (unary ///runtime.io//throw!))
          (bundle.install "exit" (unary _.exit/1))
          (bundle.install "current-time" (nullary (|>> _.time/0 (_.* (_.int +1,000))))))))

(the .public bundle
  Bundle
  (<| (bundle.prefix "lux")
      (|> lux_procs
          (dictionary.composite i64_procs)
          (dictionary.composite integer_procs)
          (dictionary.composite decimal_procs)
          (dictionary.composite text_procs)
          (dictionary.composite io_procs)
          )))
