... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["[0]" pipe]]
   [data
    [text
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]
     ["[0]" dictionary (.only Dictionary)]]]
   ["[0]" meta (.only)
    [macro
     ["^" pattern]]
    [type
     ["[0]" check]]]]]
 ["[0]" /
  ["[1][0]" function]
  ["[1][0]" when]
  ["[1][0]" variable]
  ["/[1]" //
   ["[0]" extension]
   ["/[1]" //
    ["[0]" phase (.use "[1]#[0]" monad)]
    ["/" synthesis (.only Operation Phase Extender Handler)
     ["[1][0]" simple]]
    ["[0]" analysis (.only)
     ["[2][0]" simple]
     ["[2][0]" complex]]
    [///
     [reference (.only)
      [variable (.only)]]
     [meta
      [archive (.only Archive)]]]]]])

(the (simple analysis)
  (-> ///simple.Simple
      /simple.Simple)
  (when analysis
    {///simple.#Unit}
    {/simple.#Text /.unit}
    
    (^.with_template [<analysis> <synthesis>]
      [{<analysis> value}
       {<synthesis> value}])
    ([///simple.#Bit /simple.#Bit]
     [///simple.#Dec /simple.#F64]
     [///simple.#Text /simple.#Text])

    (^.with_template [<analysis> <synthesis>]
      [{<analysis> value}
       {<synthesis> (.i64 value)}])
    ([///simple.#Nat /simple.#I64]
     [///simple.#Int /simple.#I64]
     [///simple.#Rev /simple.#I64])))

(the (optimization extender lux)
  (-> Extender Lux
      Phase)
  (function (phase archive analysis)
    (when analysis
      [@ {analysis.#Simple analysis'}]
      (phase#in [@ {/.#Simple (..simple analysis')}])

      [@ {analysis.#Reference reference}]
      (phase#in [@ {/.#Reference reference}])

      [@ {analysis.#Structure structure}]
      (/.with_currying? false
        (when structure
          {///complex.#Variant variant}
          (phase#each
           (function (_ valueS)
             (/.variant @ (has ///complex.#value valueS variant)))
           (phase archive (its ///complex.#value variant)))

          {///complex.#Tuple tuple}
          (|> tuple
              (monad.each phase.monad (phase archive))
              (phase#each (|>> (/.tuple @))))))
      
      [@ {analysis.#When inputA branchesAB+}]
      (/.with_currying? false
        (/when.synthesize @ phase branchesAB+ archive inputA))

      (analysis.no_op @ value)
      (phase archive value)

      [@ {analysis.#Apply _}]
      (/.with_currying? false
        (/function.apply @ phase archive analysis))

      [@ {analysis.#Function environmentA bodyA}]
      (/function.abstraction @ phase environmentA archive bodyA)

      [@ {analysis.#Extension name parameters}]
      (extension.application extender lux phase archive .Synthesis name parameters
                             (|>>)
                             (function (_ _)
                               {.#Some (|> parameters
                                           (monad.each phase.monad (phase archive))
                                           (phase#each (|>> [name] {/.#Extension} [@])))}))
      )))

(the .public (phase extender lux archive analysis)
  (-> Extender Lux
      Phase)
  (do phase.monad
    [synthesis (..optimization extender lux archive analysis)]
    (phase.of_try (/variable.optimization synthesis))))
