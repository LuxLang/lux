... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Variant Tuple)
   [abstract
    ["[0]" monad (.only do)]]
   [meta
    [compiler
     [target
      ["_" c++]]]]]]
 [//
  ["[0]" runtime (.only Term)]
  [////
   ["[0]" phase]
   ["[0]" synthesis]
   [analysis
    [complex (.only Variant Tuple)]]]])

(def .public (variant next archive [lefts right? value])
  (Term Variant)
  (do phase.monad
    [value (next archive value)]
    (in (runtime.variant (_.int (.int lefts))
                         (_.bool right?)
                         value))))

(def .public (tuple next archive values)
  (Term Tuple)
  (let [! phase.monad]
    (when values
      {.#End}
      (of ! in runtime.unit)

      {.#Item it {.#End}}
      (next archive it)

      _
      (|> values
          (monad.each ! (next archive))
          (of ! each runtime.tuple)))))
