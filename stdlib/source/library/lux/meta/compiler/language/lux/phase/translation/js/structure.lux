... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Variant Tuple
                variant tuple)
   [abstract
    ["[0]" monad (.only do)]]
   [meta
    [compiler
     [target
      ["_" js (.only Expression)]]]]]]
 ["[0]" //
  ["[1][0]" runtime (.only Operation Phase Translator)]
  ["[1][0]" primitive]
  ["///[1]" ////
   ["[0]" phase (.use "[1]#[0]" monad)]
   ["[0]" synthesis]
   [analysis
    [complex (.only Variant Tuple)]]]])

(the .public (tuple translate archive elemsS+)
  (Translator (Tuple synthesis.Term))
  (when elemsS+
    {.#End}
    (phase#in //runtime.unit)

    {.#Item singletonS {.#End}}
    (translate archive singletonS)

    _
    (do [! phase.monad]
      [elemsT+ (monad.each ! (translate archive) elemsS+)]
      (in (_.array elemsT+)))))

(the .public (variant translate archive [lefts right? valueS])
  (Translator (Variant synthesis.Term))
  (phase#each (//runtime.variant (_.i32 (.int lefts))
                                 (//runtime.flag right?))
              (translate archive valueS)))
