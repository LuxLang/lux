... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except #Bit #Nat #Int #Rev #Frac #Text)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\format (.only Format)]]]
   [math
    [number
     ["n" nat]
     ["i" int]
     ["r" rev]
     ["f" frac]]]
   [meta
    [macro
     ["^" pattern]]]]])

(every .public Simple
  (Variant
   {#Unit}
   {#Bit Bit}
   {#Nat Nat}
   {#Int Int}
   {#Rev Rev}
   {#Frac Frac}
   {#Text Text}))

(the .public equivalence
  (Equivalence Simple)
  (implementation
   (the (= reference sample)
     (when [reference sample]
       [{#Unit} {#Unit}]
       true
       
       (^.with_template [<tag> <=>]
         [[{<tag> reference} {<tag> sample}]
          (<=> reference sample)])
       ([#Bit bit#=]
        [#Nat n.=]
        [#Int i.=]
        [#Rev r.=]
        [#Frac f.=]
        [#Text text#=])
       
       _
       false))))

(the .public (format it)
  (Format Simple)
  (when it
    {#Unit}
    "[]"

    (^.with_template [<tag> <format>]
      [{<tag> value}
       (<format> value)])
    ([#Bit %.bit]
     [#Nat %.nat]
     [#Int %.int]
     [#Rev %.rev]
     [#Frac %.frac]
     [#Text %.text])))
