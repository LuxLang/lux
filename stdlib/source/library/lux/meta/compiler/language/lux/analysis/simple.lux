... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except #Bit #Nat #Int #Rev #Dec #Text)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" bit]
    ["[0]" text (.only Injection)
     ["%" \\injection]]]
   [math
    [number
     ["n" nat]
     ["i" int]
     ["r" rev]
     ["d" dec]]]
   [meta
    [macro
     ["[0]" template]]]]])

(every .public Simple
  (Variant
   {#Unit}
   {#Bit Bit}
   {#Nat Nat}
   {#Int Int}
   {#Rev Rev}
   {#Dec Dec}
   {#Text Text}))

(the .public equivalence
  (Equivalence Simple)
  (implementation
   (the (= expected actual)
     (`` (when [expected actual]
           [{#Unit} {#Unit}]
           true
           
           (,, (template.with [<tag> <=>]
                 [[{<tag> expected} {<tag> actual}]
                  (<=> expected actual)]

                 [#Bit bit.=]
                 [#Nat n.=]
                 [#Int i.=]
                 [#Rev r.=]
                 [#Dec d.=]
                 [#Text text.=]))
           
           _
           false)))))

(alias [=]
       ..equivalence)

(the .public (injection it)
  (Injection Simple)
  (`` (when it
        {#Unit}
        "[]"

        (,, (template.with [<tag> <injection>]
              [{<tag> value}
               (<injection> value)]

              [#Bit bit.as_text]
              [#Nat %.nat]
              [#Int %.int]
              [#Rev %.rev]
              [#Dec %.dec]
              [#Text %.text])))))
