... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except #Bit #Nat #Int #Rev #Dec #Text)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\injection (.only Injection)]]]
   [math
    [number
     ["n" nat]
     ["i" int]
     ["r" rev]
     ["d" dec]]]
   [meta
    [macro
     ["^" pattern]]]]])

(every .public Simple
  (Variant
   {#Unit}
   {#Bit Bit}
   {#Nat Nat}
   {#Int Int}
   {#Rev Rev}
   {#Dec Dec}
   {#Text Text}))

(the .public equivalence
  (Equivalence Simple)
  (implementation
   (the (= expected actual)
     (when [expected actual]
       [{#Unit} {#Unit}]
       true
       
       (^.with_template [<tag> <=>]
         [[{<tag> expected} {<tag> actual}]
          (<=> expected actual)])
       ([#Bit bit#=]
        [#Nat n.=]
        [#Int i.=]
        [#Rev r.=]
        [#Dec d.=]
        [#Text text#=])
       
       _
       false))))

(the .public (format it)
  (Injection Simple)
  (when it
    {#Unit}
    "[]"

    (^.with_template [<tag> <format>]
      [{<tag> value}
       (<format> value)])
    ([#Bit %.bit]
     [#Nat %.nat]
     [#Int %.int]
     [#Rev %.rev]
     [#Dec %.dec]
     [#Text %.text])))
