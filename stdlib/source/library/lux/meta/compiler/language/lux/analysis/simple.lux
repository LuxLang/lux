... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except #Bit #Natural #Int #Rev #Decimal #Text)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" bit]
    ["[0]" text (.only Injection)
     ["%" \\injection]]]
   [math
    [number
     ["n" natural]
     ["i" int]
     ["r" rev]
     ["d" decimal]]]
   [meta
    [macro
     ["[0]" template]]]]])

(every .public Simple
  (Variant
   {#Unit}
   {#Bit Bit}
   {#Natural Natural}
   {#Int Int}
   {#Rev Rev}
   {#Decimal Decimal}
   {#Text Text}))

(the .public equivalence
  (Equivalence Simple)
  (implementation
   (the (= expected actual)
     (`` (when [expected actual]
           [{#Unit} {#Unit}]
           true
           
           (,, (template.with [<tag> <=>]
                 [[{<tag> expected} {<tag> actual}]
                  (<=> expected actual)]

                 [#Bit bit.=]
                 [#Natural n.=]
                 [#Int i.=]
                 [#Rev r.=]
                 [#Decimal d.=]
                 [#Text text.=]))
           
           _
           false)))))

(alias [=]
       ..equivalence)

(the .public (injection it)
  (Injection Simple)
  (`` (when it
        {#Unit}
        "[]"

        (,, (template.with [<tag> <injection>]
              [{<tag> value}
               (<injection> value)]

              [#Bit bit.as_text]
              [#Natural %.natural]
              [#Int %.int]
              [#Rev %.rev]
              [#Decimal %.decimal]
              [#Text %.text])))))
