... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except i64)
   [data
    ["[0]" text]]
   [meta
    [compiler
     [target
      ["_" c++]]]]]]
 [//
  ["[0]" runtime]
  ["[0]" type]])

(the .public (bit it)
  (-> Bit
      _.Expression)
  (if it
    runtime.true
    runtime.false))

(the .public i64'
  (-> (I64 Any)
      _.Expression)
  (|>> .int
       _.int
       _.int64_t))

(the .public i64
  (-> (I64 Any)
      _.Expression)
  (|>> ..i64'
       (runtime.simple runtime.I64)))

(the .public f64'
  (-> Dec
      _.Expression)
  _.double)

(the .public f64
  (-> Dec
      _.Expression)
  (|>> ..f64'
       (runtime.simple runtime.F64)))

(the .public (text' it)
  (-> Text
      _.Expression)
  (let [size (|> it text.size .int _.int _.int64_t)]
    (|> it
        _.u32_string
        (runtime.static_text size))))

(the .public text
  (-> Text
      _.Expression)
  (|>> ..text'
       (runtime.simple runtime.Text)))
