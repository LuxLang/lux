... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [hash (.only Hash)]
    [format (.only Format)]
    ["[0]" order (.only Order)]]
   [data
    ["[0]" text (.use "[1]#[0]" monoid)]
    ["[0]" product]]]])

(the with_template' (.in_module# .prelude .with_template))

... (every Symbol
...   [Text Text])

(with_template' [<name>]
  [(the .public (<name> [module short])
     (-> Symbol
         Text)
     <name>)]

  [module]
  [short]
  )

(the .public hash
  (Hash Symbol)
  (product.hash text.hash text.hash))

(the .public equivalence
  (Equivalence Symbol)
  (by ..hash equivalence))

(alias [=]
       ..equivalence)

(the .public order
  (Order Symbol)
  (implementation
   (the equivalence ..equivalence)
   (the (< [moduleP shortP] [moduleS shortS])
     (if (text.= moduleP moduleS)
       (by text.order < shortP shortS)
       (by text.order < moduleP moduleS)))))

(order.definitions [] Symbol ..order)

(the .public separator
  ".")

(the .public absolute
  (Format Text Symbol)
  (implementation
   (the (injection [module short])
     (when module
       ""
       short
       
       _
       (all text#composite module ..separator short)))
   
   (the (projection input)
     (when (text.all_split_by ..separator input)
       (list short)
       {.#Right ["" short]}

       (list module short)
       {.#Right [module short]}

       _
       {.#Left (text#composite "Invalid format for Symbol: " input)}))))

(the .public (relative expected)
  (-> Text
      (Format Text Symbol))
  (implementation
   (the (injection [module short])
     (when module
       ""
       short
       
       .prelude
       (all text#composite ..separator short)
       
       _
       (all text#composite
            (if (text.= expected module)
              ..separator
              module)
            ..separator short)))
   
   (the (projection input)
     (when (text.all_split_by ..separator input)
       (list short)
       {.#Right ["" short]}

       (list "" short)
       {.#Right [.prelude short]}

       (list module short)
       {.#Right [module short]}

       (list "" "" short)
       {.#Right [expected short]}

       _
       {.#Left (text#composite "Invalid format for Symbol: " input)}))))

(the do (.in_module# .prelude .do))

(the .public of
  (let [meta#monad (.in_module# .prelude .meta#monad)
        global_symbol (.in_module# .prelude .global_symbol)
        as_text (.in_module# .prelude .as_text)
        failure (.in_module# .prelude .failure)]
    (macro (_ tokens)
      (when tokens
        {.#Item [_ {.#Symbol it}] {.#End}}
        (do meta#monad
          [module,short (global_symbol it)
           .let [[module short] module,short]]
          (in (list (` [(, (as_text module))
                        (, (as_text short))]))))

        _
        (failure .wrong_syntax)))))

(the .public as_text
  (text.Injection Symbol)
  (by ..absolute injection))
