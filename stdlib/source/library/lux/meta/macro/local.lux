... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except with let)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["<>" parser]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" product]
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)
      ["[0]" property]]]]
   ["[0]" meta (.only)
    ["[0]" code (.only)
     ["<[1]>" \\parser]]]]]
 ["[0]" // (.only)
  [syntax (.only syntax)]])

(exception.def .public (unknown_module module)
  (Exception Text)
  (exception.report
   (list ["Module" (text.format module)])))

(with_template [<name>]
  [(exception.def .public (<name> [module definition])
     (Exception [Text Text])
     (exception.report
      (list ["Module" (text.format module)]
            ["Definition" (text.format definition)])))]

  [cannot_shadow_definition]
  [unknown_definition]
  )

(def (with_module name body)
  (All (_ of)
    (-> Text (-> Module (Try [Module of]))
        (Meta of)))
  (function (_ compiler)
    (when (|> compiler (the .#modules) (property.value name))
      {.#Some module}
      (when (body module)
        {try.#Success [module' output]}
        {try.#Success [(revised .#modules (property.has name module') compiler)
                       output]}
        
        {try.#Failure error}
        {try.#Failure error})

      {.#None}
      (exception.except ..unknown_module [name]))))

(def (push_one [name macro])
  (-> [Symbol Macro]
      (Meta Any))
  (do meta.monad
    [[module_name definition_name] (meta.normal name)
     .let [definition (is Global {.#Definition [.Macro macro]})
           add_macro! (is (-> (property.List [Bit Global]) (property.List [Bit Global]))
                          (property.has definition_name [false definition]))]]
    (..with_module module_name
      (function (_ module)
        (when (|> module (the .#definitions) (property.value definition_name))
          {.#None}
          {try.#Success [(revised .#definitions add_macro! module)
                         []]}
          
          {.#Some _}
          (exception.except ..cannot_shadow_definition [module_name definition_name]))))))

(def (pop_one name)
  (-> Symbol
      (Meta Any))
  (do meta.monad
    [[module_name definition_name] (meta.normal name)
     .let [lacks_macro! (is (-> (property.List [Bit Global]) (property.List [Bit Global]))
                            (property.lacks definition_name))]]
    (..with_module module_name
      (function (_ module)
        (when (|> module (the .#definitions) (property.value definition_name))
          {.#Some _}
          {try.#Success [(revised .#definitions lacks_macro! module)
                         []]}

          {.#None}
          (exception.except ..unknown_definition [module_name definition_name]))))))

(def (pop_all macros self)
  (-> (List Symbol) Symbol
      Macro)
  (//.macro
    (function (_ _)
      (do [! meta.monad]
        [_ (monad.each ! ..pop_one macros)
         _ (..pop_one self)
         compiler meta.compiler_state]
        (in (when (the .#expected compiler)
              {.#Some _}
              (list (' []))
              
              {.#None}
              (list)))))))

(def .public (push macros)
  (-> (List [Symbol Macro])
      (Meta Code))
  (do meta.monad
    [_ (monad.each meta.monad ..push_one macros)
     seed meta.seed
     g!pop (//.symbol "pop")
     _ (.let [g!pop (is Symbol
                        ["" (code.format g!pop)])]
         (..push_one [g!pop (..pop_all (list#each product.left macros) g!pop)]))]
    (in (` ((, g!pop))))))

(def .public (with macros expression? body)
  (-> (List [Symbol Macro]) Bit Code
      (Meta (List Code)))
  (do [! meta.monad]
    [g!pop (..push macros)]
    (.if expression?
      (//.with_symbols [g!body]
        (in (list (` (.let [(, g!body) (, body)]
                       (exec
                         (, g!pop)
                         (, g!body)))))))
      (in (list body
                g!pop)))))

(def .public let
  (syntax (_ [locals (<code>.tuple (<>.some (<>.and <code>.local <code>.any)))
              body <code>.any])
    (do [! meta.monad]
      [here meta.current_module_name
       locals (monad.each ! (function (_ [name value])
                              (|> value
                                  (meta.eval .Macro)
                                  (of ! each (|>> (as .Macro)
                                                  [[here name]]))))
                          locals)
       expression? (is (Meta Bit)
                       (function (_ lux)
                         {try.#Success [lux (when (the .#expected lux)
                                              {.#None}
                                              false

                                              {.#Some _}
                                              true)]}))]
      (..with locals expression? body))))
