... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except global revised)
   [abstract
    [monad (.only do)]]
   [control
    ["?" parser]
    ["[0]" try (.only Try) (.use "[1]#[0]" functor)]
    ["[0]" exception (.only Exception)]
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" text (.use "[1]#[0]" equivalence monoid)]
    [collection
     ["[0]" list (.only)
      ["[0]" property]]]]
   ["[0]" meta (.only)
    [type (.only sharing by_example)]
    ["[0]" symbol (.use "[1]#[0]" absolute)]
    ["[0]" code (.only)
     ["?[1]" \\parser]]]]]
 ["[0]" // (.only)
  [syntax (.only syntax)
   ["[0]" export]]])

(every .public Stack
  List)

(.the Stack'
  (template (_ value)
    [[(Stack value)
      Symbol]]))

(with_template [<name> <type> <format>]
  [(exception.the .public (<name> it)
     (Exception <type>)
     (exception.report
      (list ["Definition" (<format> it)])))]

  [not_a_definition Symbol symbol#encoded]
  [not_a_global Symbol symbol#encoded]
  [not_a_module Text text.format]
  [no_example Symbol symbol#encoded]
  )

(.the (global it)
  (-> Symbol
      (Meta Any))
  (do meta.monad
    [.let [[@ expected_name] it]
     defs (meta.definitions @)]
    (when (list.one (function (_ [actual_name [exported? [type value]]])
                      (if (text#= expected_name actual_name)
                        {.#Some value}
                        {.#None}))
                    defs)
      {try.#Success it}
      (in it)
      
      {try.#Failure _}
      (meta.failure (exception.error ..not_a_definition [it])))))

(exception.the .public no_active_context)

(.the .public (peek' [_ context])
  (All (_ of)
    (-> (Stack' of)
        (Meta of)))
  (do meta.monad
    [stack (..global context)]
    (when (|> stack
              (as (Stack Any))
              list.head)
      {try.#Success top}
      (in (as_expected top))
      
      {try.#Failure _}
      (meta.failure (exception.error ..no_active_context [])))))

(.the .public peek
  (syntax (_ [g!it (of ?.monad each code.symbol ?code.global)])
    (in (list (` (..peek' [(, g!it) (.symbol (, g!it))]))))))

(.the .public (search' ? [_ context])
  (All (_ of)
    (-> (Predicate of) (Stack' of)
        (Meta of)))
  (do meta.monad
    [stack (..global context)]
    (when (|> stack
              (as (Stack Any))
              (list.example (as (Predicate Any) ?)))
      {try.#Success it}
      (in (as_expected it))
      
      {try.#Failure _}
      (meta.failure (exception.error ..no_example [context])))))

(.the .public search
  (syntax (_ [g!? ?code.any
              g!context (of ?.monad each code.symbol ?code.global)])
    (in (list (` (..search' (, g!?) [(, g!context) (.symbol (, g!context))]))))))

(.the (alter on_definition [_ definition])
  (All (_ value)
    (-> (-> Symbol Definition (Try Definition)) (Stack' value)
        (Meta Any)))
  (function (_ lux)
    (let [[@ context] definition
          on_global (is (-> Global (Try Global))
                        (function (_ it)
                          (when it
                            {.#Definition it}
                            (try#each (|>> {.#Definition}) (on_definition definition it))
                            
                            _
                            (exception.except ..not_a_definition [definition]))))
          on_globals (is (-> (property.List [Bit Global]) (Try (property.List [Bit Global])))
                         (function (_ globals)
                           (when (property.value context globals)
                             {try.#Success [exported? global]}
                             (try#each (function (_ global)
                                         (property.has context [exported? global] globals))
                                       (on_global global))
                             
                             failure
                             (exception.except ..not_a_global [definition]))))
          on_module (is (-> Module (Try Module))
                        (function (_ module)
                          (try#each (function (_ globals)
                                      (has .#definitions globals module))
                                    (on_globals (its .#definitions module)))))
          on_lux (is (-> Lux (Try Lux))
                     (function (_ lux)
                       (when (property.value @ (its .#modules lux))
                         {try.#Success module}
                         (try#each (function (_ module)
                                     (.revised .#modules (property.has @ module) lux))
                                   (on_module module))
                         
                         failure
                         (exception.except ..not_a_module [@]))))]
      (when (on_lux lux)
        {try.#Success it}
        {try.#Success [it []]}
        
        {try.#Failure error}
        ((meta.failure error) lux)))))

(.the .public (push' top)
  (All (_ value)
    (-> value (Stack' value)
        (Meta Any)))
  (alter (function (_ _ [type stack])
           (|> stack
               (as (Stack Any))
               {.#Item top}
               (is (Stack Any))
               [type]
               {try.#Success}))))

(.the .public push
  (syntax (_ [g!it ?code.any
              g!context (of ?.monad each code.symbol ?code.global)])
    (in (list (` (..push' (, g!it) [(, g!context) (.symbol (, g!context))]))))))

(.the .public (revised' ? !)
  (All (_ value)
    (-> (Maybe (Predicate value)) (-> value value) (Stack' value)
        (Meta Any)))
  (alter (function (_ @ [type stack])
           (let [stack (sharing [value]
                         (is (-> value value)
                             !)
                         (is (Stack value)
                             (as_expected stack)))]
             (when ?
               {.#Some ?}
               (do try.monad
                 [stack (loop (again [stack stack])
                          (when stack
                            (list.partial top stack')
                            (if (? top)
                              (in (list.partial (! top) stack'))
                              (do try.monad
                                [stack' (again stack')]
                                (in (list.partial top stack'))))

                            _
                            (exception.except ..no_example [@])))]
                 (in [type stack]))
               
               {.#None}
               (when stack
                 (list.partial top stack')
                 (|> stack'
                     (list.partial (! top))
                     (is (Stack Any))
                     [type]
                     {try.#Success})
                 
                 _
                 (exception.except ..no_example [@])))))))

(.the .public revised
  (syntax (_ [g!predicate ?code.any
              g!revision ?code.any
              g!context (of ?.monad each code.symbol ?code.global)])
    (in (list (` (..revised' (, g!predicate)
                             (, g!revision)
                             [(, g!context) (.symbol (, g!context))]))))))

(.the .public pop''
  (All (_ value)
    (-> (Stack' value)
        (Meta Any)))
  (alter (function (_ _ [type value])
           (|> value
               (as (Stack Any))
               list.tail
               (try.else value)
               [type]
               {try.#Success}))))

(.the .public pop'
  (syntax (_ [expression? ?code.bit
              context ?code.global])
    (do meta.monad
      [_ (..pop'' [(list) context])]
      (in (if expression?
            (list (' []))
            (list))))))

(.the .public pop
  (syntax (_ [g!context (of ?.monad each code.symbol ?code.global)])
    (in (list (` (..pop'' [(, g!context) (.symbol (, g!context))]))))))

(.the .public def
  (syntax (_ [.let [! ?.monad
                    ?local (of ! each code.local ?code.local)]

              [export_$? $] (?code.tuple (export.with ?code.local))
              [export_expression? g!expression] (?code.tuple (export.with ?local))
              [export_declaration? g!declaration] (?code.tuple (export.with ?local))
              
              context_type ?code.any])
    (do [! meta.monad]
      [@ meta.current_module_name
       .let [g!context (code.symbol [@ $])]]
      (//.with_symbols [g!it g!body g!_]
        (in (list (` (.the (, export_$?) (, (code.local $))
                       (..Stack (, context_type))
                       (list)))
                  (` (.the (, export_expression?) ((, g!expression) (, g!it) (, g!body))
                       (-> (, context_type) Code
                           (Meta Code))
                       (do meta.monad
                         [(, g!_) (..push (, g!it) (, g!context))]
                         ((,' in) (` (let [((,' ,') (, g!body)) ((,' ,) (, g!body))
                                           ((,' ,') (, g!_)) (..pop' #1 (, g!context))]
                                       ((,' ,') (, g!body))))))))
                  (` (.the (, export_declaration?) ((, g!declaration) (, g!it) (, g!body))
                       (-> (, context_type) Code
                           (Meta (List Code)))
                       (do meta.monad
                         [(, g!_) (..push (, g!it) (, g!context))]
                         ((,' in) (list (, g!body)
                                        (` (..pop' #0 (, g!context))))))))
                  ))))))
