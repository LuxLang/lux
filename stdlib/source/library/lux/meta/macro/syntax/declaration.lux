... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Declaration)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    ["<>" parser (.use "[1]#[0]" monad)]]
   [data
    ["[0]" product]
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\parser (.only Parser)]]]]])

(type .public Declaration
  (Record
   [#name Text
    #arguments (List Text)]))

(def .public equivalence
  (Equivalence Declaration)
  (all product.equivalence
       text.equivalence
       (list.equivalence text.equivalence)
       ))

(def .public parser
  (Parser Declaration)
  (<>.either (<>.and <code>.local
                     (<>#in (list)))
             (<code>.form (<>.and <code>.local
                                  (<>.some <code>.local)))))

(def .public (format value)
  (-> Declaration Code)
  (let [g!name (code.local (its #name value))]
    (when (its #arguments value)
      {.#End}
      g!name
      
      arguments
      (` ((, g!name) (,* (list#each code.local arguments)))))))
