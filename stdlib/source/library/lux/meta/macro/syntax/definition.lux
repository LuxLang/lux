(.require
 [library
  [lux (.except Definition)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]]
   [control
    ["<>" parser]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" sum]
    ["[0]" product]
    ["[0]" bit]
    ["[0]" text (.only)
     ["%" \\format]]
    [collection
     ["[0]" list]]]
   ["[0]" meta (.only)
    ["[0]" location]
    [macro
     ["[0]" expansion]]
    ["[0]" code (.only)
     ["<[1]>" \\parser (.only Parser)]]]]]
 ["[0]" // (.only)
  ["[1][0]" check (.only Check)]])

(type .public Definition
  (Record
   [#name Text
    #value (Either Check
                   Code)
    #export? Bit]))

(def .public equivalence
  (Equivalence Definition)
  (all product.equivalence
       text.equivalence
       (all sum.equivalence
            //check.equivalence
            code.equivalence
            )
       bit.equivalence
       ))

(def extension
  (symbol .def#))

(def dummy
  Code
  (` [.#module (, (code.text (the .#module location.dummy)))
      .#line   (, (code.nat (the .#line location.dummy)))
      .#column (, (code.nat (the .#column location.dummy)))]))

(def .public (format (open "_[0]"))
  (-> Definition Code)
  (` ((, (code.symbol ..extension))
      (, (code.local _#name))
      (, (when _#value
           {.#Left check}
           (//check.format check)

           {.#Right value}
           value))
      (, (code.bit _#export?)))))

(def .public (parser compiler)
  (-> Lux (Parser Definition))
  (do [! <>.monad]
    [raw <code>.any
     me_raw (|> raw
                expansion.total
                (meta.result compiler)
                <>.of_try)]
    (<| (<code>.locally me_raw)
        <code>.form
        (<>.after (<code>.this_symbol ..extension))
        (all <>.and
             <code>.local
             (<>.or //check.parser
                    <code>.any)
             <code>.bit
             ))))

(exception.def .public (lacks_type definition)
  (Exception Definition)
  (exception.report
   (list ["Definition" (%.code (..format definition))])))

(def .public (typed compiler)
  (-> Lux (Parser Definition))
  (do <>.monad
    [definition (..parser compiler)
     _ (when (the #value definition)
         {.#Left _}
         (in [])

         {.#Right _}
         (<>.of_try (exception.except ..lacks_type [definition])))]
    (in definition)))
