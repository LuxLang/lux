... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except local symbol function let)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["[0]" try]]
   [data
    ["[0]" text (.use "[1]#[0]" monoid)]
    [collection
     ["[0]" list (.use "[1]#[0]" monoid monad)]]]]]
 ["[0]" /// (.only)
  ["[0]" code]
  ["[0]" location]
  ["[0]" symbol (.use "[1]#[0]" absolute)]
  ["[0]" binding]])

(the with_template' (.in_module# .prelude .with_template))

(the .public (single syntax)
  (-> Code
      (Meta (List Code)))
  (when syntax
    [_ {.#Form {.#Item [[_ {.#Symbol name}] args]}}]
    (do ///.monad
      [?macro (///.try (binding.macro name))]
      (when ?macro
        {try.#Success macro}
        ((as Macro' macro) args)
        
        {try.#Failure _}
        (of ///.monad in (list syntax))))

    _
    (of ///.monad in (list syntax))))

(the .public (complete syntax)
  (-> Code
      (Meta (List Code)))
  (when syntax
    [_ {.#Form {.#Item [[_ {.#Symbol name}] args]}}]
    (do ///.monad
      [?macro (///.try (binding.macro name))]
      (when ?macro
        {try.#Success macro}
        (do [! ///.monad]
          [top_level_complete ((as Macro' macro) args)]
          (|> top_level_complete
              (monad.each ///.monad complete)
              (of ! each list#conjoint)))
        
        {try.#Failure _}
        (of ///.monad in (list syntax))))

    _
    (of ///.monad in (list syntax))))

(the .public (total syntax)
  (-> Code
      (Meta (List Code)))
  (when syntax
    [_ {.#Form {.#Item [[_ {.#Symbol name}] args]}}]
    (do ///.monad
      [?macro (///.try (binding.macro name))]
      (when ?macro
        {try.#Success macro}
        (do ///.monad
          [complete ((as Macro' macro) args)
           complete' (monad.each ///.monad total complete)]
          (in (list#conjoint complete')))
        
        {try.#Failure _}
        (do ///.monad
          [parts' (monad.each ///.monad total (list.partial (code.symbol name) args))]
          (in (list (code.form (list#conjoint parts')))))))

    [_ {.#Form {.#Item [harg targs]}}]
    (do ///.monad
      [harg+ (total harg)
       targs+ (monad.each ///.monad total targs)]
      (in (list (code.form (list#composite harg+ (list#conjoint (is (List (List Code)) targs+)))))))

    [_ {.#Variant members}]
    (do ///.monad
      [members' (monad.each ///.monad total members)]
      (in (list (code.variant (list#conjoint members')))))

    [_ {.#Tuple members}]
    (do ///.monad
      [members' (monad.each ///.monad total members)]
      (in (list (code.tuple (list#conjoint members')))))

    _
    (of ///.monad in (list syntax))))

(the .public cannot_expand_to_more_than_1
  Error
  "Macro expanded to more than 1 element.")

(the .public (one token)
  (-> Code
      (Meta Code))
  (do ///.monad
    [token+ (..complete token)]
    (when token+
      (list token')
      (in token')

      _
      (///.failure ..cannot_expand_to_more_than_1))))

(with_template' [<macro> <func>]
  [(the .public <macro>
     (.let [[module _] (.symbol .._)
            [_ short] (.symbol <macro>)
            macro_name [module short]]
       (macro (_ tokens)
         (when (is (Maybe [Bit Code])
                   (when tokens
                     (list [_ {.#Text "omit"}]
                           token)
                     {.#Some [true token]}

                     (list token)
                     {.#Some [false token]}

                     _
                     {.#None}))
           {.#Some [omit? token]}
           (do ///.monad
             [location ///.location
              output (<func> token)
              .let [_ (.log!# (all text#composite
                                   (symbol#injection macro_name) " " (location.injection location)
                                   (|> output
                                       (list#each (|>> code.absolute
                                                       (all text#composite text.\n text.\t)))
                                       text.together)
                                   text.\n))]]
             (in (if omit?
                   (list)
                   output)))

           {.#None}
           (///.failure .wrong_syntax)))))]

  [log_single!   ..single]
  [log_complete! ..complete]
  [log_total!    ..total]
  )

(the .public let
  (<| (.in_module# .prelude)
      .expansion#let))
