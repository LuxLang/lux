... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)]])

(type .public (Co it)
  (-> Any it))

(type .public (Contra it)
  (-> it Any))

(type .public (In it)
  (-> it it))

(type .public (Mutable r w)
  (Nominal "#Mutable" [(-> w r)]))

(with_template [<name> <type>]
  [(def .public <name>
     (template (<name> it)
       [((.is (.All (_ r w) <type>)
              (.|>> .as_expected))
         it)]))]

  [read (.-> (..Mutable r w) r)]
  [write (.-> w (..Mutable r w))]
  )

(type .public (Read_Only a)
  (Mutable a Nothing))

(type .public (Write_Only a)
  (Mutable Any a))

(with_template [<name> <type>]
  [(def .public <name>
     (template (<name> it)
       [((.is (.All (_ r w) <type>)
              (.|>>))
         it)]))]

  [read_only (.-> (..Mutable r w) (..Read_Only r))]
  [write_only (.-> (..Mutable r w) (..Write_Only w))]
  )
