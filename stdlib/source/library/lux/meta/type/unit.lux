... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... https://en.wikipedia.org/wiki/Dimensional_analysis
(.require
 [library
  [lux (.except type)
   [abstract
    [equivalence (.only Equivalence)]
    [order (.only Order)]
    [enum (.only Enum)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    [number
     ["i" int]]]
   [meta
    ["[0]" code
     ["<[1]>" \\projection]]
    ["[0]" macro (.only)
     ["[0]" syntax]
     ["[0]" template]]]]]
 ["[0]" // (.only)
  ["[0]" nominal]])

(nominal.every .public (Measure scale unit)
  Int

  (the .public measure
    (for_any (_ scale unit)
      (-> Int
          (Measure scale unit)))
    (|>> nominal.abstraction))

  (the .public number
    (for_any (_ scale unit)
      (-> (Measure scale unit)
          Int))
    (|>> nominal.representation))

  (the .public equivalence
    (for_any (_ scale unit)
      (Equivalence (Measure scale unit)))
    (implementation
     (the (= expected actual)
       (i.= (nominal.representation expected) (nominal.representation actual)))))

  (alias [=]
         ..equivalence)

  (the .public order
    (for_any (_ scale unit)
      (Order (Measure scale unit)))
    (implementation
     (the equivalence ..equivalence)
     
     (the (< reference it)
       (i.< (nominal.representation reference) (nominal.representation it)))))

  (the .public enum
    (for_any (_ scale unit)
      (Enum (Measure scale unit)))
    (implementation
     (the order ..order)
     (the succ (|>> nominal.representation ++ nominal.abstraction))
     (the pred (|>> nominal.representation -- nominal.abstraction))))

  (template.with [<name> <op>]
    [(the .public (<name> param subject)
       (for_any (_ scale unit)
         (-> (Measure scale unit) (Measure scale unit)
             (Measure scale unit)))
       (nominal.abstraction
        (<op> (nominal.representation param)
              (nominal.representation subject))))]

    [+ i.+]
    [- i.-]
    )

  (template.with [<name> <op> <p> <s> <p*s>]
    [(the .public (<name> param subject)
       (for_any (_ scale p s)
         (-> (Measure scale <p>) (Measure scale <s>)
             (Measure scale <p*s>)))
       (nominal.abstraction
        (<op> (nominal.representation param)
              (nominal.representation subject))))]

    [* i.* p s [p s]]
    [/ i./ p [p s] s]
    )

  (.every .public (Unit of)
    (Interface
     (is (-> Int
             (Measure Any of))
         in)
     (is (-> (Measure Any of)
             Int)
         out)
     (is (-> (Measure Any of)
             Text)
         injection)))

  (the .public (unit descriptor)
    (for_some (_ of)
      (-> Text
          (Unit of)))
    (implementation
     (the in ..measure)
     (the out ..number)
     (the injection (|>> ..number %.int (text.suffix descriptor)))))
  )

(the .public type
  (syntax.macro (_ [it <code>.any])
    (macro.with_symbols [g!a]
      (in (list (` (//.by_example [(, g!a)]
                     (is (..Unit (, g!a))
                         (, it))
                     (, g!a))))))))

(template.with [<descriptor> <unit> <type>]
  [(the .public <unit>
     (..unit <descriptor>))
   
   (the .public <type>
     (let [[module _] (symbol .._)
           [_ short] (symbol <type>)]
       {.#Named [module short]
                (..type <unit>)}))]

  ["g" gram Gram]
  ["m" meter Meter]
  ["l" litre Litre]
  ["s" second Second]
  )
