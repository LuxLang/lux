... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

... https://en.wikipedia.org/wiki/Object-oriented_programming
(.require
 [library
  [lux (.except every as)
   ["[0]" debug]
   [abstract
    [monad (.only do)]]
   [control
    ["?" projection]
    ["[0]" maybe (.use "[1]#[0]" functor)]
    [function
     ["[0]" mixin (.only Mixin)]]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" monad)]]]
   ["[0]" meta (.only)
    ["[0]" code (.only)
     ["?[1]" \\projection]]
    ["[0]" macro (.only)
     [template (.only with_locals)]
     [syntax (.only syntax)
      ["[0]" export]]]
    [type
     ["[0]" nominal]]]]])

... https://en.wikipedia.org/wiki/Instance_(computer_science)#Object-oriented_programming
(nominal.every .public (Instance interface state)
  (Record
   [#class (interface state)
    #state state])

  (the .public (object class state)
    (All (_ interface state)
      (-> (interface state) state
          (Instance interface state)))
    (nominal.abstraction
     [#class class
      #state state]))

  (the .public class
    (All (_ interface state)
      (-> (Instance interface state)
          (interface state)))
    (|>> nominal.representation
         (its #class)))

  (the .public state
    (All (_ interface state)
      (-> (Instance interface state)
          state))
    (|>> nominal.representation
         (its #state)))
  )

(.every .public (Object interface)
  (Ex (_ state)
    (Instance interface state)))

... https://en.wikipedia.org/wiki/Interface_(object-oriented_programming)
(the .public every
  (syntax (_ [['export 'name '*parameters 'state]
              (export.with
                (?code.form (all ?.and
                                 ?code.any
                                 (?code.tuple (?.some ?code.any))
                                 ?code.any)))
              '*record (?code.tuple (?.some (?.and ?code.any ?code.any)))])
    (in (list (` (.every (, 'export) ((, 'name) (,* '*parameters) (, 'state))
                   (Record
                    [(,* (|> '*record
                             (list#each (function (_ ['member 'method])
                                          (list 'member
                                                (` ((, 'method) ((, 'name) (,* '*parameters)) (, 'state))))))
                             list#conjoint))])))))))

(the Complete
  (template (_ input output interface state)
    [(-> [(Instance interface state) input]
         output)]))

(the Partial
  (template (_ input output interface state)
    [(Mixin [(Instance interface state) input]
            output)]))

... https://en.wikipedia.org/wiki/Method_(computer_programming)
(.every .public (Method input output interface state)
  (Record
   [#complete (Complete input output interface state)
    #partial (Partial input output interface state)]))

(the .public (method partial)
  (All (_ interface state input output)
    (-> (Partial input output interface state)
        (Method input output interface state)))
  [#complete (mixin.fixed partial)
   #partial partial])

(the .public on
  (with_locals ['it]
    (template (_ <method> <input> <it>)
      [(let ['it <it>]
         ((its [<method> ..#complete] (..class 'it))
          ['it <input>]))])))

... https://en.wikipedia.org/wiki/Method_overriding
(the Change
  (template (_ of)
    [(-> of
         of)]))

(the Revision
  (template (_ inner outer)
    [(-> (Change inner)
         (Change outer))]))

(the (override' revision partial class)
  (All (_ interface state input output)
    (-> (Revision (Method input output interface state)
                  (interface state))
        (Partial input output interface state)
        (Change (interface state))))
  (revision (function (_ method)
              (let [partial (mixin.mixed partial (its #partial method))]
                [#complete (mixin.fixed partial)
                 #partial partial]))
            class))

(the .public override
  (template (_ <method> <partial> <class>)
    [((debug.private ..override') (.revised <method>) <partial> <class>)]))

(for .old
     (these)
     
     (the .public (as class object)
       (All (_ interface state)
         (-> (interface state) (Object interface Any)
             (Maybe (Object interface state))))
       (if (same? class (..class object))
         {.#Some (as_expected object)}
         {.#None})))
