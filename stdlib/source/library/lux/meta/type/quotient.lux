... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except type)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]]
   [control
    ["?" parser]]
   ["[0]" meta (.only)
    ["[0]" code
     ["<[1]>" \\parser]]
    ["[0]" macro (.only)
     [syntax (.only syntax)]]]]]
 ["[0]" // (.only)
  ["?[1]" \\parser]
  ["[0]" nominal (.except def)]])

(nominal.def .public (Class super sub %)
  (-> super
      sub)

  (def .public class
    (All (_ super sub)
      (Ex (_ %)
        (-> (-> super sub)
            (Class super sub %))))
    (|>> abstraction))

  (nominal.def .public (Quotient super sub %)
    (Record
     [#value super
      #label sub])

    (def .public (quotient class value)
      (All (_ super sub %)
        (-> (Class super sub %) super
            (Quotient super sub %)))
      (abstraction [#value value
                    #label ((representation Class class) value)]))

    (with_template [<name> <slot> <output>]
      [(def .public <name>
         (All (_ super sub %)
           (-> (Quotient super sub %)
               <output>))
         (|>> representation
              (the <slot>)))]

      [value #value super]
      [label #label sub]
      )
    )
  )

(def .public type
  (syntax (_ [it <code>.any])
    (macro.with_symbols ['_ 'super 'sub '%]
      (do meta.monad
        [it (meta.eval Type (` (.type_of ((is (All ((, '_) (, 'super) (, 'sub) (, '%))
                                                (-> (..Class (, 'super) (, 'sub) (, '%))
                                                    (..Class (, 'super) (, 'sub) (, '%))))
                                              (|>>))
                                          (, it)))))
         [super sub %] (|> (as Type it)
                           (?//.result (?//.applied (?.after (?//.exactly ..Class)
                                                             (all ?.and ?//.any ?//.any ?//.any))))
                           meta.of_try)]
        (in (list (` (.type_literal (..Quotient (, (//.code super))
                                                (, (//.code sub))
                                                (, (//.code %)))))))))))

(def .public (equivalence super)
  (All (_ super sub %)
    (-> (Equivalence sub)
        (Equivalence (..Quotient super sub %))))
  (implementation
   (def (= reference sample)
     (of super =
         (..label reference)
         (..label sample)))))
