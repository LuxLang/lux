... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except static)
   ["[0]" debug]
   [control
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    [text
     ["%" \\format]]]
   [meta
    ["[0]" code
     ["<[1]>" \\parser]]
    [macro (.only with_symbols)
     ["[0]" syntax (.only syntax)]]]]]
 ["[0]" // (.only)
  ["[0]" nominal (.only)]])

(exception.the .public (wrong_type [expected actual])
  (Exception [Type Type])
  (exception.report
   (list ["Expected" (%.type expected)]
         ["Actual" (%.type actual)])))

(with_expansions [<representation> [Type Any]]
  (nominal.every .public Dynamic
    <representation>
    
    (the .public dynamic
      (syntax (_ [value <code>.any])
        (with_symbols [g!value]
          (in (list (` (.let [(, g!value) (, value)]
                         (as Dynamic [(.type_of (, g!value)) (, g!value)]))))))))

    (the .public static
      (syntax (_ [type <code>.any
                  value <code>.any])
        (with_symbols [g!type g!value]
          (in (list (` (.let [[(, g!type) (, g!value)] (|> (, value)
                                                           (is Dynamic)
                                                           (as <representation>))]
                         (.is (try.Try (, type))
                              (.if (.of //.equivalence (,' =)
                                        (.type (, type))
                                        (, g!type))
                                {try.#Success (.as (, type) (, g!value))}
                                (exception.except ..wrong_type [(.type (, type)) (, g!type)]))))))))))

    (the .public (format value)
      (-> Dynamic (Try Text))
      (let [[type value] (nominal.representation value)]
        (debug.representation type value)))
    ))
