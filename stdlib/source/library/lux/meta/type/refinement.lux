... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except only type)
   [abstract
    [monad (.only do)]]
   [control
    ["?" projection]
    [function
     [predicate (.only Predicate)]]]
   ["[0]" meta (.only)
    ["[0]" code
     ["<[1]>" \\projection]]
    ["[0]" macro (.only)
     [syntax (.only syntax)]]]]]
 ["[0]" // (.only)
  ["?[1]" \\projection]
  ["[0]" nominal]])

(nominal.every .public (Refined super %)
  (Record
   [#value super
    #predicate (Predicate super)])

  (.every .public (Refiner super %)
    (-> super
        (Maybe (Refined super %))))

  (the .public (refiner predicate)
    (for_any (_ super)
      (for_some (_ %)
        (-> (Predicate super)
            (Refiner super %))))
    (function (_ value)
      (if (predicate value)
        {.#Some (nominal.abstraction
                 [#value value
                  #predicate predicate])}
        {.#None})))

  (with_template [<name> <slot> <output>]
    [(the .public <name>
       (for_any (_ super %)
         (-> (Refined super %)
             <output>))
       (|>> nominal.representation
            (its <slot>)))]

    [value #value super]
    [predicate #predicate (Predicate super)]
    )

  (the .public (lifted transform)
    (for_any (_ super %)
      (-> (-> super super)
          (-> (Refined super %)
              (Maybe (Refined super %)))))
    (function (_ refined)
      (let [(open "_[0]") (nominal.representation refined)
            value' (transform _#value)]
        (if (_#predicate value')
          {.#Some (nominal.abstraction
                   [..#value value'
                    ..#predicate _#predicate])}
          {.#None}))))
  )

(the .public (only refiner values)
  (for_any (_ super %)
    (-> (Refiner super %) (List super)
        (List (Refined super %))))
  (when values
    {.#Item head tail}
    (when (refiner head)
      {.#Some refined}
      {.#Item refined (only refiner tail)}
      
      {.#None}
      (only refiner tail))

    {.#End}
    {.#End}))

(the .public (partition refiner values)
  (for_any (_ super %)
    (-> (Refiner super %) (List super)
        [(List (Refined super %))
         (List super)]))
  (when values
    {.#Item head tail}
    (let [[yes no] (partition refiner tail)]
      (when (refiner head)
        {.#Some refined}
        [{.#Item refined yes}
         no]
        
        {.#None}
        [yes
         {.#Item head no}]))

    {.#End}
    [{.#End} {.#End}]))

(the .public type
  (syntax (_ [it <code>.any])
    (macro.with_symbols ['_ 'super '%]
      (do meta.monad
        [it (meta.eval Type (` (.type_of ((is (for_any ((, '_) (, 'super) (, '%))
                                                (-> (..Refiner (, 'super) (, '%))
                                                    (..Refiner (, 'super) (, '%))))
                                              (|>>))
                                          (, it)))))
         [super %] (|> (as Type it)
                       (?//.value (?//.applied (?.after (?//.exactly ..Refiner)
                                                        (all ?.and ?//.any ?//.any))))
                       meta.of_try)]
        (in (list (` (.type (..Refined (, (//.code super))
                                       (, (//.code %)))))))))))
