... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except for)
   [abstract
    ["<>" projection]
    ["[0]" monad]]
   [error
    ["[0]" exception]]
   [data
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]#[0]" mix)
      ["?[1]" \\projection]]]]
   [macro
    ["[0]" syntax]]
   ["[0]" meta (.use "[1]#[0]" functor)
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]
   [compiler
    [version (.only Version)]]]])

(the .public latest
  Version
  00,09,00)

(the .public current
  (syntax.macro (_ [])
    (meta#each (|>> code.text list)
               meta.version)))

(exception.the .public invalid)

(the .public for
  (syntax.macro (_ [specializations (<>.some (<>.and <code>.text ?list.any))
                    default (<>.maybe ?list.any)])
    (monad.let meta.monad
      [current meta.version]
      (when (list#mix (function (_ [when then] choice)
                        (if (text.= when current)
                            {.#Some then}
                            choice))
                      default
                      specializations)
        {.#Some it}
        (pure (list it))
        
        {.#None}
        (meta.failure (exception.error ..invalid []))))))
