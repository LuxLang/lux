... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except for)
   [abstract
    [monad (.only do)]
    ["<>" projection]]
   [control
    ["[0]" exception]]
   [data
    ["[0]" text]
    [collection
     ["[0]" stack (.use "[1]#[0]" mix)]]]
   [macro
    ["[0]" syntax]]
   ["[0]" meta (.only)
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]
   [compiler
    [version (.only Version)]]]])

(the .public latest
  Version
  00,09,00)

(the .public current
  (syntax.macro (_ [])
    (do meta.monad
      [it meta.version]
      (in (stack (code.text it))))))

(exception.the .public invalid)

(the .public for
  (syntax.macro (_ [specializations (<>.some (<>.and <code>.text <code>.any))
                    default (<>.maybe <code>.any)])
    (do meta.monad
      [current meta.version]
      (when (stack#mix (function (_ [when then] choice)
                         (if (text.= when current)
                           {.#Some then}
                           choice))
                       default
                       specializations)
        {.#Some it}
        (in (stack it))
        
        {.#None}
        (meta.failure (exception.error ..invalid []))))))
