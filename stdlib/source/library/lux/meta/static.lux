... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except nat int rev if cond when)
   [abstract
    [monad (.only do)]]
   [control
    ["<>" projection]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix)]]]
   [math
    [number (.only hex)]
    ["[0]" random (.only Random)]]
   ["[0]" meta (.use "[1]#[0]" functor)
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro
     [syntax (.only syntax)]]]]])

(with_template [<name> <type> <injection>]
  [(the .public <name>
     (syntax (_ [expression <code>.any])
       (of meta.monad each
           (|>> (as <type>) <injection> list)
           (meta.eval <type> expression))))]

  [bit .Bit code.bit]
  [nat .Nat code.nat]
  [int .Int code.int]
  [rev .Rev code.rev]
  [dec .Dec code.dec]
  [text .Text code.text]
  )

(the pcg_32_magic_inc
  (hex "FEDCBA9876543210"))

(with_expansions [<type> (for_some (_ a)
                           [(-> a Code)
                            a])]
  (the .public literal
    (syntax (_ [injection <code>.any
                expression <code>.any])
      (do meta.monad
        [pair (meta.eval (type <type>)
                         (` [(, injection) (, expression)]))
         .let [[injection expression] (as <type> pair)]]
        (in (list (injection expression)))))))

(with_expansions [<type> (Meta (List Code))]
  (the .public expansion
    (syntax (_ [expression <code>.any])
      (do meta.monad
        [expression (meta.eval (type <type>) expression)]
        (as <type> expression)))))

(with_expansions [<type> (for_some (_ a)
                           [(-> a Code)
                            (List a)])]
  (the .public literals
    (syntax (_ [injection <code>.any
                expression <code>.any])
      (do meta.monad
        [pair (meta.eval (type <type>)
                         (` [(, injection) (, expression)]))
         .let [[injection expression] (as <type> pair)]]
        (in (list#each injection expression))))))

(the .public seed
  (syntax (_ [])
    (meta#each (|>> code.nat list) meta.seed)))

(with_template [<name> <random> <injection>]
  [(the .public <name>
     (syntax (_ [])
       (do meta.monad
         [seed meta.seed
          .let [[_ value] (random.value random.pcg_32
                                        [random.#increase ..pcg_32_magic_inc
                                         random.#seed seed]
                                        <random>)]]
         (in (list (<injection> value))))))]

  [random_bit random.bit code.bit]
  [random_nat random.nat code.nat]
  [random_int random.int code.int]
  [random_rev random.rev code.rev]
  [random_dec random.dec code.dec]
  )

(with_expansions [<type> (for_some (_ a)
                           [(-> a Code)
                            (Random a)])]
  (the .public random
    (syntax (_ [injection <code>.any
                random <code>.any])
      (do meta.monad
        [pair (meta.eval (type <type>)
                         (` [(, injection) (, random)]))
         .let [[injection random] (as <type> pair)]
         seed meta.seed
         .let [[_ value] (random.value random.pcg_32
                                       [random.#increase ..pcg_32_magic_inc
                                        random.#seed seed]
                                       random)]]
        (in (list (injection value)))))))

(with_expansions [<type> (for_some (_ a)
                           [(-> a Code)
                            (Random (List a))])]
  (the .public randoms
    (syntax (_ [injection <code>.any
                random <code>.any])
      (do meta.monad
        [pair (meta.eval (type <type>)
                         (` [(, injection) (, random)]))
         .let [[injection random] (as <type> pair)]
         seed meta.seed
         .let [[_ value] (random.value random.pcg_32
                                       [random.#increase ..pcg_32_magic_inc
                                        random.#seed seed]
                                       random)]]
        (in (list#each injection value))))))

(the .public if
  (syntax (_ [test <code>.any
              then <code>.any
              else <code>.any])
    (do meta.monad
      [test (meta.eval .Bit test)]
      (in (list (.if (as .Bit test)
                  then
                  else))))))

(the .public cond
  (syntax (_ [test,then/* (<>.some (<>.and <code>.any <code>.any))
              else <code>.any])
    (in (list (list#mix (function (_ [test then] else)
                          (` (..if (, test)
                               (, then)
                               (, else))))
                        else
                        (list.reversed test,then/*))))))

(the .public when
  (syntax (_ [test <code>.any
              then <code>.any])
    (do meta.monad
      [test (meta.eval .Bit test)]
      (in (.if (as .Bit test)
            (list then)
            (list))))))
