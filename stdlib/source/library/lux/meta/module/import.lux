... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except all)
   [data
    ["[0]" text (.use "[1]#[0]" equivalence)]
    [collection
     ["[0]" list]]]]]
 ["[0]" /// (.use "[1]#[0]" monad)])

(the .public (all module)
  (-> Text
      (Meta (List Text)))
  (///#each (its .#imports)
            (///.module module)))

(the .public (by? import module)
  (-> Text Text
      (Meta Bit))
  (///#each (|>> (its .#imports)
                 (list.any? (text#= import)))
            (///.module module)))

(the .public (current? import)
  (-> Text
      (Meta Bit))
  (|> ///.current_module_name
      (///#each (by? import))
      ///#conjoint))
