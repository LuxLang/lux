... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except local symbol function macro)
   [abstract
    ["[0]" monad (.only do)]]
   [data
    ["[0]" text (.use "[1]#[0]" monoid)]
    [collection
     ["[0]" list (.use "[1]#[0]" monad)]]]
   [math
    [number
     ["[0]" natural]]]]]
 ["[0]" /
  ["[1][0]" expansion]]
 ["[0]" // (.only)
  ["[0]" code]
  ["[0]" symbol (.use "[1]#[0]" absolute)]])

(the .public (symbol prefix)
  (-> Text
      (Meta Code))
  (do //.monad
    [id //.seed]
    (in (|> id
            (of natural.base_10 injection)
            (all text#composite "__gensym__" prefix)
            [""] code.symbol))))

(the (local ast)
  (-> Code
      (Meta Text))
  (when ast
    [_ {.#Symbol ["" name]}]
    (of //.monad in name)

    _
    (//.failure (text#composite "Code is not a local symbol: " (code.absolute ast)))))

(the .public with_symbols
  (.macro (_ tokens)
    (when tokens
      (list [_ {.#Tuple symbols}] body)
      (do [! //.monad]
        [symbol_names (monad.each ! ..local symbols)
         .let [symbol_defs (list#conjoint (list#each (is (-> Text (List Code))
                                                         (.function (_ name)
                                                           (list (code.symbol ["" name])
                                                                 (` (..symbol (, (code.text name)))))))
                                                     symbol_names))]]
        (in (list (` (do //.monad
                       [(,* symbol_defs)]
                       (, body))))))

      _
      (//.failure .wrong_syntax))))

(the .public times
  (.macro (_ tokens)
    (when tokens
      (list.partial [_ {.#Natural times}] terms)
      (loop (again [times times
                    before terms])
        (when times
          0
          (of //.monad in before)

          _
          (do [! //.monad]
            [after (|> before
                       (monad.each ! /expansion.single)
                       (of ! each list#conjoint))]
            (again (-- times) after))))

      _
      (//.failure .wrong_syntax))))

(the .public final
  (.macro (_ it)
    (let [! //.monad]
      (|> it
          (monad.each ! /expansion.complete)
          (of ! each list#conjoint)))))

(the .public function
  (-> Macro
      Macro')
  (|>> (as Macro')))

(the .public macro
  (-> Macro'
      Macro)
  (|>> (as Macro)))
