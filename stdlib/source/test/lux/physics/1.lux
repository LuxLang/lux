... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["[0]" integer]
      ["[0]" decimal]]]]
   [test
    ["_" property (.only Test)]]
   [meta
    ["[0]" static]]
   [logic
    ["[0]" bit]]
   ["[0]" type
    ["[1]" check]]]]
 [\\library
  ["[0]" /]])

(the margin_of_error
  +0.000,000,000,000,000,000,000,000,1)

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [.let [range 1,000,000
               random (/.random range)]
         initial_time random
         final_time random

         initial_position random
         final_position random])
      (all _.and
           (<| (_.for [/.Dimension /.Vector
                       /.random])
               (all _.and
                    (_.coverage [/.equivalence /.=]
                      (and (same? decimal.equivalence /.equivalence)
                           (same? decimal.= /.=)))
                    
                    (_.coverage [/.forward /.backward]
                      (/.= /.forward
                           (/.opposite /.backward)))
                    (_.coverage [/.origin]
                      (decimal.= /.origin
                                 (/.opposite /.origin)))
                    (_.coverage [/.opposite]
                      (decimal.= /.origin
                                 (/.+ (/.opposite initial_position)
                                      initial_position)))
                    (_.coverage [/.+]
                      (and (decimal.= (/.+ initial_position
                                           final_position)
                                      (/.+ final_position
                                           initial_position))
                           (decimal.= initial_position
                                      (/.+ /.origin
                                           initial_position))))
                    (_.coverage [/.-]
                      (decimal.= (/.+ (/.opposite final_position)
                                      initial_position)
                                 (/.- final_position
                                      initial_position)))
                    (_.coverage [/.Time]
                      (type.< /.Dimension /.Time))
                    (_.coverage [/.Position]
                      (type.< /.Dimension /.Position))
                    ))
           (<| (_.for [/.Difference])
               (all _.and
                    (_.coverage [/.stasis]
                      (decimal.= final_time
                                 (/.stasis final_time)))
                    (_.coverage [/.difference]
                      (decimal.= final_position
                                 ((/.difference initial_position final_position) initial_position)))
                    (_.coverage [/.value]
                      (let [difference (/.value (/.difference initial_position final_position))]
                        (decimal.= final_position
                                   (decimal.+ difference initial_position))))
                    (<| (_.for [/.Interval])
                        (all _.and
                             (_.coverage [/.interval]
                               (decimal.= (decimal.opposite (/.value (/.interval initial_time final_time)))
                                          (/.value (/.interval final_time initial_time))))
                             ))
                    (<| (_.for [/.Displacement])
                        (all _.and
                             (_.coverage [/.displacement]
                               (decimal.= (decimal.opposite (/.value (/.displacement initial_position final_position)))
                                          (/.value (/.displacement final_position initial_position))))
                             ))
                    ))
           (<| (_.for [/.Variable])
               (all _.and
                    (_.coverage [/.position]
                      (/.= initial_position
                           ((/.position /.uniform_motion /.rest initial_position) final_time)))
                    (_.coverage [/.velocity]
                      (let [interval (/.interval final_time initial_time)
                            velocity (/.average_velocity interval
                                                         (/.displacement initial_position final_position))
                            acceleration (/.average_acceleration interval (/.difference /.rest velocity))]
                        ((decimal.approximately? ..margin_of_error)
                         velocity
                         ((/.velocity acceleration /.rest) (/.value interval)))))
                    ))
           (<| (_.for [/.Velocity])
               (all _.and
                    (_.coverage [/.rest]
                      (/.= initial_position
                           ((/.position /.uniform_motion /.rest initial_position) final_time)))
                    (_.coverage [/.average_velocity]
                      (let [interval (/.interval final_time initial_time)
                            velocity (/.average_velocity interval
                                                         (/.displacement initial_position final_position))]
                        ((decimal.approximately? ..margin_of_error)
                         final_position
                         ((/.position /.uniform_motion velocity initial_position) (/.value interval)))))
                    ))
           (<| (_.for [/.Acceleration])
               (all _.and
                    (_.coverage [/.uniform_motion]
                      (/.= initial_position
                           ((/.position /.uniform_motion /.rest initial_position) final_time)))
                    (_.coverage [/.average_acceleration]
                      (let [interval (/.interval final_time initial_time)
                            velocity (/.average_velocity interval
                                                         (/.displacement initial_position final_position))
                            acceleration (/.average_acceleration interval (/.difference /.rest velocity))

                            mid_point (decimal./ +2.0 (/.- initial_position final_position))
                            half_way (/.+ initial_position mid_point)]
                        ((decimal.approximately? ..margin_of_error)
                         half_way
                         ((/.position acceleration /.rest initial_position) (/.value interval)))))
                    ))
           )))
