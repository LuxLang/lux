(.module:
  [lux #*
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do)]
    [\spec
     ["$." equivalence]
     ["$." hash]
     ["$." order]
     ["$." codec]]]
   [control
    pipe]
   [data
    ["." text ("#\." equivalence)]]
   [math
    ["." random (#+ Random)]
    [number
     ["n" nat]]]]
  [\\
   ["." /]])

(def: #export (random module_size short_size)
  (-> Nat Nat (Random Name))
  (random.and (random.ascii/alpha module_size)
              (random.ascii/alpha short_size)))

(def: #export test
  Test
  (<| (_.covering /._)
      (do {! random.monad}
        [## First Name
         sizeM1 (|> random.nat (\ ! map (n.% 100)))
         sizeS1 (|> random.nat (\ ! map (|>> (n.% 100) (n.max 1))))
         (^@ name1 [module1 short1]) (..random sizeM1 sizeS1)
         ## Second Name
         sizeM2 (|> random.nat (\ ! map (n.% 100)))
         sizeS2 (|> random.nat (\ ! map (|>> (n.% 100) (n.max 1))))
         (^@ name2 [module2 short2]) (..random sizeM2 sizeS2)]
        (_.for [.Name]
               ($_ _.and
                   (_.for [/.equivalence]
                          ($equivalence.spec /.equivalence (..random sizeM1 sizeS1)))
                   (_.for [/.hash]
                          (|> (random.ascii 1)
                              (\ ! map (|>> [""]))
                              ($hash.spec /.hash)))
                   (_.for [/.order]
                          ($order.spec /.order (..random sizeM1 sizeS1)))
                   (_.for [/.codec]
                          (_.and ($codec.spec /.equivalence /.codec (..random sizeM1 sizeS1))
                                 (let [(^open "/\.") /.codec]
                                   (_.test "Encoding an name without a module component results in text equal to the short of the name."
                                           (if (text.empty? module1)
                                             (text\= short1 (/\encode name1))
                                             #1)))))
                   
                   (_.cover [/.module /.short]
                            (and (is? module1 (/.module name1))
                                 (is? short1 (/.short name1))))
                   
                   (_.for [.name_of]
                          (let [(^open "/\.") /.equivalence]
                            ($_ _.and
                                (_.test "Can obtain Name from identifier."
                                        (and (/\= ["lux" "yolo"] (.name_of .yolo))
                                             (/\= ["test/lux/data/name" "yolo"] (.name_of ..yolo))
                                             (/\= ["" "yolo"] (.name_of yolo))
                                             (/\= ["lux/test" "yolo"] (.name_of lux/test.yolo))))
                                (_.test "Can obtain Name from tag."
                                        (and (/\= ["lux" "yolo"] (.name_of #.yolo))
                                             (/\= ["test/lux/data/name" "yolo"] (.name_of #..yolo))
                                             (/\= ["" "yolo"] (.name_of #yolo))
                                             (/\= ["lux/test" "yolo"] (.name_of #lux/test.yolo)))))))
                   )))))
