(.module:
  [lux #*
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do)]
    [\spec
     ["$." equivalence]
     ["$." hash]
     ["$." monoid]]]
   [data
    [collection
     ["." list]]]
   [macro
    ["." template]]
   ["." math
    ["." random (#+ Random)]
    [number
     ["n" nat]
     ["." int]
     ["f" frac]
     ["r" rev]]]]
  [\\
   ["." /]])

(def: #export random
  (Random /.Color)
  (|> ($_ random.and random.nat random.nat random.nat)
      (\ random.monad map /.from_rgb)))

(def: scale
  (-> Nat Frac)
  (|>> .int int.frac))

(def: square
  (-> Frac Frac)
  (math.pow +2.0))

(def: square_root
  (-> Frac Frac)
  (math.pow +0.5))

(def: (distance/1 from to)
  (-> Frac Frac Frac)
  (square_root
   (square
    (f.- from to))))

(def: (distance/3 from to)
  (-> /.Color /.Color Frac)
  (let [[fr fg fb] (/.to_rgb from)
        [tr tg tb] (/.to_rgb to)]
    (square_root
     ($_ f.+
         (|> (scale tr) (f.- (scale fr)) square)
         (|> (scale tg) (f.- (scale fg)) square)
         (|> (scale tb) (f.- (scale fb)) square)))))

(def: rgb_error_margin +1.8)

(template [<field>]
  [(def: (<field> color)
     (-> /.Color Frac)
     (let [[hue saturation luminance] (/.to_hsl color)]
       <field>))]

  [saturation]
  [luminance]
  )

(def: (encoding expected)
  (-> /.Color Test)
  ($_ _.and
      (_.cover [/.RGB /.to_rgb /.from_rgb]
               (|> expected /.to_rgb /.from_rgb
                   (\ /.equivalence = expected)))
      (_.cover [/.HSL /.to_hsl /.from_hsl]
               (|> expected /.to_hsl /.from_hsl
                   (distance/3 expected)
                   (f.<= ..rgb_error_margin)))
      (_.cover [/.HSB /.to_hsb /.from_hsb]
               (|> expected /.to_hsb /.from_hsb
                   (distance/3 expected)
                   (f.<= ..rgb_error_margin)))
      (_.cover [/.CMYK /.to_cmyk /.from_cmyk]
               (|> expected /.to_cmyk /.from_cmyk
                   (distance/3 expected)
                   (f.<= ..rgb_error_margin)))
      ))

(def: transformation
  Test
  (do random.monad
    [colorful (|> ..random
                  (random.filter (function (_ color) (|> (distance/3 color /.black) (f.>= +100.0))))
                  (random.filter (function (_ color) (|> (distance/3 color /.white) (f.>= +100.0)))))
     mediocre (|> ..random
                  (random.filter (|>> saturation
                                      ((function (_ saturation)
                                         (and (f.>= +0.25 saturation)
                                              (f.<= +0.75 saturation)))))))
     ratio (|> random.safe_frac (random.filter (f.>= +0.5)))]
    ($_ _.and
        (_.cover [/.darker /.brighter]
                 (and (f.<= (distance/3 colorful /.black)
                            (distance/3 (/.darker ratio colorful) /.black))
                      (f.<= (distance/3 colorful /.white)
                            (distance/3 (/.brighter ratio colorful) /.white))))
        (_.cover [/.interpolate]
                 (and (f.<= (distance/3 colorful /.black)
                            (distance/3 (/.interpolate ratio /.black colorful) /.black))
                      (f.<= (distance/3 colorful /.white)
                            (distance/3 (/.interpolate ratio /.white colorful) /.white))))
        (_.cover [/.saturate]
                 (f.> (saturation mediocre)
                      (saturation (/.saturate ratio mediocre))))
        (_.cover [/.de_saturate]
                 (f.< (saturation mediocre)
                      (saturation (/.de_saturate ratio mediocre))))
        (_.cover [/.gray_scale]
                 (let [gray'ed (/.gray_scale mediocre)]
                   (and (f.= +0.0
                             (saturation gray'ed))
                        (|> (luminance gray'ed)
                            (f.- (luminance mediocre))
                            f.abs
                            (f.<= ..rgb_error_margin)))))
        )))

(def: palette
  Test
  (_.for [/.Spread /.Palette]
         (do {! random.monad}
           [eH (\ ! map (|>> f.abs (f.% +0.9) (f.+ +0.05))
                  random.safe_frac)
            #let [eS +0.5]
            variations (\ ! map (|>> (n.% 3) (n.+ 2)) random.nat)
            #let [max_spread (f./ (|> variations inc .int int.frac)
                                  +1.0)
                  min_spread (f./ +2.0 max_spread)
                  spread_space (f.- min_spread max_spread)]
            spread (\ ! map (|>> f.abs (f.% spread_space) (f.+ min_spread))
                      random.safe_frac)]
           (`` ($_ _.and
                   (~~ (template [<brightness> <palette>]
                         [(_.cover [<palette>]
                                   (let [eB <brightness>
                                         expected (/.from_hsb [eH eS eB])
                                         palette (<palette> spread variations expected)]
                                     (and (n.= variations (list.size palette))
                                          (not (list.any? (\ /.equivalence = expected) palette)))))]
                         [+1.0 /.analogous]
                         [+0.5 /.monochromatic]
                         ))
                   (~~ (template [<palette>]
                         [(_.cover [<palette>]
                                   (let [expected (/.from_hsb [eH eS +0.5])
                                         [c0 c1 c2] (<palette> expected)]
                                     (and (\ /.equivalence = expected c0)
                                          (not (\ /.equivalence = expected c1))
                                          (not (\ /.equivalence = expected c2)))))]

                         [/.triad]
                         [/.clash]
                         [/.split_complement]))
                   (~~ (template [<palette>]
                         [(_.cover [<palette>]
                                   (let [expected (/.from_hsb [eH eS +0.5])
                                         [c0 c1 c2 c3] (<palette> expected)]
                                     (and (\ /.equivalence = expected c0)
                                          (not (\ /.equivalence = expected c1))
                                          (not (\ /.equivalence = expected c2))
                                          (not (\ /.equivalence = expected c3)))))]

                         [/.square]
                         [/.tetradic]))
                   )))))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [/.Color])
      (do {! random.monad}
        [expected ..random]
        ($_ _.and
            (_.for [/.equivalence]
                   ($equivalence.spec /.equivalence ..random))
            (_.for [/.hash]
                   ($hash.spec /.hash ..random))
            (_.for [/.addition]
                   ($monoid.spec /.equivalence /.addition ..random))
            (_.for [/.subtraction]
                   ($monoid.spec /.equivalence /.addition ..random))
            
            (..encoding expected)
            (_.cover [/.complement]
                     (let [~expected (/.complement expected)
                           (^open "/\.") /.equivalence]
                       (and (not (/\= expected ~expected))
                            (/\= expected (/.complement ~expected)))))
            (_.cover [/.black /.white]
                     (and (\ /.equivalence = /.white (/.complement /.black))
                          (\ /.equivalence = /.black (/.complement /.white))))
            ..transformation
            ..palette
            (_.for [/.Alpha /.Pigment]
                   ($_ _.and
                       (_.cover [/.transparent /.opaque]
                                (and (r.= /.opaque (dec /.transparent))
                                     (r.= /.transparent (inc /.opaque))))
                       (_.cover [/.translucent]
                                (r.= /.transparent (r.+ /.translucent /.translucent)))
                       ))
            ))))
