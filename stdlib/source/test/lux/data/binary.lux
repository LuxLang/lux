(.module:
  [lux #*
   ["%" data/text/format (#+ format)]
   ["r" math/random (#+ Random)]
   ["_" test (#+ Test)]
   [abstract
    ["." monad (#+ do)]
    ["." enum]
    {[0 #spec]
     [/
      ["$." equivalence]]}]
   [control
    ["." try (#+ Try)]]
   [data
    [number
     ["." i64]
     ["n" nat]]
    [collection
     ["." list]]]]
  {1
   ["." / (#+ Binary)]})

(def: (succeed result)
  (-> (Try Bit) Bit)
  (case result
    (#try.Failure _)
    #0

    (#try.Success output)
    output))

(def: #export (binary size)
  (-> Nat (Random Binary))
  (let [output (/.create size)]
    (loop [idx 0]
      (if (n.< size idx)
        (do r.monad
          [byte r.nat]
          (exec (try.assume (/.write/8 idx byte output))
            (recur (inc idx))))
        (:: r.monad wrap output)))))

(def: (bits-io bytes read write value)
  (-> Nat (-> Nat Binary (Try Nat)) (-> Nat Nat Binary (Try Any)) Nat Bit)
  (let [binary (/.create bytes)
        cap (case bytes
              8 (dec 0)
              _ (|> 1 (i64.left-shift (n.* 8 bytes)) dec))
        capped-value (i64.and cap value)]
    (succeed
     (do try.monad
       [_ (write 0 value binary)
        output (read 0 binary)]
       (wrap (n.= capped-value output))))))

(def: #export test
  Test
  (<| (_.context (%.name (name-of /._)))
      (do {@ r.monad}
        [#let [gen-size (|> r.nat (:: @ map (|>> (n.% 100) (n.max 8))))]
         binary-size gen-size
         random-binary (binary binary-size)
         value r.nat
         #let [gen-idx (|> r.nat (:: @ map (n.% binary-size)))]
         [from to] (r.and gen-idx gen-idx)
         #let [[from to] [(n.min from to) (n.max from to)]]]
        ($_ _.and
            ($equivalence.spec /.equivalence (binary binary-size))
            (_.test "Can get size of binary."
                    (|> random-binary /.size (n.= binary-size)))
            (_.test "Can read/write 8-bit values."
                    (bits-io 1 /.read/8 /.write/8 value))
            (_.test "Can read/write 16-bit values."
                    (bits-io 2 /.read/16 /.write/16 value))
            (_.test "Can read/write 32-bit values."
                    (bits-io 4 /.read/32 /.write/32 value))
            (_.test "Can read/write 64-bit values."
                    (bits-io 8 /.read/64 /.write/64 value))
            (_.test "Can slice binaries."
                    (let [slice-size (|> to (n.- from) inc)
                          random-slice (try.assume (/.slice from to random-binary))
                          idxs (enum.range n.enum 0 (dec slice-size))
                          reader (function (_ binary idx) (/.read/8 idx binary))]
                      (and (n.= slice-size (/.size random-slice))
                           (case [(monad.map try.monad (reader random-slice) idxs)
                                  (monad.map try.monad (|>> (n.+ from) (reader random-binary)) idxs)]
                             [(#try.Success slice-vals) (#try.Success binary-vals)]
                             (:: (list.equivalence n.equivalence) = slice-vals binary-vals)

                             _
                             #0))))
            ))))
