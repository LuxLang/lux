... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [ffi (.only)]
   [abstract
    [equivalence (.only Equivalence)]
    ["[0]" monad (.only do)]
    ["[0]" enum]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]]
   [control
    ["<>" projection]
    ["[0]" pipe]
    ["[0]" maybe]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]
    [function
     [predicate (.only Predicate)]]]
   [data
    ["[0]" sum]
    ["[0]" bit]
    ["[0]" text (.use "[1]#[0]" equivalence)
     [encoding
      ["[0]" utf8]]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]
     ["[0]" sequence]
     ["[0]" set]
     [array
      [\\unsafe (.only)]]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]
     ["[0]" i64]
     ["[0]" int]
     ["[0]" rev]
     ["[0]" dec]]]
   [meta
    ["[0]" symbol]
    ["[0]" type]
    ["[0]" code]
    [macro
     ["^" pattern]]]
   [world
    [time
     ["[0]" instant]
     ["[0]" duration]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only) (.use "[1]#[0]" equivalence)
   ["!" \\unsafe]
   ["[0]" \\injection]
   ["[0]" \\projection]]])

(the !expect
  (template (_ <expectation> <computation>)
    [(when <computation>
       <expectation>
       true

       _
       false)]))

(the segment_size 10)

(the (utf8_conversion_does_not_alter? value)
  (Predicate Text)
  (|> value
      (of utf8.codec injection)
      (of utf8.codec projection)
      (pipe.when
        {try.#Success converted}
        (text#= value converted)
        
        {try.#Failure error}
        false)))

(the random_text
  (Random Text)
  (random.only ..utf8_conversion_does_not_alter?
               (random.unicode ..segment_size)))

(the random_symbol
  (Random Symbol)
  (random.and ..random_text ..random_text))

(the location_equivalence
  (Equivalence Location)
  (implementation
   (the (= [expected_module expected_line expected_column]
           [sample_module sample_line sample_column])
     (and (text#= expected_module sample_module)
          (n.= expected_line sample_line)
          (n.= expected_column sample_column)))))

(the random_location
  (Random Location)
  (all random.and
       ..random_text
       random.nat
       random.nat))

(the random_code
  (Random Code)
  (random.rec
   (function (_ again)
     (let [random_sequence (is (Random (List Code))
                               (do [! random.monad]
                                 [size (of ! each (n.% 2) random.nat)]
                                 (random.list size again)))]
       (all random.and
            ..random_location
            (is (Random (Code' (Ann Location)))
                (all random.or
                     random.bit
                     random.nat
                     random.int
                     random.rev
                     random.safe_dec
                     ..random_text
                     ..random_symbol
                     random_sequence
                     random_sequence
                     random_sequence
                     )))))))

(the random_type
  (Random Type)
  (let [(open "[0]") random.monad]
    (all random.either
         (in .Nat)
         (in .List)
         (in .Code)
         (in .Type))))

(the size
  Test
  (<| (_.for [\\projection.Size])
      (`` (all _.and
               (,, (with_template [<size> <projection> <format>]
                     [(do [! random.monad]
                        [expected (of ! each (i64.and (i64.mask <size>))
                                      random.nat)]
                        (_.coverage [<size> <projection> <format>]
                          (|> (\\injection.value <format> expected)
                              (\\projection.value <projection>)
                              (!expect (^.multi {try.#Success actual}
                                                (n.= (.nat expected)
                                                     (.nat actual)))))))]

                     [\\projection.size_8 \\projection.bits_8 \\injection.bits_8]
                     [\\projection.size_16 \\projection.bits_16 \\injection.bits_16]
                     [\\projection.size_32 \\projection.bits_32 \\injection.bits_32]
                     [\\projection.size_64 \\projection.bits_64 \\injection.bits_64]
                     ))))))

(the binary
  Test
  (`` (all _.and
           (,, (with_template [<projection> <format>]
                 [(do [! random.monad]
                    [expected (of ! each (of utf8.codec injection) (random.ascii ..segment_size))]
                    (_.coverage [<projection> <format>]
                      (|> (\\injection.value <format> expected)
                          (\\projection.value <projection>)
                          (!expect (^.multi {try.#Success actual}
                                            (of /.equivalence = expected actual))))))]

                 [\\projection.binary_8 \\injection.binary_8]
                 [\\projection.binary_16 \\injection.binary_16]
                 [\\projection.binary_32 \\injection.binary_32]
                 [\\projection.binary_64 \\injection.binary_64]
                 )))))

(the utf8
  Test
  (`` (all _.and
           (,, (with_template [<projection> <format>]
                 [(do [! random.monad]
                    [expected (random.ascii ..segment_size)]
                    (_.coverage [<projection> <format>]
                      (|> (\\injection.value <format> expected)
                          (\\projection.value <projection>)
                          (!expect (^.multi {try.#Success actual}
                                            (of text.equivalence = expected actual))))))]

                 [\\projection.utf8_8 \\injection.utf8_8]
                 [\\projection.utf8_16 \\injection.utf8_16]
                 [\\projection.utf8_32 \\injection.utf8_32]
                 [\\projection.utf8_64 \\injection.utf8_64]
                 [\\projection.text \\injection.text]
                 )))))

(the sequence
  Test
  (`` (all _.and
           (,, (with_template [<projection> <format>]
                 [(do [! random.monad]
                    [expected (random.sequence ..segment_size random.nat)]
                    (_.coverage [<projection> <format>]
                      (|> expected
                          (\\injection.value (<format> \\injection.nat))
                          (\\projection.value (<projection> \\projection.nat))
                          (!expect (^.multi {try.#Success actual}
                                            (of (sequence.equivalence n.equivalence) = expected actual))))))]

                 [\\projection.sequence_8 \\injection.sequence_8]
                 [\\projection.sequence_16 \\injection.sequence_16]
                 [\\projection.sequence_32 \\injection.sequence_32]
                 [\\projection.sequence_64 \\injection.sequence_64]
                 )))))

(the simple
  Test
  (`` (all _.and
           (,, (with_template [<projection> <format> <random> <equivalence>]
                 [(do [! random.monad]
                    [expected <random>]
                    (_.coverage [<projection> <format>]
                      (|> expected
                          (\\injection.value <format>)
                          (\\projection.value <projection>)
                          (!expect (^.multi {try.#Success actual}
                                            (of <equivalence> = expected actual))))))]

                 [\\projection.bit \\injection.bit random.bit bit.equivalence]
                 [\\projection.nat \\injection.nat random.nat n.equivalence]
                 [\\projection.int \\injection.int random.int int.equivalence]
                 [\\projection.rev \\injection.rev random.rev rev.equivalence]
                 [\\projection.instant \\injection.instant random.instant instant.equivalence]
                 [\\projection.duration \\injection.duration random.duration duration.equivalence]))
           (do [! random.monad]
             [expected random.dec]
             (_.coverage [\\projection.dec \\injection.dec]
               (|> expected
                   (\\injection.value \\injection.dec)
                   (\\projection.value \\projection.dec)
                   (!expect (^.multi {try.#Success actual}
                                     (or (of dec.equivalence = expected actual)
                                         (and (dec.not_a_number? expected)
                                              (dec.not_a_number? actual))))))))
           (do [! random.monad]
             [expected (of ! each (|>> (i64.and (i64.mask \\projection.size_8))
                                       (n.max 2))
                           random.nat)]
             (_.coverage [\\projection.not_a_bit]
               (|> expected
                   (\\injection.value \\injection.bits_8)
                   (\\projection.value \\projection.bit)
                   (!expect (^.multi {try.#Failure error}
                                     (exception.is? \\projection.not_a_bit error))))))
           )))

(the complex
  Test
  (`` (all _.and
           (,, (with_template [<projection> <format> <random> <equivalence>]
                 [(do [! random.monad]
                    [expected <random>]
                    (_.coverage [<projection> <format>]
                      (|> expected
                          (\\injection.value <format>)
                          (\\projection.value <projection>)
                          (!expect (^.multi {try.#Success actual}
                                            (of <equivalence> = expected actual))))))]

                 [\\projection.location \\injection.location random_location location_equivalence]
                 [\\projection.code \\injection.code random_code code.equivalence]
                 [\\projection.type \\injection.type random_type type.equivalence]
                 ))
           (,, (with_template [<projection_coverage> <projection> <coverage_format> <format> <random> <equivalence>]
                 [(do [! random.monad]
                    [expected <random>]
                    (_.coverage [<projection_coverage> <coverage_format>]
                      (|> expected
                          (\\injection.value <format>)
                          (\\projection.value <projection>)
                          (!expect (^.multi {try.#Success actual}
                                            (of <equivalence> = expected actual))))))]

                 [\\projection.maybe (\\projection.maybe \\projection.nat) \\injection.maybe (\\injection.maybe \\injection.nat) (random.maybe random.nat) (maybe.equivalence n.equivalence)]
                 [\\projection.list (\\projection.list \\projection.nat) \\injection.list (\\injection.list \\injection.nat) (random.list ..segment_size random.nat) (list.equivalence n.equivalence)]
                 [\\projection.set (\\projection.set n.hash \\projection.nat) \\injection.set (\\injection.set \\injection.nat) (random.set n.hash ..segment_size random.nat) set.equivalence]
                 [\\projection.symbol \\projection.symbol \\injection.symbol \\injection.symbol ..random_symbol symbol.equivalence]))
           (do [! random.monad]
             [expected (of ! each (list.repeated ..segment_size) random.nat)]
             (_.coverage [\\projection.set_elements_are_not_unique]
               (|> expected
                   (\\injection.value (\\injection.list \\injection.nat))
                   (\\projection.value (\\projection.set n.hash \\projection.nat))
                   (!expect (^.multi {try.#Failure error}
                                     (exception.is? \\projection.set_elements_are_not_unique error))))))
           (do [! random.monad]
             [expected (random.or random.bit random.nat)]
             (_.coverage [\\projection.or \\injection.or]
               (|> expected
                   (\\injection.value (\\injection.or \\injection.bit \\injection.nat))
                   (\\projection.value (is (\\projection.Projection (Either Bit Nat))
                                           (\\projection.or \\projection.bit \\projection.nat)))
                   (!expect (^.multi {try.#Success actual}
                                     (of (sum.equivalence bit.equivalence n.equivalence) =
                                         expected
                                         actual))))))
           (do [! random.monad]
             [tag (of ! each (|>> (i64.and (i64.mask \\projection.size_8))
                                  (n.max 2))
                      random.nat)
              value random.bit]
             (_.coverage [\\projection.invalid_tag]
               (|> [tag value]
                   (\\injection.value (\\injection.and \\injection.bits_8 \\injection.bit))
                   (\\projection.value (is (\\projection.Projection (Either Bit Nat))
                                           (\\projection.or \\projection.bit \\projection.nat)))
                   (!expect (^.multi {try.#Failure error}
                                     (exception.is? \\projection.invalid_tag error))))))
           (do [! random.monad]
             [expected (random.list ..segment_size random.nat)]
             (_.coverage [\\projection.rec \\injection.rec \\injection.and \\injection.any]
               (|> expected
                   (\\injection.value (\\injection.rec (|>> (\\injection.and \\injection.nat)
                                                            (\\injection.or \\injection.any))))
                   (\\projection.value (is (\\projection.Projection (List Nat))
                                           (\\projection.rec
                                            (function (_ again)
                                              (\\projection.or \\projection.any
                                                               (<>.and \\projection.nat
                                                                       again))))))
                   (!expect (^.multi {try.#Success actual}
                                     (of (list.equivalence n.equivalence) =
                                         expected
                                         actual))))))
           )))

(the \\projection
  Test
  (<| (_.covering \\projection._)
      (_.for [\\projection.Projection])
      (`` (all _.and
               (_.coverage [\\projection.value \\projection.any
                            \\injection.no_op \\injection.instance]
                 (|> (\\injection.instance \\injection.no_op)
                     (\\projection.value \\projection.any)
                     (!expect {try.#Success _})))
               (do [! random.monad]
                 [data (of ! each (of utf8.codec injection) (random.ascii ..segment_size))]
                 (_.coverage [\\projection.binary_was_not_fully_read]
                   (|> data
                       (\\projection.value \\projection.any)
                       (!expect (^.multi {try.#Failure error}
                                         (exception.is? \\projection.binary_was_not_fully_read error))))))
               (do [! random.monad]
                 [expected (of ! each (of utf8.codec injection) (random.ascii ..segment_size))]
                 (_.coverage [\\projection.segment \\injection.segment \\injection.value]
                   (|> expected
                       (\\injection.value (\\injection.segment ..segment_size))
                       (\\projection.value (\\projection.segment ..segment_size))
                       (!expect (^.multi {try.#Success actual}
                                         (of /.equivalence = expected actual))))))
               (do [! random.monad]
                 [data (of ! each (of utf8.codec injection) (random.ascii ..segment_size))]
                 (_.coverage [\\projection.end?]
                   (|> data
                       (\\projection.value (do <>.monad
                                             [pre \\projection.end?
                                              _ (\\projection.segment ..segment_size)
                                              post \\projection.end?]
                                             (in (and (not pre)
                                                      post))))
                       (!expect {try.#Success .true}))))
               (do [! random.monad]
                 [to_read (of ! each (n.% (++ ..segment_size)) random.nat)
                  data (of ! each (of utf8.codec injection) (random.ascii ..segment_size))]
                 (_.coverage [\\projection.Offset \\projection.offset]
                   (|> data
                       (\\projection.value (do <>.monad
                                             [start \\projection.offset
                                              _ (\\projection.segment to_read)
                                              offset \\projection.offset
                                              _ (\\projection.segment (n.- to_read ..segment_size))
                                              nothing_left \\projection.offset]
                                             (in (and (n.= 0 start)
                                                      (n.= to_read offset)
                                                      (n.= ..segment_size nothing_left)))))
                       (!expect {try.#Success .true}))))
               (do [! random.monad]
                 [to_read (of ! each (n.% (++ ..segment_size)) random.nat)
                  data (of ! each (of utf8.codec injection) (random.ascii ..segment_size))]
                 (_.coverage [\\projection.remaining]
                   (|> data
                       (\\projection.value (do <>.monad
                                             [_ (\\projection.segment to_read)
                                              remaining \\projection.remaining
                                              _ (\\projection.segment (n.- to_read ..segment_size))
                                              nothing_left \\projection.remaining]
                                             (in (and (n.= ..segment_size
                                                           (n.+ to_read remaining))
                                                      (n.= 0 nothing_left)))))
                       (!expect {try.#Success .true}))))
               ..size
               ..binary
               ..utf8
               ..sequence
               ..simple
               ..complex
               ))))

(the equivalence
  (Equivalence \\injection.Specification)
  (implementation
   (the (= expected actual)
     (/#= (\\injection.instance expected)
          (\\injection.instance actual)))))

(the random_specification
  (Random \\injection.Specification)
  (of random.monad each \\injection.nat random.nat))

(the \\injection
  Test
  (<| (_.covering \\injection._)
      (_.for [\\injection.Mutation \\injection.Specification \\injection.Injection])
      (all _.and
           (_.for [\\injection.monoid]
                  (monoidT.spec ..equivalence \\injection.monoid ..random_specification))
           )))

(the (succeed it)
  (-> (Try Bit) Bit)
  (when it
    {try.#Failure _}
    false

    {try.#Success output}
    output))

(the .public (random size)
  (-> Nat (Random /.Binary))
  (let [output (/.empty size)]
    (loop (again [idx 0])
      (if (n.< size idx)
        (do random.monad
          [byte random.nat]
          (exec (try.trusted (/.has_8! idx byte output))
            (again (++ idx))))
        (of random.monad in output)))))

(the (throws? exception try)
  (All (_ e a) (-> (Exception e) (Try a) Bit))
  (when try
    {try.#Failure error}
    (exception.is? exception error)

    {try.#Success _}
    false))

(the (binary_io power read write value)
  (-> Nat (-> Nat /.Binary (Try Nat)) (-> Nat Nat /.Binary (Try Any)) Nat Bit)
  (let [bytes (i64.left_shifted power 1)
        binary (/.empty bytes)
        cap (when bytes
              8 (-- 0)
              _ (|> 1 (i64.left_shifted (n.* 8 bytes)) --))
        capped_value (i64.and cap value)]
    (and (..succeed
          (do try.monad
            [pre (read 0 binary)
             _ (write 0 value binary)
             post (read 0 binary)]
            (in (and (n.= 0 pre)
                     (n.= capped_value post)))))
         (throws? /.index_out_of_bounds (read 1 binary))
         (throws? /.index_out_of_bounds (write 1 value binary)))))

(the as_list
  (-> /.Binary (List Nat))
  (/.mix (function (_ head tail)
           {.#Item head tail})
         (list)))

(the test|unsafe
  Test
  (<| (_.covering !._)
      (_.for [!.Binary])
      (do [! random.monad]
        [.let [gen_size (|> random.nat (of ! each (|>> (n.% 100) (n.max 8))))]
         size gen_size
         sample (..random size)
         value random.nat
         .let [gen_idx (|> random.nat (of ! each (n.% size)))]
         offset gen_idx
         length (of ! each (n.% (n.- offset size)) random.nat)]
        (`` (all _.and
                 (_.for [!.=]
                        (equivalenceT.spec (function (_ left right)
                                             (!.= left right))
                                           (..random size)))
                 (_.coverage [!.empty]
                   (!.= (!.empty size) (!.empty size)))
                 (_.coverage [!.size]
                   (|> (!.empty size) !.size (n.= size)))
                 (,, (with_template [<power> <bytes/?> <has/?>]
                       [(_.coverage [<bytes/?> <has/?>]
                          (let [bytes (i64.left_shifted <power> 1)
                                binary (!.empty bytes)
                                cap (when bytes
                                      8 (-- 0)
                                      _ (|> 1 (i64.left_shifted (n.* 8 bytes)) --))
                                capped_value (i64.and cap value)
                                
                                pre (<bytes/?> 0 binary)
                                _ (<has/?> 0 value binary)
                                post (<bytes/?> 0 binary)]
                            (and (n.= 0 pre)
                                 (n.= capped_value post))))]

                       [0 !.bits_8 !.has_8!]
                       [1 !.bits_16 !.has_16!]
                       [2 !.bits_32 !.has_32!]
                       [3 !.bits_64 !.has_64!]))
                 (_.coverage [!.slice]
                   (let [random_slice (!.slice offset length sample)
                         idxs (is (List Nat)
                                  (when length
                                    0 (list)
                                    _ (enum.range n.enum 0 (-- length))))
                         reader (function (_ binary idx)
                                  (!.bits_8 idx binary))]
                     (and (n.= length (!.size random_slice))
                          (of (list.equivalence n.equivalence) =
                              (list#each (|>> (n.+ offset) (reader sample)) idxs)
                              (list#each (reader random_slice) idxs)))))
                 (_.coverage [!.copy!]
                   (and (let [it (!.copy! size 0 sample 0 (!.empty size))]
                          (and (not (same? sample it))
                               (!.= sample it)))
                        (let [sample/0 (!.bits_8 0 sample)
                              copy (!.copy! 1 0 sample 0 (!.empty 2))
                              copy/0 (!.bits_8 0 copy)
                              copy/1 (!.bits_8 1 copy)]
                          (and (n.= sample/0 copy/0)
                               (n.= 0 copy/1)))))
                 )))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Binary])
      (do [! random.monad]
        [.let [gen_size (|> random.nat (of ! each (|>> (n.% 100) (n.max 8))))]
         size gen_size
         sample (..random size)
         value random.nat
         .let [gen_idx (|> random.nat (of ! each (n.% size)))]
         offset (of ! each (n.max 1) gen_idx)
         length (of ! each (n.% (n.- offset size)) random.nat)]
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec /.equivalence (..random size)))
             (_.for [/.monoid]
                    (monoidT.spec /.equivalence /.monoid (..random size)))
             (_.coverage [/.mix]
               (n.= (of list.mix mix n.+ 0 (..as_list sample))
                    (/.mix n.+ 0 sample)))
             
             (_.coverage [/.empty]
               (of /.equivalence =
                   (/.empty size)
                   (/.empty size)))
             (_.coverage [/.size]
               (|> (/.empty size) /.size (n.= size)))
             (_.for [/.index_out_of_bounds]
                    (all _.and
                         (_.coverage [/.bits_8 /.has_8!]
                           (..binary_io 0 /.bits_8 /.has_8! value))
                         (_.coverage [/.bits_16 /.has_16!]
                           (..binary_io 1 /.bits_16 /.has_16! value))
                         (_.coverage [/.bits_32 /.has_32!]
                           (..binary_io 2 /.bits_32 /.has_32! value))
                         (_.coverage [/.bits_64 /.has_64!]
                           (..binary_io 3 /.bits_64 /.has_64! value))))
             (_.coverage [/.slice]
               (let [random_slice (try.trusted (/.slice offset length sample))
                     idxs (is (List Nat)
                              (when length
                                0 (list)
                                _ (enum.range n.enum 0 (-- length))))
                     reader (function (_ binary idx)
                              (/.bits_8 idx binary))]
                 (and (n.= length (/.size random_slice))
                      (when [(monad.each try.monad (|>> (n.+ offset) (reader sample)) idxs)
                             (monad.each try.monad (reader random_slice) idxs)]
                        [{try.#Success binary_vals} {try.#Success slice_vals}]
                        (of (list.equivalence n.equivalence) = binary_vals slice_vals)

                        _
                        false))))
             (_.coverage [/.slice_out_of_bounds]
               (and (throws? /.slice_out_of_bounds (/.slice size size sample))
                    (let [verdict (throws? /.slice_out_of_bounds (/.slice offset size sample))]
                      (when offset
                        0 (not verdict)
                        _ verdict))))
             (_.coverage [/.after]
               (and (of /.equivalence = sample (/.after 0 sample))
                    (of /.equivalence = (/.empty 0) (/.after size sample))
                    (n.= (n.- offset size) (/.size (/.after offset sample)))
                    (when (list.reversed (..as_list sample))
                      {.#End}
                      false

                      {.#Item head tail}
                      (n.= (list.mix n.+ 0 tail)
                           (/.mix n.+ 0 (/.after 1 sample))))))
             (_.coverage [/.copy!]
               (and (when (/.copy! size 0 sample 0 (/.empty size))
                      {try.#Success output}
                      (and (not (same? sample output))
                           (of /.equivalence = sample output))

                      {try.#Failure _}
                      false)
                    (succeed
                     (do try.monad
                       [sample/0 (/.bits_8 0 sample)
                        copy (/.copy! 1 0 sample 0 (/.empty 2))
                        copy/0 (/.bits_8 0 copy)
                        copy/1 (/.bits_8 1 copy)]
                       (in (and (n.= sample/0 copy/0)
                                (n.= 0 copy/1)))))))
             (_.coverage [/.cannot_copy]
               (and (not (throws? /.cannot_copy
                                  (/.copy! size 0 sample 0 (/.empty size))))
                    (throws? /.cannot_copy
                             (/.copy! (n.+ offset size) 0 sample 0 (/.empty size)))
                    (throws? /.cannot_copy
                             (/.copy! size offset sample 0 (/.empty size)))
                    (throws? /.cannot_copy
                             (/.copy! size 0 sample offset (/.empty size)))))

             ..test|unsafe
             ..\\injection
             ..\\projection
             ))))
