... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the part
  (Random Text)
  (do [! random.monad]
    [size (by ! each (|>> (n.% 10) ++) random.natural)]
    (random.alphabetic size)))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Buffer])
      (do random.monad
        [left ..part
         mid ..part
         right ..part]
        (all _.and
             (_.coverage [/.empty]
               (n.= 0(/.size /.empty)))
             (_.coverage [/.size /.then]
               (n.= (text.size left)
                    (/.size (/.then left /.empty))))
             (_.coverage [/.text]
               (text.= (text left mid right)
                       (|> /.empty
                           (/.then left)
                           (/.then mid)
                           (/.then right)
                           /.text)))
             ))))
