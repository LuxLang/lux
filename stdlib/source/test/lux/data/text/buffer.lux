(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [abstract
     [monad (#+ do)]]
    [data
     ["." text ("#\." equivalence)
      ["%" format (#+ format)]]]
    [math
     ["." random (#+ Random)]
     [number
      ["n" nat]]]]]
  [\\library
   ["." /]])

(def: part
  (Random Text)
  (do {! random.monad}
    [size (\ ! map (|>> (n.% 10) inc) random.nat)]
    (random.ascii/alpha size)))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [/.Buffer])
      (do random.monad
        [left ..part
         mid ..part
         right ..part]
        ($_ _.and
            (_.cover [/.empty]
                     (n.= 0(/.size /.empty)))
            (_.cover [/.size /.append]
                     (n.= (text.size left)
                          (/.size (/.append left /.empty))))
            (_.cover [/.text]
                     (text\= (format left mid right)
                             (|> /.empty
                                 (/.append left)
                                 (/.append mid)
                                 (/.append right)
                                 /.text)))
            ))))
