... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [data
    [collection
     ["[0]" list]
     ["[0]" set]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["[0]" nat]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(def .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [])
      (_.for [/.Unicode /.Char])
      (`` (all _.and
               (with_expansions [<chars> (these /.null
                                                /.alarm
                                                /.back_space
                                                /.tab
                                                /.new_line
                                                /.vertical_tab
                                                /.form_feed
                                                /.carriage_return
                                                /.double_quote

                                                /.start_of_heading
                                                /.start_of_text
                                                /.end_of_text
                                                /.end_of_transmission
                                                /.enquiry
                                                /.acknowledgement
                                                /.shift_out
                                                /.shift_in
                                                /.data_link_escape
                                                /.device_control_1
                                                /.device_control_2
                                                /.device_control_3
                                                /.device_control_4
                                                /.negative_acknowledgement
                                                /.synchronous_idle
                                                /.end_of_transmission_block
                                                /.cancel
                                                /.end_of_medium
                                                /.substitute
                                                /.escape
                                                /.file_separator
                                                /.group_separator
                                                /.record_separator
                                                /.unit_separator
                                                /.delete
                                                )]
                 (_.coverage [<chars>]
                   (let [options (list <chars>)
                         uniques (set.of_list nat.hash options)]
                     (nat.= (list.size options)
                            (set.size uniques)))))
               (,, (with_template [<short> <long>]
                     [(_.coverage [<short>]
                        (same? <long> <short>))]

                     [/.\0  /.null]
                     [/.\a  /.alarm]
                     [/.\b  /.back_space]
                     [/.\t  /.tab]
                     [/.\n  /.new_line]
                     [/.\v  /.vertical_tab]
                     [/.\f  /.form_feed]
                     [/.\r  /.carriage_return]
                     [/.\'' /.double_quote]
                     ))
               (_.coverage [/.line_feed]
                 (same? /.new_line /.line_feed))
               ))))
