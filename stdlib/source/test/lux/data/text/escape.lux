(.require
 [library
  [lux (.except)
   ["[0]" debug]
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text (.use "[1]#[0]" equivalence)
     [char (.only Char)]
     ["%" \\format (.only format)]]
    [collection
     ["[0]" set (.only Set)]]]
   [math
    ["[0]" random (.only Random)]
    [number (.only hex)
     ["n" nat]]]
   ["[0]" meta (.only)
    ["[0]" code (.only)
     ["<[1]>" \\parser]]
    [macro
     [syntax (.only syntax)]
     ["[0]" template]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" unicode
     ["[1]" set]]]]])

(def (range max min)
  (-> Char Char (Random Char))
  (let [range (n.- min max)]
    (of random.monad each
        (|>> (n.% range) (n.+ min))
        random.nat)))

(def under_range
  (Random Char)
  (..range (debug.private /.ascii_bottom) 0))

(def over_range
  (Random Char)
  (..range (hex "FFFF") (++ (debug.private /.ascii_top))))

(def in_range
  (Random Char)
  (..range (++ (debug.private /.ascii_top)) (debug.private /.ascii_bottom)))

(def ascii_range
  (Random Char)
  (..range (++ (debug.private /.ascii_top)) 0))

(def valid_sigils
  (Set Char)
  (set.of_list n.hash
               (list (debug.private /.\0_sigil)
                     (debug.private /.\a_sigil)
                     (debug.private /.\b_sigil)
                     (debug.private /.\t_sigil)
                     (debug.private /.\n_sigil)
                     (debug.private /.\v_sigil)
                     (debug.private /.\f_sigil)
                     (debug.private /.\r_sigil)
                     (debug.private /.\''_sigil)
                     (debug.private /.\\_sigil)
                     (debug.private /.\u_sigil))))

(def static_sample
  (syntax (_ [])
    (do meta.monad
      [seed meta.seed
       .let [[_ expected] (|> (random.ascii 10)
                              (random.only (|>> (text.contains? text.\0) not))
                              (random.result (random.pcg_32 [seed seed])))]]
      (in (list (code.text expected))))))

(def static_escaped
  (syntax (_ [un_escaped <code>.text])
    (in (list (code.text (/.escaped un_escaped))))))

(def .public test
  Test
  (<| (_.covering /._)
      (all _.and
           (do random.monad
             [ascii ..ascii_range]
             (_.coverage [/.escapable?]
               (`` (if (or (,, (with_template [<char>]
                                 [(n.= (debug.private <char>) ascii)]
                                 
                                 [/.\0] [/.\a] [/.\b] [/.\t]
                                 [/.\n] [/.\v] [/.\f] [/.\r]
                                 [/.\''] [/.\\])))
                     (/.escapable? ascii)
                     (bit#= (/.escapable? ascii)
                            (or (n.< (debug.private /.ascii_bottom) ascii)
                                (n.> (debug.private /.ascii_top) ascii)))))))
           (do random.monad
             [left (random.char unicode.character)
              right (random.char unicode.character)]
             (_.coverage [/.escaped /.un_escaped]
               (let [expected (format (text.of_char left) (text.of_char right))]
                 (if (or (/.escapable? left)
                         (/.escapable? right))
                   (let [escaped (/.escaped expected)]
                     (when (/.un_escaped escaped)
                       {try.#Success un_escaped}
                       (and (not (text#= escaped expected))
                            (text#= un_escaped expected))
                       
                       {try.#Failure error}
                       false))
                   (text#= expected (/.escaped expected))))))
           (do [! random.monad]
             [dummy (|> (random.char unicode.character)
                        (of ! each text.of_char))]
             (_.coverage [/.dangling_escape]
               (when (/.un_escaped (format (/.escaped dummy) "\"))
                 {try.#Success _}
                 false

                 {try.#Failure error}
                 (exception.match? /.dangling_escape error))))
           (do [! random.monad]
             [dummy (|> (random.char unicode.character)
                        (random.only (|>> (set.member? ..valid_sigils) not))
                        (of ! each text.of_char))]
             (_.coverage [/.invalid_escape]
               (when (/.un_escaped (format "\" dummy))
                 {try.#Success _}
                 false

                 {try.#Failure error}
                 (exception.match? /.invalid_escape error))))
           (do [! random.monad]
             [too_short (|> (random.char unicode.character)
                            (of ! each (n.% (hex "1000"))))
              code (|> (random.unicode 4)
                       (random.only (function (_ code)
                                      (when (of n.hex decoded code)
                                        {try.#Failure error} true
                                        {try.#Success _} false))))]
             (_.coverage [/.invalid_unicode_escape]
               (template.let [(!invalid <code>)
                              [(when (/.un_escaped (format "\u" <code>))
                                 {try.#Success _}
                                 false

                                 {try.#Failure error}
                                 (exception.match? /.invalid_unicode_escape error))]]
                 (and (!invalid (of n.hex encoded too_short))
                      (!invalid code)))))
           (_.coverage [/.literal]
             (with_expansions [<example> (..static_sample)]
               (text#= <example> (`` (/.literal (,, (..static_escaped <example>)))))))
           )))
