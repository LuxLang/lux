(.module:
  [lux #*
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do)]
    {[0 #spec]
     [/
      ["$." equivalence]]}]
   [data
    ["." product]
    ["." bit ("#\." equivalence)]
    [number
     ["n" nat]]
    [collection
     ["." set ("#\." equivalence)]]]
   [math
    ["." random (#+ Random)]]]
  ["." / #_
   ["/#" // #_
    ["#." block]]]
  {1
   ["." /
    [//
     ["." block]]]})

(def: #export random
  (Random /.Set)
  (do {! random.monad}
    [left //block.random
     right //block.random]
    (wrap (/.set [left (list right)]))))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [/.Set])
      (do {! random.monad}
        [block //block.random
         inside (\ ! map
                   (|>> (n.% (block.size block))
                        (n.+ (block.start block)))
                   random.nat)
         left //block.random
         right //block.random
         #let [equivalence (product.equivalence n.equivalence
                                                n.equivalence)]]
        (`` ($_ _.and
                (_.for [/.equivalence]
                       ($equivalence.spec /.equivalence ..random))
                
                (_.cover [/.range]
                         (let [[start end] (/.range (/.set [left (list right)]))]
                           (and (n.= (n.min (block.start left)
                                            (block.start right))
                                     start)
                                (n.= (n.max (block.end left)
                                            (block.end right))
                                     end))))
                (_.cover [/.member?]
                         (bit\= (block.within? block inside)
                                (/.member? (/.set [block (list)]) inside)))
                (_.cover [/.compose]
                         (\ equivalence =
                            [(n.min (block.start left)
                                    (block.start right))
                             (n.max (block.end left)
                                    (block.end right))]
                            (/.range (/.compose (/.set [left (list)])
                                                (/.set [right (list)])))))
                (_.cover [/.set]
                         (\ equivalence =
                            (/.range (/.compose (/.set [left (list)])
                                                (/.set [right (list)])))
                            (/.range (/.set [left (list right)]))))
                (~~ (template [<set>]
                      [(do random.monad
                         [char (random.char <set>)
                          #let [[start end] (/.range <set>)]]
                         (_.cover [<set>]
                                  (and (/.member? <set> char)
                                       (not (/.member? <set> (dec start)))
                                       (not (/.member? <set> (inc end))))))]

                      [/.ascii]
                      [/.ascii/alpha]
                      [/.ascii/alpha_num]
                      [/.ascii/lower_alpha]
                      [/.ascii/upper_alpha]
                      [/.character]
                      [/.non_character]
                      [/.full]
                      ))

                //block.test
                )))))
