... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [data
    ["[0]" product]
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [meta
    [macro
     ["[0]" template]]]
   [test
    ["_" property (.only Test)]]]]
 ["[0]" /
  ["/[1]" //
   ["[1][0]" block]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" block]]]])

(the .public random
  (Random /.Set)
  (do [! random.monad]
    [left //block.random
     right //block.random]
    (in (/.set [left (list right)]))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Set])
      (do [! random.monad]
        [block //block.random
         inside (of ! each
                    (|>> (n.% (block.size block))
                         (n.+ (block.start block)))
                    random.natural)
         left //block.random
         right //block.random
         .let [equivalence (product.equivalence n.equivalence
                                                n.equivalence)]]
        (`` (all _.and
                 (_.for [/.equivalence]
                        (equivalenceT.spec /.equivalence ..random))
                 
                 (_.coverage [/.set]
                   (and (n.= (block.start left)
                             (/.start (/.set [left (list)])))
                        (n.= (block.end left)
                             (/.end (/.set [left (list)])))))
                 (_.coverage [/.start]
                   (n.= (n.min (block.start left)
                               (block.start right))
                        (/.start (/.set [left (list right)]))))
                 (_.coverage [/.end]
                   (n.= (n.max (block.end left)
                               (block.end right))
                        (/.end (/.set [left (list right)]))))
                 (_.coverage [/.member?]
                   (bit.= (block.within? block inside)
                          (/.member? (/.set [block (list)]) inside)))
                 (_.coverage [/.composite]
                   (let [composed (/.composite (/.set [left (list)])
                                               (/.set [right (list)]))]
                     (and (n.= (n.min (block.start left)
                                      (block.start right))
                               (/.start composed))
                          (n.= (n.max (block.end left)
                                      (block.end right))
                               (/.end composed)))))
                 (,, (template.with [<set>]
                       [(do random.monad
                          [character (random.character <set>)
                           .let [start (/.start <set>)
                                 end (/.end <set>)]]
                          (_.coverage [<set>]
                            (and (/.member? <set> character)
                                 (not (/.member? <set> (-- start)))
                                 (not (/.member? <set> (++ end))))))]

                       [/.ascii]
                       [/.alphabetic]
                       [/.alpha_numeric]
                       [/.lower_case]
                       [/.upper_case]
                       [/.numeric]
                       [/.character]
                       [/.non_character]
                       [/.full]
                       ))

                 //block.test
                 )))))
