(.module:
  [lux #*
   ["%" data/text/format (#+ format)]
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do)]
    {[0 #spec]
     [/
      ["$." equivalence]
      ["$." order]
      ["$." enum]
      ["$." interval]
      ["$." monoid]
      ["$." codec]]}]
   [data
    [number
     ["." i64]]]
   [math
    ["r" random]]]
  {1
   ["." /
    //]})

(def: #export test
  Test
  (<| (_.context (%.name (name-of /._)))
      (`` ($_ _.and
              ($equivalence.spec /.equivalence r.rev)
              ($order.spec /.order r.rev)
              ($enum.spec /.enum r.rev)
              ($interval.spec /.interval r.rev)
              (~~ (template [<monoid>]
                    [(<| (_.context (%.name (name-of <monoid>)))
                         ($monoid.spec /.equivalence <monoid> r.rev))]

                    [/.addition] [/.minimum] [/.maximum]
                    ))
              (~~ (template [<codec>]
                    [(<| (_.context (%.name (name-of /.binary)))
                         ($codec.spec /.equivalence <codec> r.rev))]

                    [/.binary] [/.octal] [/.decimal] [/.hex]
                    ))
              (_.test "Alternate notations."
                      (and (/.= (bin ".11001001")
                                (bin ".11,00,10,01"))
                           (/.= (oct ".615243")
                                (oct ".615,243"))
                           (/.= (hex ".deadBEEF")
                                (hex ".dead,BEEF"))))
              (~~ (template [<half> <whole>]
                    [(_.cover [<half>]
                              (/.= <whole>
                                   (/.+ <half> <half>)))]

                    [/./2 .0]
                    [/./4 /./2]
                    [/./8 /./4]
                    [/./16 /./8]
                    [/./32 /./16]
                    [/./64 /./32]
                    [/./128 /./64]
                    [/./256 /./128]
                    [/./512 /./256]
                    [/./1024 /./512]
                    [/./2048 /./1024]
                    [/./4096 /./2048]
                    ))
              ))))
