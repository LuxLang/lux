... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [order (.only Order)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [error
    ["[0]" try]]
   [data
    ["[0]" bit]
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number
     [/64
      ["n" natural]]]]
   [macro
    ["[0]" template]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only Set)
   ["[0]" //]]])

(the size
  (random.Random Natural)
  (by random.monad each (n.% 100) random.natural))

(the .public (random size order gen_value)
  (for_any (_ a)
    (-> Natural (Order a) (Random a)
        (Random (Set a))))
  (when size
    0
    (random#pure (/.empty order))

    _
    (do random.monad
      [partial (random (-- size) order gen_value)
       value (random.only (|>> (/.member? partial) not)
                          gen_value)]
      (pure (/.has value partial)))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Set])
      (do [! random.monad]
        [sizeL ..size
         sizeR ..size
         usetL (random.set n.hash sizeL random.natural)
         non_memberL (random.only (|>> (//.member? usetL) not)
                                  random.natural)
         .let [listL (//.as_list usetL)]
         listR (|> (random.set n.hash sizeR random.natural)
                   (by ! each //.as_list))
         .let [(open "/#[0]") /.equivalence
               setL (/.of_list n.order listL)
               setR (/.of_list n.order listR)
               empty (/.empty n.order)]]
        (`` (all _.and
                 (_.for [/.equivalence]
                        (equivalenceT.spec /.equivalence (..random sizeL n.order random.natural)))

                 (_.coverage [/.size]
                   (n.= sizeL (/.size setL)))
                 (_.coverage [/.empty?]
                   (bit.= (n.= 0 (/.size setL))
                          (/.empty? setL)))
                 (_.coverage [/.empty]
                   (/.empty? (/.empty n.order)))
                 (_.coverage [/.as_list]
                   (by (list.equivalence n.equivalence) =
                       (/.as_list (/.of_list n.order listL))
                       (list.sorted (by n.order <) listL)))
                 (_.coverage [/.of_list]
                   (|> setL
                       /.as_list (/.of_list n.order)
                       (/#= setL)))
                 (,, (template.with [<coverage> <comparison>]
                       [(_.coverage [<coverage>]
                          (when (<coverage> setL)
                            {try.#Success value}
                            (|> setL /.as_list (list.every? (<comparison> value)))

                            failure
                            (/.empty? setL)))]

                       [/.minor n.>=]
                       [/.major n.<=]
                       ))
                 (_.coverage [/.member?]
                   (let [members_are_identified!
                         (list.every? (/.member? setL) (/.as_list setL))

                         non_members_are_not_identified!
                         (not (/.member? setL non_memberL))]
                     (and members_are_identified!
                          non_members_are_not_identified!)))
                 (_.coverage [/.has]
                   (let [setL+ (/.has non_memberL setL)]
                     (and (not (/.member? setL non_memberL))
                          (/.member? setL+ non_memberL)
                          (n.= (++ (/.size setL))
                               (/.size setL+)))))
                 (_.coverage [/.lacks]
                   (|> setL
                       (/.has non_memberL)
                       (/.lacks non_memberL)
                       (by /.equivalence = setL)))
                 (_.coverage [/.sub?]
                   (let [self!
                         (/.sub? setL setL)

                         empty!
                         (/.sub? setL empty)]
                     (and self!
                          empty!)))
                 (_.coverage [/.super?]
                   (let [self!
                         (/.super? setL setL)

                         empty!
                         (/.super? empty setL)

                         symmetry!
                         (bit.= (/.super? setL setR)
                                (/.sub? setR setL))]
                     (and self!
                          empty!
                          symmetry!)))
                 (,, (template.with [<coverage> <relation> <empty?>]
                       [(_.coverage [<coverage>]
                          (let [self!
                                (by /.equivalence =
                                    setL
                                    (<coverage> setL setL))

                                super!
                                (and (<relation> (<coverage> setL setR) setL)
                                     (<relation> (<coverage> setL setR) setR))

                                empty!
                                (by /.equivalence =
                                    (if <empty?> empty setL)
                                    (<coverage> setL empty))

                                idempotence!
                                (by /.equivalence =
                                    (<coverage> setL (<coverage> setL setR))
                                    (<coverage> setR (<coverage> setL setR)))]
                            (and self!
                                 super!
                                 empty!
                                 idempotence!)))]

                       [/.union /.sub? false]
                       [/.intersection /.super? true]
                       ))
                 (_.coverage [/.difference]
                   (let [self!
                         (|> setL
                             (/.difference setL)
                             (by /.equivalence = empty))

                         empty!
                         (|> setL
                             (/.difference empty)
                             (by /.equivalence = setL))

                         difference!
                         (not (list.any? (/.member? (/.difference setL setR))
                                         (/.as_list setL)))

                         idempotence!
                         (by /.equivalence =
                             (/.difference setL setR)
                             (/.difference setL (/.difference setL setR)))]
                     (and self!
                          empty!
                          difference!
                          idempotence!)))
                 )))))
