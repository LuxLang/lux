... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" functor
     ["[1]T" \\test (.only Injection)]]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [control
    ["[0]" maybe]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the (injection value)
  (Injection /.Stack)
  (/.top value /.empty))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Stack])
      (do random.monad
        [size (of random.monad each (n.% 100) random.nat)
         sample (random.stack size random.nat)
         expected_top random.nat]
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec (/.equivalence n.equivalence) (random.stack size random.nat)))
             (_.for [/.functor]
                    (functorT.spec ..injection /.equivalence /.functor))
             
             (_.coverage [/.size]
               (n.= size (/.size sample)))
             (_.coverage [/.empty?]
               (bit#= (n.= 0 (/.size sample))
                      (/.empty? sample)))
             (_.coverage [/.empty]
               (/.empty? /.empty))
             (_.coverage [/.value]
               (when (/.value sample)
                 {.#None}
                 (/.empty? sample)
                 
                 {.#Some _}
                 (not (/.empty? sample))))
             (_.coverage [/.next]
               (when (/.next sample)
                 {.#None}
                 (/.empty? sample)
                 
                 {.#Some [top remaining]}
                 (of (/.equivalence n.equivalence) =
                     sample
                     (/.top top remaining))))
             (_.coverage [/.top]
               (when (/.next (/.top expected_top sample))
                 {.#Some [actual_top actual_sample]}
                 (and (same? expected_top actual_top)
                      (same? sample actual_sample))
                 
                 {.#None}
                 false))
             ))))
