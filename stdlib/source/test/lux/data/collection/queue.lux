... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" functor
     ["[1]T" \\test (.only Injection)]]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [error
    ["[0]" try]]
   [data
    ["[0]" bit]
    [collection
     ["[0]" set]
     ["[0]" list (.use "[1]#[0]" monoid)]]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the injection
  (Injection /.Queue)
  (|>> list /.of_list))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Queue
              /.#front /.#rear])
      (do [! random.monad]
        [size (by ! each (n.% 100) random.natural)
         members (random.set n.hash size random.natural)
         non_member (random.only (|>> (set.member? members) not)
                                 random.natural)
         .let [members (set.as_list members)
               sample (/.of_list members)]]
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec (/.equivalence n.equivalence) (random.queue size random.natural)))
             (_.for [/.functor]
                    (functorT.spec ..injection /.equivalence /.functor))

             (_.coverage [/.size]
               (n.= size (/.size sample)))
             (_.coverage [/.empty?]
               (bit.= (n.= 0 size) (/.empty? sample)))
             (_.coverage [/.empty]
               (let [empty_is_empty!
                     (/.empty? /.empty)

                     all_empty_queues_look_the_same!
                     (bit.= (/.empty? sample)
                            (by (/.equivalence n.equivalence) =
                                sample
                                /.empty))]
                 (and empty_is_empty!
                      all_empty_queues_look_the_same!)))
             (_.coverage [/.front]
               (when [(list.as_stack members) (/.front sample)]
                 [{.#Top head tail} {try.#Success first}]
                 (n.= head first)
                 
                 [{.#Empty} {try.#Failure _}]
                 true

                 _
                 false))
             (_.coverage [/.member?]
               (let [every_member_is_identified!
                     (list.every? (/.member? n.equivalence sample)
                                  (/.as_list sample))

                     non_member_is_not_identified!
                     (not (/.member? n.equivalence sample non_member))]
                 (and every_member_is_identified!
                      non_member_is_not_identified!)))
             (_.coverage [/.end]
               (let [pushed (/.end non_member sample)

                     size_increases!
                     (n.= (++ (/.size sample)) (/.size pushed))

                     new_member_is_identified!
                     (/.member? n.equivalence pushed non_member)

                     has_expected_order!
                     (by (list.equivalence n.equivalence) =
                         (list#composite (/.as_list sample) (list non_member))
                         (/.as_list pushed))]
                 (and size_increases!
                      new_member_is_identified!
                      has_expected_order!)))
             (_.coverage [/.next]
               (when (list.as_stack members)
                 {.#Top target expected}
                 (let [popped (/.next sample)

                       size_decreases!
                       (n.= (-- (/.size sample))
                            (/.size popped))

                       popped_member_is_not_identified!
                       (not (/.member? n.equivalence popped target))

                       has_expected_order!
                       (by (list.equivalence n.equivalence) =
                           (list.of_stack expected)
                           (/.as_list popped))]
                   (and size_decreases!
                        popped_member_is_not_identified!
                        has_expected_order!))
                 
                 {.#Empty}
                 (and (/.empty? sample)
                      (/.empty? (/.next sample)))))
             ))))
