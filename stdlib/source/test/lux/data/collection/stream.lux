... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [equivalence (.only Equivalence)]
    ["[0]" enum]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]
    ["[0]" comonad
     ["[1]T" \\test]]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor)]
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    ["[0]" random]
    [number
     ["n" natural]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the (equivalence super)
  (for_any (_ a)
    (-> (Equivalence a)
        (Equivalence (/.Stream a))))
  (implementation
   (the (= expected actual)
     (by (stack.equivalence super) =
         (/.first 100 expected)
         (/.first 100 actual)))))

(the (iterations step)
  (for_any (_ a)
    (-> (-> a a)
        (-> a (/.Stream a))))
  (/.iterations
   (function (_ state)
     (let [state' (step state)]
       [state' state]))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Stream])
      (let [(open "stack#[0]") (stack.equivalence n.equivalence)])
      (do [! random.monad]
        [repeated random.natural
         index (by ! each (n.% 100) random.natural)
         size (by ! each (|>> (n.% 10) ++) random.natural)
         offset (by ! each (n.% 100) random.natural)
         cycle_start random.natural
         cycle_next (random.stack size random.natural)]
        (all _.and
             (_.for [/.functor]
                    (functorT.spec /.repeated ..equivalence /.functor))
             (_.for [/.comonad]
                    (comonadT.spec /.repeated ..equivalence /.comonad))
             
             (_.coverage [/.item]
               (n.= (n.+ offset index)
                    (/.item index (..iterations ++ offset))))
             (_.coverage [/.repeated]
               (n.= repeated
                    (/.item index (/.repeated repeated))))
             (_.coverage [/.first]
               (stack#= (list.as_stack (enum.range n.enum offset (-- (n.+ size offset))))
                        (/.first size (..iterations ++ offset))))
             (_.coverage [/.after]
               (stack#= (list.as_stack (enum.range n.enum offset (-- (n.+ size offset))))
                        (/.first size (/.after offset (..iterations ++ 0)))))
             (_.coverage [/.split_at]
               (let [[drops takes] (/.split_at size (..iterations ++ 0))]
                 (and (stack#= (list.as_stack (enum.range n.enum 0 (-- size)))
                               drops)
                      (stack#= (list.as_stack (enum.range n.enum size (-- (n.* 2 size))))
                               (/.first size takes)))))
             (_.coverage [/.while]
               (stack#= (list.as_stack (enum.range n.enum 0 (-- size)))
                        (/.while (n.< size) (..iterations ++ 0))))
             (_.coverage [/.until]
               (stack#= (list.as_stack (enum.range n.enum offset (-- (n.+ size offset))))
                        (/.while (n.< (n.+ size offset))
                                 (/.until (n.< offset) (..iterations ++ 0)))))
             (_.coverage [/.split_when]
               (let [[drops takes] (/.split_when (n.= size) (..iterations ++ 0))]
                 (and (stack#= (list.as_stack (enum.range n.enum 0 (-- size)))
                               drops)
                      (stack#= (list.as_stack (enum.range n.enum size (-- (n.* 2 size))))
                               (/.while (n.< (n.* 2 size)) takes)))))
             (_.coverage [/.head]
               (n.= offset
                    (/.head (..iterations ++ offset))))
             (_.coverage [/.tail]
               (stack#= (list.as_stack (enum.range n.enum (++ offset) (n.+ size offset)))
                        (/.first size (/.tail (..iterations ++ offset)))))
             (_.coverage [/.only]
               (stack#= (list.as_stack (list#each (n.* 2) (enum.range n.enum 0 (-- size))))
                        (/.first size (/.only n.even? (..iterations ++ 0)))))
             (_.coverage [/.partition]
               (let [[evens odds] (/.partition n.even? (..iterations ++ 0))]
                 (and (n.= (n.* 2 offset)
                           (/.item offset evens))
                      (n.= (++ (n.* 2 offset))
                           (/.item offset odds)))))
             (_.coverage [/.iterations]
               (let [(open "/#[0]") /.functor
                     (open "stack#[0]") (stack.equivalence text.equivalence)]
                 (stack#= (/.first size
                                   (/#each %.natural (..iterations ++ offset)))
                          (/.first size
                                   (/.iterations (function (_ n) [(++ n) (%.natural n)])
                                                 offset)))))
             (_.coverage [/.cycle]
               (let [cycle (stack.partial cycle_start cycle_next)]
                 (stack#= (stack.together (stack.repeated size cycle))
                          (/.first (n.* size (stack.size cycle))
                                   (/.cycle [cycle_start cycle_next])))))
             (_.coverage [/.pattern]
               (let [(/.pattern first second third next) (..iterations ++ offset)]
                 (and (n.= offset first)
                      (n.= (n.+ 1 offset) second)
                      (n.= (n.+ 2 offset) third))))
             ))))
