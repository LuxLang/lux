(.using
 [library
  [lux (.except)
   ["_" test (.only Test)]
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" maybe (.open: "[1]#[0]" functor)]]
   [data
    ["[0]" text (.open: "[1]#[0]" equivalence monoid)]
    [collection
     ["[0]" list (.open: "[1]#[0]" mix)]]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [type (.only by_example)]]]
 [\\library
  ["[0]" /]])

(def builder
  (/.builder text.monoid))

(def :@:
  (by_example [@]
    (is (/.Builder @ Text)
        ..builder)
    @))

(def .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Tree])
      (do [! random.monad]
        [tag_left (random.alpha_numeric 1)
         tag_right (random.only (|>> (text#= tag_left) not)
                                (random.alpha_numeric 1))
         expected_left random.nat
         expected_right random.nat]
        (all _.and
             (_.coverage [/.Builder /.builder]
               (exec (/.builder text.monoid)
                 true))
             (_.coverage [/.tag]
               (and (text#= tag_left
                            (/.tag (at ..builder leaf tag_left expected_left)))
                    (text#= (text#composite tag_left tag_right)
                            (/.tag (at ..builder branch
                                       (at ..builder leaf tag_left expected_left)
                                       (at ..builder leaf tag_right expected_right))))))
             (_.coverage [/.root]
               (and (case (/.root (at ..builder leaf tag_left expected_left))
                      {.#Left actual}
                      (n.= expected_left actual)
                      
                      {.#Right _}
                      false)
                    (case (/.root (at ..builder branch
                                      (at ..builder leaf tag_left expected_left)
                                      (at ..builder leaf tag_right expected_right)))
                      {.#Left _}
                      false
                      
                      {.#Right [left right]}
                      (case [(/.root left)
                             (/.root right)]
                        [{.#Left actual_left} {.#Left actual_right}]
                        (and (n.= expected_left actual_left)
                             (n.= expected_right actual_right))
                        
                        _
                        false))))
             (_.coverage [/.value]
               (and (n.= expected_left
                         (/.value (at ..builder leaf tag_left expected_left)))
                    (n.= expected_left
                         (/.value (at ..builder branch
                                      (at ..builder leaf tag_left expected_left)
                                      (at ..builder leaf tag_right expected_right))))))
             (do random.monad
               [.let [tags_equivalence (list.equivalence text.equivalence)
                      values_equivalence (list.equivalence n.equivalence)]
                tags/H (random.alpha_numeric 1)
                tags/T (random.list 5 (random.alpha_numeric 1))
                values/H random.nat
                values/T (random.list 5 random.nat)]
               (_.coverage [/.tags /.values]
                 (let [tree (list#mix (function (_ [tag value] tree)
                                        (at builder branch tree (at builder leaf tag value)))
                                      (at builder leaf tags/H values/H)
                                      (list.zipped_2 tags/T values/T))]
                   (and (at tags_equivalence = (list.partial tags/H tags/T) (/.tags tree))
                        (at values_equivalence = (list.partial values/H values/T) (/.values tree))))))
             (_.coverage [/.one]
               (let [can_find_correct_one!
                     (|> (at ..builder leaf tag_left expected_left)
                         (/.one (text.contains? tag_left))
                         (maybe#each (n.= expected_left))
                         (maybe.else false))

                     cannot_find_incorrect_one!
                     (|> (at ..builder leaf tag_right expected_right)
                         (/.one (text.contains? tag_left))
                         (maybe#each (n.= expected_left))
                         (maybe.else false)
                         not)

                     can_find_left!
                     (|> (at ..builder branch
                             (at ..builder leaf tag_left expected_left)
                             (at ..builder leaf tag_right expected_right))
                         (/.one (text.contains? tag_left))
                         (maybe#each (n.= expected_left))
                         (maybe.else false))

                     can_find_right!
                     (|> (at ..builder branch
                             (at ..builder leaf tag_left expected_left)
                             (at ..builder leaf tag_right expected_right))
                         (/.one (text.contains? tag_right))
                         (maybe#each (n.= expected_right))
                         (maybe.else false))]
                 (and can_find_correct_one!
                      cannot_find_incorrect_one!
                      can_find_left!
                      can_find_right!)))
             (_.coverage [/.exists?]
               (let [can_find_correct_one!
                     (/.exists? (text.contains? tag_left)
                                (at ..builder leaf tag_left expected_left))

                     cannot_find_incorrect_one!
                     (not (/.exists? (text.contains? tag_left)
                                     (at ..builder leaf tag_right expected_right)))

                     can_find_left!
                     (/.exists? (text.contains? tag_left)
                                (at ..builder branch
                                    (at ..builder leaf tag_left expected_left)
                                    (at ..builder leaf tag_right expected_right)))

                     can_find_right!
                     (/.exists? (text.contains? tag_right)
                                (at ..builder branch
                                    (at ..builder leaf tag_left expected_left)
                                    (at ..builder leaf tag_right expected_right)))]
                 (and can_find_correct_one!
                      cannot_find_incorrect_one!
                      can_find_left!
                      can_find_right!)))
             ))))
