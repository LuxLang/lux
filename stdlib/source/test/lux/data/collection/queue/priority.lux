(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [abstract
     ["." monad (#+ do)]]
    [control
     ["." maybe ("#\." functor)]]
    [data
     ["." bit ("#\." equivalence)]]
    [math
     ["." random (#+ Random)]
     [number
      ["n" nat]]]]]
  [\\library
   ["." / (#+ Queue)]])

(def: .public (random size)
  (-> Nat (Random (Queue Nat)))
  (do {! random.monad}
    [inputs (random.list size random.nat)]
    (monad.fold ! (function (_ head tail)
                    (do !
                      [priority random.nat]
                      (in (/.end priority head tail))))
                /.empty
                inputs)))

(def: .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Queue])
      (do {! random.monad}
        [size (\ ! map (n.% 100) random.nat)
         sample (..random size)
         non_member_priority random.nat
         non_member (random.only (|>> (/.member? n.equivalence sample) not)
                                 random.nat)

         max_member random.nat
         min_member random.nat]
        ($_ _.and
            (_.cover [/.size]
                     (n.= size (/.size sample)))
            (_.cover [/.empty?]
                     (bit\= (n.= 0 (/.size sample))
                            (/.empty? sample)))
            (_.cover [/.empty]
                     (/.empty? /.empty))
            (_.cover [/.front]
                     (case (/.front sample)
                       (#.Some first)
                       (n.> 0 (/.size sample))
                       
                       #.None
                       (/.empty? sample)))
            (_.cover [/.member?]
                     (case (/.front sample)
                       (#.Some first)
                       (/.member? n.equivalence sample first)
                       
                       #.None
                       (/.empty? sample)))
            (_.cover [/.end]
                     (let [sample+ (/.end non_member_priority non_member sample)]
                       (and (not (/.member? n.equivalence sample non_member))
                            (n.= (inc (/.size sample))
                                 (/.size sample+))
                            (/.member? n.equivalence sample+ non_member))))
            (_.cover [/.next]
                     (let [sample- (/.next sample)]
                       (or (and (/.empty? sample)
                                (/.empty? sample-))
                           (n.= (dec (/.size sample))
                                (/.size sample-)))))
            (_.for [/.Priority]
                   ($_ _.and
                       (_.cover [/.max]
                                (|> /.empty
                                    (/.end /.min min_member)
                                    (/.end /.max max_member)
                                    /.front
                                    (maybe\map (n.= max_member))
                                    (maybe.else false)))
                       (_.cover [/.min]
                                (|> /.empty
                                    (/.end /.max max_member)
                                    (/.end /.min min_member)
                                    /.next
                                    /.front
                                    (maybe\map (n.= min_member))
                                    (maybe.else false)))
                       ))
            ))))
