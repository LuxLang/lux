(.module:
  [lux #*
   [control
    [monad (#+ Monad do)]
    [equivalence (#+ Equivalence)]]
   [data
    ["." product]
    ["." number]
    [collection
     ["s" set]
     ["dict" dictionary
      ["&" ordered]]
     ["." list ("#/." functor)]]]
   [math
    ["r" random]]]
  lux/test)

(context: "Dictionary"
  (<| (times 100)
      (do @
        [size (|> r.nat (:: @ map (n/% 100)))
         keys (r.set number.nat-hash size r.nat)
         values (r.set number.nat-hash size r.nat)
         extra-key (|> r.nat (r.filter (|>> (s.member? keys) not)))
         extra-value r.nat
         #let [pairs (list.zip2 (s.to-list keys)
                                (s.to-list values))
               sample (&.from-list number.nat-order pairs)
               sorted-pairs (list.sort (function (_ [left _] [right _])
                                         (n/< left right))
                                       pairs)
               sorted-values (list/map product.right sorted-pairs)
               (^open "&/.") (&.equivalence number.nat-equivalence)]]
        ($_ seq
            (test "Can query the size of a dictionary."
                  (n/= size (&.size sample)))

            (test "Can query value for minimum key."
                  (case [(&.min sample) (list.head sorted-values)]
                    [#.None #.None]
                    #1

                    [(#.Some reference) (#.Some sample)]
                    (n/= reference sample)

                    _
                    #0))

            (test "Can query value for maximum key."
                  (case [(&.max sample) (list.last sorted-values)]
                    [#.None #.None]
                    #1

                    [(#.Some reference) (#.Some sample)]
                    (n/= reference sample)

                    _
                    #0))

            (test "Converting dictionaries to/from lists cannot change their values."
                  (|> sample
                      &.entries (&.from-list number.nat-order)
                      (&/= sample)))

            (test "Order is preserved."
                  (let [(^open "list/.") (list.equivalence (: (Equivalence [Nat Nat])
                                                              (function (_ [kr vr] [ks vs])
                                                                (and (n/= kr ks)
                                                                     (n/= vr vs)))))]
                    (list/= (&.entries sample)
                            sorted-pairs)))

            (test "Every key in a dictionary must be identifiable."
                  (list.every? (function (_ key) (&.contains? key sample))
                               (&.keys sample)))

            (test "Can add and remove elements in a dictionary."
                  (and (not (&.contains? extra-key sample))
                       (let [sample' (&.put extra-key extra-value sample)
                             sample'' (&.remove extra-key sample')]
                         (and (&.contains? extra-key sample')
                              (not (&.contains? extra-key sample''))
                              (case [(&.get extra-key sample')
                                     (&.get extra-key sample'')]
                                [(#.Some found) #.None]
                                (n/= extra-value found)

                                _
                                #0)))
                       ))
            ))))
