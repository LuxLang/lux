... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [equivalence (.only Equivalence)]
    [order (.only Order)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [error
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    ["[0]" product]
    ["[0]" bit]
    [collection
     ["[0]" set]
     ["[0]" list (.use "[1]#[0]" functor)]
     ["[0]" stack (.use "[1]#[0]" functor)]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number
     ["n" natural]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public (dictionary order gen_key gen_value size)
  (for_any (_ k v)
    (-> (Order k) (Random k) (Random v) Natural
        (Random (/.Dictionary k v))))
  (when size
    0
    (random#in (/.empty order))
    
    _
    (do random.monad
      [partial (dictionary order gen_key gen_value (-- size))
       key (random.only (|>> (/.key? partial) not)
                        gen_key)
       value gen_value]
      (in (/.has key value partial)))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Dictionary
              /.#order /.#root])
      (do [! random.monad]
        [size (by ! each (n.% 100) random.natural)
         keys (random.set n.hash size random.natural)
         values (random.set n.hash size random.natural)
         extra_key (random.only (|>> (set.member? keys) not)
                                random.natural)
         extra_value random.natural
         shift random.natural
         .let [pairs (list.zipped_2 (set.as_list keys)
                                    (set.as_list values))
               sample (/.of_list n.order pairs)
               sorted_pairs (list.of_stack (stack.sorted (function (_ [left _] [right _])
                                                           (n.< left right))
                                                         (list.as_stack pairs)))
               sorted_values (list#each product.right sorted_pairs)
               (open "list#[0]") (list.equivalence (is (Equivalence [Natural Natural])
                                                       (function (_ [kr vr] [ks vs])
                                                         (and (n.= kr ks)
                                                              (n.= vr vs)))))
               (open "/#[0]") (/.equivalence n.equivalence)]]
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec (/.equivalence n.equivalence) (..dictionary n.order random.natural random.natural size)))
             
             (_.coverage [/.size]
               (n.= size (/.size sample)))
             (_.coverage [/.empty?]
               (bit.= (n.= 0 (/.size sample))
                      (/.empty? sample)))
             (_.coverage [/.empty]
               (/.empty? (/.empty n.order)))
             (_.coverage [/.minor]
               (when [(/.minor sample) (list.item 0 sorted_values)]
                 [{try.#Failure _} {try.#Failure _}]
                 true

                 [{try.#Success reference} {try.#Success sample}]
                 (n.= reference sample)

                 _
                 false))
             (_.coverage [/.major]
               (when [(/.major sample) (list.item (-- size) sorted_values)]
                 [{try.#Failure _} {try.#Failure _}]
                 true

                 [{try.#Success reference} {try.#Success sample}]
                 (n.= reference sample)

                 _
                 false))
             (_.coverage [/.entries]
               (list#= (/.entries sample)
                       sorted_pairs))
             (_.coverage [/.keys /.values]
               (list#= (/.entries sample)
                       (list.zipped_2 (/.keys sample)
                                      (/.values sample))))
             (_.coverage [/.of_list]
               (|> sample
                   /.entries
                   (/.of_list n.order)
                   (/#= sample)))
             (_.coverage [/.key?]
               (and (list.every? (/.key? sample)
                                 (/.keys sample))
                    (not (/.key? sample extra_key))))
             (_.coverage [/.has]
               (and (not (/.key? sample extra_key))
                    (let [sample+ (/.has extra_key extra_value sample)]
                      (and (/.key? sample+ extra_key)
                           (n.= (++ (/.size sample))
                                (/.size sample+))))))
             (_.coverage [/.value]
               (let [sample+ (/.has extra_key extra_value sample)]
                 (when [(/.value extra_key sample)
                        (/.value extra_key sample+)]
                   [{try.#Failure _} {try.#Success actual}]
                   (n.= extra_value actual)
                   
                   _
                   false)))
             (_.coverage [/.lacks]
               (|> sample
                   (/.has extra_key extra_value)
                   (/.lacks extra_key)
                   (/#= sample)))
             (_.coverage [/.revised]
               (|> sample
                   (/.has extra_key extra_value)
                   (/.revised extra_key (n.+ shift))
                   (/.value extra_key)
                   (try#each (n.= (n.+ shift extra_value)))
                   (try.else false)))
             ))))
