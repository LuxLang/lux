(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [abstract
     [monad (#+ do)]
     [\\specification
      ["$." equivalence]
      ["$." monoid]
      ["$." fold]
      ["$." functor (#+ Injection)]
      ["$." apply]
      ["$." monad]]]
    [control
     ["." try (#+ Try)]
     ["." exception]]
    [data
     ["." bit ("#\." equivalence)]
     [collection
      ["." list ("#\." fold)]
      ["." set]]]
    [math
     ["." random]
     [number
      ["n" nat]]]]]
  [\\library
   ["." / ("#\." monad)]])

(def: signatures
  Test
  (do {! random.monad}
    [size (\ ! map (n.% 100) random.nat)]
    ($_ _.and
        (_.for [/.equivalence]
               ($equivalence.spec (/.equivalence n.equivalence) (random.row size random.nat)))
        (_.for [/.monoid]
               ($monoid.spec (/.equivalence n.equivalence) /.monoid (random.row size random.nat)))
        (_.for [/.fold]
               ($fold.spec /\wrap /.equivalence /.fold))
        (_.for [/.functor]
               ($functor.spec /\wrap /.equivalence /.functor))
        (_.for [/.apply]
               ($apply.spec /\wrap /.equivalence /.apply))
        (_.for [/.monad]
               ($monad.spec /\wrap /.equivalence /.monad))
        )))

(def: whole
  Test
  (do {! random.monad}
    [size (\ ! map (n.% 100) random.nat)
     sample (random.set n.hash size random.nat)
     #let [sample (|> sample set.to_list /.from_list)]
     #let [(^open "/\.") (/.equivalence n.equivalence)]]
    ($_ _.and
        (_.cover [/.size]
                 (n.= size (/.size sample)))
        (_.cover [/.empty?]
                 (bit\= (/.empty? sample) (n.= 0 (/.size sample))))
        (_.cover [/.empty]
                 (/.empty? /.empty))
        (_.cover [/.to_list /.from_list]
                 (|> sample /.to_list /.from_list (/\= sample)))
        (_.cover [/.reverse]
                 (or (n.< 2 (/.size sample))
                     (let [not_same!
                           (not (/\= sample
                                     (/.reverse sample)))

                           self_symmetry!
                           (/\= sample
                                (/.reverse (/.reverse sample)))]
                       (and not_same!
                            self_symmetry!))))
        (_.cover [/.every? /.any?]
                 (if (/.every? n.even? sample)
                   (not (/.any? (bit.complement n.even?) sample))
                   (/.any? (bit.complement n.even?) sample)))
        )))

(def: index_based
  Test
  (do {! random.monad}
    [size (\ ! map (|>> (n.% 100) inc) random.nat)]
    ($_ _.and
        (do !
          [good_index (|> random.nat (\ ! map (n.% size)))
           #let [bad_index (n.+ size good_index)]
           sample (random.set n.hash size random.nat)
           non_member (random.filter (|>> (set.member? sample) not)
                                     random.nat)
           #let [sample (|> sample set.to_list /.from_list)]]
          ($_ _.and
              (_.cover [/.nth]
                       (case (/.nth good_index sample)
                         (#try.Success member)
                         (/.member? n.equivalence sample member)
                         
                         (#try.Failure error)
                         false))
              (_.cover [/.put]
                       (<| (try.default false)
                           (do try.monad
                             [sample (/.put good_index non_member sample)
                              actual (/.nth good_index sample)]
                             (wrap (is? non_member actual)))))
              (_.cover [/.update]
                       (<| (try.default false)
                           (do try.monad
                             [sample (/.put good_index non_member sample)
                              sample (/.update good_index inc sample)
                              actual (/.nth good_index sample)]
                             (wrap (n.= (inc non_member) actual)))))
              (_.cover [/.within_bounds?]
                       (and (/.within_bounds? sample good_index)
                            (not (/.within_bounds? sample bad_index))))
              (_.cover [/.index_out_of_bounds]
                       (let [fails! (: (All [a] (-> (Try a) Bit))
                                       (function (_ situation)
                                         (case situation
                                           (#try.Success member)
                                           false
                                           
                                           (#try.Failure error)
                                           (exception.match? /.index_out_of_bounds error))))]
                         (and (fails! (/.nth bad_index sample))
                              (fails! (/.put bad_index non_member sample))
                              (fails! (/.update bad_index inc sample)))))
              ))
        )))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [/.Row])
      (do {! random.monad}
        [size (\ ! map (|>> (n.% 100) inc) random.nat)]
        ($_ _.and
            ..signatures
            ..whole
            ..index_based

            (do !
              [sample (random.set n.hash size random.nat)
               non_member (random.filter (|>> (set.member? sample) not)
                                         random.nat)
               #let [sample (|> sample set.to_list /.from_list)]
               #let [(^open "/\.") (/.equivalence n.equivalence)]]
              ($_ _.and
                  (do !
                    [value/0 random.nat
                     value/1 random.nat
                     value/2 random.nat]
                    (_.cover [/.row]
                             (/\= (/.from_list (list value/0 value/1 value/2))
                                  (/.row value/0 value/1 value/2))))
                  (_.cover [/.member?]
                           (and (list.every? (/.member? n.equivalence sample)
                                             (/.to_list sample))
                                (not (/.member? n.equivalence sample non_member))))
                  (_.cover [/.add]
                           (let [added (/.add non_member sample)

                                 size_increases!
                                 (n.= (inc (/.size sample))
                                      (/.size added))

                                 is_a_member!
                                 (/.member? n.equivalence added non_member)]
                             (and size_increases!
                                  is_a_member!)))
                  (_.cover [/.pop]
                           (if (/.empty? sample)
                             (/.empty? (/.pop sample))
                             (let [expected_size!
                                   (n.= (dec (/.size sample))
                                        (/.size (/.pop sample)))

                                   symmetry!
                                   (|> sample
                                       (/.add non_member)
                                       /.pop
                                       (/\= sample))]
                               (and expected_size!
                                    symmetry!))))
                  ))
            ))))
