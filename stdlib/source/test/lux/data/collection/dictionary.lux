... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [hash (.only Hash)]
    [monad (.only do)]
    ["[0]" functor
     ["[1]T" \\test (.only Injection)]]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]]
   [control
    ["[0]" try (.use "[1]#[0]" functor)]
    ["[0]" exception]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]
     ["[0]" set]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the injection
  (Injection (/.Dictionary Natural))
  (|>> [0] list (/.of_list n.hash)))

(the for_dictionaries
  Test
  (do [! random.monad]
    [.let [capped_natural (is (Random Natural)
                              (of ! each (n.% 100) random.natural))]
     size capped_natural
     dict (random.dictionary n.hash size random.natural capped_natural)
     non_key (random.only (|>> (/.key? dict) not)
                          random.natural)
     test_val (random.only (|>> (list.member? n.equivalence (/.values dict)) not)
                           random.natural)]
    (all _.and
         (_.coverage [/.size]
           (n.= size (/.size dict)))
         
         (_.coverage [/.empty?]
           (when size
             0 (/.empty? dict)
             _ (not (/.empty? dict))))
         
         (_.coverage [/.empty]
           (let [sample (/.empty n.hash)]
             (and (n.= 0 (/.size sample))
                  (/.empty? sample))))

         (do !
           [constant random.natural
            .let [hash (is (Hash Natural)
                           (implementation
                            (the equivalence n.equivalence)
                            (the (hash _)
                              constant)))]]
           (_.coverage [/.key_hash]
             (same? hash (/.key_hash (/.empty hash)))))
         
         (_.coverage [/.entries]
           (let [entries (/.entries dict)

                 correct_size!
                 (n.= (/.size dict)
                      (list.size entries))

                 unique_keys!
                 (|> entries
                     (list#each product.left)
                     (set.of_list n.hash)
                     set.size
                     (n.= (/.size dict)))

                 correct_pairing!
                 (list.every? (function (_ [key value])
                                (|> dict
                                    (/.value key)
                                    (try#each (n.= value))
                                    (try.else false)))
                              entries)]
             (and correct_size!
                  unique_keys!
                  correct_pairing!)))
         (_.coverage [/.keys]
           (let [keys (/.keys dict)

                 correct_size!
                 (n.= (/.size dict)
                      (list.size keys))

                 unique_keys!
                 (|> keys
                     (set.of_list n.hash)
                     set.size
                     (n.= (/.size dict)))

                 recognized!
                 (list.every? (/.key? dict) keys)]
             (and correct_size!
                  unique_keys!
                  recognized!)))
         (_.coverage [/.values]
           (n.= (/.size dict)
                (list.size (/.values dict))))

         (_.coverage [/.composite]
           (let [merging_with_oneself (let [(open "[0]") (/.equivalence n.equivalence)]
                                        (= dict (/.composite dict dict)))
                 overwritting_keys (let [dict' (|> dict /.entries
                                                   (list#each (function (_ [k v]) [k (++ v)]))
                                                   (/.of_list n.hash))
                                         (open "[0]") (/.equivalence n.equivalence)]
                                     (= dict' (/.composite dict' dict)))]
             (and merging_with_oneself
                  overwritting_keys)))
         
         (_.coverage [/.composite_with]
           (list.every? (function (_ [x x*2]) (n.= (n.* 2 x) x*2))
                        (list.zipped_2 (/.values dict)
                                       (/.values (/.composite_with n.+ dict dict)))))

         (_.coverage [/.of_list]
           (let [(open "[0]") (/.equivalence n.equivalence)]
             (and (= dict dict)
                  (|> dict /.entries (/.of_list n.hash) (= dict)))))
         )))

(the for_entries
  Test
  (do [! random.monad]
    [.let [capped_natural (is (Random Natural)
                              (of ! each (n.% 100) random.natural))]
     size capped_natural
     dict (random.dictionary n.hash size random.natural capped_natural)
     non_key (random.only (|>> (/.key? dict) not)
                          random.natural)
     test_val (random.only (|>> (list.member? n.equivalence (/.values dict)) not)
                           random.natural)]
    (all _.and
         (_.coverage [/.key?]
           (list.every? (/.key? dict)
                        (/.keys dict)))
         
         (_.coverage [/.value]
           (and (list.every? (function (_ key) (when (/.value key dict)
                                                 {try.#Success _}
                                                 true
                                                 
                                                 failure
                                                 false))
                             (/.keys dict))
                (when (/.value non_key dict)
                  {try.#Success _}
                  false
                  
                  failure
                  true)))
         
         (_.coverage [/.has]
           (and (n.= (++ (/.size dict))
                     (/.size (/.has non_key test_val dict)))
                (when (/.value non_key (/.has non_key test_val dict))
                  {try.#Success v}
                  (n.= test_val v)
                  
                  failure
                  true)))
         
         (_.coverage [/.has' /.key_already_exists]
           (let [can_put_new_keys!
                 (when (/.has' non_key test_val dict)
                   {try.#Success dict}
                   (when (/.value non_key dict)
                     {try.#Success v}
                     (n.= test_val v)
                     
                     _
                     true)

                   {try.#Failure _}
                   false)
                 
                 cannot_put_old_keys!
                 (or (n.= 0 size)
                     (let [first_key (|> dict /.keys list.head try.trusted)]
                       (when (/.has' first_key test_val dict)
                         {try.#Success _}
                         false
                         
                         {try.#Failure error}
                         (exception.is? /.key_already_exists error))))]
             (and can_put_new_keys!
                  cannot_put_old_keys!)))
         
         (_.coverage [/.lacks]
           (and (let [base (/.has non_key test_val dict)]
                  (and (/.key? base non_key)
                       (not (/.key? (/.lacks non_key base) non_key))))
                (when (list.head (/.keys dict))
                  {try.#Failure _}
                  true
                  
                  {try.#Success known_key}
                  (n.= (-- (/.size dict))
                       (/.size (/.lacks known_key dict))))))
         
         (_.coverage [/.revised]
           (let [base (/.has non_key test_val dict)
                 updt (/.revised non_key ++ base)]
             (when [(/.value non_key base) (/.value non_key updt)]
               [{try.#Success x} {try.#Success y}]
               (n.= (++ x) y)

               _
               false)))
         
         (_.coverage [/.revised']
           (let [can_upsert_new_key!
                 (when (/.value non_key (/.revised' non_key test_val ++ dict))
                   {try.#Success inserted}
                   (n.= (++ test_val) inserted)

                   failure
                   false)

                 can_upsert_old_key!
                 (when (list.head (/.entries dict))
                   {try.#Failure _}
                   true
                   
                   {try.#Success [known_key known_value]}
                   (when (/.value known_key (/.revised' known_key test_val ++ dict))
                     {try.#Success updated}
                     (n.= (++ known_value) updated)

                     failure
                     false))]
             (and can_upsert_new_key!
                  can_upsert_old_key!)))

         (_.coverage [/.sub]
           (|> dict
               (/.has non_key test_val)
               (/.sub (list non_key))
               /.size
               (n.= 1)))
         
         (_.coverage [/.re_bound]
           (or (n.= 0 size)
               (let [first_key (|> dict /.keys list.head try.trusted)
                     rebound (/.re_bound first_key non_key dict)]
                 (and (n.= (/.size dict) (/.size rebound))
                      (/.key? rebound non_key)
                      (not (/.key? rebound first_key))
                      (n.= (try.trusted (/.value first_key dict))
                           (try.trusted (/.value non_key rebound)))))))
         )))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Dictionary
              /.#hash /.#root])
      (do [! random.monad]
        [.let [capped_natural (is (Random Natural)
                                  (of ! each (n.% 100) random.natural))]
         size capped_natural
         dict (random.dictionary n.hash size random.natural capped_natural)
         non_key (random.only (|>> (/.key? dict) not)
                              random.natural)
         test_val (random.only (|>> (list.member? n.equivalence (/.values dict)) not)
                               random.natural)]
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec (/.equivalence n.equivalence)
                                       (random.dictionary n.hash size random.natural random.natural)))
             (_.for [/.monoid]
                    (monoidT.spec (/.equivalence n.equivalence)
                                  (/.monoid n.hash)
                                  (random.dictionary n.hash size random.natural random.natural)))
             
             (_.for [/.functor]
                    (functorT.spec ..injection /.equivalence /.functor))

             ..for_dictionaries
             ..for_entries
             ))))
