... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [hash (.only Hash)]
    ["[0]" monad]
    ["[0]" functor
     ["[1]T" \\test (.only Injection)]]
    ["[0]" equality
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]]
   [error
    ["[0]" try (.use "[1]:[0]" functor)]
    ["[0]" exception]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]:[0]" functor)]
     ["[0]" set]]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the injection
  (Injection (/.Dictionary Natural))
  (|>> [0] list (/.of_list n.hash)))

(the for_dictionaries
  Test
  (monad.let [! random.monad]
    [.let [capped_natural (is (Random Natural)
                              (by ! each (n.% 100) random.natural))]
     size capped_natural
     dict (random.dictionary n.hash size random.natural capped_natural)
     non_key (random.only (|>> (/.in_domain? dict) not)
                          random.natural)
     test_val (random.only (|>> (list.member? n.equality (/.values dict)) not)
                           random.natural)]
    (all _.and
         (_.coverage [/.size]
           (n.= size (/.size dict)))
         
         (_.coverage [/.empty?]
           (when size
             0 (/.empty? dict)
             _ (not (/.empty? dict))))
         
         (_.coverage [/.empty]
           (let [sample (/.empty n.hash)]
             (and (n.= 0 (/.size sample))
                  (/.empty? sample))))

         (monad.let !
           [constant random.natural
            .let [hash (is (Hash Natural)
                           (implementation
                            (the equality n.equality)
                            (the (hash _)
                              constant)))]]
           (_.coverage [/.hash]
             (identical? hash (/.hash (/.empty hash)))))
         
         (_.coverage [/.entries]
           (let [entries (/.entries dict)

                 correct_size!
                 (n.= (/.size dict)
                      (list.size entries))

                 unique_keys!
                 (|> entries
                     (list:each product.left)
                     (set.of_list n.hash)
                     set.size
                     (n.= (/.size dict)))

                 correct_pairing!
                 (list.every? (function (_ [key value])
                                (|> dict
                                    (/.value key)
                                    (try:each (n.= value))
                                    (try.else false)))
                              entries)]
             (and correct_size!
                  unique_keys!
                  correct_pairing!)))
         (_.coverage [/.domain]
           (let [keys (/.domain dict)

                 correct_size!
                 (n.= (/.size dict)
                      (list.size keys))

                 unique_keys!
                 (|> keys
                     (set.of_list n.hash)
                     set.size
                     (n.= (/.size dict)))

                 recognized!
                 (list.every? (/.in_domain? dict) keys)]
             (and correct_size!
                  unique_keys!
                  recognized!)))
         (_.coverage [/.values]
           (n.= (/.size dict)
                (list.size (/.values dict))))

         (_.coverage [/.composite]
           (let [merging_with_oneself (let [(open "[0]") (/.equality n.equality)]
                                        (= dict (/.composite dict dict)))
                 overwritting_keys (let [dict' (|> dict
                                                   /.entries
                                                   (list:each (function (_ [k v]) [k (++ v)]))
                                                   (/.of_list n.hash))
                                         (open "[0]") (/.equality n.equality)]
                                     (= dict' (/.composite dict' dict)))]
             (and merging_with_oneself
                  overwritting_keys)))
         
         (_.coverage [/.composite_with]
           (list.every? (function (_ [x xx2]) (n.= (n.x 2 x) xx2))
                        (list.zipped_2 (/.values dict)
                                       (/.values (/.composite_with n.+ dict dict)))))

         (_.coverage [/.of_list]
           (let [(open "[0]") (/.equality n.equality)]
             (and (= dict dict)
                  (|> dict
                      /.entries
                      (/.of_list n.hash)
                      (= dict)))))
         )))

(the for_entries
  Test
  (monad.let [! random.monad]
    [.let [capped_natural (is (Random Natural)
                              (by ! each (n.% 100) random.natural))]
     size capped_natural
     dict (random.dictionary n.hash size random.natural capped_natural)
     non_key (random.only (|>> (/.in_domain? dict) not)
                          random.natural)
     test_val (random.only (|>> (list.member? n.equality (/.values dict)) not)
                           random.natural)]
    (all _.and
         (_.coverage [/.in_domain?]
           (list.every? (/.in_domain? dict)
                        (/.domain dict)))
         
         (_.coverage [/.value]
           (and (list.every? (function (_ key) (when (/.value key dict)
                                                 {try.:Success _}
                                                 true
                                                 
                                                 failure
                                                 false))
                             (/.domain dict))
                (when (/.value non_key dict)
                  {try.:Success _}
                  false
                  
                  failure
                  true)))
         
         (_.coverage [/.has]
           (and (n.= (++ (/.size dict))
                     (/.size (/.has non_key test_val dict)))
                (when (/.value non_key (/.has non_key test_val dict))
                  {try.:Success v}
                  (n.= test_val v)
                  
                  failure
                  true)))
         
         (_.coverage [/.has' /.key_already_exists]
           (let [can_put_new_keys!
                 (when (/.has' non_key test_val dict)
                   {try.:Success dict}
                   (when (/.value non_key dict)
                     {try.:Success v}
                     (n.= test_val v)
                     
                     _
                     true)

                   {try.:Failure _}
                   false)
                 
                 cannot_put_old_keys!
                 (or (n.= 0 size)
                     (let [first_key (|> dict /.domain (list.item 0) try.trusted)]
                       (when (/.has' first_key test_val dict)
                         {try.:Success _}
                         false
                         
                         {try.:Failure error}
                         (exception.is? /.key_already_exists error))))]
             (and can_put_new_keys!
                  cannot_put_old_keys!)))
         
         (_.coverage [/.lacks]
           (and (let [base (/.has non_key test_val dict)]
                  (and (/.in_domain? base non_key)
                       (not (/.in_domain? (/.lacks non_key base) non_key))))
                (when (list.item 0 (/.domain dict))
                  {try.:Failure _}
                  true
                  
                  {try.:Success known_key}
                  (n.= (-- (/.size dict))
                       (/.size (/.lacks known_key dict))))))
         
         (_.coverage [/.revised]
           (let [base (/.has non_key test_val dict)
                 updt (/.revised non_key ++ base)]
             (when [(/.value non_key base) (/.value non_key updt)]
               [{try.:Success x} {try.:Success y}]
               (n.= (++ x) y)

               _
               false)))
         
         (_.coverage [/.revised']
           (let [can_upsert_new_key!
                 (when (/.value non_key (/.revised' non_key test_val ++ dict))
                   {try.:Success inserted}
                   (n.= (++ test_val) inserted)

                   failure
                   false)

                 can_upsert_old_key!
                 (when (list.item 0 (/.entries dict))
                   {try.:Failure _}
                   true
                   
                   {try.:Success [known_key known_value]}
                   (when (/.value known_key (/.revised' known_key test_val ++ dict))
                     {try.:Success updated}
                     (n.= (++ known_value) updated)

                     failure
                     false))]
             (and can_upsert_new_key!
                  can_upsert_old_key!)))

         (_.coverage [/.sub]
           (|> dict
               (/.has non_key test_val)
               (/.sub (list non_key))
               /.size
               (n.= 1)))
         
         (_.coverage [/.re_bound]
           (or (n.= 0 size)
               (let [first_key (|> dict /.domain (list.item 0) try.trusted)
                     rebound (/.re_bound first_key non_key dict)]
                 (and (n.= (/.size dict) (/.size rebound))
                      (/.in_domain? rebound non_key)
                      (not (/.in_domain? rebound first_key))
                      (n.= (try.trusted (/.value first_key dict))
                           (try.trusted (/.value non_key rebound)))))))
         )))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Dictionary
              /.:hash /.:root])
      (monad.let [! random.monad]
        [.let [capped_natural (is (Random Natural)
                                  (by ! each (n.% 100) random.natural))]
         size capped_natural
         dict (random.dictionary n.hash size random.natural capped_natural)
         non_key (random.only (|>> (/.in_domain? dict) not)
                              random.natural)
         test_val (random.only (|>> (list.member? n.equality (/.values dict)) not)
                               random.natural)]
        (all _.and
             (_.for [/.equality]
                    (equalityT.spec (/.equality n.equality)
                                    (random.dictionary n.hash size random.natural random.natural)))
             (_.for [/.monoid]
                    (monoidT.spec (/.equality n.equality)
                                  (/.monoid n.hash)
                                  (random.dictionary n.hash size random.natural random.natural)))
             
             (_.for [/.functor]
                    (functorT.spec ..injection /.equality /.functor))

             ..for_dictionaries
             ..for_entries
             ))))
