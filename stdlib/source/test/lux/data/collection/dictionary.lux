(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [abstract
     [hash (#+ Hash)]
     [monad (#+ do)]
     [\\specification
      ["$." equivalence]
      ["$." functor (#+ Injection)]]]
    [control
     ["." maybe ("#\." functor)]
     ["." try]
     ["." exception]]
    [data
     ["." product]
     [collection
      ["." list ("#\." functor)]
      ["." set]]]
    [math
     ["." random]
     [number
      ["n" nat]]]]]
  [\\library
   ["." /]])

(def: injection
  (Injection (/.Dictionary Nat))
  (|>> [0] list (/.of_list n.hash)))

(def: for_dictionaries
  Test
  (do {! random.monad}
    [.let [capped_nat (\ random.monad each (n.% 100) random.nat)]
     size capped_nat
     dict (random.dictionary n.hash size random.nat capped_nat)
     non_key (random.only (|>> (/.key? dict) not)
                          random.nat)
     test_val (random.only (|>> (list.member? n.equivalence (/.values dict)) not)
                           random.nat)]
    ($_ _.and
        (_.cover [/.size]
                 (n.= size (/.size dict)))
        
        (_.cover [/.empty?]
                 (case size
                   0 (/.empty? dict)
                   _ (not (/.empty? dict))))
        
        (_.cover [/.empty]
                 (let [sample (/.empty n.hash)]
                   (and (n.= 0 (/.size sample))
                        (/.empty? sample))))

        (do !
          [constant random.nat
           .let [hash (: (Hash Nat)
                         (implementation
                          (def: &equivalence n.equivalence)
                          (def: (hash _)
                            constant)))]]
          (_.cover [/.key_hash]
                   (same? hash (/.key_hash (/.empty hash)))))
        
        (_.cover [/.entries]
                 (let [entries (/.entries dict)

                       correct_size!
                       (n.= (/.size dict)
                            (list.size entries))

                       unique_keys!
                       (|> entries
                           (list\each product.left)
                           (set.of_list n.hash)
                           set.size
                           (n.= (/.size dict)))

                       correct_pairing!
                       (list.every? (function (_ [key value])
                                      (|> dict
                                          (/.value key)
                                          (maybe\each (n.= value))
                                          (maybe.else false)))
                                    entries)]
                   (and correct_size!
                        unique_keys!
                        correct_pairing!)))
        (_.cover [/.keys]
                 (let [keys (/.keys dict)

                       correct_size!
                       (n.= (/.size dict)
                            (list.size keys))

                       unique_keys!
                       (|> keys
                           (set.of_list n.hash)
                           set.size
                           (n.= (/.size dict)))

                       recognized!
                       (list.every? (/.key? dict) keys)]
                   (and correct_size!
                        unique_keys!
                        recognized!)))
        (_.cover [/.values]
                 (n.= (/.size dict)
                      (list.size (/.values dict))))

        (_.cover [/.merged]
                 (let [merging_with_oneself (let [(^open ".") (/.equivalence n.equivalence)]
                                              (= dict (/.merged dict dict)))
                       overwritting_keys (let [dict' (|> dict /.entries
                                                         (list\each (function (_ [k v]) [k (++ v)]))
                                                         (/.of_list n.hash))
                                               (^open ".") (/.equivalence n.equivalence)]
                                           (= dict' (/.merged dict' dict)))]
                   (and merging_with_oneself
                        overwritting_keys)))
        
        (_.cover [/.merged_with]
                 (list.every? (function (_ [x x*2]) (n.= (n.* 2 x) x*2))
                              (list.zipped/2 (/.values dict)
                                             (/.values (/.merged_with n.+ dict dict)))))

        (_.cover [/.of_list]
                 (let [(^open ".") (/.equivalence n.equivalence)]
                   (and (= dict dict)
                        (|> dict /.entries (/.of_list n.hash) (= dict)))))
        )))

(def: for_entries
  Test
  (do random.monad
    [.let [capped_nat (\ random.monad each (n.% 100) random.nat)]
     size capped_nat
     dict (random.dictionary n.hash size random.nat capped_nat)
     non_key (random.only (|>> (/.key? dict) not)
                          random.nat)
     test_val (random.only (|>> (list.member? n.equivalence (/.values dict)) not)
                           random.nat)]
    ($_ _.and
        (_.cover [/.key?]
                 (list.every? (/.key? dict)
                              (/.keys dict)))
        
        (_.cover [/.value]
                 (and (list.every? (function (_ key) (case (/.value key dict)
                                                       (#.Some _) true
                                                       _          false))
                                   (/.keys dict))
                      (case (/.value non_key dict)
                        (#.Some _) false
                        _          true)))
        
        (_.cover [/.has]
                 (and (n.= (++ (/.size dict))
                           (/.size (/.has non_key test_val dict)))
                      (case (/.value non_key (/.has non_key test_val dict))
                        (#.Some v) (n.= test_val v)
                        _          true)))
        
        (_.cover [/.has' /.key_already_exists]
                 (let [can_put_new_keys!
                       (case (/.has' non_key test_val dict)
                         (#try.Success dict)
                         (case (/.value non_key dict)
                           (#.Some v) (n.= test_val v)
                           _          true)

                         (#try.Failure _)
                         false)
                       
                       cannot_put_old_keys!
                       (or (n.= 0 size)
                           (let [first_key (|> dict /.keys list.head maybe.trusted)]
                             (case (/.has' first_key test_val dict)
                               (#try.Success _)
                               false
                               
                               (#try.Failure error)
                               (exception.match? /.key_already_exists error))))]
                   (and can_put_new_keys!
                        cannot_put_old_keys!)))
        
        (_.cover [/.lacks]
                 (and (let [base (/.has non_key test_val dict)]
                        (and (/.key? base non_key)
                             (not (/.key? (/.lacks non_key base) non_key))))
                      (case (list.head (/.keys dict))
                        #.None
                        true
                        
                        (#.Some known_key)
                        (n.= (-- (/.size dict))
                             (/.size (/.lacks known_key dict))))))
        
        (_.cover [/.revised]
                 (let [base (/.has non_key test_val dict)
                       updt (/.revised non_key ++ base)]
                   (case [(/.value non_key base) (/.value non_key updt)]
                     [(#.Some x) (#.Some y)]
                     (n.= (++ x) y)

                     _
                     false)))
        
        (_.cover [/.revised']
                 (let [can_upsert_new_key!
                       (case (/.value non_key (/.revised' non_key test_val ++ dict))
                         (#.Some inserted)
                         (n.= (++ test_val) inserted)

                         #.None
                         false)

                       can_upsert_old_key!
                       (case (list.head (/.entries dict))
                         #.None
                         true
                         
                         (#.Some [known_key known_value])
                         (case (/.value known_key (/.revised' known_key test_val ++ dict))
                           (#.Some updated)
                           (n.= (++ known_value) updated)

                           #.None
                           false))]
                   (and can_upsert_new_key!
                        can_upsert_old_key!)))

        (_.cover [/.sub]
                 (|> dict
                     (/.has non_key test_val)
                     (/.sub (list non_key))
                     /.size
                     (n.= 1)))
        
        (_.cover [/.re_bound]
                 (or (n.= 0 size)
                     (let [first_key (|> dict /.keys list.head maybe.trusted)
                           rebound (/.re_bound first_key non_key dict)]
                       (and (n.= (/.size dict) (/.size rebound))
                            (/.key? rebound non_key)
                            (not (/.key? rebound first_key))
                            (n.= (maybe.trusted (/.value first_key dict))
                                 (maybe.trusted (/.value non_key rebound)))))))
        )))

(def: .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Dictionary])
      (do random.monad
        [.let [capped_nat (\ random.monad each (n.% 100) random.nat)]
         size capped_nat
         dict (random.dictionary n.hash size random.nat capped_nat)
         non_key (random.only (|>> (/.key? dict) not)
                              random.nat)
         test_val (random.only (|>> (list.member? n.equivalence (/.values dict)) not)
                               random.nat)]
        ($_ _.and
            (_.for [/.equivalence]
                   ($equivalence.spec (/.equivalence n.equivalence)
                                      (random.dictionary n.hash size random.nat random.nat)))
            
            (_.for [/.functor]
                   ($functor.spec ..injection /.equivalence /.functor))

            ..for_dictionaries
            ..for_entries
            ))))
