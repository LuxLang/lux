... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [function
    ["[0]" predicate]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only Bits)]])

(the (size minimum maximum)
  (-> Natural Natural
      (Random Natural))
  (|> random.natural
      (by random.monad each
          (|>> (n.% (++ maximum))
               (n.major minimum)))))

(the .public random
  (Random Bits)
  (do [! random.monad]
    [size (by ! each (n.% 1,000) random.natural)]
    (when size
      0 (in /.empty)
      _ (do [! random.monad]
          [idx (|> random.natural (by ! each (n.% size)))]
          (in (/.one idx /.empty))))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Bits])
      (all _.and
           (_.for [/.equivalence]
                  (equivalenceT.spec /.equivalence ..random))

           (do random.monad
             [sample ..random]
             (_.coverage [/.empty? /.size]
               (if (/.empty? sample)
                 (n.= 0 (/.size sample))
                 (n.> 0 (/.size sample)))))
           (_.coverage [/.empty]
             (/.empty? /.empty))
           
           (do [! random.monad]
             [size (by ! each (|>> (n.% 1,000) ++) random.natural)
              idx (by ! each (n.% size) random.natural)
              sample ..random]
             (all _.and
                  (_.coverage [/.bit /.one]
                    (and (|> /.empty (/.bit idx) not)
                         (|> /.empty (/.one idx) (/.bit idx))))
                  (_.coverage [/.zero]
                    (|> /.empty (/.one idx) (/.zero idx) (/.bit idx) not))
                  (_.coverage [/.flipped]
                    (and (|> /.empty (/.flipped idx) (/.bit idx))
                         (|> /.empty (/.flipped idx) (/.flipped idx) (/.bit idx) not)))
                  (_.coverage [/.Chunk /.capacity /.chunk_size]
                    (and (n.= 0 (/.capacity /.empty))
                         (|> /.empty (/.one idx) /.capacity
                             (n.- idx)
                             (predicate.or (n.>= 0)
                                           (n.< /.chunk_size)))
                         (let [grown (/.flipped idx /.empty)]
                           (and (n.> 0 (/.capacity grown))
                                (same? /.empty (/.flipped idx grown))))))
                  (_.coverage [/.intersects?]
                    (and (not (/.intersects? /.empty
                                             /.empty))
                         (/.intersects? (/.one idx /.empty)
                                        (/.one idx /.empty))
                         (not (/.intersects? (/.one (++ idx) /.empty)
                                             (/.one idx /.empty)))
                         (not (/.intersects? sample (/.not sample)))))
                  (_.coverage [/.not]
                    (and (same? /.empty (/.not /.empty))
                         (or (same? /.empty sample)
                             (and (not (by /.equivalence = sample (/.not sample)))
                                  (by /.equivalence = sample (/.not (/.not sample)))))))
                  (_.coverage [/.xor]
                    (and (same? /.empty (/.xor sample sample))
                         (n.= (/.size (/.xor sample (/.not sample)))
                              (/.capacity sample))))
                  (_.coverage [/.or]
                    (and (by /.equivalence = sample (/.or sample sample))
                         (n.= (/.size (/.or sample (/.not sample)))
                              (/.capacity sample))))
                  (_.coverage [/.and]
                    (and (by /.equivalence = sample (/.and sample sample))
                         (same? /.empty (/.and sample (/.not sample)))))
                  )))))
