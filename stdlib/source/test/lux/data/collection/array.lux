... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [functor (.only)]
    [monad (.only do)]
    ["[0]" functor
     ["[1]T" \\test (.only Injection)]]
    ["[0]" monoid
     ["[1]T" \\test]]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [algorithm
    ["[0]" mix (.only Mix)
     ["[1]T" \\test]]]
   [control
    ["[0]" maybe (.use "[1]#[0]" functor)]
    ["[0]" try]]
   [data
    ["[0]" bit]
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]
     ["[0]" set]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]
    [number
     ["n" nat]]]
   [meta
    ["[0]" static]
    ["[0]" code]
    [macro
     ["[0]" template]
     ["[0]" expansion]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only Array)
   ["!" \\unsafe]]])

(the injection
  (Injection Array)
  (|>> list /.of_list))

(the bounded_size
  (Random Nat)
  (of random.monad each (|>> (n.% 20) ++)
      random.nat))

(the structures
  Test
  (do [! random.monad]
    [size ..bounded_size]
    (all _.and
         (_.for [/.equivalence]
                (equivalenceT.spec (/.equivalence n.equivalence) (random.array size random.nat)))
         (_.for [/.monoid]
                (monoidT.spec (/.equivalence n.equivalence) /.monoid (random.array size random.nat)))
         (_.for [/.functor]
                (functorT.spec ..injection /.equivalence /.functor))
         (_.for [/.mix]
                (mixT.spec ..injection /.equivalence /.mix))
         )))

(the search
  Test
  (do [! random.monad]
    [size ..bounded_size
     base random.nat
     shift random.nat
     .let [expected (n.+ base shift)]
     the_array (random.array size random.nat)
     evens (random.array size (random.only n.even? random.nat))]
    (all _.and
         (let [(open "/#[0]") /.functor
               choose (is (-> Nat (Maybe Text))
                          (function (_ value)
                            (if (n.even? value)
                              {.#Some (of n.decimal injection value)}
                              {.#None})))]
           (_.coverage [/.one]
             (when [(|> evens
                        (/#each (of n.decimal injection))
                        (/.item 0))
                    (/.one choose evens)]
               [{try.#Success expected} {try.#Success actual}]
               (text.= expected actual)

               [{try.#Failure _} {try.#Failure _}]
               true

               _
               false)))
         (_.coverage [/.example]
           (of (maybe.equivalence n.equivalence) =
               (/.example n.even? the_array)
               (list.example n.even? (/.list {.#None} the_array))))
         (_.coverage [/.example']
           (when [(/.example n.even? the_array)
                  (/.example' (function (_ idx member)
                                (n.even? member))
                              the_array)]
             [{try.#Success expected} {try.#Success [idx actual]}]
             (when (/.item idx the_array)
               {try.#Success again}
               (and (n.= expected actual)
                    (n.= actual again))
               
               {try.#Failure _}
               false)

             [{try.#Failure _} {try.#Failure _}]
             true

             _
             false))
         (_.coverage [/.every?]
           (of bit.equivalence =
               (list.every? n.even? (/.list {.#None} the_array))
               (/.every? n.even? the_array)))
         (_.coverage [/.any?]
           (of bit.equivalence =
               (list.any? n.even? (/.list {.#None} the_array))
               (/.any? n.even? the_array)))
         )))

(the test|unsafe
  Test
  (<| (_.covering !._)
      (_.for [!.Array])
      (do [! random.monad]
        [size ..bounded_size
         base random.nat
         shift random.nat
         dummy (random.only (|>> (n.= base) not) random.nat)
         .let [expected (n.+ base shift)]
         the_array (random.array size random.nat)
         evens (random.array size (random.only n.even? random.nat))]
        (`` (all _.and
                 (_.for [!.=]
                        (equivalenceT.spec (function (_ left right)
                                             (!.= n.equivalence left right))
                                           (random.array size random.nat)))
                 (_.for [!.composite]
                        (monoidT.spec (/.equivalence n.equivalence)
                                      (implementation
                                       (the identity (!.empty 0))
                                       (the (composite left right)
                                         (!.composite left right)))
                                      (random.array size random.nat)))
                 (_.for [!.each]
                        (functorT.spec ..injection /.equivalence
                                       (function (_ $ it)
                                         (!.each $ it))))
                 (_.for [!.mix]
                        (mixT.spec ..injection /.equivalence
                                   (is (Mix !.Array)
                                       (function (_ $ init it)
                                         (!.mix (function (_ index item output)
                                                  ($ item output))
                                                init
                                                it)))))
                 
                 (_.coverage [!.empty !.size]
                   (n.= size (!.size (is (Array Nat)
                                         (!.empty size)))))
                 (_.coverage [!.nominal]
                   (when !.Array
                     (<| {.#Named (symbol !.Array)}
                         {.#Universal (list)}
                         {.#Nominal !.nominal (list _)})
                     true

                     _
                     false))
                 (_.coverage [!.lacks?]
                   (let [the_array (|> (!.empty 2)
                                       (is (Array Nat))
                                       (!.has! 0 expected))]
                     (and (not (!.lacks? 0 the_array))
                          (!.lacks? 1 the_array))))
                 (_.coverage [!.item !.has!]
                   (|> (!.empty 2)
                       (is (Array Nat))
                       (!.has! 0 expected)
                       (!.item 0)
                       (n.= expected)))
                 (_.coverage [!.lacks!]
                   (|> (!.empty 1)
                       (is (Array Nat))
                       (!.has! 0 expected)
                       (!.lacks! 0)
                       (!.lacks? 0)))
                 (_.coverage [!.lacks?]
                   (let [the_array (|> (!.empty 2)
                                       (is (Array Nat))
                                       (!.has! 0 expected))]
                     (and (not (!.lacks? 0 the_array))
                          (!.lacks? 1 the_array))))
                 (_.coverage [!.has?]
                   (let [the_array (|> (!.empty 2)
                                       (is (Array Nat))
                                       (!.has! 0 expected))]
                     (and (!.has? 0 the_array)
                          (not (!.has? 1 the_array)))))
                 (_.coverage [!.revised!]
                   (|> (!.empty 1)
                       (is (Array Nat))
                       (!.has! 0 base)
                       (!.revised! 0 (n.+ shift))
                       (!.item 0)
                       (n.= expected)))
                 (_.coverage [!.upsert!]
                   (let [the_array (|> (!.empty 2)
                                       (is (Array Nat))
                                       (!.has! 0 base)
                                       (!.upsert! 0 dummy (n.+ shift))
                                       (!.upsert! 1 base (n.+ shift)))]
                     (and (n.= expected (!.item 0 the_array))
                          (n.= expected (!.item 1 the_array)))))
                 (do !
                   [occupancy (of ! each (n.% (++ size)) random.nat)]
                   (_.coverage [!.occupancy !.vacancy]
                     (let [the_array (loop (again [output (is (Array Nat)
                                                              (!.empty size))
                                                   idx 0])
                                       (if (n.< occupancy idx)
                                         (again (!.has! idx expected output)
                                                (++ idx))
                                         output))]
                       (and (n.= occupancy (!.occupancy the_array))
                            (n.= size (n.+ (!.occupancy the_array)
                                           (!.vacancy the_array)))))))
                 (do !
                   [the_list (random.list size random.nat)
                    .let [the_array (!.clone the_array)
                          members (|> the_array (!.list {.#None}) (set.of_list n.hash))]
                    default (random.only (function (_ value)
                                           (not (or (n.even? value)
                                                    (set.member? members value))))
                                         random.nat)]
                   (_.coverage [!.of_list !.list]
                     (and (|> the_list !.of_list (!.list {.#None})
                              (of (list.equivalence n.equivalence) = the_list))
                          (|> the_array (!.list {.#None}) !.of_list
                              (!.= n.equivalence the_array))
                          (exec
                            (!.only! n.even? the_array)
                            (list.every? (function (_ value)
                                           (or (n.even? value)
                                               (same? default value)))
                                         (!.list {.#Some default} the_array))))))
                 (do !
                   [amount (of ! each (n.% (++ size)) random.nat)]
                   (_.coverage [!.copy!]
                     (let [copy (is (Array Nat)
                                    (!.empty size))]
                       (exec (!.copy! amount 0 the_array 0 copy)
                         (of (list.equivalence n.equivalence) =
                             (list.first amount (!.list {.#None} the_array))
                             (!.list {.#None} copy))))))
                 (_.coverage [!.clone]
                   (let [clone (!.clone the_array)]
                     (and (not (same? the_array clone))
                          (!.= n.equivalence the_array clone))))
                 (let [the_array (!.clone the_array)
                       evens (|> the_array (!.list {.#None}) (list.only n.even?))
                       odds (|> the_array (!.list {.#None}) (list.only n.odd?))]
                   (_.coverage [!.only!]
                     (exec (!.only! n.even? the_array)
                       (and (n.= (list.size evens) (!.occupancy the_array))
                            (n.= (list.size odds) (!.vacancy the_array))
                            (|> the_array
                                (!.list {.#None})
                                (of (list.equivalence n.equivalence) = evens))))))
                 (let [choose (is (-> Nat (Maybe Text))
                                  (function (_ value)
                                    (if (n.even? value)
                                      {.#Some (of n.decimal injection value)}
                                      {.#None})))]
                   (_.coverage [!.one]
                     (|> evens
                         (!.one choose)
                         (maybe#each (text.= (|> evens
                                                 (!.each (of n.decimal injection))
                                                 (!.item 0))))
                         (maybe.else false))))
                 (_.coverage [!.example]
                   (of (try.equivalence n.equivalence) =
                       (!.example n.even? the_array)
                       (list.example n.even? (!.list {.#None} the_array))))
                 (_.coverage [!.example']
                   (when [(!.example n.even? the_array)
                          (!.example' (function (_ idx member)
                                        (n.even? member))
                                      the_array)]
                     [{try.#Success expected} {try.#Success [idx actual]}]
                     (and (not (!.lacks? idx the_array))
                          (n.= expected actual)
                          (n.= actual (!.item idx the_array)))

                     [{try.#Failure _} {try.#Failure _}]
                     true

                     _
                     false))
                 (_.coverage [!.every?]
                   (of bit.equivalence =
                       (list.every? n.even? (!.list {.#None} the_array))
                       (!.every? n.even? the_array)))
                 (_.coverage [!.any?]
                   (of bit.equivalence =
                       (list.any? n.even? (!.list {.#None} the_array))
                       (!.any? n.even? the_array)))
                 )))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Array])
      (do [! random.monad]
        [size ..bounded_size
         base random.nat
         shift random.nat
         dummy (random.only (|>> (n.= base) not) random.nat)
         .let [expected (n.+ base shift)]
         the_array (random.array size random.nat)]
        (all _.and
             ..structures
             ..search

             (_.coverage [/.empty /.size]
               (n.= size (/.size (is (Array Nat)
                                     (/.empty size)))))
             (_.coverage [/.nominal]
               (when /.Array
                 (<| {.#Named (symbol /.Array)}
                     {.#Named (symbol !.Array)}
                     {.#Universal (list)}
                     {.#Nominal /.nominal (list _)})
                 true

                 _
                 false))
             (_.coverage [/.item /.has!]
               (let [the_array (|> (/.empty 2)
                                   (is (Array Nat))
                                   (/.has! 0 expected))]
                 (when [(/.item 0 the_array)
                        (/.item 1 the_array)]
                   [{try.#Success actual} {try.#Failure _}]
                   (n.= expected actual)

                   _
                   false)))
             (_.coverage [/.lacks!]
               (let [the_array (|> (/.empty 1)
                                   (is (Array Nat))
                                   (/.has! 0 expected))]
                 (when [(/.item 0 the_array)
                        (/.item 0 (/.lacks! 0 the_array))]
                   [{try.#Success actual} {try.#Failure _}]
                   (n.= expected actual)

                   _
                   false)))
             (_.coverage [/.lacks?]
               (let [the_array (|> (/.empty 2)
                                   (is (Array Nat))
                                   (/.has! 0 expected))]
                 (and (not (/.lacks? 0 the_array))
                      (/.lacks? 1 the_array))))
             (_.coverage [/.has?]
               (let [the_array (|> (/.empty 2)
                                   (is (Array Nat))
                                   (/.has! 0 expected))]
                 (and (/.has? 0 the_array)
                      (not (/.has? 1 the_array)))))
             (_.coverage [/.revised!]
               (let [the_array (|> (/.empty 1)
                                   (is (Array Nat))
                                   (/.has! 0 base)
                                   (/.revised! 0 (n.+ shift)))]
                 (when (/.item 0 the_array)
                   {try.#Success actual}
                   (n.= expected actual)

                   _
                   false)))
             (_.coverage [/.upsert!]
               (let [the_array (|> (/.empty 2)
                                   (is (Array Nat))
                                   (/.has! 0 base)
                                   (/.upsert! 0 dummy (n.+ shift))
                                   (/.upsert! 1 base (n.+ shift)))]
                 (when [(/.item 0 the_array)
                        (/.item 1 the_array)]
                   [{try.#Success actual/0} {try.#Success actual/1}]
                   (and (n.= expected actual/0)
                        (n.= expected actual/1))

                   _
                   false)))
             (do !
               [occupancy (of ! each (n.% (++ size)) random.nat)]
               (_.coverage [/.occupancy /.vacancy]
                 (let [the_array (loop (again [output (is (Array Nat)
                                                          (/.empty size))
                                               idx 0])
                                   (if (n.< occupancy idx)
                                     (again (/.has! idx expected output)
                                            (++ idx))
                                     output))]
                   (and (n.= occupancy (/.occupancy the_array))
                        (n.= size (n.+ (/.occupancy the_array)
                                       (/.vacancy the_array)))))))
             (do !
               [the_list (random.list size random.nat)
                .let [the_array (/.clone the_array)
                      members (|> the_array (/.list {.#None}) (set.of_list n.hash))]
                default (random.only (function (_ value)
                                       (not (or (n.even? value)
                                                (set.member? members value))))
                                     random.nat)]
               (_.coverage [/.of_list /.list]
                 (and (|> the_list /.of_list (/.list {.#None})
                          (of (list.equivalence n.equivalence) = the_list))
                      (|> the_array (/.list {.#None}) /.of_list
                          (of (/.equivalence n.equivalence) = the_array))
                      (exec
                        (/.only! n.even? the_array)
                        (list.every? (function (_ value)
                                       (or (n.even? value)
                                           (same? default value)))
                                     (/.list {.#Some default} the_array))))))
             (do !
               [amount (of ! each (n.% (++ size)) random.nat)]
               (_.coverage [/.copy!]
                 (let [copy (is (Array Nat)
                                (/.empty size))]
                   (exec (/.copy! amount 0 the_array 0 copy)
                     (of (list.equivalence n.equivalence) =
                         (list.first amount (/.list {.#None} the_array))
                         (/.list {.#None} copy))))))
             (_.coverage [/.clone]
               (let [clone (/.clone the_array)]
                 (and (not (same? the_array clone))
                      (of (/.equivalence n.equivalence) = the_array clone))))
             (let [the_array (/.clone the_array)
                   evens (|> the_array (/.list {.#None}) (list.only n.even?))
                   odds (|> the_array (/.list {.#None}) (list.only n.odd?))]
               (_.coverage [/.only!]
                 (exec (/.only! n.even? the_array)
                   (and (n.= (list.size evens) (/.occupancy the_array))
                        (n.= (list.size odds) (/.vacancy the_array))
                        (|> the_array (/.list {.#None}) (of (list.equivalence n.equivalence) = evens))))))
             (_.coverage [/.array]
               (expansion.let [<expected_size> (static.random code.nat
                                                              (random#each (n.% 10) random.nat))
                               <elements> (static.random (|>> (list#each code.nat)
                                                              code.tuple)
                                                         (random.list <expected_size> random.nat))]
                 (let [it (is (/.Array Nat)
                              (`` (/.array (,, (template.spliced <elements>)))))
                       (open "list#[0]") (list.equivalence n.equivalence)]
                   (and (n.= <expected_size> (/.size it))
                        (list#= (is (List Nat)
                                    (`` (list (,, (template.spliced <elements>)))))
                                (/.list {.#None} it))))))

             ..test|unsafe
             ))))
