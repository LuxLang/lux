(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [\\specification
     ["[0]S" equivalence]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number
     ["f" frac]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" rgb]]]]
 [//
  ["[0]T" rgb]])

(def .public value
  (Random /.Value)
  (random#each /.value random.safe_frac))

(def .public random
  (Random /.HSB)
  (do random.monad
    [hue ..value
     saturation ..value
     brightness ..value]
    (in (/.hsb hue saturation brightness))))

(def .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [expected_value ..value
         expected_rgb rgbT.random
         expected_hsb ..random

         possible_value random.frac])
      (all _.and
           (_.for [/.Value]
                  (all _.and
                       (_.coverage [/.value?]
                         (and (/.value? expected_value)
                              (not (/.value? (f.+ f.smallest /.most)))
                              (not (/.value? (f.- f.smallest /.least)))))
                       (_.coverage [/.value]
                         (if (/.value? possible_value)
                           (|> possible_value
                               /.value
                               (f.= possible_value))
                           (or (f.= /.least (/.value possible_value))
                               (f.= /.most (/.value possible_value)))))
                       (_.coverage [/.least]
                         (and (f.< /.most
                                   /.least)
                              (/.value? /.least)
                              (/.value? (f.+ f.smallest /.least))
                              (not (/.value? (f.- f.smallest /.least)))))
                       (_.coverage [/.most]
                         (and (f.> /.least
                                   /.most)
                              (/.value? /.most)
                              (/.value? (f.- f.smallest /.most))
                              (not (/.value? (f.+ f.smallest /.most)))))
                       ))
           (_.for [/.HSB]
                  (all _.and
                       (_.for [/.equivalence]
                              (equivalenceS.spec /.equivalence ..random))

                       (_.coverage [/.hsb
                                    /.hue /.saturation /.brightness]
                         (|> (/.hsb (/.hue expected_hsb) (/.saturation expected_hsb) (/.brightness expected_hsb))
                             (at /.equivalence = expected_hsb)))
                       (_.coverage [/.of_rgb /.rgb]
                         (and (|> expected_rgb
                                  /.of_rgb
                                  /.rgb
                                  (at rgb.equivalence = expected_rgb))
                              (|> expected_hsb
                                  /.rgb
                                  /.of_rgb
                                  (at /.equivalence = expected_hsb))))
                       ))
           )))
