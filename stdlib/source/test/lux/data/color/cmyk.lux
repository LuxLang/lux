(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [\\specification
     ["[0]S" equivalence]]]
   [control
    ["[0]" try (.use "[1]#[0]" functor)]
    ["[0]" exception]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]
    [number
     ["f" frac]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" rgb]]]]
 [//
  ["[0]T" rgb]])

(def .public value
  (Random /.Value)
  (random#each /.value random.safe_frac))

(def .public random
  (Random /.CMYK)
  (do random.monad
    [cyan ..value
     magenta ..value
     yellow ..value
     key ..value]
    (in [/.#cyan cyan
         /.#magenta magenta
         /.#yellow yellow
         /.#key key])))

(def .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [expected_value ..value
         expected_rgb rgbT.random
         expected_cmyk ..random

         possible_value random.frac])
      (all _.and
           (_.for [/.Value]
                  (all _.and
                       (_.coverage [/.value?]
                         (and (/.value? expected_value)
                              (not (/.value? (f.+ f.smallest /.most)))
                              (not (/.value? (f.- f.smallest /.least)))))
                       (_.coverage [/.value]
                         (if (/.value? possible_value)
                           (|> possible_value
                               /.value
                               (f.= possible_value))
                           (or (f.= /.least (/.value possible_value))
                               (f.= /.most (/.value possible_value)))))
                       (_.coverage [/.least]
                         (and (f.< /.most
                                   /.least)
                              (/.value? /.least)
                              (/.value? (f.+ f.smallest /.least))
                              (not (/.value? (f.- f.smallest /.least)))))
                       (_.coverage [/.most]
                         (and (f.> /.least
                                   /.most)
                              (/.value? /.most)
                              (/.value? (f.- f.smallest /.most))
                              (not (/.value? (f.+ f.smallest /.most)))))
                       ))
           (_.for [/.CMYK
                   /.#cyan /.#magenta /.#yellow /.#key]
                  (all _.and
                       (_.for [/.equivalence]
                              (equivalenceS.spec /.equivalence ..random))

                       (_.coverage [/.cmyk /.rgb]
                         (and (|> expected_rgb
                                  /.cmyk
                                  /.rgb
                                  (of rgb.equivalence = expected_rgb))
                              (|> expected_cmyk
                                  /.rgb
                                  /.cmyk
                                  (of /.equivalence = expected_cmyk))))
                       ))
           )))
