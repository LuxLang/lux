... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text (.use "[1]#[0]" equivalence)]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number
     ["n" nat]
     ["f" frac]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" rgb]
    ["[0]" hsb]]]]
 [//
  ["[0]T" rgb]])

(with_template [<name> <as> <of>]
  [(the <name>
     (Random /.Value)
     (let [range (f.nat (<as> +1.0))]
       (|> random.nat
           (random#each (|>> (n.% range)
                             n.frac
                             <of>
                             /.value)))))]

  [degree f.as_degree f.of_degree]
  [percentage f.as_percentage f.of_percentage]
  )

(the .public value
  (Random /.Value)
  (random#each /.value random.safe_frac))

(the .public random
  (Random /.HSL)
  (do random.monad
    [hue ..degree
     saturation ..percentage
     luminance ..percentage]
    (in (/.hsl hue saturation luminance))))

(the rgb_error_margin
  +1.8)

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [expected_value ..value
         expected_rgb rgbT.random
         expected_hsl ..random

         possible_value random.frac

         mediocre (|> ..random
                      (random.only (|>> (its /.#saturation)
                                        ((function (_ it)
                                           (and (f.>= +0.25 it)
                                                (f.<= +0.75 it)))))))
         ratio (|> random.safe_frac (random.only (f.>= +0.5)))
         .let [delta +0.000000001]

         left ..random
         right ..random])
      (all _.and
           (_.for [/.Value]
                  (all _.and
                       (_.coverage [/.value?]
                         (and (/.value? expected_value)
                              (not (/.value? (f.+ delta /.most)))
                              (not (/.value? (f.- delta /.least)))))
                       (_.coverage [/.value]
                         (if (/.value? possible_value)
                           (|> possible_value
                               /.value
                               (f.= possible_value))
                           (or (f.= /.least (/.value possible_value))
                               (f.= /.most (/.value possible_value)))))
                       (_.coverage [/.least]
                         (and (f.< /.most
                                   /.least)
                              (/.value? /.least)
                              (/.value? (f.+ delta /.least))
                              (not (/.value? (f.- delta /.least)))))
                       (_.coverage [/.most]
                         (and (f.> /.least
                                   /.most)
                              (/.value? /.most)
                              (/.value? (f.- delta /.most))
                              (not (/.value? (f.+ delta /.most)))))
                       ))
           (_.for [/.HSL
                   /.#hue /.#saturation /.#luminance]
                  (`` (all _.and
                           (_.for [/.equivalence]
                                  (equivalenceT.spec /.equivalence ..random))

                           (_.coverage [/.hsl]
                             (|> (/.hsl (its /.#hue expected_hsl)
                                        (its /.#saturation expected_hsl)
                                        (its /.#luminance expected_hsl))
                                 (of /.equivalence = expected_hsl)))
                           (_.coverage [/.of_rgb /.rgb]
                             (|> expected_rgb
                                 /.of_rgb
                                 /.rgb
                                 (of rgb.equivalence = expected_rgb)))

                           (_.coverage [/.saturated]
                             (f.> (its /.#saturation mediocre)
                                  (its /.#saturation (/.saturated ratio mediocre))))
                           (_.coverage [/.un_saturated]
                             (f.< (its /.#saturation mediocre)
                                  (its /.#saturation (/.un_saturated ratio mediocre))))
                           (_.coverage [/.gray_scale]
                             (let [gray'ed (/.gray_scale mediocre)]
                               (and (f.= +0.0
                                         (its /.#saturation gray'ed))
                                    (|> (its /.#luminance gray'ed)
                                        (f.- (its /.#luminance mediocre))
                                        f.abs
                                        (f.<= ..rgb_error_margin)))))
                           (_.coverage [/.format]
                             (bit#= (of /.equivalence = left right)
                                    (text#= (/.format left) (/.format right))))
                           )))
           )))
