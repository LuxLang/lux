(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [\\specification
     ["$[0]" hash]]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" codec
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]]
   [control
    ["[0]" function]]
   [math
    ["[0]" random]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(def .public test
  Test
  (<| (_.covering /._)
      (do random.monad
        [value random.bit]
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec /.equivalence random.bit))
             (_.for [/.hash]
                    ($hash.spec /.hash random.bit))
             (_.for [/.disjunction]
                    (monoidT.spec /.equivalence /.disjunction random.bit))
             (_.for [/.conjunction]
                    (monoidT.spec /.equivalence /.conjunction random.bit))
             (_.for [/.codec]
                    (codecT.spec /.equivalence /.codec random.bit))
             
             (_.coverage [/.no /.yes]
               (and (of /.equivalence = false /.no)
                    (of /.equivalence = true /.yes)))
             (_.coverage [/.off /.on]
               (and (of /.equivalence = false /.off)
                    (of /.equivalence = true /.on)))
             (_.coverage [/.complement]
               (and (not (of /.equivalence = value ((/.complement function.identity) value)))
                    (of /.equivalence = value ((/.complement not) value))))
             ))))
