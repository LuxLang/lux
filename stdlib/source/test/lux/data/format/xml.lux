... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except character symbol)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" format
     ["[1]T" \\test]]]
   [control
    ["<>" projection (.use "[1]#[0]" monad)]
    ["[0]" maybe]
    ["[0]" try (.use "[1]#[0]" functor)]
    ["[0]" exception]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" dictionary]
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number
     ["n" natural]]]
   [meta
    ["[0]" static]
    ["[0]" symbol]
    ["[0]" type]
    [macro
     ["^" pattern]
     ["[0]" template]
     ["[0]" expansion]]]
   [test
    ["_" property (.only Test)]]]]
 ["[0]" \\projection]
 [\\library
  ["[0]" /]])

(the !expect
  (template.macro (_ <pattern> <value>)
    [(when <value>
       <pattern>
       true
       
       _
       false)]))

(the !failure
  (template.macro (_ <exception> <cases>)
    [(expansion.let [<<cases>> (template.spliced <cases>)]
       (do [! random.monad]
         [expected (random.alphabetic 1)]
         (_.coverage [<exception>]
           (`` (and (,, (template.with [<projection> <input>]
                          [(|> (\\projection.value <projection> (list <input>))
                               (!expect (^.multi {try.#Failure error}
                                                 (exception.is? <exception> error))))]

                          <<cases>>)))))))]))

(the random_label
  (Random Symbol)
  (random.and (random.alphabetic 1)
              (random.alphabetic 1)))

(the random_tag ..random_label)
(the random_attribute ..random_label)

(the \\projection
  Test
  (<| (_.covering \\projection._)
      (_.for [\\projection.Projection])
      (all _.and
           (do [! random.monad]
             [expected (random.alphabetic 1)]
             (_.coverage [\\projection.value \\projection.text]
               (|> (\\projection.value \\projection.text (list {/.#Text expected}))
                   (!expect (^.multi {try.#Success actual}
                                     (text.= expected actual))))))
           (!failure \\projection.unconsumed_inputs
                     [[(<>#in expected)
                       {/.#Text expected}]])
           (do [! random.monad]
             [expected (by ! each (|>> {/.#Text}) (random.alphabetic 1))]
             (_.coverage [\\projection.any]
               (|> (\\projection.value \\projection.any (list expected))
                   (try#each (/.= expected))
                   (try.else false))))
           (do [! random.monad]
             [expected ..random_tag]
             (_.coverage [\\projection.tag]
               (|> (\\projection.value (do <>.monad
                                         [actual \\projection.tag
                                          _ \\projection.any]
                                         (in (symbol.= expected actual)))
                                       (list {/.#Node expected (dictionary.empty symbol.hash) (list)}))
                   (!expect {try.#Success .true}))))
           (do [! random.monad]
             [expected ..random_tag]
             (_.coverage [\\projection.node]
               (|> (\\projection.value (\\projection.node expected (<>#in []))
                                       (list {/.#Node expected (dictionary.empty symbol.hash) (list)}))
                   (!expect {try.#Success []}))))
           (!failure \\projection.wrong_tag
                     [[(\\projection.node ["" expected] (<>#in []))
                       {/.#Node [expected ""] (dictionary.empty symbol.hash) (list)}]])
           (do [! random.monad]
             [expected_tag ..random_tag
              expected_attribute ..random_attribute
              expected_value (random.alphabetic 1)]
             (_.coverage [\\projection.attribute]
               (|> (\\projection.value (<| (\\projection.node expected_tag)
                                           (<>.after (\\projection.attribute expected_attribute))
                                           (<>#in []))
                                       (list {/.#Node expected_tag
                                                      (|> (dictionary.empty symbol.hash)
                                                          (dictionary.has expected_attribute expected_value))
                                                      (list)}))
                   (!expect {try.#Success []}))))
           (!failure \\projection.unknown_attribute
                     [[(\\projection.attribute ["" expected])
                       {/.#Node [expected expected]
                                (|> (dictionary.empty symbol.hash)
                                    (dictionary.has [expected ""] expected))
                                (list)}]])
           (!failure \\projection.empty_input
                     [[(do <>.monad
                         [_ \\projection.any]
                         \\projection.any)
                       {/.#Text expected}]
                      [(do <>.monad
                         [_ \\projection.any]
                         \\projection.text)
                       {/.#Text expected}]
                      [(do <>.monad
                         [_ \\projection.any]
                         (\\projection.node [expected expected]
                                            (<>#in [])))
                       {/.#Node [expected expected]
                                (dictionary.empty symbol.hash)
                                (list)}]
                      [(do <>.monad
                         [_ \\projection.any]
                         (\\projection.node [expected expected]
                                            (\\projection.attribute [expected expected])))
                       {/.#Node [expected expected]
                                (|> (dictionary.empty symbol.hash)
                                    (dictionary.has [expected expected] expected))
                                (list)}]])
           (!failure \\projection.unexpected_input
                     [[\\projection.text
                       {/.#Node [expected expected] (dictionary.empty symbol.hash) (list)}]
                      [(\\projection.node [expected expected]
                                          (<>#in []))
                       {/.#Text expected}]
                      [(\\projection.node [expected expected]
                                          (\\projection.attribute [expected expected]))
                       {/.#Text expected}]])
           (do [! random.monad]
             [.let [node (is (-> /.Tag (List /.XML) /.XML)
                             (function (_ tag children)
                               {/.#Node tag (dictionary.empty symbol.hash) children}))]
              parent ..random_tag
              right ..random_tag
              wrong (random.only (|>> (symbol.= right) not)
                                 ..random_tag)
              .let [projection (<| (\\projection.node parent)
                                   (do <>.monad
                                     [_ (<| \\projection.somewhere
                                            (\\projection.node right)
                                            (<>#in []))
                                      _ (<>.some \\projection.any)]
                                     (in [])))]
              repetitions (by ! each (n.% 10) random.natural)]
             (all _.and
                  (_.coverage [\\projection.somewhere]
                    (|> (\\projection.value projection
                                            (list (node parent
                                                        (list.together (list (list.repeated repetitions (node wrong (list)))
                                                                             (list (node right (list)))
                                                                             (list.repeated repetitions (node wrong (list))))))))
                        (!expect {try.#Success []})))
                  (_.coverage [\\projection.nowhere]
                    (|> (\\projection.value projection
                                            (list (node parent
                                                        (list.repeated repetitions (node wrong (list))))))
                        (!expect (^.multi {try.#Failure error}
                                          (exception.is? \\projection.nowhere error)))))
                  ))
           )))

(the character_range
  Text
  (%.message "_"
             "abcdefghijklmnopqrstuvwxyz"
             "ABCDEFGHIJKLMNOPQRSTUVWXYZ"))

(the character
  (Random Natural)
  (do [! random.monad]
    [idx (|> random.natural (by ! each (n.% (text.size character_range))))]
    (in (maybe.trusted (text.character idx character_range)))))

(the (size bottom top)
  (-> Natural Natural (Random Natural))
  (let [constraint (|>> (n.% top) (n.max bottom))]
    (random#each constraint random.natural)))

(the (text bottom top)
  (-> Natural Natural (Random Text))
  (do random.monad
    [size (..size bottom top)]
    (random.text ..character size)))

(the symbol
  (Random Symbol)
  (random.and (..text 0 10)
              (..text 1 10)))

(the .public random
  (Random /.XML)
  (random.rec (function (_ random)
                (random.or (..text 1 10)
                           (do random.monad
                             [size (..size 0 2)]
                             (all random.and
                                  ..symbol
                                  (random.dictionary symbol.hash size ..symbol (..text 0 10))
                                  (random.list size random)))))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.XML /.#Text /.#Node])
      (all _.and
           (_.for [/.equivalence /.=]
                  (static.when (same? /.equivalence /.=)
                    (equivalenceT.spec /.equivalence ..random)))
           (_.for [/.format]
                  (formatT.spec /.equivalence /.format ..random))

           (do [! random.monad]
             [(^.let symbol [namespace name]) ..symbol]
             (`` (all _.and
                      (,, (template.with [<type> <injection>]
                            [(_.coverage [<type> <injection>]
                               (and (text.= name (<injection> ["" name]))
                                    (let [symbol (<injection> symbol)]
                                      (and (text.starts_with? namespace symbol)
                                           (text.ends_with? name symbol)))))]

                            [/.Tag /.tag]
                            [/.Attribute /.attribute]
                            ))
                      (_.coverage [/.Attrs /.attributes]
                        (dictionary.empty? /.attributes))
                      )))
           (do random.monad
             [expected ..random]
             (_.coverage [/.as_text]
               (|> expected
                   /.as_text
                   (by /.format projection)
                   (try#each (by /.equivalence = expected))
                   (try.else false))))

           ..\\projection
           )))
