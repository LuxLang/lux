... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]
    ["[0]" equality
     ["[1]T" \\test]]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]
      ["i" integer]]]]
   [test
    ["_" property (.only Test)]]]]
 ["/" \\library]
 ["!" \\theorem])

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [.Tuple .And])
      (monad.let random.monad
        [expected random.natural
         shift random.natural
         dummy (random.only (|>> (n.= expected) not)
                            random.natural)]
        (all _.and
             (_.for [/.equality]
                    (equalityT.spec (/.equality n.equality i.equality)
                                    (random.and random.natural random.integer)))
             (monad.let random.monad
               [left random.integer
                right random.natural]
               (_.coverage [/.hash]
                 (let [hash (/.hash i.hash n.hash)]
                   (n.= (n.+ (by i.hash hash left)
                             (by n.hash hash right))
                        (by hash hash [left right])))))
             
             (_.by_proof [/.left]
                         [!.left])
             (_.by_proof [/.right]
                         [!.right])
             (_.by_proof [/.in_reverse]
                         [!.in_reverse
                          !.involution_of_in_reverse])
             (_.by_proof [/.by]
                         [!.by
                          !.identity_by_by])
             ))))
