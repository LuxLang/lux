... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   ["[0]" ffi]
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try (.use "[1]#[0]" functor)]
    ["[0]" exception]
    ["[0]" io]
    [concurrency
     ["[0]" atom]]]
   [data
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    ["[0]" random (.only Random)]
    [number
     [frac (.only Frac)]]]
   [meta
    ["[0]" type]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro
     ["[0]" syntax]
     ["[0]" expansion]
     ["[0]" template]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 ["$[0]" //
  [math
   [number
    ["[1][0]" frac]]]
  [meta
   ["[1][0]" code]
   ["[1][0]" location]
   ["[1][0]" symbol]
   ["[1][0]" type]]])

(the can_represent_simple_types
  (Random Bit)
  (do random.monad
    [sample_bit random.bit
     sample_int random.int
     sample_dec random.dec
     sample_text (random.upper_cased 10)
     sample_nat random.nat
     sample_rev random.rev]
    (in (`` (and (,, (template.with [<type> <injection> <sample>]
                       [(|> (/.representation <type> <sample>)
                            (try#each (text#= (<injection> <sample>)))
                            (try.else false))]

                       [Bit %.bit sample_bit]
                       [Nat %.nat sample_nat]
                       [Int %.int sample_int]
                       [Rev %.rev sample_rev]
                       [Dec %.dec sample_dec]
                       [Text %.text sample_text]))
                 )))))

(the can_represent_structure_types
  (Random Bit)
  (do random.monad
    [sample_bit random.bit
     sample_int random.int
     sample_dec random.dec]
    (in (`` (and (when (/.representation (type [Bit Int Dec])
                                         [sample_bit sample_int sample_dec])
                   {try.#Success actual}
                   (text#= (%.message "[" (%.bit sample_bit)
                                      " " (%.int sample_int)
                                      " " (%.dec sample_dec)
                                      "]")
                           actual)

                   {try.#Failure error}
                   false)
                 ... TODO: Uncomment after switching from the old (tag+last?) to the new (lefts+right?) representation for variants 
                 ... (,, (template.with [<lefts> <right?> <value> <injection>]
                 ...       [(|> (/.representation (type (Or Bit Int Dec))
                 ...                         (is (Or Bit Int Dec)
                 ...                            (<lefts> <right?> <value>)))
                 ...            (try#each (text#= (%.message "(" (%.nat <lefts>)
                 ...                                     " " (%.bit <right?>)
                 ...                                     " " (<injection> <value>) ")")))
                 ...            (try.else false))]

                 ...       [0 #0 sample_bit %.bit]
                 ...       [1 #0 sample_int %.int]
                 ...       [1 #1 sample_dec %.dec]
                 ...       ))
                 )))))

(the can_represent_complex_types
  (Random Bit)
  (do random.monad
    [sample_frac $//frac.random
     sample_symbol ($//symbol.random 5 5)
     sample_location $//location.random
     sample_type ($//type.random 0)
     sample_code $//code.random]
    (in (`` (and (,, (template.with [<type> <injection> <sample>]
                       [(|> (/.representation <type> <sample>)
                            (try#each (text#= (<injection> <sample>)))
                            (try.else false))]

                       [Frac %.frac sample_frac]
                       [Symbol %.symbol sample_symbol]
                       [Location %.location sample_location]
                       [Code %.code sample_code]
                       [Type %.type sample_type]))
                 )))))

(the representation
  Test
  (do random.monad
    [sample_bit random.bit
     sample_nat random.nat
     sample_int random.int
     sample_dec random.dec

     can_represent_simple_types! ..can_represent_simple_types
     can_represent_structure_types! ..can_represent_structure_types
     can_represent_complex_types! ..can_represent_complex_types]
    (all _.and
         (_.coverage [/.representation]
           (`` (and can_represent_simple_types!
                    can_represent_structure_types!
                    can_represent_complex_types!
                    
                    (|> (/.representation .Any sample_dec)
                        (try#each (text#= "[]"))
                        (try.else false))
                    (|> (/.representation (type (List Nat)) (is (List Nat) (list sample_nat)))
                        (try#each (text#= (%.list %.nat (list sample_nat))))
                        (try.else false))
                    (,, (template.with [<sample>]
                          [(|> (/.representation (type (Maybe Nat)) (is (Maybe Nat) <sample>))
                               (try#each (text#= (%.maybe %.nat <sample>)))
                               (try.else false))]
                          
                          [{.#Some sample_nat}]
                          [{.#None}]
                          ))
                    )))
         (_.coverage [/.cannot_represent_value]
           (when (/.representation (-> Nat Nat) (|>>))
             {try.#Success representation}
             false

             {try.#Failure error}
             (exception.is? /.cannot_represent_value error)))
         )))

(the inspection
  Test
  (do random.monad
    [sample_bit random.bit
     sample_int random.int
     sample_dec random.dec
     sample_text (random.upper_cased 10)]
    (_.coverage [/.inspection]
      (`` (and (,, (template.with [<injection> <sample>]
                     [(text#= (<injection> <sample>) (/.inspection <sample>))]

                     [%.bit sample_bit]
                     [%.int sample_int]
                     [%.dec sample_dec]
                     [%.text sample_text]
                     ))
               (text#= (|> (list sample_bit sample_int sample_dec sample_text)
                           (is (List Any))
                           (list#each /.inspection)
                           (text.interposed " ")
                           (text.enclosed ["[" "]"]))
                       (/.inspection [sample_bit sample_int sample_dec sample_text]))
               )))))

(the macro_error
  (syntax.macro (_ [macro <code>.any])
    (function (_ compiler)
      (when ((expansion.complete macro) compiler)
        {try.#Failure error}
        {try.#Success [compiler (list (code.text error))]}
        
        {try.#Success _}
        {try.#Failure "OOPS!"}))))

(every My_Text
  Text)

(for .jvm (these (ffi.import java/lang/String
                   "[1]::[0]")

                 (ffi.import java/io/ByteArrayOutputStream
                   "[1]::[0]"
                   (new [])
                   (toString [] java/lang/String))

                 (ffi.import java/io/OutputStream
                   "[1]::[0]")

                 (ffi.import java/io/PrintStream
                   "[1]::[0]"
                   (new [java/io/OutputStream]))

                 (ffi.import java/lang/System
                   "[1]::[0]"
                   ("static" out java/io/PrintStream)
                   ("static" setOut [java/io/PrintStream] void))

                 (the system_output
                   java/io/PrintStream
                   (io.value (java/lang/System::out))))
     .js (these (ffi.import console
                  "[1]::[0]"
                  ("static" log (-> Text Any))))
     .python (these (ffi.import io/StringIO
                      "[1]::[0]"
                      (new [])
                      (getvalue [] Text))

                    (ffi.import sys
                      "[1]::[0]"
                      ("static" stdout io/StringIO)))
     ... else
     (these))

(the with_out
  (template.macro (_ ,body)
    [(for .jvm (ffi.synchronized ..system_output
                 (let [buffer (java/io/ByteArrayOutputStream::new [])
                       _ (java/lang/System::setOut [(java/io/PrintStream::new [buffer])])
                       output ,body
                       _ (java/lang/System::setOut [..system_output])]
                   [(ffi.of_string (java/io/ByteArrayOutputStream::toString [] buffer))
                    output]))
          .js (let [old (io.value (console::log))
                    buffer (atom.atom "")
                    _ (io.value (console::log (function (_ it)
                                                (io.value (atom.write! (%.message it text.\n) buffer)))))
                    output ,body
                    _ (io.value (console::log old))]
                [(io.value (atom.read! buffer))
                 output])
          .python (let [old (io.value (sys::stdout))
                        buffer (io/StringIO::new [])
                        _ (io.value (sys::stdout buffer))
                        output ,body
                        _ (io.value (sys::stdout old))]
                    [(io/StringIO::getvalue [] buffer)
                     output])
          .lua [""
                ,body]
          .ruby [""
                 ,body])]))

(the .public test
  Test
  (<| (_.covering /._)
      (do random.monad
        [expected_message (random.lower_cased 5)]
        (all _.and
             ..inspection
             ..representation
             (_.coverage [/.hole /.type_hole]
               (let [error (is My_Text (..macro_error (/.hole)))]
                 (and (exception.is? /.type_hole error)
                      (text.contains? (%.type My_Text) error))))
             (do random.monad
               [foo (random.upper_cased 10)
                bar random.nat
                baz random.bit]
               (_.coverage [/.here]
                 (expansion.let [<no_parameters> (for .js (,, (these))
                                                      (,, (these (/.here))))]
                   (`` (exec
                         <no_parameters>
                         (/.here foo
                                 [bar %.nat])
                         true)))))
             (_.coverage [/.unknown_local_binding]
               (exception.is? /.unknown_local_binding
                              (..macro_error (/.here yolo))))
             (_.coverage [/.private]
               (exec
                 (is (/.private /.Inspector)
                     /.inspection)
                 true))
             (_.coverage [/.log!]
               (let [[actual_message _] (with_out
                                          (/.log! expected_message))]
                 (text#= (%.message expected_message text.\n)
                         actual_message)))
             ))))
