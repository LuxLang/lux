... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" hash
     ["[1]T" \\test]]]
   [function
    ["?" predicate]]
   [data
    ["[0]" text]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor monoid)]
     ["[0]" set]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [macro
    ["[0]" expansion]]
   [meta
    ["[0]" static]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" category]
    ["[0]" box]
    [//
     [encoding
      [name
       ["[0]" internal]]]]]]])

(<| (expansion.let [,*primitives (these /.boolean
                                        /.byte
                                        /.short
                                        /.int
                                        /.long
                                        /.float
                                        /.double
                                        /.char)
                    ,*simple (these /.void
                                    ,*primitives)
                    ,*objects (these /.var
                                     /.wildcard)
                    ,*boxes (these box.boolean
                                   box.byte
                                   box.short
                                   box.int
                                   box.long
                                   box.float
                                   box.double
                                   box.char)])
    (these (the .public random
             (Random (/.Descriptor category.Value))
             (let [primitives (is (Stack (/.Descriptor category.Primitive))
                                  (stack ,*primitives))
                   primitives_amount (stack.size primitives)

                   boxes (stack ,*boxes)
                   boxes_amount (stack.size boxes)]
               (all random.either
                    (do [! random.monad]
                      [choice (by ! each (n.% primitives_amount) random.natural)]
                      (random.try (in (stack.item choice primitives))))
                    (do [! random.monad]
                      [choice (by ! each (n.% boxes_amount) random.natural)
                       choice (random.try (in (stack.item choice boxes)))]
                      (in (/.class choice)))))))
    (the .public test
      Test
      (<| (_.covering /._)
          (do [! random.monad]
            [argument_0 ..random
             argument_1 ..random
             return (is (Random (/.Descriptor category.Return))
                        (all random.either
                             (in /.void)
                             ..random))])
          (_.for [/.Descriptor])
          (all _.and
               (_.for [/.equivalence /.=]
                      (static.when (same? /.equivalence /.=)
                        (equivalenceT.spec /.equivalence ..random)))
               (_.for [/.hash]
                      (hashT.spec /.hash ..random))

               (_.coverage [,*simple]
                 (let [options (is (Stack (/.Descriptor category.Return))
                                   (stack ,*simple))
                       uniques (set.of_stack /.hash options)]
                   (n.= (stack.size options)
                        (set.size uniques))))
               (_.coverage [,*objects]
                 (let [options (is (Stack (/.Descriptor category.Return))
                                   (stack ,*objects))
                       uniques (set.of_stack /.hash options)]
                   (n.= 1
                        (set.size uniques))))
               (_.coverage [/.descriptor]
                 (let [options (is (Stack (/.Descriptor category.Return))
                                   (all stack#composite
                                        (stack ,*simple)
                                        (stack#each /.class (stack ,*boxes))))
                       uniques (set.of_stack /.hash options)]
                   (n.= (stack.size options)
                        (set.size uniques))))
               
               (_.coverage [/.class]
                 (let [options (is (Stack (/.Descriptor category.Class))
                                   (stack#each /.class (stack ,*boxes)))
                       uniques (set.of_stack /.hash options)]
                   (n.= (stack.size options)
                        (set.size uniques))))
               (_.coverage [/.class_name]
                 (let [options (stack#each (|>> /.class /.class_name internal.external) (stack ,*boxes))
                       uniques (set.of_stack text.hash options)]
                   (n.= (stack.size options)
                        (set.size uniques))))
               (_.coverage [/.class_prefix /.class_suffix]
                 (|> (stack ,*boxes)
                     (stack#each (|>> /.class /.descriptor))
                     (stack.every? (?.and (text.starts_with? /.class_prefix)
                                          (text.ends_with? /.class_suffix)))))
               (_.coverage [/.declaration /.as_class]
                 (let [expected (is (Stack (/.Descriptor category.Class))
                                    (stack#each (|>> /.declaration /.as_class) (stack ,*boxes)))
                       actual (is (Stack (/.Descriptor category.Class))
                                  (stack#each /.class (stack ,*boxes)))]
                   (by (stack.equivalence /.equivalence) =
                       expected
                       actual)))
               (_.coverage [/.lower]
                 (let [options (is (Stack (/.Descriptor category.Parameter))
                                   (stack#each (|>> /.class /.lower) (stack ,*boxes)))
                       uniques (set.of_stack /.hash options)]
                   (n.= 1
                        (set.size uniques))))
               (_.coverage [/.upper]
                 (let [options (is (Stack (/.Descriptor category.Parameter))
                                   (stack#each (|>> /.class /.upper) (stack ,*boxes)))
                       uniques (set.of_stack /.hash options)]
                   (n.= (stack.size options)
                        (set.size uniques))))
               (_.coverage [/.array]
                 (and (let [options (is (Stack (/.Descriptor category.Array))
                                        (stack#each (|>> /.class /.array) (stack ,*boxes)))
                            uniques (set.of_stack /.hash options)]
                        (n.= (stack.size options)
                             (set.size uniques)))
                      (let [single (stack#each (|>> /.class /.array) (stack ,*boxes))
                            double (stack#each /.array single)]
                        (not (by (stack.equivalence /.equivalence) =
                                 single
                                 double)))))
               (_.coverage [/.array_prefix]
                 (|> (stack ,*boxes)
                     (stack#each (|>> /.class /.array /.descriptor))
                     (stack.every? (text.starts_with? /.array_prefix))))
               (_.coverage [/.method]
                 (let [it (/.method [(stack argument_0 argument_1) return])]
                   (and (text.contains? (/.descriptor argument_0)
                                        (/.descriptor it))
                        (text.contains? (/.descriptor argument_1)
                                        (/.descriptor it))
                        (text.contains? (/.descriptor return)
                                        (/.descriptor it)))))
               ))))
