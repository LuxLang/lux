... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   ["[0]" ffi]
   [abstract
    ["[0]" monad]]
   [control
    ["[0]" io]]
   [error
    ["[0]" try (.use "[1]#[0]" functor)]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["[0]" /64 (.only)
      ["n" natural]]]]
   [target
    [jvm
     ["[0]" modifier (.only Modifier)]
     ["[0]" method (.only Method)]
     ["[0]" type]
     ["!" bytecode]]]
   [compiler
    [meta
     ["[0]" archive]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [///
    [field
     [constant
      ["[0]" arity]]]
    ["[0]" // (.only)
     ["[0]" host]
     ["[0]" runtime]
     [///
      ["[0]" extension]
      [//
       ["[0]" phase]
       ["[0]" synthesis]
       ["[0]" translation]]]]]]]
 [////
  ["[0]T" complex]])

(ffi.import (java/lang/Class of)
            "[1]::[0]"
            (getCanonicalName [] java/lang/String))

(ffi.import java/lang/Object
            "[1]::[0]"
            (getClass [] (java/lang/Class java/lang/Object)))

(the .public (test valid_modifier?)
  (-> (-> (Modifier Method) Bit)
      Test)
  (<| (_.covering /._)
      (monad.let [! random.monad]
        [module (random.lower_cased 1)

         expected_i64 random.i64

         .let [extender (is extension.Extender
                            (function (_ _)
                              (undefined)))
               next (//.translate extender complexT.lux)
               @ [module 0 0]]

         arity (by ! each (|>> (n.% (-- arity.maximum)) (n.+ 2)) random.natural)
         inner_arity (by ! each (|>> (n.% arity) (n.+ 1)) random.natural)])
      (all _.and
           (_.coverage [/.modifier]
             (and (valid_modifier? /.modifier)
                  (modifier.has? method.static /.modifier)))
           (_.coverage [/.name /.type /.method /.call]
             (|> (monad.let try.monad
                   [[_ archive] (archive.reserve "" archive.empty)
                    [_ archive] (archive.reserve module archive)
                    .let [[_ host] (io.value host.host)
                          state (is runtime.State
                                    (translation.state host module))]]
                   (<| (phase.value state)
                       (monad.let phase.monad
                         [_ (translation.set_buffer translation.empty_buffer)
                          parameter (next archive (synthesis.i64 @ expected_i64))
                          function (next archive (synthesis.abstraction @ [(list) 1 (synthesis.local @ 1)]))
                          it (|> function
                                 [{.#None}]
                                 (by host evaluate)
                                 phase.of_try)
                          .let [class (|> it
                                          (as (-> Any java/lang/Object))
                                          (java/lang/Object::getClass [])
                                          (java/lang/Class::getCanonicalName [])
                                          ffi.of_string
                                          (type.class (list)))]]
                         (pure (|> (monad.let !.monad
                                     [_ function
                                      _ parameter]
                                     (/.call class 1))
                                   [{.#None}]
                                   (by host evaluate)
                                   (try#each (|>> (as (-> Any I64))
                                                  (/64.= expected_i64)))
                                   (try.else false)
                                   )))))
                 (try.else false)))
           )))
