... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" hash
     ["[1]T" \\test]]]
   [data
    ["[0]" text]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]
   [meta
    ["[0]" static]]]]
 [\\library
  ["[0]" /]])

(the .public random
  (Random /.Simple)
  (all random.or
       random.bit
       random.i64
       random.decimal
       (random.lower_cased 1)
       ))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Simple
              /.#Bit /.#I64 /.#F64 /.#Text])
      (monad.let [! random.monad]
        [left ..random
         right ..random]
        (all _.and
             (_.for [/.equivalence /.=]
                    (static.when (same? /.equivalence /.=)
                      (equivalenceT.spec /.equivalence ..random)))
             (_.for [/.hash]
                    (hashT.spec /.hash ..random))

             (_.coverage [/.as_text]
               (bit.= (text.= (/.as_text left) (/.as_text right))
                      (by /.equivalence = left right)))
             ))))
