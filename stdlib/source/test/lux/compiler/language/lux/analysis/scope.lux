... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [control
    ["[0]" pipe]
    ["[0]" maybe (.use "[1]#[0]" functor)]]
   [error
    ["[0]" try (.use "[1]#[0]" functor)]
    ["[0]" exception]]
   [data
    ["[0]" product]
    [collection
     ["[0]" stack]]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [macro
    ["[0]" template]]
   ["[0]" type
    ["$[1]" \\test]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" // (.only)
    [//
     [phase
      ["[1][0]" extension]]
     [///
      ["[1][0]" phase (.use "[1]#[0]" monad)]
      [reference
       ["[1][0]" variable (.only Register Variable)]]]]]]]
 ["$[0]" //
  ["[1][0]" type]])

(template.with [<name> <tag>]
  [(the (<name> expected_type expected_register [actual_type actual_var])
     (-> Type Register [Type Variable] Bit)
     (and (identical? expected_type actual_type)
          (when actual_var
            {<tag> actual_register}
            (n.= expected_register actual_register)
            
            _
            false)))]

  [[local? //variable.#Local]
   [foreign? //variable.#Foreign]])

(the .public test
  Test
  (<| (_.covering /._)
      (monad.let [! random.monad]
        [lux $//type.random_state
         .let [state [//extension.#bundle //extension.empty
                      //extension.#state lux]]
         name/0 (random.lower_cased 1)
         name/1 (random.lower_cased 2)
         type/0 ($type.random 0)
         type/1 ($type.random 0)]
        (all _.and
             (_.coverage [/.variable]
               (|> (/.variable name/0)
                   /.with
                   (//phase.value state)
                   (try#each (|>> product.right
                                  (pipe.when
                                    {.#None} true
                                    {.#Some _} false)))
                   (try.else false)))
             (_.coverage [/.with_local]
               (|> (/.with_local [name/0 type/0]
                     (/.variable name/0))
                   /.with
                   (//phase.value state)
                   (try#each (|>> product.right
                                  (maybe#each (..local? type/0 0))
                                  (maybe.else false)))
                   (try.else false)))
             (_.coverage [/.next]
               (|> (<| (monad.let [! //phase.monad]
                         [register/0 /.next])
                       (/.with_local [name/0 type/0])
                       (monad.let !
                         [var/0 (/.variable name/0)])
                       (monad.let !
                         [register/1 /.next])
                       (/.with_local [name/1 type/1])
                       (monad.let !
                         [var/1 (/.variable name/1)])
                       (pure (monad.let maybe.monad
                               [var/0 var/0
                                var/1 var/1]
                               (pure [[register/0 var/0] [register/1 var/1]]))))
                   /.with
                   (//phase.value state)
                   (try#each (|>> product.right
                                  (maybe#each (function (_ [[register/0 var/0] [register/1 var/1]])
                                                (and (..local? type/0 register/0 var/0)
                                                     (..local? type/1 register/1 var/1))))
                                  (maybe.else false)))
                   (try.else false)))
             (_.coverage [/.no_scope]
               (and (|> (/.with_local [name/0 type/0]
                          (//phase#pure false))
                        (//phase.value state)
                        (exception.else (exception.is? /.no_scope)))
                    (|> (monad.let //phase.monad
                          [_ /.next]
                          (pure false))
                        (//phase.value state)
                        (exception.else (exception.is? /.no_scope)))))
             (_.coverage [/.reset]
               (and (|> /.next
                        (/.with_local [name/0 type/0])
                        /.with
                        (//phase.value state)
                        (try#each (|>> product.right
                                       (n.= 1)))
                        (try.else false))
                    (|> /.next
                        /.reset
                        (/.with_local [name/0 type/0])
                        /.with
                        (//phase.value state)
                        (try#each (|>> product.right
                                       (n.= 0)))
                        (try.else false))))
             (_.coverage [/.drained]
               (|> (function (_ [bundle state])
                     {try.#Success [[bundle (has .#scopes (stack) state)]
                                    false]})
                   (/.with_local [name/0 type/0])
                   /.with
                   (//phase#each product.right)
                   (//phase.value state)
                   (exception.else (exception.is? /.drained))))
             (_.coverage [/.with]
               (|> (<| /.with
                       (/.with_local [name/0 type/0])
                       (monad.let //phase.monad
                         [var/0' (/.variable name/0)
                          [scope/1 var/0''] (/.with (/.variable name/0))]
                         (<| //phase.of_try
                             (try.of_maybe "")
                             (monad.let maybe.monad
                               [var/0' var/0'
                                var/0'' var/0'']
                               (pure [var/0' scope/1 var/0''])))))
                   (//phase.value state)
                   (try#each (function (_ [scope/0 var/0' scope/1 var/0''])
                               (and (local? type/0 0 var/0')
                                    (n.= 0 (stack.size (its [.#locals .#mappings] scope/0)))
                                    (n.= 0 (stack.size (its [.#captured .#mappings] scope/0)))

                                    (foreign? type/0 0 var/0'')
                                    (n.= 0 (stack.size (its [.#locals .#mappings] scope/1)))
                                    (n.= 1 (stack.size (its [.#captured .#mappings] scope/1))))))
                   (try.else false)))
             (_.coverage [/.environment]
               (let [(open "stack#[0]") (stack.equivalence //variable.equivalence)]
                 (and (|> (<| /.with
                              (/.with_local [name/0 type/0])
                              (/.with_local [name/1 type/1])
                              (monad.let //phase.monad
                                [[scope/1 _] (/.with (pure []))]
                                (pure (/.environment scope/1))))
                          (//phase.value state)
                          (try#each (|>> product.right
                                         (stack#= (stack))))
                          (try.else false))
                      (|> (<| /.with
                              (monad.let [! //phase.monad]
                                [register/0 /.next])
                              (/.with_local [name/0 type/0])
                              (/.with_local [name/1 type/1])
                              (monad.let !
                                [[scope/1 _] (/.with (/.variable name/0))]
                                (pure [register/0 (/.environment scope/1)])))
                          (//phase.value state)
                          (try#each (function (_ [_ [register/0 environment]])
                                      (stack#= (stack {//variable.#Local register/0})
                                               environment)))
                          (try.else false))
                      (|> (<| /.with
                              (monad.let [! //phase.monad]
                                [register/0 /.next])
                              (/.with_local [name/0 type/0])
                              (monad.let [! //phase.monad]
                                [register/1 /.next])
                              (/.with_local [name/1 type/1])
                              (monad.let [! //phase.monad]
                                [[scope/1 _] (/.with (monad.let !
                                                       [_ (/.variable name/1)
                                                        _ (/.variable name/0)]
                                                       (pure [])))]
                                (pure [register/0 register/1 (/.environment scope/1)])))
                          (//phase.value state)
                          (try#each (function (_ [_ [register/0 register/1 environment]])
                                      (stack#= (stack {//variable.#Local register/1}
                                                      {//variable.#Local register/0})
                                               environment)))
                          (try.else false)))))
             ))))
