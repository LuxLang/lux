... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [control
    ["[0]" pipe]]
   [error
    ["[0]" try (.use "[1]#[0]" functor)]
    ["[0]" exception]]
   [data
    ["[0]" text]
    [collection
     ["[0]" list]
     ["[0]" set]]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [macro
    ["[0]" template]]
   ["[0]" meta (.only)
    ["[0]" module (.only)
     ["[0]" import]]
    ["[0]" configuration
     ["$[1]" \\test]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" // (.only)
    [//
     ["[2][0]" phase]]]]])

(the random_state
  (Random Lux)
  (monad.let random.monad
    [version random.natural
     host (random.lower_cased 1)
     configuration ($configuration.random 5)]
    (pure (//.state (//.info version host configuration)))))

(the nominal
  (Random Type)
  (monad.let random.monad
    [name (random.lower_cased 1)]
    (pure {.#Nominal name (list)})))

(the (new? hash it)
  (-> Natural .Module Bit)
  (and (identical? hash (its .#module_hash it))
       (list.empty? (its .#module_aliases it))
       (list.empty? (its .#definitions it))
       (list.empty? (its .#imports it))
       (when (its .#module_state it)
         {.#Active}
         true
         
         _
         false)))

(the test|module
  Test
  (monad.let [! random.monad]
    [state ..random_state
     name (random.lower_cased 1)
     hash random.natural
     expected_import (random.lower_cased 2)
     expected_alias (random.lower_cased 3)]
    (all _.and
         (_.coverage [/.empty]
           (..new? hash (/.empty hash)))
         (_.coverage [/.create]
           (|> (monad.let /phase.monad
                 [_ (/.create hash name)]
                 (module.by_name name))
               (/phase.value state)
               (try#each (..new? hash))
               (try.else false)))
         (_.coverage [/.exists?]
           (|> (monad.let /phase.monad
                 [pre (/.exists? name)
                  _ (/.create hash name)
                  post (/.exists? name)]
                 (pure (and (not pre) post)))
               (/phase.value state)
               (try.else false)))
         (_.coverage [/.with]
           (|> (monad.let /phase.monad
                 [[it _] (/.with hash name
                           (pure []))]
                 (pure it))
               (/phase.value state)
               (try#each (..new? hash))
               (try.else false)))
         (_.coverage [/.import]
           (`` (and (,, (template.with [<expected>]
                          [(|> (monad.let [! /phase.monad]
                                 [_ (/.create hash expected_import)
                                  [it ?] (/.with hash name
                                           (monad.let !
                                             [_ (if <expected>
                                                    (/.import expected_import)
                                                    (pure []))]
                                             (import.current? expected_import)))]
                                 (pure ?))
                               (/phase.value state)
                               (try#each (bit.= <expected>))
                               (try.else false))]

                          [[false]
                           [true]])))))
         (_.coverage [/.alias]
           (|> (monad.let [! /phase.monad]
                 [_ (/.create hash expected_import)
                  [it _] (/.with hash name
                           (monad.let !
                             [_ (/.import expected_import)]
                             (/.alias expected_alias expected_import)))]
                 (pure it))
               (/phase.value state)
               (try#each (|>> (its .#module_aliases)
                              (pipe.when
                                (list [actual_alias actual_import])
                                (and (identical? expected_alias actual_alias)
                                     (identical? expected_import actual_import))

                                _
                                false)))
               (try.else false)))
         )))

(the test|state
  Test
  (monad.let [! random.monad]
    [state ..random_state
     name (random.lower_cased 1)
     hash random.natural]
    (`` (all _.and
             (,, (template.with [<set> <query> <not/0> <not/1>]
                   [(_.coverage [<set> <query>]
                      (|> (monad.let [! /phase.monad]
                            [[it ?] (/.with hash name
                                      (monad.let !
                                        [_ (<set> name)
                                         ? (<query> name)
                                         ~0 (<not/0> name)
                                         ~1 (<not/1> name)]
                                        (pure (and ? (not ~0) (not ~1)))))]
                            (pure ?))
                          (/phase.value state)
                          (try.else false)))]

                   [[/.set_active   /.active?   /.compiled? /.cached?]
                    [/.set_compiled /.compiled? /.cached?   /.active?]
                    [/.set_cached   /.cached?   /.active?   /.compiled?]]))
             (_.coverage [/.can_only_change_state_of_active_module]
               (and (,, (template.with [<pre> <post>]
                          [(|> (/.with hash name
                                 (monad.let /phase.monad
                                   [_ (<pre> name)]
                                   (<post> name)))
                               (/phase.value state)
                               (pipe.when
                                 {try.#Success _}
                                 false
                                 
                                 {try.#Failure error}
                                 (text.contains? (its exception.#label /.can_only_change_state_of_active_module) error)))]

                          [[/.set_compiled /.set_active]
                           [/.set_compiled /.set_compiled]
                           [/.set_compiled /.set_cached]
                           [/.set_cached /.set_active]
                           [/.set_cached /.set_compiled]
                           [/.set_cached /.set_cached]]))))
             (_.coverage [/.unknown_module]
               (and (,, (template.with [<set>]
                          [(|> (<set> name)
                               (/phase.value state)
                               (pipe.when
                                 {try.#Success _}
                                 false
                                 
                                 {try.#Failure error}
                                 (text.contains? (its exception.#label /.unknown_module) error)))]

                          [[/.set_active]
                           [/.set_compiled]
                           [/.set_cached]]))))
             ))))

(the test|definition
  Test
  (monad.let [! random.monad]
    [state ..random_state
     module_name (random.lower_cased 1)
     hash random.natural
     def_name (random.lower_cased 2)
     alias_name (random.lower_cased 3)

     public? random.bit
     def_type ..nominal
     arity (by ! each (|>> (n.% 10) ++) random.natural)
     labels|head (random.lower_cased 1)
     labels|tail (|> (random.lower_cased 1)
                     (random.only (|>> (text.= labels|head) not))
                     (random.set text.hash (-- arity))
                     (by ! each set.as_list))
     index (by ! each (n.% arity) random.natural)
     .let [definition {.#Definition [public? def_type []]}
           alias {.#Alias [module_name def_name]}]]
    (all _.and
         (_.coverage [/.define]
           (`` (and (,, (template.with [<global>]
                          [(|> (/.with hash module_name
                                 (/.define def_name <global>))
                               (/phase.value state)
                               (pipe.when
                                 {try.#Success _} true
                                 {try.#Failure _} false))]

                          [[definition]
                           [{.#Type [public? def_type {.#Left [labels|head labels|tail]}]}]
                           [{.#Type [public? def_type {.#Right [labels|head labels|tail]}]}]
                           [{.#Tag [public? def_type (list.partial labels|head labels|tail) index]}]
                           [{.#Slot [public? def_type (list.partial labels|head labels|tail) index]}]]))
                    (|> (/.with hash module_name
                          (monad.let /phase.monad
                            [_ (/.define def_name definition)]
                            (/.define alias_name alias)))
                        (/phase.value state)
                        (pipe.when
                          {try.#Success _} true
                          {try.#Failure _} false)))))
         (_.coverage [/.cannot_define_more_than_once]
           (`` (and (,, (template.with [<global>]
                          [(|> (/.with hash module_name
                                 (monad.let /phase.monad
                                   [_ (/.define def_name <global>)]
                                   (/.define def_name <global>)))
                               (/phase.value state)
                               (pipe.when
                                 {try.#Success _} false
                                 {try.#Failure _} true))]

                          [[{.#Definition [public? def_type []]}]
                           [{.#Type [public? def_type {.#Left [labels|head labels|tail]}]}]
                           [{.#Type [public? def_type {.#Right [labels|head labels|tail]}]}]
                           [{.#Tag [public? def_type (list.partial labels|head labels|tail) index]}]
                           [{.#Slot [public? def_type (list.partial labels|head labels|tail) index]}]]))
                    (|> (/.with hash module_name
                          (monad.let /phase.monad
                            [_ (/.define def_name definition)
                             _ (/.define alias_name alias)]
                            (/.define alias_name alias)))
                        (/phase.value state)
                        (pipe.when
                          {try.#Success _} false
                          {try.#Failure _} true)))))
         )))

(the .public test
  Test
  (<| (_.covering /._)
      (all _.and
           ..test|module
           ..test|state
           ..test|definition
           )))
