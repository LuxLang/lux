... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" hash
     ["[1]T" \\test]]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the test|tag
  Test
  (do [! random.monad]
    [multiplicity (by ! each (n.major 2) random.natural)
     tag (by ! each (n.% multiplicity) random.natural)
     lefts random.natural
     right? random.bit]
    (all _.and
         (_.coverage [/.tag /.lefts]
           (and (|> lefts
                    (/.tag right?)
                    (/.lefts right?)
                    (n.= lefts))
                (|> tag
                    (/.lefts right?)
                    (/.tag right?)
                    (n.= tag))))
         (_.coverage [/.choice]
           (let [[lefts right?] (/.choice multiplicity tag)]
             (if right?
                 (n.= (-- tag) lefts)
                 (n.= tag lefts))))
         )))

(the .public (random multiplicity it)
  (for_any (_ a)
    (-> Natural (Random a)
        (Random (/.Complex a))))
  (all random.or
       (all random.and
            (random#each (n.% (-- multiplicity)) random.natural)
            random.bit
            it)
       (random.list multiplicity it)
       ))

(the .public test
  Test
  (let [random (..random 3 random.natural)]
    (<| (_.covering /._)
        (_.for [/.Complex /.Variant /.Tuple])
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec (/.equivalence n.equivalence) random))
             (_.for [/.hash]
                    (hashT.spec (/.hash n.hash) random))

             (_.for [/.Tag]
                    ..test|tag)
             
             (do random.monad
               [left random
                right random]
               (_.coverage [/.as_text]
                 (bit.= (by (/.equivalence n.equivalence) = left right)
                        (text.= (/.as_text %.natural left) (/.as_text %.natural right)))))
             ))))
