... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [data
    ["[0]" text]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random)]]
   [test
    ["_" property (.only Test)]]
   [compiler
    ["@[0]" type]]
   [macro
    ["[0]" template]]
   [meta
    ["[0]" static]]]]
 [\\library
  ["[0]" /]]
 ["[0]" //
  ["[1][0]" simple]
  ["[1][0]" complex]])

(the .public random
  (Random @type.Pattern)
  (<| random.rec
      (function (_ random))
      (all random.or
           //simple.random
           (//complex.random 4 random)
           (random.list 4 random)
           random.natural
           )))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [@type.Pattern
              @type.#Simple_Pattern
              @type.#Complex_Pattern
              @type.#List_Pattern
              @type.#Bind_Pattern])
      (monad.let random.monad
        [expected_register random.natural
         expected_bit random.bit
         expected_natural random.natural
         expected_integer random.integer
         expected_revolution random.revolution
         expected_decimal random.decimal
         expected_text (random.lower_cased 2)

         expected_lefts random.natural
         expected_right? random.bit

         left ..random
         right ..random])
      (`` (all _.and
               (_.for [/.equivalence /.=]
                      (static.when (identical? /.equivalence /.=)
                        (equivalenceT.spec /.equivalence ..random)))
               
               (_.coverage [/.as_text]
                 (bit.= (by /.equivalence = left right)
                        (text.= (/.as_text left) (/.as_text right))))
               (_.coverage [/.unit]
                 (when (/.unit)
                   (/.unit)
                   true

                   _
                   false))
               (,, (template.with [<tag> <value>]
                     [(_.coverage [<tag>]
                        (when (<tag> <value>)
                          (<tag> actual)
                          (identical? <value> actual)

                          _
                          false))]

                     [[/.bind expected_register]
                      [/.bit expected_bit]
                      [/.natural expected_natural]
                      [/.integer expected_integer]
                      [/.revolution expected_revolution]
                      [/.decimal expected_decimal]
                      [/.text expected_text]]
                     ))
               (_.coverage [/.variant]
                 (when (/.variant [expected_lefts expected_right? (/.text expected_text)])
                   (/.variant [actual_lefts actual_right? (/.text actual_text)])
                   (and (identical? expected_lefts actual_lefts)
                        (identical? expected_right? actual_right?)
                        (identical? expected_text actual_text))

                   _
                   false))
               (,, (template.with [,case]
                     [(_.coverage [,case]
                        (when (,case (list (/.bit expected_bit)
                                           (/.natural expected_natural)
                                           (/.integer expected_integer)
                                           (/.revolution expected_revolution)
                                           (/.decimal expected_decimal)
                                           (/.text expected_text)))
                          (,case (list (/.bit actual_bit)
                                       (/.natural actual_natural)
                                       (/.integer actual_integer)
                                       (/.revolution actual_revolution)
                                       (/.decimal actual_decimal)
                                       (/.text actual_text)))
                          (and (identical? expected_bit actual_bit)
                               (identical? expected_natural actual_natural)
                               (identical? expected_integer actual_integer)
                               (identical? expected_revolution actual_revolution)
                               (identical? expected_decimal actual_decimal)
                               (identical? expected_text actual_text))

                          _
                          false))]

                     [[/.tuple]
                      [/.list]]))
               ))))
