... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [error
    ["[0]" try (.use "[1]:[0]" functor)]]
   [data
    ["[0]" text]
    ["[0]" binary
     ["[1]F" \\injection]
     ["<[1]>" \\projection]]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random)]]
   [meta
    ["[0]" static]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 ["[0]" ////
  ["[1][0]" version]])

(the .public random
  (Random /.Signature)
  (all random.and
       (all random.and
            (random.upper_cased 1)
            (random.lower_cased 1))
       ////version.random
       ))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Signature
              /.:name /.:version])
      (all _.and
           (_.for [/.equivalence /.=]
                  (static.when (identical? /.equivalence /.=)
                    (equivalenceT.spec /.equivalence ..random)))
           
           (monad.let random.monad
             [left ..random
              right ..random]
             (_.coverage [/.description]
               (bit.= (by /.equivalence = left right)
                      (text.= (/.description left) (/.description right)))))
           (monad.let random.monad
             [expected ..random]
             (_.coverage [/.as_binary /.of_binary]
               (|> expected
                   (binaryF.value /.as_binary)
                   (<binary>.value /.of_binary)
                   (try:each (by /.equivalence = expected))
                   (try.else false))))
           )))
