... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]]
   [math
    ["[0]" random (.only Random)]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(def .public (spec (open "[0]") (open "[0]") random)
  (All (_ of)
    (-> (Equivalence of) (/.Arithmetic of) (Random of)
        Test))
  (do random.monad
    [any random
     .let [@0 (- any any)
           non_zero (random.only (|>> (= @0) not)
                                 random)]
     any non_zero
     .let [@1 (/ any any)
           @2 (+ @1 @1)
           @4 (+ @2 @2)
           @8 (+ @4 @4)

           power_of_two (all random.either
                             (in @1)
                             (in @2)
                             (in @4)
                             (in @8)
                             )]
     left power_of_two
     right power_of_two]
    (<| (_.covering /._)
        (_.for [/.Arithmetic])
        (all _.and
             (_.coverage [/.+ /.-]
               (and (|> left (+ right) (- right) (= left))
                    (|> left (- right) (+ right) (= left))
                    
                    (|> left (+ @0) (= left))
                    (|> left (- @0) (= left))
                    
                    (|> left (- left) (= @0))))
             (_.coverage [/.* /./]
               (and (|> left (* @0) (= @0))
                    
                    (|> left (* @1) (= left))
                    (|> left (/ @1) (= left))
                    
                    (|> left (/ left) (= @1))

                    (|> left (* @2) (/ @2) (= left))))
             ))))
