... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["i" integer]]]
   ["[0]" meta (.only)
    ["[0]" code]
    [macro
     ["[0]" syntax]
     ["[0]" expansion]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the |divisor|
  (syntax.macro (_ [])
    (do meta.monad
      [divisor meta.seed]
      (in (list (code.integer (when divisor
                                0 +1
                                _ (.integer divisor))))))))

(the .public (random range)
  (for_some (_ %)
    (-> Integer
        (Random (/.Modulus %))))
  (|> random.integer
      (by random.monad each (i.% range))
      (random.one (|>> /.modulus try.maybe))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Modulus])
      (do random.monad
        [divisor random.integer
         modulus (..random +1,000,000)
         dividend random.integer]
        (all _.and
             (_.coverage [/.modulus /.divisor]
               (when (/.modulus divisor)
                 {try.#Success modulus}
                 (i.= divisor (/.divisor modulus))
                 
                 {try.#Failure error}
                 (i.= +0 divisor)))
             (_.coverage [/.zero_cannot_be_a_modulus]
               (when (/.modulus +0)
                 {try.#Failure error}
                 (exception.is? /.zero_cannot_be_a_modulus error)

                 {try.#Success modulus}
                 false))
             (_.coverage [/.literal]
               (expansion.let [<divisor> (|divisor|)]
                 (i.= <divisor> (/.divisor (/.literal <divisor>)))))
             (_.coverage [/.=]
               (expansion.let [<divisor> (|divisor|)]
                 (/.= (/.literal <divisor>)
                      (/.literal <divisor>))))
             (_.coverage [/.congruent?]
               (and (/.congruent? modulus dividend dividend)
                    (or (not (/.congruent? modulus dividend (++ dividend)))
                        (i.= +1 (/.divisor modulus)))))
             ))))
