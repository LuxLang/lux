... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]
    ["[0]" arithmetic
     ["[1]T" \\test]]
    [geometry
     ["[0]" circle]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["n" natural]
    ["i" integer]
    ["d" decimal]]]])

... This margin of error is necessary because floating-point arithmetic is not exact.
(the margin_of_error
  +0.000000001)

(the dimension
  (Random Decimal)
  (let [side_range +1,000
        full_range (i.* side_range side_range)]
    (|> random.integer
        (random#each (|>> (i.% full_range)
                          i.decimal
                          (d./ (i.decimal side_range)))))))

(the .public random
  (Random /.Complex)
  (do random.monad
    [real ..dimension
     imaginary ..dimension]
    (in (/.complex real imaginary))))

(the angle
  (Random /.Complex)
  (of random.monad each
      (|>> d.abs
           (d.* circle.tau)
           (/.polar +1.0))
      random.unit_decimal))

(the construction
  Test
  (do random.monad
    [real ..dimension
     imaginary ..dimension]
    (all _.and
         (_.coverage [/.complex]
           (and (let [r+i (/.complex real imaginary)]
                  (and (d.= real (its /.#real r+i))
                       (d.= imaginary (its /.#imaginary r+i))))
                (let [r+i (/.complex real)]
                  (and (d.= real (its /.#real r+i))
                       (d.= +0.0 (its /.#imaginary r+i))))))
         (_.coverage [/.approximately?]
           (/.approximately? ..margin_of_error
                             (/.complex real imaginary)
                             (/.complex real imaginary)))
         (_.coverage [/.not_a_number?]
           (and (/.not_a_number? (/.complex d.not_a_number imaginary))
                (/.not_a_number? (/.complex real d.not_a_number))))
         )))

(the constant
  Test
  (do random.monad
    [sample ..random
     dimension ..dimension]
    (all _.and
         (_.coverage [/.zero]
           (/.= /.zero (/.* /.zero sample)))
         (_.coverage [/.+one]
           (/.= sample (/.* /.+one sample)))
         (_.coverage [/.-one]
           (and (/.= /.zero
                     (/.+ sample
                          (/.* /.-one sample)))
                (/.= sample (/.* /.-one (/.* /.-one sample)))))
         (_.coverage [/.i]
           (and (/.= (/.complex +0.0 dimension)
                     (/.* /.i (/.complex dimension)))
                (/.= (/.* /.-one sample)
                     (/.* /.i (/.* /.i sample)))))
         )))

(the absolute_value&argument
  Test
  (do random.monad
    [real ..dimension
     imaginary ..dimension]
    (all _.and
         (_.coverage [/.abs]
           (let [normal!
                 (let [r+i (/.complex real imaginary)]
                   (and (d.>= (d.abs real) (/.abs r+i))
                        (d.>= (d.abs imaginary) (/.abs r+i))))

                 not_a_number!
                 (and (d.not_a_number? (/.abs (/.complex d.not_a_number imaginary)))
                      (d.not_a_number? (/.abs (/.complex real d.not_a_number))))

                 infinity!
                 (and (d.= d.positive_infinity (/.abs (/.complex d.positive_infinity imaginary)))
                      (d.= d.positive_infinity (/.abs (/.complex real d.positive_infinity)))
                      (d.= d.positive_infinity (/.abs (/.complex d.negative_infinity imaginary)))
                      (d.= d.positive_infinity (/.abs (/.complex real d.negative_infinity))))]
             (and normal!
                  not_a_number!
                  infinity!)))
         ... https://en.wikipedia.org/wiki/Argument_(complex_analysis)#Identities
         (_.coverage [/.argument]
           (let [sample (/.complex real imaginary)]
             (or (/.= /.zero sample)
                 (/.approximately? ..margin_of_error
                                   sample
                                   (/.*' (/.abs sample)
                                         (/.exp (/.* /.i (/.complex (/.argument sample)))))))))
         )))

(the number
  Test
  (do random.monad
    [x ..random
     y ..random
     factor ..dimension]
    (all _.and
         (_.coverage [/.+]
           (let [z (/.+ y x)]
             (and (/.= z
                       (/.complex (d.+ (its /.#real y)
                                       (its /.#real x))
                                  (d.+ (its /.#imaginary y)
                                       (its /.#imaginary x)))))))
         (_.coverage [/.-]
           (let [normal!
                 (let [z (/.- y x)]
                   (and (/.= z
                             (/.complex (d.- (its /.#real y)
                                             (its /.#real x))
                                        (d.- (its /.#imaginary y)
                                             (its /.#imaginary x))))))

                 inverse!
                 (and (|> x (/.+ y) (/.- y) (/.approximately? ..margin_of_error x))
                      (|> x (/.- y) (/.+ y) (/.approximately? ..margin_of_error x)))]
             (and normal!
                  inverse!)))
         (_.coverage [/.* /./]
           (|> x
               (/.* y)
               (/./ y)
               (/.approximately? ..margin_of_error x)))
         (_.coverage [/.*' /./']
           (|> x
               (/.*' factor)
               (/./' factor)
               (/.approximately? ..margin_of_error x)))
         (_.coverage [/.%]
           (let [rem (/.% y x)
                 quotient (|> x (/.- rem) (/./ y))
                 floored (|> quotient
                             (revised /.#real d.floor)
                             (revised /.#imaginary d.floor))]
             (/.approximately? +0.000000000001
                               x
                               (|> quotient (/.* y) (/.+ rem)))))
         )))

(the conjugate&reciprocal&signum&negation
  Test
  (do random.monad
    [x ..random]
    (all _.and
         (_.coverage [/.conjugate]
           (let [cx (/.conjugate x)]
             (and (d.= (its /.#real x)
                       (its /.#real cx))
                  (d.= (d.opposite (its /.#imaginary x))
                       (its /.#imaginary cx)))))
         (_.coverage [/.reciprocal]
           (let [reciprocal!
                 (|> x (/.* (/.reciprocal x)) (/.approximately? ..margin_of_error /.+one))
                 
                 own_inverse!
                 (|> x /.reciprocal /.reciprocal (/.approximately? ..margin_of_error x))]
             (and reciprocal!
                  own_inverse!)))
         (_.coverage [/.signum]
           ... Absolute value of signum is always root_2(2), 1 or 0.
           (let [signum_abs (|> x /.signum /.abs)]
             (or (d.= +0.0 signum_abs)
                 (d.= +1.0 signum_abs)
                 (d.= (d.pow +0.5 +2.0) signum_abs))))
         (_.coverage [/.opposite]
           (let [own_inverse!
                 (let [there (/.opposite x)
                       back_again (/.opposite there)]
                   (and (not (/.= there x))
                        (/.= back_again x)))

                 absolute!
                 (d.= (/.abs x)
                      (/.abs (/.opposite x)))]
             (and own_inverse!
                  absolute!)))
         )))

(the (trigonometric_symmetry? forward backward angle)
  (-> (-> /.Complex /.Complex) (-> /.Complex /.Complex) /.Complex
      Bit)
  (let [normal (|> angle forward backward)]
    (|> normal
        forward
        backward
        (/.approximately? ..margin_of_error normal))))

(the trigonometry
  Test
  (do [! random.monad]
    [angle ..angle]
    (all _.and
         (_.coverage [/.sin /.asin]
           (trigonometric_symmetry? /.sin /.asin angle))
         (_.coverage [/.cos /.acos]
           (trigonometric_symmetry? /.cos /.acos angle))
         (_.coverage [/.tan /.atan]
           (trigonometric_symmetry? /.tan /.atan angle))
         )))

(the hyperbolic
  Test
  (do [! random.monad]
    [angle ..angle]
    (all _.and
         (_.coverage [/.sinh]
           (/.approximately? ..margin_of_error
                             (|> angle (/.* /.i) /.sin (/.* /.i) (/.* /.-one))
                             (/.sinh angle)))
         (_.coverage [/.cosh]
           (/.approximately? ..margin_of_error
                             (|> angle (/.* /.i) /.cos)
                             (/.cosh angle)))
         (_.coverage [/.tanh]
           (/.approximately? ..margin_of_error
                             (|> angle (/.* /.i) /.tan (/.* /.i) (/.* /.-one))
                             (/.tanh angle)))
         )))

(the exponentiation&logarithm
  Test
  (do random.monad
    [it ..random]
    (all _.and
         (_.coverage [/.pow /.root_2]
           (|> it
               /.root_2
               (/.pow (/.complex +2.0))
               (/.approximately? ..margin_of_error it)))
         (_.coverage [/.pow']
           (|> it
               (/.pow' +0.5)
               (/.pow' +2.0)
               (/.approximately? ..margin_of_error it)))
         (_.coverage [/.log /.exp]
           (|> it
               /.log
               /.exp
               (/.approximately? ..margin_of_error it)))
         )))

(the root
  Test
  (do [! random.monad]
    [sample ..random
     degree (|> random.natural (of ! each (|>> (n.max 1) (n.% 5))))]
    (_.coverage [/.roots]
      (|> sample
          (/.roots degree)
          (list#each (/.pow' (|> degree .integer i.decimal)))
          (list.every? (/.approximately? ..margin_of_error sample))))))

(the polar
  Test
  (do [! random.monad]
    [it ..random]
    (all _.and
         (_.coverage [/.magnitude]
           (d.>= +0.0 (/.magnitude it)))
         (_.coverage [/.phase]
           (and (d.>= (d.* -1.0 circle.pi) (/.phase it))
                (d.<= circle.pi (/.phase it))))
         (_.coverage [/.polar]
           (/.approximately? ..margin_of_error
                             it
                             (/.polar (/.magnitude it) (/.phase it))))
         )))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Complex
              /.#imaginary /.#real])
      (all _.and
           (_.for [/.= /.equivalence]
                  (equivalenceT.spec /.equivalence ..random))
           ... (_.for [/.arithmetic]
           ...        (arithmeticT.spec /.equivalence /.arithmetic ..random))

           ..construction
           ..constant
           ..absolute_value&argument
           ..number
           ..conjugate&reciprocal&signum&negation
           ..trigonometry
           ..hyperbolic
           ..exponentiation&logarithm
           ..root
           ..polar
           )))
