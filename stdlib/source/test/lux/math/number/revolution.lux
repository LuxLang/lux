... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" hash
     ["[1]T" \\test]]
    ["[0]" enum
     ["[1]T" \\test]]
    ["[0]" interval
     ["[1]T" \\test]]
    ["[0]" format
     ["[1]T" \\test]]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" order
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]]
   [data
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random)]]
   [meta
    [macro
     ["[0]" template]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [// (.only hex)
    ["n" natural]
    ["d" decimal]
    ["[0]" i64 (.use "[1]#[0]" hash)]]]])

(the signature
  Test
  (`` (all _.and
           (_.for [/.equivalence /.=]
                  (equivalenceT.spec /.equivalence random.revolution))
           (_.for [/.hash]
                  (hashT.spec /.hash random.revolution))
           (_.for [/.order /.<]
                  (orderT.spec /.order random.revolution))
           (_.for [/.enum]
                  (enumT.spec /.enum random.revolution))
           (_.for [/.interval]
                  (intervalT.spec /.interval random.revolution))
           (,, (template.with [<composite> <monoid>]
                 [(_.for [<monoid> <composite>]
                         (monoidT.spec /.equivalence <monoid> random.revolution))]

                 [/.+ /.addition]

                 [/.min /.minimum]
                 [/.max /.maximum]
                 ))
           (,, (template.with [<format>]
                 [(_.for [<format>]
                         (formatT.spec /.equivalence <format> random.revolution))]

                 [/.binary] [/.octal] [/.base_10] [/.hex]
                 ))
           (_.coverage [/.base_02]
             (same? /.binary /.base_02))
           (_.coverage [/.base_08]
             (same? /.octal /.base_08))
           ... (_.coverage [/.base_10]
           ...   (same? /.decimal /.base_10))
           (_.coverage [/.base_16]
             (same? /.hex /.base_16))
           (_.coverage [/.arithmetic]
             (and (same? /.+ (of /.arithmetic +))
                  (same? /.- (of /.arithmetic -))
                  (same? /.* (of /.arithmetic *))
                  (same? /./ (of /.arithmetic /))))
           )))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [.Revolution])
      (`` (all _.and
               (,, (template.with [<half> <whole>]
                     [(_.coverage [<half>]
                        (/.= <whole>
                             (/.+ <half> <half>)))]

                     [/./1 (-- /./1)]
                     [/./2 .0]
                     [/./4 /./2]
                     [/./8 /./4]
                     [/./16 /./8]
                     [/./32 /./16]
                     [/./64 /./32]
                     [/./128 /./64]
                     [/./256 /./128]
                     [/./512 /./256]
                     [/./1024 /./512]
                     [/./2048 /./1024]
                     [/./4096 /./2048]
                     ))
               (do random.monad
                 [sample random.revolution]
                 (_.coverage [/.-]
                   (and (/.= .0 (/.- sample sample))
                        (/.= sample (/.- .0 sample)))))
               (do [! random.monad]
                 [left random.revolution
                  right random.revolution]
                 (_.coverage [/.*]
                   (and (/.< left (/.* left right))
                        (/.< right (/.* left right)))))
               (do [! random.monad]
                 [.let [dividend (is (Random Revolution)
                                     (of ! each (i64.and (hex "FFFF"))
                                         random.revolution))
                        divisor (is (Random Revolution)
                                    (of ! each (|>> (i64.and (hex "F"))
                                                    (i64.or (hex "1"))
                                                    (i64.right_rotated 8)
                                                    .revolution)
                                        random.natural))]
                  dividend (random.only (/.> .0) dividend)
                  divisor/0 divisor
                  divisor/1 (random.only (|>> (/.= divisor/0) not)
                                         divisor)
                  scale (of ! each (|>> (n.% 10) ++)
                            random.natural)]
                 (all _.and
                      (_.coverage [/./]
                        (bit.= (/.< divisor/0 divisor/1)
                               (/.> (/./ divisor/0 dividend) (/./ divisor/1 dividend))))
                      (_.coverage [/.%]
                        (of i64.equivalence =
                            (.i64 (n.% (.natural divisor/0) (.natural dividend)))
                            (.i64 (/.% divisor/0 dividend))))
                      (_.coverage [/.up /.down]
                        (let [symmetry!
                              (|> dividend
                                  (/.up scale)
                                  (/.down scale)
                                  (/.= dividend))

                              discrete_division!
                              (/.= (/.% (.revolution scale) dividend)
                                   (/.- (|> dividend
                                            (/.down scale)
                                            (/.up scale))
                                        dividend))]
                          (and symmetry!
                               discrete_division!)))
                      (_.coverage [/.ratio]
                        (|> dividend
                            (/.up scale)
                            (/.ratio dividend)
                            (n.= scale)))
                      ))
               (do [! random.monad]
                 [dividend random.revolution
                  divisor (random.only (|>> (/.= .0) not)
                                       random.revolution)]
                 (_.coverage [/./%]
                   (let [[quotient remainder] (/./% divisor dividend)]
                     (and (/.= (/./ divisor dividend) quotient)
                          (/.= (/.% divisor dividend) remainder)))))
               (do random.monad
                 [left random.revolution
                  right random.revolution]
                 (all _.and
                      (_.coverage [/.>]
                        (bit.= (/.> left right)
                               (/.< right left)))
                      (_.coverage [/.<= /.>=]
                        (bit.= (/.<= left right)
                               (/.>= right left)))
                      ))
               (do random.monad
                 [sample random.natural]
                 (_.coverage [/.reciprocal]
                   (/.= (/.reciprocal sample)
                        (|> sample /.reciprocal .natural /.reciprocal .natural /.reciprocal))))
               (do [! random.monad]
                 [expected (of ! each (|>> d.abs (d.% +1.0))
                               random.unit_decimal)
                  sample random.revolution]
                 (_.coverage [/.decimal]
                   (and (|> expected d.revolution /.decimal (d.= expected))
                        (d.number? (/.decimal sample)))))

               ..signature
               ))))
