... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" format
     ["[1]T" \\test]]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" order
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]]
   [control
    ["[0]" maybe (.use "[1]#[0]" functor)]
    ["[0]" try]]
   [data
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random)]
    ["[0]" arithmetic
     ["[1]T" \\test]]]
   [meta
    [macro
     ["[0]" template]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["n" natural]]]])

(the part
  (Random Natural)
  (of random.monad each
      (|>> (n.% 1,000,000) (n.max 1))
      random.natural))

(the .public random
  (Random /.Fraction)
  (do random.monad
    [numerator ..part
     denominator (random.only (|>> (n.= 0) not)
                              ..part)]
    (in (/.fraction numerator denominator))))

(the (identical? reference exemplar)
  (-> /.Fraction /.Fraction
      Bit)
  (and (n.= (its /.#numerator reference)
            (its /.#numerator exemplar))
       (n.= (its /.#denominator reference)
            (its /.#denominator exemplar))))

(the (normal? it)
  (-> /.Fraction
      Bit)
  (identical? it (/.normal it)))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Fraction
              /.#numerator /.#denominator])
      (`` (all _.and
               (_.for [/.equivalence /.=]
                      (equivalenceT.spec /.equivalence ..random))
               (_.for [/.order /.<]
                      (orderT.spec /.order ..random))
               (,, (template.with [<composite> <monoid>]
                     [(_.for [<monoid> <composite>]
                             (monoidT.spec /.equivalence <monoid> ..random))]

                     [/.+ /.addition]
                     [/.* /.multiplication]
                     ))
               (_.for [/.format]
                      (formatT.spec /.equivalence /.format ..random))
               (_.for [/.arithmetic]
                      (arithmeticT.spec /.equivalence /.arithmetic ..random))

               (do random.monad
                 [denom/0 ..part
                  denom/1 ..part]
                 (_.coverage [/.fraction]
                   (/.= (/.fraction 0 denom/0)
                        (/.fraction 0 denom/1))))
               (do [! random.monad]
                 [before_normal (random.and ..part ..part)]
                 (_.coverage [/.normal]
                   (or (normal? before_normal)
                       (let [it (/.normal before_normal)]
                         (and (normal? it)
                              (/.= before_normal it))))))
               (do random.monad
                 [numerator ..part
                  denominator (random.only (|>> (n.= 1) not)
                                           ..part)]
                 (_.coverage [/.natural]
                   (let [only_numerator!
                         (|> (/.fraction numerator)
                             /.natural
                             (maybe#each (n.= numerator))
                             (maybe.else false))

                         denominator_1!
                         (|> (/.fraction numerator 1)
                             /.natural
                             (maybe#each (n.= numerator))
                             (maybe.else false))

                         with_denominator!
                         (when (/.natural (/.fraction numerator denominator))
                           {try.#Success factor}
                           (and (n.= 0 (n.% denominator numerator))
                                (n.= numerator (n.* factor denominator)))
                           
                           {try.#Failure _}
                           (not (n.= 0 (n.% denominator numerator))))]
                     (and only_numerator!
                          denominator_1!
                          with_denominator!))))
               (do random.monad
                 [sample ..random]
                 (all _.and
                      (_.coverage [/.-]
                        (and (/.= (/.fraction 0) (/.- sample sample))
                             (/.= sample (/.- (/.fraction 0) sample))))
                      (_.coverage [/./]
                        (and (/.= (/.fraction 1) (/./ sample sample))
                             (/.= sample (/./ (/.fraction 1) sample))))
                      (_.coverage [/.reciprocal]
                        (/.= (/.fraction 1)
                             (/.* sample (/.reciprocal sample))))
                      ))
               (do random.monad
                 [left (random.only (|>> (/.= (/.fraction 0)) not)
                                    ..random)
                  right ..random]
                 (_.coverage [/.%]
                   (let [rem (/.% left right)
                         div (|> right (/.- rem) (/./ left))]
                     (and (/.= right
                               (|> div (/.* left) (/.+ rem)))
                          (when (/.natural div)
                            {try.#Success _} true
                            {try.#Failure _} false)))))
               (do random.monad
                 [left ..random
                  right ..random]
                 (all _.and
                      (_.coverage [/.>]
                        (bit.= (/.> left right)
                               (/.< right left)))
                      (_.coverage [/.<= /.>=]
                        (bit.= (/.<= left right)
                               (/.>= right left)))
                      ))
               ))))
