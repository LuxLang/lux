... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" hash
     ["[1]T" \\test]]
    ["[0]" format
     ["[1]T" \\test]]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" order
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]]
   [data
    ["[0]" any]
    ["[0]" bit]
    ["[0]" text]
    [collection
     ["[0]" list]]]
   [function
    [predicate (.only Predicate)]]
   [macro
    ["[0]" template]]
   [meta
    ["[0]" static]]
   [math
    ["[0]" random]
    [number
     [/64
      ["[0]" natural]]]]
   [test
    ["_" property (.only Test)]]
   [aspect
    ["[0]" case (.only)
     ["[1]T" \\test]]]]]
 [\\library
  ["[0]" /]])

(the number_as_text
  (-> /.Number
      Text)
  (|>> list.reversed
       (list.as_text (by natural.base_10 as))))

(the test_for_every_interface
  Test
  (`` (all _.and
           (_.for [/.equivalence /.=]
                  (static.when (same? /.equivalence /.=)
                    (equivalenceT.spec /.equivalence (/.random 3))))
           (_.for [/.hash]
                  (hashT.spec /.hash (/.random 1)))
           (_.for [/.order /.<]
                  (orderT.spec /.order (/.random 3)))
           (,, (template.with [<composite> <monoid>]
                 [(_.for [<monoid> <composite>]
                         (static.when (same? <composite> (by <monoid> composite))
                           (monoidT.spec /.equivalence <monoid> (/.random 3))))]
                 
                 [[/.+ /.addition]
                  [/.x /.multiplication]

                  [/.major /.maximum]]))
           (,, (template.with [<format>]
                 [(_.for [<format>]
                         (formatT.spec /.equivalence <format> (/.random 3)))]

                 [[/.base_02]
                  [/.base_08]
                  [/.base_10]
                  [/.base_16]]))
           )))

(the lattice
  Test
  (<| (do [! random.monad]
        [left (/.random 3)
         right (/.random 3)

         digits (by ! each (natural.% 4)
                    random.natural)
         
         expected (/.random digits)])
      (all _.and
           (_.coverage [/.lcm /.least_common_multiple
                        /.multiple?]
             (and (/.= /.zero
                       (/.least_common_multiple /.zero expected))
                  (/.= (/.least_common_multiple right left)
                       (/.least_common_multiple left right))
                  (and (/.multiple? left (/.least_common_multiple left right))
                       (/.multiple? right (/.least_common_multiple left right)))
                  (alias? /.least_common_multiple /.lcm)))
           (_.coverage [/.gcd /.greatest_common_divisor
                        /.divisor?]
             (and (/.= expected
                       (/.greatest_common_divisor /.zero expected))
                  (/.= (/.greatest_common_divisor right left)
                       (/.greatest_common_divisor left right))
                  (and (/.divisor? left (/.greatest_common_divisor left right))
                       (/.divisor? right (/.greatest_common_divisor left right)))
                  (alias? /.greatest_common_divisor /.gcd)))
           (_.coverage [/.co_prime?]
             (and (bit.= (/.co_prime? expected expected)
                         (/.= /.one expected))
                  (bit.= (/.= /.one (/.greatest_common_divisor left right))
                         (/.co_prime? left right))))
           )))

(the comparison
  Test
  (<| (do [! random.monad]
        [bit_0 random.bit
         bit_1 random.bit
         bit_2 random.bit
         .let [[random_0 random_1 random_2] (if bit_0
                                                (if bit_1
                                                    (if bit_2
                                                        [(/.random 1) (/.random 2) (/.random 3)]
                                                        [(/.random 1) (/.random 3) (/.random 2)])
                                                    (if bit_2
                                                        [(/.random 2) (/.random 1) (/.random 3)]
                                                        [(/.random 2) (/.random 3) (/.random 1)]))
                                                (if bit_1
                                                    [(/.random 3) (/.random 1) (/.random 2)]
                                                    [(/.random 3) (/.random 2) (/.random 1)]))]
         origin random_0
         other random_1
         it random_2

         it_0 (/.random 3)
         it_1 (/.random 3)
         it_2 (/.random 3)

         left (/.random 3)
         right (/.random 3)])
      (all _.and
           (_.coverage [/.=]
             (let [reflection!
                   (/.= it it)

                   symmetry!
                   (bit.= (/.= origin it)
                          (/.= it origin))

                   transitivity!
                   (bit.= (and (/.= it_0 it_1)
                               (/.= it_1 it_2))
                          (/.= it_0 it_2))]
               (and reflection!
                    symmetry!
                    transitivity!)))
           (_.coverage [/.<]
             (let [anti_reflection!
                   (not (/.< it it))

                   anti_symmetry!
                   (or (/.= it origin)
                       (bit.= (not (/.< origin it))
                              (/.< it origin)))

                   ++ (all /.major origin other it)
                   -- (all /.minor origin other it)
                   not_extreme (is (Change /.Number)
                                   (function (_ it)
                                     (if (/.< ++ it)
                                         (if (/.> -- it)
                                             it
                                             /.zero)
                                         /.zero)))
                   +- (all /.+
                           (not_extreme origin)
                           (not_extreme other)
                           (not_extreme it))

                   transitivity!
                   (bit.= (and (/.< ++ +-)
                               (/.< +- --))
                          (/.< ++ --))]
               (and anti_reflection!
                    anti_symmetry!
                    transitivity!)))
           (_.coverage [/.>]
             (bit.= (/.< it origin)
                    (/.> origin it)))
           (_.coverage [/.<=]
             (bit.= (or (/.< origin it)
                        (/.<= origin it))
                    (/.<= origin it)))
           (_.coverage [/.>=]
             (bit.= (or (/.> origin it)
                        (/.>= origin it))
                    (/.>= origin it)))
           (_.coverage [/.major /.minor]
             (let [idempotency!
                   (and (/.= it (/.minor it it))
                        (/.= it (/.major it it)))

                   absorption!
                   (/.= /.zero (/.minor /.zero it))

                   identity!
                   (/.= it (/.major /.zero it))

                   commutativity!
                   (and (/.= (/.minor origin it)
                             (/.minor it origin))
                        (/.= (/.major origin it)
                             (/.major it origin)))

                   relationship!
                   (and (/.< (/.major origin it)
                             (/.minor origin it))
                        (/.> (/.minor origin it)
                             (/.major origin it)))]
               (and idempotency!
                    
                    absorption!
                    identity!

                    commutativity!
                    
                    relationship!)))
           )))

(the arithmetic
  Test
  (<| (do [! random.monad]
        [bit_0 random.bit
         bit_1 random.bit
         bit_2 random.bit
         .let [[random_0 random_1 random_2] (if bit_0
                                                (if bit_1
                                                    (if bit_2
                                                        [(/.random 1) (/.random 2) (/.random 3)]
                                                        [(/.random 1) (/.random 3) (/.random 2)])
                                                    (if bit_2
                                                        [(/.random 2) (/.random 1) (/.random 3)]
                                                        [(/.random 2) (/.random 3) (/.random 1)]))
                                                (if bit_1
                                                    [(/.random 3) (/.random 1) (/.random 2)]
                                                    [(/.random 3) (/.random 2) (/.random 1)]))]
         origin random_0
         other random_1
         it random_2

         exponent_0 (by ! each (natural.% 200) random.natural)
         exponent_1 (by ! each (natural.% 200) random.natural)

         left (/.random 3)
         right (/.random 3)

         sample (/.random 3)
         power (by ! each (natural.% 10)
                   random.natural)])
      (all _.and
           (_.coverage [/.+]
             (and (/.= (/.+ origin it)
                       (/.+ it origin))
                  (bit.= (/.= it (/.+ it it))
                         (/.= /.zero it))
                  (/.= (/.+ origin (/.+ other it))
                       (/.+ (/.+ origin other) it))))
           (_.coverage [/.x]
             (let [commutativity!
                   (/.= (/.x origin it)
                        (/.x it origin))

                   identity!
                   (bit.= (/.= it (/.x it it))
                          (/.= /.one it))

                   associativity!
                   (/.= (/.x origin (/.x other it))
                        (/.x (/.x origin other) it))]
               (and commutativity!
                    identity!
                    associativity!)))
           (_.coverage [/.zero]
             (and (/.= it
                       (/.+ /.zero it))
                  (/.= /.zero
                       (/.x /.zero it))))
           (_.coverage [/.one]
             (/.= it
                  (/.x /.one it)))
           (_.coverage [/.-]
             (let [balance!
                   (let [sub (/.- origin origin)]
                     (|> (/.- it it)
                         (its natural.#negative)
                         (/.= /.zero)))

                   reconstitution!
                   (let [positive!
                         (let [major (/.major origin it)
                               minor (/.minor origin it)
                               sub (/.- minor major)]
                           (and (/.= /.zero (its natural.#negative sub))
                                (/.= major (/.+ minor (its natural.#positive sub)))))

                         negative!
                         (let [major (/.major origin it)
                               minor (/.minor origin it)
                               sub (/.- major minor)]
                           (and (/.= /.zero (its natural.#positive sub))
                                (/.= major (/.+ minor (its natural.#negative sub)))))]
                     (and positive!
                          negative!))]
               (and balance!
                    reconstitution!)))
           (_.coverage [/./]
             (let [itself!
                   (/.= /.one (/./ it it))

                   one!
                   (/.= it (/./ /.one it))

                   of_zero!
                   (/.= /.zero (/./ it /.zero))

                   small!
                   (bit.= (/.< other it)
                          (/.= /.zero (/./ other it)))

                   large!
                   (bit.= (/.> other it)
                          (/.>= /.one (/./ other it)))

                   factor!
                   (and (|> it
                            (/.x other)
                            (/./ other)
                            (/.= it))
                        (|> other
                            (/.x it)
                            (/./ it)
                            (/.= other)))

                   exponential!
                   (let [e (/.exponential_2 exponent_0)]
                     (and (/.= e (/./ it (/.x e it)))
                          (/.= it (/./ e (/.x e it)))))

                   logarithm!
                   (let [e (/.exponential_2 exponent_0)]
                     (natural.= (natural.+ (/.logarithm_2 e)
                                           (/.logarithm_2 it))
                                (/.logarithm_2 (/.x e it))))]
               (and itself!
                    one!
                    of_zero!
                    small!
                    large!
                    factor!
                    exponential!
                    logarithm!)))
           (_.coverage [/.division_by_zero]
             (when (try (/./ /.zero it))
               {.#Left error}
               (text.contains? /.division_by_zero error)

               else
               false))
           (_.coverage [/.%]
             (let [itself!
                   (/.= /.zero (/.% it it))

                   no_room!
                   (/.= /.zero (/.% /.one it))

                   factor!
                   (and (/.= /.zero (/.% it (/.x other it)))
                        (/.= /.zero (/.% other (/.x other it))))

                   reconstitution!
                   (|> it
                       (/./ other)
                       (/.x other)
                       (/.+ (/.% other it))
                       (/.= it))]
               (and itself!
                    no_room!
                    factor!
                    reconstitution!)))
           (_.coverage [/./%]
             (let [[div rem] (/./% left right)]
               (and (/.= div (/./ left right))
                    (/.= rem (/.% left right)))))
           (<| (_.for [/.Exponent])
               (all _.and
                    (_.coverage [/.^]
                      (and (/.= /.one (/.^ 0 sample))
                           (/.= sample (/.^ 1 sample))
                           (/.= (list.mix /.x /.one (list.repeated power sample))
                                (/.^ power sample))))
                    (_.coverage [/.exponential_2]
                      (let [double (is (Change /.Number)
                                       (function (_ it)
                                         (/.+ it it)))]
                        (and (/.= (|> it)
                                  (/.x it (/.exponential_2 0)))
                             (/.= (|> it double)
                                  (/.x it (/.exponential_2 1)))
                             (/.= (double (/.x it (/.exponential_2 exponent_0)))
                                  (/.x it (/.exponential_2 (++ exponent_0))))
                             (let [e2^x (/.exponential_2 exponent_0)]
                               (and (|> e2^x
                                        (/.x it)
                                        (/./ it)
                                        (/.= e2^x))
                                    (|> it
                                        (/.x e2^x)
                                        (/./ e2^x)
                                        (/.= it))))
                             (/.= (/.exponential_2 (natural.+ exponent_0 exponent_1))
                                  (/.x (/.exponential_2 exponent_0)
                                       (/.exponential_2 exponent_1))))))
                    (_.coverage [/.logarithm_2]
                      (and (|> (/.logarithm_2 /.zero)
                               (natural.= 0))
                           (|> (/.logarithm_2 /.one)
                               (natural.= 0))
                           (|> (/.logarithm_2 (/.+ /.one /.one))
                               (natural.= 1))
                           (natural.= (/.logarithm_2 (/.+ it it))
                                      (++ (/.logarithm_2 it)))
                           (|> exponent_0
                               /.exponential_2
                               /.logarithm_2
                               (natural.= exponent_0))))
                    ))
           )))

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [bit_0 random.bit
         bit_1 random.bit
         bit_2 random.bit
         .let [[random_0 random_1 random_2] (if bit_0
                                                (if bit_1
                                                    (if bit_2
                                                        [(/.random 1) (/.random 2) (/.random 3)]
                                                        [(/.random 1) (/.random 3) (/.random 2)])
                                                    (if bit_2
                                                        [(/.random 2) (/.random 1) (/.random 3)]
                                                        [(/.random 2) (/.random 3) (/.random 1)]))
                                                (if bit_1
                                                    [(/.random 3) (/.random 1) (/.random 2)]
                                                    [(/.random 3) (/.random 2) (/.random 1)]))]
         origin random_0
         other random_1
         it random_2

         sample (/.random 3)])
      (_.for [/.Number
              /.random])
      (all _.and
           ..test_for_every_interface
           
           (_.coverage [/.Natural /.N]
             (and (alias? /.Natural /.Number)
                  (alias? /.N /.Number)))
           ..comparison
           ..arithmetic
           ..lattice
           (_.coverage [/.even? /.odd?]
             (bit.= (/.even? sample)
                    (not (/.odd? sample))))
           (all _.and
                (<| (_.for [/.#Zero])
                    (caseT.specification /.#Zero
                                         /.equivalence any.equivalence
                                         (/.random 0) (pure [])))
                (_.coverage [/.#Zero]
                  (and (case.when /.zero
                         {/.#Zero _}
                         true
                         
                         else
                         false)
                       (/.= /.zero (case.some /.#Zero [])))))
           (all _.and
                (<| (_.for [/.#Successor])
                    (caseT.specification /.#Successor
                                         /.equivalence /.equivalence
                                         (/.random 1) (/.random 1)))
                (_.coverage [/.#Successor]
                  (and (case.when origin
                         {/.#Successor predecessor}
                         (and (/.< origin predecessor)
                              (/.= origin (/.+ /.one predecessor)))
                         
                         else
                         (/.= /.zero origin))
                       (/.= (/.+ /.one origin)
                            (case.some /.#Successor origin)))))
           )))
