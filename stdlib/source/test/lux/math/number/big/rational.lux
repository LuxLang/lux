... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except identical?)
   [abstract
    [monad (.only do)]
    ["[0]" format
     ["[1]T" \\test]]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" order
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]]
   [control
    ["[0]" maybe (.use "[1]#[0]" functor)]]
   [error
    ["[0]" try]]
   [data
    ["[0]" bit]
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]#[0]" mix)]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]
    ["[0]" arithmetic
     ["[1]T" \\test]]]
   [macro
    ["[0]" template]]
   [meta
    ["[0]" static]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" natural]
    ["[0]" integer]
    [//
     ["[0]" /64
      ["[0][1]" integer]]
     [//
      ["[0]" unit]]]]]])

(the (identical? reference exemplar)
  (-> /.Rational /.Rational
      Bit)
  (and (integer.= (its /.#numerator reference)
                  (its /.#numerator exemplar))
       (natural.= (its /.#denominator reference)
                  (its /.#denominator exemplar))))

(the (normal? it)
  (-> /.Rational
      Bit)
  (identical? it (/.normal it)))

(the unit
  Test
  (`` (all _.and
           (,, (template.with [,format <as> <of> ,unit]
                 [(let [threshold (|> (its unit.#factor ,unit)
                                      .integer
                                      integer.small
                                      /.new
                                      /.reciprocal)]
                    (all _.and
                         (_.for [,format]
                                (formatT.spec (/.approximately threshold) ,format
                                              (/.random 2)))
                         (do [! random.monad]
                           [expected (/.random 2)
                            .let [change_of_scale!
                                  (|> expected
                                      <as>
                                      (by /.equivalence = expected)
                                      not)

                                  iso_morphism!
                                  (|> expected
                                      <as>
                                      <of>
                                      (by /.equivalence = expected))]]
                           (_.coverage [<as> <of>]
                             (and change_of_scale!
                                  iso_morphism!)))
                         ))]

                 [[/.degree /.as_degree /.of_degree unit.degree]
                  [/.gradian /.as_gradian /.of_gradian unit.gradian]
                  [/.per_cent /.as_per_cent /.of_per_cent unit.per_cent]
                  [/.per_mille /.as_per_mille /.of_per_mille unit.per_mille]
                  [/.per_myriad /.as_per_myriad /.of_per_myriad unit.per_myriad]]))
           )))

(the each_interface
  Test
  (`` (all _.and
           (_.for [/.equivalence /.=]
                  (static.when (same? /.equivalence /.=)
                    (equivalenceT.spec /.equivalence (/.random 2))))
           (_.for [/.order /.<]
                  (static.when (same? (by /.order <) /.<)
                    (orderT.spec /.order (/.random 2))))
           (,, (template.with [<composite> <monoid>]
                 [(_.for [<monoid> <composite>]
                         (static.when (same? <composite> (by <monoid> composite))
                           (monoidT.spec /.equivalence <monoid> (/.random 2))))]

                 [[/.+ /.addition]
                  [/.x /.multiplication]]))
           (_.for [/.text]
                  (formatT.spec /.equivalence /.text (/.random 2)))
           (_.for [/.arithmetic]
                  (arithmeticT.spec /.equivalence /.arithmetic (/.random 2)))
           )))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Number
              /.#numerator /.#denominator

              /.random])
      (do random.monad
        [expected (/.random 2)

         it_0 (/.random 2)
         it_1 (/.random 2)])
      (all _.and
           ..each_interface

           (_.coverage [/.Rational /.Q]
             (and (alias? /.Rational /.Number)
                  (alias? /.Q /.Number)))
           (_.coverage [/.approximately]
             (and (bit.= (by (/.approximately /.zero) = it_0 it_1)
                         (/.= it_0 it_1))
                  (by (/.approximately (/.major it_0 it_1)) = it_0 it_1)
                  (or (not (by (/.approximately (/.minor it_0 it_1)) = it_0 it_1))
                      (/.= it_0 it_1))))
           (_.coverage [/.delimiter]
             (text.contains? /.delimiter
                             (by /.text as expected)))
           (_.coverage [/.zero]
             (and (/.= expected (/.+ /.zero expected))
                  (/.= expected (/.- /.zero expected))
                  
                  (/.= /.zero (/.x /.zero expected))
                  (/.= /.infinity (/./ /.zero expected))))
           (_.coverage [/.positive_one]
             (and (/.= expected (/.x /.positive_one expected))
                  (/.= expected (/./ /.positive_one expected))))
           (_.coverage [/.infinity]
             (and (/.= /.infinity (/./ /.zero /.positive_one))

                  (/.= /.infinity (/.+ /.infinity expected))
                  (/.= /.infinity (/.- /.infinity expected))
                  
                  (/.= /.infinity (/.x /.infinity expected))
                  (/.= /.zero (/./ /.infinity expected))))
           (_.coverage [/.indeterminate]
             (and (/.= /.indeterminate expected)

                  (/.= (/.+ /.indeterminate expected)
                       (/.- /.indeterminate expected))
                  
                  (/.= (/.x /.indeterminate expected)
                       (/./ /.indeterminate expected))))
           (_.coverage [/.finite?]
             (and (/.finite? expected)
                  (not (/.finite? /.infinity))))
           (_.coverage [/.determinate?]
             (and (/.determinate? expected)
                  (not (/.determinate? /.indeterminate))))
           (_.coverage [/.absolute]
             (/.>= expected (/.absolute expected)))
           (_.coverage [/.signum]
             (and (/.= (/.absolute expected)
                       (/.x (/.signum expected)
                            expected))
                  (/.= expected
                       (/.x (/.signum expected)
                            (/.absolute expected)))))
           (_.coverage [/.whole /.partial]
             (/.= expected
                  (/.+ (/.whole expected)
                       (/.partial expected))))
           (_.coverage [/.positive? /.negative?]
             (bit.= (/.positive? expected)
                    (not (/.negative? expected))))
           (_.coverage [/.negative_one]
             (and (not (/.= /.positive_one /.negative_one))
                  (/.= /.positive_one (/.x /.negative_one /.negative_one))))
           (_.coverage [/.opposite]
             (and (not (/.= /.positive_one (/.opposite expected)))
                  (/.= expected (/.opposite (/.opposite expected)))))
           
           (do random.monad
             [denom/0 (natural.random 2)
              denom/1 (natural.random 2)]
             (_.coverage [/.new]
               (/.= (/.new integer.zero denom/0)
                    (/.new integer.zero denom/1))))
           (do [! random.monad]
             [before_normal (random.and (integer.random 2)
                                        (natural.random 2))]
             (_.coverage [/.normal]
               (or (normal? before_normal)
                   (let [it (/.normal before_normal)]
                     (and (normal? it)
                          (/.= before_normal it))))))
           (do random.monad
             [numerator (integer.random 2)
              denominator (random.only (|>> (natural.= natural.one) not)
                                       (natural.random 2))]
             (_.coverage [/.integer]
               (let [only_numerator!
                     (|> (/.new numerator)
                         /.integer
                         (maybe#each (integer.= numerator))
                         (maybe.else false))

                     denominator_1!
                     (|> (/.new numerator natural.one)
                         /.integer
                         (maybe#each (integer.= numerator))
                         (maybe.else false))

                     with_denominator!
                     (when (/.integer (/.new numerator denominator))
                       {try.#Success factor}
                       (and (integer.= integer.zero (integer.% (integer.natural denominator) numerator))
                            (integer.= numerator (integer.x factor (integer.natural denominator))))
                       
                       {try.#Failure _}
                       (not (integer.= integer.zero (integer.% (integer.natural denominator) numerator))))]
                 (and only_numerator!
                      denominator_1!
                      with_denominator!))))
           (_.coverage [/.not_an_integer]
             (or (|> (/.partial expected)
                     /.integer
                     (try.is? /.not_an_integer))
                 (and (/.= /.zero (/.partial expected))
                      (/.= expected (/.whole expected)))))
           (do [! random.monad]
             [sample (/.random 2)
              power (by ! each (integer/64.% +10)
                        random.integer)]
             (all _.and
                  (_.coverage [/.-]
                    (and (/.= (/.new integer.zero) (/.- sample sample))
                         (/.= sample (/.- (/.new integer.zero) sample))))
                  (_.coverage [/./]
                    (and (/.= (/.new integer.zero) (/./ sample sample))
                         (/.= sample (/./ (/.new integer.zero) sample))))
                  (_.coverage [/.reciprocal]
                    (/.= (/.new integer.zero)
                         (/.x sample (/.reciprocal sample))))
                  (_.coverage [/.^]
                    (and (/.= /.positive_one (/.^ +0 sample))
                         (/.= sample (/.^ +1 sample))
                         (/.= (/.reciprocal sample)
                              (/.^ -1 sample))
                         (/.= (/.reciprocal (/.^ power sample))
                              (/.^ (integer/64.opposite power) sample))
                         (if (integer/64.< +0 power)
                             (/.= (list#mix /.x /.positive_one (list.repeated (.natural (integer/64.opposite power)) sample))
                                  (/.reciprocal (/.^ power sample)))
                             (/.= (list#mix /.x /.positive_one (list.repeated (.natural power) sample))
                                  (/.^ power sample)))))
                  ))
           (do random.monad
             [left (random.only (|>> (/.= (/.new integer.zero))
                                     not)
                                (/.random 2))
              right (/.random 2)]
             (_.coverage [/.%]
               (let [rem (/.% left right)
                     div (|> right (/.- rem) (/./ left))]
                 (and (/.= right
                           (|> div (/.x left) (/.+ rem)))
                      (when (/.integer div)
                        {try.#Success _} true
                        {try.#Failure _} false)))))
           (do random.monad
             [left (/.random 2)
              right (/.random 2)]
             (all _.and
                  (_.coverage [/.>]
                    (bit.= (/.> left right)
                           (/.< right left)))
                  (_.coverage [/.<= /.>=]
                    (bit.= (/.<= left right)
                           (/.>= right left)))
                  ))

           ..unit
           )))
