... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except identical?)
   [abstract
    [monad (.only do)]
    ["[0]" format
     ["[1]T" \\test]]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" order
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]]
   [control
    ["[0]" maybe (.use "[1]#[0]" functor)]]
   [error
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    ["[0]" bit]
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]#[0]" mix)]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]]
   [macro
    ["[0]" template]]
   [meta
    ["[0]" static]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["n" natural]
    [//
     ["[0]" /64
      ["n[1]" natural]
      ["i" integer]
      ["d" decimal]]
     [//
      ["[0]" unit]]]]]])

(the (identical? reference exemplar)
  (-> /.Fraction /.Fraction
      Bit)
  (and (n.= (its /.#numerator reference)
            (its /.#numerator exemplar))
       (n.= (its /.#denominator reference)
            (its /.#denominator exemplar))))

(the (normal? it)
  (-> /.Fraction
      Bit)
  (identical? it (/.normal it)))

(the unit
  Test
  (`` (all _.and
           (,, (template.with [,format <as> <of> ,unit]
                 [(let [threshold (|> (its unit.#factor ,unit)
                                      n.small
                                      /.new
                                      /.reciprocal)]
                    (all _.and
                         (_.for [,format]
                                (formatT.spec (/.approximately? threshold) ,format
                                              (/.random 2)))
                         (do [! random.monad]
                           [expected (/.random 2)
                            .let [change_of_scale!
                                  (|> expected
                                      <as>
                                      (by /.equivalence = expected)
                                      not)

                                  iso_morphism!
                                  (|> expected
                                      <as>
                                      <of>
                                      (by /.equivalence = expected))]]
                           (_.coverage [<as> <of>]
                             (and change_of_scale!
                                  iso_morphism!)))
                         ))]

                 [[/.degree /.as_degree /.of_degree unit.degree]
                  [/.gradian /.as_gradian /.of_gradian unit.gradian]
                  [/.per_cent /.as_per_cent /.of_per_cent unit.per_cent]
                  [/.per_mille /.as_per_mille /.of_per_mille unit.per_mille]
                  [/.per_myriad /.as_per_myriad /.of_per_myriad unit.per_myriad]]))
           )))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Fraction
              /.#numerator /.#denominator

              /.random])
      (do random.monad
        [expected (/.random 2)

         it_0 (/.random 2)
         it_1 (/.random 2)])
      (`` (all _.and
               (_.for [/.equivalence /.=]
                      (static.when (same? /.equivalence /.=)
                        (equivalenceT.spec /.equivalence (/.random 2))))
               (_.for [/.order /.<]
                      (static.when (same? (by /.order <) /.<)
                        (orderT.spec /.order (/.random 2))))
               (,, (template.with [<composite> <monoid>]
                     [(_.for [<monoid> <composite>]
                             (static.when (same? <composite> (by <monoid> composite))
                               (monoidT.spec /.equivalence <monoid> (/.random 2))))]

                     [[/.+ /.addition]
                      [/.x /.multiplication]

                      [/.minor /.minimum]
                      [/.major /.maximum]]))
               (_.for [/.text /.as_text]
                      (static.when (same? /.as_text
                                          (by /.text as))
                        (formatT.spec /.equivalence /.text (/.random 2))))

               (_.coverage [/.Number /.Q+]
                 (and (alias? /.Fraction /.Number)
                      (alias? /.Fraction /.Q+)))
               (_.coverage [/.approximately?]
                 (and (bit.= (/.approximately? /.zero it_0 it_1)
                             (/.= it_0 it_1))
                      (/.approximately? (/.major it_0 it_1) it_0 it_1)
                      (or (not (/.approximately? (/.minor it_0 it_1) it_0 it_1))
                          (/.= it_0 it_1))))
               (_.coverage [/.zero]
                 (and (/.= expected (/.+ /.zero expected))
                      (/.= expected (its n/64.#positive (/.- /.zero expected)))
                      
                      (/.= /.zero (/.x /.zero expected))
                      (/.= /.infinity (/./ /.zero expected))))
               (_.coverage [/.one]
                 (and (not (/.= expected (/.+ /.one expected)))
                      (/.= expected (/.x /.one expected))
                      (/.= expected (/./ /.one expected))))
               (_.coverage [/.infinity]
                 (and (/.= /.infinity (/./ /.zero /.one))

                      (/.= /.infinity (/.+ /.infinity expected))
                      (/.= /.infinity (its n/64.#negative (/.- /.infinity expected)))
                      
                      (/.= /.infinity (/.x /.infinity expected))
                      (/.= /.zero (/./ /.infinity expected))))
               (_.coverage [/.indeterminate]
                 (and (/.= /.indeterminate expected)

                      (/.= (/.+ /.indeterminate expected)
                           (its n/64.#positive (/.- /.indeterminate expected)))
                      
                      (/.= (/.x /.indeterminate expected)
                           (/./ /.indeterminate expected))))
               (_.coverage [/.finite?]
                 (and (/.finite? expected)
                      (not (/.finite? /.infinity))))
               (_.coverage [/.determinate?]
                 (and (/.determinate? expected)
                      (not (/.determinate? /.indeterminate))))
               (_.coverage [/.whole /.partial]
                 (/.= expected
                      (/.+ (/.whole expected)
                           (/.partial expected))))
               (_.coverage [/.natural]
                 (|> (/.natural (/.whole expected))
                     (try#each (|>> /.new
                                    (/.+ (/.partial expected))
                                    (/.= expected)))
                     (try.else false)))
               (_.coverage [/.not_a_natural]
                 (or (|> (/.partial expected)
                         /.natural
                         (try.is? /.not_a_natural))
                     (and (/.= /.zero (/.partial expected))
                          (/.= expected (/.whole expected)))))
               (do random.monad
                 [denom/0 (n.random 2)
                  denom/1 (n.random 2)]
                 (_.coverage [/.new]
                   (/.= (/.new n.zero denom/0)
                        (/.new n.zero denom/1))))
               (do [! random.monad]
                 [before_normal (random.and (n.random 2)
                                            (n.random 2))]
                 (_.coverage [/.normal]
                   (or (normal? before_normal)
                       (let [it (/.normal before_normal)]
                         (and (normal? it)
                              (/.= before_normal it))))))
               (do random.monad
                 [numerator (n.random 2)
                  denominator (random.only (|>> (n.= n.one) not)
                                           (n.random 2))]
                 (_.coverage [/.natural]
                   (let [only_numerator!
                         (|> (/.new numerator)
                             /.natural
                             (maybe#each (n.= numerator))
                             (maybe.else false))

                         denominator_1!
                         (|> (/.new numerator n.one)
                             /.natural
                             (maybe#each (n.= numerator))
                             (maybe.else false))

                         with_denominator!
                         (when (/.natural (/.new numerator denominator))
                           {try.#Success factor}
                           (and (n.= n.zero (n.% denominator numerator))
                                (n.= numerator (n.x factor denominator)))
                           
                           {try.#Failure _}
                           (not (n.= n.zero (n.% denominator numerator))))]
                     (and only_numerator!
                          denominator_1!
                          with_denominator!))))
               (do [! random.monad]
                 [sample (/.random 2)
                  power (by ! each (i.% +10)
                            random.integer)]
                 (all _.and
                      (_.coverage [/.-]
                        (and (/.= /.zero
                                  (its n/64.#positive (/.- sample sample)))
                             (/.= sample
                                  (its n/64.#positive (/.- /.zero sample)))))
                      (_.coverage [/./]
                        (and (/.= /.one (/./ sample sample))
                             (/.= sample (/./ /.one sample))))
                      (_.coverage [/.reciprocal]
                        (/.= /.one
                             (/.x sample (/.reciprocal sample))))
                      (_.coverage [/.Exponent /.^]
                        (and (/.= /.one (/.^ +0 sample))
                             (/.= sample (/.^ +1 sample))
                             (/.= (/.reciprocal sample)
                                  (/.^ -1 sample))
                             (/.= (/.reciprocal (/.^ power sample))
                                  (/.^ (i.opposite power) sample))
                             (if (i.< +0 power)
                                 (/.= (list#mix /.x /.one (list.repeated (.natural (i.opposite power)) sample))
                                      (/.reciprocal (/.^ power sample)))
                                 (/.= (list#mix /.x /.one (list.repeated (.natural power) sample))
                                      (/.^ power sample)))))
                      ))
               (do random.monad
                 [left (random.only (|>> (/.= /.zero) not)
                                    (/.random 2))
                  right (/.random 2)]
                 (_.coverage [/.%]
                   (let [rem (/.% left right)
                         div (|> right
                                 (/.- rem)
                                 (its n/64.#positive)
                                 (/./ left))]
                     (and (/.= right
                               (|> div (/.x left) (/.+ rem)))
                          (when (/.natural div)
                            {try.#Success _} true
                            {try.#Failure _} false)))))
               (do random.monad
                 [left (/.random 2)
                  right (/.random 2)]
                 (all _.and
                      (_.coverage [/.>]
                        (bit.= (/.> left right)
                               (/.< right left)))
                      (_.coverage [/.<= /.>=]
                        (bit.= (/.<= left right)
                               (/.>= right left)))
                      ))

               ..unit
               ))))
