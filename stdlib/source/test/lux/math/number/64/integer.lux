... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]
    ["[0]" hash
     ["[1]T" \\test]]
    ["[0]" enum
     ["[1]T" \\test]]
    ["[0]" interval
     ["[1]T" \\test]]
    ["[0]" format
     ["[1]T" \\test]]
    ["[0]" equality
     ["[1]T" \\test]]
    ["[0]" order
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]]
   [data
    [collection
     ["[0]" list (.use "[1]:[0]" mix)]]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random)]]
   [macro
    ["[0]" template]]
   [meta
    ["[0]" static]]
   [test
    ["_" property (.only Test)]]]]
 ["!" \\theorem]
 ["/" \\library (.only)
  [//
   ["n" natural]
   ["d" decimal]
   [//
    ["[0]" /64]]]])

(the signature
  Test
  (`` (all _.and
           (_.for [/.equality /.=]
                  (static.when (identical? /.equality /.=)
                    (equalityT.spec /.equality random.integer)))
           (_.for [/.hash]
                  (hashT.spec /.hash random.integer))
           (_.for [/.order /.<]
                  (orderT.spec /.order random.integer))
           (_.for [/.enum]
                  (enumT.spec /.enum random.integer))
           (_.for [/.interval]
                  (intervalT.spec /.interval random.integer))
           (,, (template.with [<composite> <monoid>]
                 [(_.for [<monoid> <composite>]
                         (static.when (identical? <composite> (by <monoid> composite))
                           (monoidT.spec /.equality <monoid> random.integer)))]
                 
                 [[/.+ /.addition]
                  [/.x /.multiplication]

                  [/.minor /.minimum]
                  [/.major /.maximum]]))
           (,, (template.with [<format>]
                 [(_.for [<format>]
                         (formatT.spec /.equality <format> random.integer))]

                 [[/.base_02]
                  [/.base_08]
                  [/.base_10]
                  [/.base_16]]))
           )))

(the predicate
  Test
  (monad.let [! random.monad]
    [sample random.integer]
    (all _.and
         (_.coverage [/.negative?]
           (bit.= (/.negative? sample)
                  (/.< +0 sample)))
         (_.coverage [/.positive?]
           (bit.= (/.positive? sample)
                  (/.> +0 sample)))
         (_.coverage [/.zero?]
           (bit.= (/.zero? sample)
                  (/.= +0 sample)))
         (_.coverage [/.even? /.odd?]
           (bit.= (/.even? sample)
                  (not (/.odd? sample))))
         )))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [.Integer /.Number])
      (let [(open "/:[0]") /.interval])
      (monad.let [! random.monad]
        [expected random.integer
         origin random.integer])
      (all _.and
           (_.coverage [/.Number .Int .Z]
             (and (identical? /.Number .Integer)
                  (identical? /.Number .Int)
                  (identical? /.Number .Z)))
           (monad.let [! random.monad]
             [sample random.integer
              left random.integer
              right random.integer
              power (by ! each (n.% 10)
                        random.natural)]
             (all _.and
                  (_.coverage [/.zero]
                    (and (/.= left (/.+ /.zero left))
                         (/.= left (/.- /.zero left))
                         (/.= /.zero (/.x /.zero left))))
                  (_.coverage [/.positive_one]
                    (and (/.= left (/.x /.positive_one left))
                         (/.= /.positive_one (/./ left left))))
                  (_.coverage [/.negative_one]
                    (and (/.= /.negative_one (/./ (/.opposite left) left))
                         (/.= /.negative_one (/./ left (/.opposite left)))
                         (/.= /.positive_one (/.x /.negative_one /.negative_one))))
                  (_.coverage [.i64_+#]
                    (and (exec
                           !.identity_of_i64_+
                           (.i64_=# (.i64_+# +0 expected)
                                    expected))
                         (exec
                           !.commutativity_of_i64_+
                           (.i64_=# (.i64_+# left right)
                                    (.i64_+# right left)))
                         (exec
                           !.associativity_of_i64_+
                           (.i64_=# (.i64_+# left (.i64_+# right sample))
                                    (.i64_+# (.i64_+# left right) sample)))))
                  (_.by_proof [/.+]
                              [!.identity_of_+
                               !.commutativity_of_+
                               !.associativity_of_+
                               !.inversion_of_-])
                  (_.coverage [.int_x#]
                    (and (exec
                           !.identity_of_int_x
                           (.i64_=# (.int_x# +1 expected)
                                    expected))
                         (exec
                           !.commutativity_of_int_x
                           (.i64_=# (.int_x# left right)
                                    (.int_x# right left)))
                         (exec
                           !.associativity_of_int_x
                           (.i64_=# (.int_x# left (.int_x# right sample))
                                    (.int_x# (.int_x# left right) sample)))
                         (exec
                           !.annihilation_of_int_x
                           (.i64_=# (.int_x# +0 expected)
                                    +0))
                         (exec
                           !.opposite_by_int_x
                           (.i64_=# (!.- expected)
                                    (.int_x# -1 expected)))
                         (exec
                           !.distributivity_of_int_x
                           (.i64_=# (.i64_+# (.int_x# expected left) (.int_x# expected right))
                                    (.int_x# expected (.i64_+# left right))))))
                  (_.by_proof [/.x]
                              [!.identity_of_x
                               !.commutativity_of_x
                               !.associativity_of_x
                               !.annihilation_of_x
                               !.opposite_by_x
                               !.distributivity_of_x])
                  (_.coverage [.i64_-#]
                    (and (exec
                           !.i64_+_opposite
                           (.i64_=# (.i64_+# (!.- left) right)
                                    (.i64_-# left right)))))
                  (_.by_proof [/.-]
                              [!.+_opposite
                               !.associativity_of_-
                               !.identity_of_-
                               !.commutativity_of_+_-
                               !.consecutive_-
                               !.commutativity_of_-
                               !.anti_associativity_of_-
                               !.annihilation_by_-
                               !.inversion_of_+
                               !.anti_commutativity_of_-])
                  (_.by_proof [/.opposite]
                              [!.opposite_of_zero
                               !.distributivity_of_opposite
                               !.-_opposite
                               !.double_opposite])
                  (_.by_proof [/.reflection]
                              [!.reflection_of_self
                               !.opposite_is_special_reflection
                               !.double_reflection
                               !.difference_of_reflection])
                  (_.coverage [/./]
                    (and (/.= +1 (/./ sample sample))
                         (/.= sample (/./ +1 sample))
                         (/.= /:minimum
                              (/./ -1 /:minimum))))
                  (_.coverage [/.absolute]
                    (bit.= (/.> sample (/.absolute sample))
                           (/.negative? sample)))
                  (_.coverage [/.signum]
                    (/.= (/.absolute sample)
                         (/.x (/.signum sample) sample)))
                  (_.coverage [/.^]
                    (and (/.= +1 (/.^ 0 sample))
                         (/.= sample (/.^ 1 sample))
                         (/.= (list:mix /.x +1 (list.repeated power sample))
                              (/.^ power sample))))
                  ))
           (monad.let random.monad
             [left random.integer
              right random.integer]
             (all _.and
                  (_.coverage [/.>]
                    (bit.= (/.> left right)
                           (/.< right left)))
                  (_.coverage [/.<= /.>=]
                    (bit.= (/.<= left right)
                           (/.>= right left)))
                  ))
           (monad.let random.monad
             [left (random.only (|>> (/.= +0) not)
                                random.integer)
              right random.integer]
             (all _.and
                  (_.coverage [/.%]
                    (let [rem (/.% left right)
                          div (|> right (/.- rem) (/./ left))]
                      (/.= right
                           (|> div (/.x left) (/.+ rem)))))
                  (_.coverage [/./%]
                    (let [[div rem] (/./% left right)]
                      (and (/.= div (/./ left right))
                           (/.= rem (/.% left right)))))
                  (_.coverage [/.modulo]
                    (and (/.= (/.signum left)
                              (/.signum (/.modulo left right)))
                         (/.= (/.signum right)
                              (/.signum (/.% left right)))
                         (if (/.= (/.signum left) (/.signum right))
                             (/.= (/.% left right)
                                  (/.modulo left right))
                             (or (and (/.= +0 (/.% left right))
                                      (/.= +0 (/.modulo left right)))
                                 (/.= (/.+ left (/.% left right))
                                      (/.modulo left right))))))
                  ))
           (monad.let [! random.monad]
             [.let [random (|> random.integer
                               (by ! each (|>> (/.% +1,000) /.absolute))
                               (random.only (/.> +0)))]
              left random
              right random]
             (all _.and
                  (_.coverage [/.lcm /.least_common_multiple]
                    (and (let [lcm (/.lcm left right)]
                           (and (/.= +0 (/.% left lcm))
                                (/.= +0 (/.% right lcm))))
                         (identical? /.lcm /.least_common_multiple)))
                  (_.coverage [/.gcd /.greatest_common_divisor]
                    (and (let [gcd (/.gcd left right)]
                           (and (/.= +0 (/.% gcd left))
                                (/.= +0 (/.% gcd right))))
                         (identical? /.gcd /.greatest_common_divisor)))
                  (_.coverage [/.extended_greatest_common_divisor]
                    (let [[[left_k right_k] gcd] (/.extended_greatest_common_divisor left right)

                          same_gcd!
                          (/.= gcd
                               (/.gcd left right))
                          
                          bezout_identity!
                          (/.= gcd
                               (/.+ (/.x left_k left)
                                    (/.x right_k right)))]
                      (and same_gcd!
                           bezout_identity!)))
                  (_.coverage [/.co_prime?]
                    (bit.= (/.= +1 (/.gcd left right))
                           (/.co_prime? left right)))
                  (_.coverage [/.multiple?]
                    (and (/.multiple? left
                                      (/.least_common_multiple left right))
                         (/.multiple? right
                                      (/.least_common_multiple left right))))
                  (_.coverage [/.divisor?]
                    (and (/.divisor? left
                                     (/.greatest_common_divisor left right))
                         (/.divisor? right
                                     (/.greatest_common_divisor left right))))
                  ))
           (monad.let random.monad
             [expected random.integer]
             (_.coverage [/.opposite]
               (let [subtraction!
                     (/.= +0 (/.+ (/.opposite expected) expected))

                     inverse!
                     (|> expected /.opposite /.opposite (/.= expected))]
                 (and subtraction!
                      inverse!))))
           (monad.let [! random.monad]
             [expected (by ! each (/.% +1,000,000) random.integer)
              sample random.integer]
             (_.coverage [/.decimal]
               (and (|> expected /.decimal d.integer (/.= expected))
                    (d.number? (/.decimal sample)))))
           (monad.let [! random.monad]
             [pattern random.integer
              idx (by ! each (n.% /64.width) random.natural)]
             (_.coverage [/.>>]
               (let [nullity!
                     (/.= pattern (/.>> 0 pattern))

                     idempotency!
                     (/.= pattern (/.>> /64.width pattern))

                     sign_mask (/64.<< (-- /64.width) 1)
                     mantissa_mask (-- (/64.<< (n.- idx /64.width) 1))
                     co_mantissa_mask (/64.not mantissa_mask)

                     sign_preservation!
                     (/.= (/64.and sign_mask pattern)
                          (/64.and sign_mask (/.>> idx pattern)))

                     mantissa_parity!
                     (/.= (/64.and mantissa_mask (/64.>> idx pattern))
                          (/64.and mantissa_mask (/.>> idx pattern)))

                     co_mantissa_disparity!
                     (or (n.= 0 idx)
                         (and (/.= +0 (/64.and co_mantissa_mask (/64.>> idx pattern)))
                              (/.= (if (/.< +0 pattern)
                                       (.integer co_mantissa_mask)
                                       +0)
                                   (/64.and co_mantissa_mask (/.>> idx pattern)))))]
                 (and nullity!
                      idempotency!
                      sign_preservation!
                      mantissa_parity!
                      co_mantissa_disparity!
                      ))))
           (_.coverage [/.reflection]
             (and (/.= (/.opposite expected)
                       (/.reflection /.zero expected))
                  (/.= expected
                       (/.reflection expected expected))
                  (|> expected
                      (/.reflection origin)
                      (/.reflection origin)
                      (/.= expected))
                  (/.= (/.+ expected expected)
                       (/.reflection expected /.zero))))
           (_.coverage [/.infinity]
             (and (and (/.= /.infinity (/.opposite /.infinity))
                       (not (/.= /.zero /.infinity)))
                  (and (/.= /.zero (/.+ /.infinity /.infinity))
                       (|> expected
                           (/.+ /.infinity)
                           (/.+ /.infinity)
                           (/.= expected)))
                  (/.= (/.reflection /.zero expected)
                       (/.reflection /.infinity expected))))

           ..predicate
           ..signature
           )))
