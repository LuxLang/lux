... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" hash
     ["[1]T" \\test]]
    ["[0]" enum
     ["[1]T" \\test]]
    ["[0]" interval
     ["[1]T" \\test]]
    ["[0]" codec
     ["[1]T" \\test]]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" order
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text]]
   [math
    ["[0]" random]
    ["[0]" arithmetic
     ["[1]T" \\test]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["f" frac]]]])

(the signature
  Test
  (`` (all _.and
           (_.for [/.equivalence /.=]
                  (equivalenceT.spec /.equivalence random.nat))
           (_.for [/.hash]
                  (hashT.spec /.hash random.nat))
           (_.for [/.order /.<]
                  (orderT.spec /.order random.nat))
           (_.for [/.enum]
                  (enumT.spec /.enum random.nat))
           (_.for [/.interval]
                  (intervalT.spec /.interval random.nat))
           (,, (with_template [<composite> <monoid>]
                 [(_.for [<monoid> <composite>]
                         (monoidT.spec /.equivalence <monoid> random.nat))]
                 
                 [/.+ /.addition]
                 [/.* /.multiplication]

                 [/.min /.minimum]
                 [/.max /.maximum]
                 ))
           (,, (with_template [<codec>]
                 [(do [! random.monad]
                    [.let [limit (|> (of /.interval top)
                                     (of <codec> encoded)
                                     text.size
                                     ++)]
                     expected_size (of ! each (/.% limit) random.nat)]
                    (_.for [<codec> /.padded]
                           (all _.and
                                (codecT.spec /.equivalence <codec> random.nat)
                                (codecT.spec /.equivalence (/.padded expected_size <codec>) random.nat)
                                )))]

                 [/.binary] [/.octal] [/.decimal] [/.hex]
                 ))
           (_.coverage [/.base_02]
             (same? /.binary /.base_02))
           (_.coverage [/.base_08]
             (same? /.octal /.base_08))
           (_.coverage [/.base_10]
             (same? /.decimal /.base_10))
           (_.coverage [/.base_16]
             (same? /.hex /.base_16))
           (_.for [/.arithmetic]
                  (arithmeticT.spec /.equivalence /.arithmetic random.nat))
           )))

(the predicate
  Test
  (do [! random.monad]
    [sample random.nat]
    (all _.and
         (_.coverage [/.even? /.odd?]
           (bit#= (/.even? sample)
                  (not (/.odd? sample))))
         )))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [.Nat])
      (all _.and
           (do random.monad
             [sample random.nat]
             (all _.and
                  (_.coverage [/.-]
                    (and (/.= 0 (/.- sample sample))
                         (/.= sample (/.- 0 sample))))
                  (_.coverage [/./]
                    (and (/.= 1 (/./ sample sample))
                         (/.= sample (/./ 1 sample))))
                  ))
           (do random.monad
             [left random.nat
              right random.nat]
             (all _.and
                  (_.coverage [/.>]
                    (bit#= (/.> left right)
                           (/.< right left)))
                  (_.coverage [/.<= /.>=]
                    (bit#= (/.<= left right)
                           (/.>= right left)))
                  ))
           (do random.monad
             [left (random.only (|>> (/.= 0) not)
                                random.nat)
              right random.nat]
             (all _.and
                  (_.coverage [/.%]
                    (let [rem (/.% left right)
                          div (|> right (/.- rem) (/./ left))]
                      (/.= right
                           (|> div (/.* left) (/.+ rem)))))
                  (_.coverage [/./%]
                    (let [[div rem] (/./% left right)]
                      (and (/.= div (/./ left right))
                           (/.= rem (/.% left right)))))
                  ))
           (do [! random.monad]
             [.let [random (of ! each (|>> (/.% 1,000) ++) random.nat)]
              left random
              right random]
             (all _.and
                  (_.coverage [/.gcd]
                    (let [gcd (/.gcd left right)]
                      (and (/.= 0 (/.% gcd left))
                           (/.= 0 (/.% gcd right)))))
                  (_.coverage [/.co_prime?]
                    (bit#= (/.= 1 (/.gcd left right))
                           (/.co_prime? left right)))
                  (_.coverage [/.lcm]
                    (let [lcm (/.lcm left right)]
                      (and (/.= 0 (/.% left lcm))
                           (/.= 0 (/.% right lcm)))))
                  ))
           (do [! random.monad]
             [expected (of ! each (/.% 1,000,000) random.nat)
              sample random.nat]
             (_.coverage [/.frac]
               (and (|> expected /.frac f.nat (/.= expected))
                    (f.number? (/.frac sample)))))

           ..predicate
           ..signature
           )))
