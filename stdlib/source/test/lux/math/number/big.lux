... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]
    ["[0]" equality
     ["[1]T" \\test]]]
   [function
    ["[0]" predicate]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["[0]" /64 (.only)
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]
   ["[0]" type
    ["[1]" check]]
   [meta
    ["[0]" static]]
   [logic
    ["[0]" bit]]
   [data
    [collection
     ["[0]" list]]]]]
 [\\library
  ["[0]" /]]
 ["[0]" /
  ["[1][0]" natural]
  ["[1][0]" integer]
  ["[1][0]" fraction]
  ["[1][0]" rational]])

(the (size minimum maximum)
  (-> Natural Natural
      (Random Natural))
  (|> random.natural
      (by random.monad each
          (|>> (n.% (++ maximum))
               (n.major minimum)))))

(the .public random
  (Random /.Number)
  (monad.let [! random.monad]
    [size (by ! each (n.% 1,000) random.natural)]
    (when size
      0 (pure /.zero)
      _ (monad.let [! random.monad]
          [idx (|> random.natural (by ! each (n.% size)))]
          (pure (/.with_one idx /.zero))))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Number])
      (all _.and
           (_.for [/.equality /.=]
                  (static.when (identical? /.equality /.=)
                    (equalityT.spec /.equality ..random)))

           (monad.let [! random.monad]
             [size (by ! each (|>> (n.% 1,000) ++) random.natural)
              idx (by ! each (n.% size) random.natural)
              sample ..random
              left ..random
              right ..random]
             (all _.and
                  (_.coverage [/.Digit]
                    (type.< .Natural /.Digit))
                  (<| (_.for [/.Address])
                      (all _.and
                           (_.coverage [/.bit /.with_one]
                             (and (|> /.zero
                                      (/.bit idx)
                                      not)
                                  (|> /.zero
                                      (/.with_one idx)
                                      (/.bit idx))))
                           (_.coverage [/.with_zero]
                             (|> /.zero
                                 (/.with_one idx)
                                 (/.with_zero idx)
                                 (/.bit idx)
                                 not))
                           (_.coverage [/.with_flipped]
                             (and (|> /.zero (/.with_flipped idx) (/.bit idx))
                                  (|> /.zero (/.with_flipped idx) (/.with_flipped idx) (/.bit idx) not)))
                           ))
                  (_.coverage [/.not]
                    (or (identical? /.zero sample)
                        (and (not (by /.equality = sample (/.not sample)))
                             (by /.equality = sample (/.not (/.not sample))))))
                  (_.coverage [/.xor]
                    (with /.equality
                      (and (= /.zero
                              (/.xor sample sample))
                           (= (/.xor left right)
                              (/.xor right left)))))
                  (_.coverage [/.or]
                    (and (by /.equality = sample (/.or sample sample))
                         (by /.equality =
                             (/.or left right)
                             (/.or right left))))
                  (_.coverage [/.and]
                    (and (by /.equality = sample (/.and sample sample))
                         (by /.equality =
                             (/.and left right)
                             (/.and right left))))
                  (_.coverage [/.zero]
                    (with /.equality
                      (and (= /.zero (/.not /.zero))
                           (= sample (/.xor /.zero sample))
                           (= sample (/.or /.zero sample))
                           (= /.zero (/.and /.zero sample)))))
                  (monad.let !
                    [big_digits (by ! each (n.% 10) random.natural)
                     binary_digits (by ! each (n.% /64.width) random.natural)
                     .let [shift (all n.+
                                      (n.x /64.width big_digits)
                                      binary_digits
                                      )]]
                    (_.coverage [/.<< /.>>]
                      (let [nullity!
                            (and (/.= sample (/.<< 0 sample))
                                 (/.= sample (/.>> 0 sample)))

                            after (|> sample
                                      (/.<< shift)
                                      (/.>> shift))
                            
                            symmetry!
                            (/.= sample
                                 after)]
                        (and nullity!
                             symmetry!))))

                  /natural.test
                  /integer.test
                  /fraction.test
                  /rational.test
                  )))))
