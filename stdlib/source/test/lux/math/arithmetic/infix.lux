... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]
      ["d" decimal]]]
    [geometry
     ["[0]" circle]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public test
  Test
  (<| (_.covering /._)
      (monad.let random.monad
        [subject random.natural
         parameter random.natural
         extra random.natural
         angle random.unit_decimal
         factor random.natural]
        (_.coverage [/.infix]
          (let [constant_values!
                (n.= subject
                     (/.infix subject))

                unary_functions!
                (d.= (circle.sin angle)
                     (/.infix [circle.sin angle]))

                binary_functions!
                (n.= (n.gcd parameter subject)
                     (/.infix [subject n.gcd parameter]))

                multiple_binary_functions!
                (n.= (n.x factor (n.gcd parameter subject))
                     (/.infix [subject n.gcd parameter n.x factor]))

                function_call!
                (n.= (n.gcd extra (n.x parameter subject))
                     (/.infix [(n.x parameter subject) n.gcd extra]))

                non_numeric!
                (bit.= (and (n.< parameter subject) (n.< extra parameter))
                       (/.infix [[subject n.< parameter] and [parameter n.< extra]]))]
            (and constant_values!
                 unary_functions!
                 binary_functions!
                 multiple_binary_functions!
                 function_call!
                 non_numeric!
                 ))))))
