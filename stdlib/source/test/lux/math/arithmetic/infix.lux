... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]]
   [math
    ["[0]" random]
    [number
     ["n" nat]
     ["f" frac]]
    [geometry
     [trigonometry
      ["[0]" plane]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public test
  Test
  (<| (_.covering /._)
      (do random.monad
        [subject random.nat
         parameter random.nat
         extra random.nat
         angle random.safe_frac
         factor random.nat]
        (_.coverage [/.infix]
          (let [constant_values!
                (n.= subject
                     (/.infix subject))

                unary_functions!
                (f.= (plane.sin angle)
                     (/.infix [plane.sin angle]))

                binary_functions!
                (n.= (n.gcd parameter subject)
                     (/.infix [subject n.gcd parameter]))

                multiple_binary_functions!
                (n.= (n.* factor (n.gcd parameter subject))
                     (/.infix [subject n.gcd parameter n.* factor]))

                function_call!
                (n.= (n.gcd extra (n.* parameter subject))
                     (/.infix [(n.* parameter subject) n.gcd extra]))

                non_numeric!
                (bit#= (and (n.< parameter subject) (n.< extra parameter))
                       (/.infix [[subject n.< parameter] and [parameter n.< extra]]))]
            (and constant_values!
                 unary_functions!
                 binary_functions!
                 multiple_binary_functions!
                 function_call!
                 non_numeric!
                 ))))))
