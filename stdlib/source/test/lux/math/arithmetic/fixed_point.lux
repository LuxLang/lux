... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" order
     ["[1]T" \\test]]]
   [control
    ["[0]" try (.use "[1]#[0]" functor)]
    ["[0]" exception]]
   [data
    ["[0]" bit]
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]
     ["i" integer]]
    ["[0]" arithmetic
     ["[1]T" \\test]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public (random @)
  (for_any (_ @)
    (-> (/.Point @)
        (Random (/.Fixed @))))
  (do [! random.monad]
    [units (of ! each (i.% +1,000) random.integer)
     sub_units random.rev]
    (in (/.fixed @ units sub_units))))

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [candidate_point random.natural
         expected_point (of ! each (n.% (++ /.maximum)) random.natural)
         .let [@ (try.trusted (/.point expected_point))]
         expected (..random @)
         parameter (..random @)
         subject (..random @)])
      (all _.and
           (<| (_.for [/.Point])
               (all _.and
                    (_.coverage [/.point /.location]
                      (|> (/.point expected_point)
                          (try#each (|>> /.location
                                         (same? expected_point)))
                          (try.else false)))
                    (_.coverage [/.maximum /.point_exceeds_maximum]
                      (when (/.point candidate_point)
                        {try.#Success it}
                        (n.<= /.maximum candidate_point)
                        
                        {try.#Failure error}
                        (and (n.> /.maximum candidate_point)
                             (exception.is? /.point_exceeds_maximum error))))
                    ))
           (<| (_.for [/.Fixed])
               (all _.and
                    (_.for [/.equivalence /.=]
                           (equivalenceT.spec (/.equivalence @) (..random @)))
                    (_.for [/.order /.<]
                           (orderT.spec (/.order @) (..random @)))
                    ... (_.for [/.arithmetic]
                    ...        (arithmeticT.spec (/.equivalence @) (/.arithmetic @) (..random @)))
                    
                    (_.coverage [/.fixed /.units /.sub_units]
                      (/.= @
                           expected
                           (/.fixed @ (/.units @ expected) (/.sub_units @ expected))))
                    (_.coverage [/.of_integer /.of_rev]
                      (/.= @
                           expected
                           (/.+ @
                                (/.of_integer @ (/.units @ expected))
                                (/.of_rev @ (/.sub_units @ expected)))))

                    (_.coverage [/.>]
                      (bit.= (/.> @ parameter subject)
                             (/.< @ subject parameter)))
                    (_.coverage [/.<= /.>=]
                      (bit.= (/.<= @ parameter subject)
                             (/.>= @ subject parameter)))
                    
                    (_.coverage [/.-]
                      (and (/.= @
                                (/.of_integer @ +0)
                                (/.- @ expected expected))
                           (/.= @
                                (/.of_rev @ .0)
                                (/.- @ expected expected))))
                    (_.coverage [/.+]
                      (|> subject
                          (/.+ @ parameter)
                          (/.- @ parameter)
                          (/.= @ subject)))
                    (_.coverage [/.*]
                      (and (|> subject
                               (/.* @ (/.of_integer @ +1))
                               (/.= @ subject))
                           (|> subject
                               (/.* @ (/.of_integer @ +0))
                               (/.= @ (/.of_integer @ +0)))
                           (/.= @
                                (/.* @ parameter subject)
                                (/.* @ subject parameter))))
                    (_.coverage [/./]
                      (/.= @
                           (/.of_integer @ +1)
                           (/./ @ expected expected)))

                    (_.coverage [/.injection]
                      (let [it (/.injection @ expected)]
                        (and (text.contains? (%.integer (/.units @ expected))
                                             it)
                             (text.contains? (%.rev (/.sub_units @ expected))
                                             it))))
                    ))
           )))
