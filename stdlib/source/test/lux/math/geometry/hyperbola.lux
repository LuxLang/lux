... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [math
    ["[0]" random]
    [number
     ["d" dec]]
    [geometry
     ["[0]" circle]]]
   [meta
    [macro
     ["[0]" template]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the margin_of_error
  +0.0000001)

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [.let [~= (d.approximately? ..margin_of_error)]
         angle (of ! each (d.* circle.tau) random.safe_dec)
         sample (of ! each d.abs random.safe_dec)
         big (of ! each (d.* +1,000,000,000.00) random.safe_dec)]
        (template.let [(odd! <function>)
                       [(_.coverage [<function>]
                          (~= (d.opposite (<function> angle))
                              (<function> (d.opposite angle))))]

                       (even! <function>)
                       [(_.coverage [<function>]
                          (~= (<function> angle)
                              (<function> (d.opposite angle))))]

                       (inverse! <left> <right> <input>)
                       [(_.coverage [<left> <right>]
                          (~= (<right> <input>)
                              (<left> (d./ <input> +1.0))))]]
          (all _.and
               (odd! /.sine)
               (even! /.co_sine)
               (odd! /.tangent)
               (odd! /.co_tangent)
               (even! /.secant)
               (odd! /.co_secant)

               (inverse! /.arc_co_sine /.arc_secant sample)
               (inverse! /.arc_sine /.arc_co_secant sample)
               (inverse! /.arc_tangent /.arc_co_tangent big)
               )))))
