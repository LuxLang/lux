... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [math
    ["[0]" random]
    [number
     [/64
      ["d" decimal]]]
    [geometry
     ["[0]" circle]]]
   [macro
    ["[0]" template]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the margin_of_error
  +0.0000001)

(the .public test
  Test
  (<| (_.covering /._)
      (monad.let [! random.monad]
        [.let [~= (d.approximately? ..margin_of_error)]
         angle (by ! each (d.x circle.tau) random.unit_decimal)
         sample (by ! each d.absolute random.unit_decimal)
         big (by ! each (d.x +1,000,000,000.00) random.unit_decimal)]
        (template.let [(odd! ,function ,alias)
                       [(_.coverage [,function
                                     ,alias]
                          (and (~= (d.opposite (,function angle))
                                   (,function (d.opposite angle)))
                               (identical? ,alias ,function)))]

                       (even! ,function ,alias)
                       [(_.coverage [,function
                                     ,alias]
                          (and (~= (,function angle)
                                   (,function (d.opposite angle)))
                               (identical? ,alias ,function)))]

                       (inverse! ,left ,right ,input ,left_alias ,right_alias)
                       [(_.coverage [,left ,right
                                     ,left_alias ,right_alias]
                          (and (~= (,right ,input)
                                   (,left (d./ ,input +1.0)))
                               (identical? ,left ,left_alias)
                               (identical? ,right ,right_alias)))]]
          (all _.and
               (odd! /.sine /.sin)
               (even! /.co_sine /.cos)
               (odd! /.tangent /.tan)
               (odd! /.co_tangent /.cot)
               (even! /.secant /.sec)
               (odd! /.co_secant /.csc)

               (inverse! /.arc_co_sine /.arc_secant sample /.acos /.asec)
               (inverse! /.arc_sine /.arc_co_secant sample /.asin /.acsc)
               (inverse! /.arc_tangent /.arc_co_tangent big /.atan /.acot)
               )))))
