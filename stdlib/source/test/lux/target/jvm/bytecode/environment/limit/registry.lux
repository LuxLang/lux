... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]
    ["[0]" equality
     ["[1]T" \\test]]]
   [data
    ["[0]" binary (.only)
     ["![1]" \\injection]]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random]
    [number
     ["[0]" /08
      ["[1]" natural]]
     [/64
      ["n" natural]]]]
   [meta
    ["[0]" static]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [/////
    ["[0]" type (.only)
     ["[1]T" \\test]]]]])

(the .public test
  Test
  (<| (_.covering /._)
      (monad.let [! random.monad]
        [input_0 typeT.random
         input_1 typeT.random
         output_0 typeT.random
         register (by ! each (n.% 256) random.natural)])
      (_.for [/.Registry
              /.registry
              
              /.random])
      (all _.and
           (_.for [/.equality /.=]
                  (static.when (identical? /.equality /.=)
                    (equalityT.spec /.equality /.random)))
           
           (_.coverage [/.static /.virtual]
             (let [method (type.method [(list) (list input_0 input_1) output_0 (list)])
                   static (/.static method)
                   virtual (/.virtual method)]
               (not (/.= static virtual))))
           (_.coverage [/.has]
             (let [method (type.method [(list) (list input_0 input_1) output_0 (list)])
                   static (/.static method)
                   virtual (/.virtual method)]
               (identical? virtual (/.has static virtual))))
           (monad.let [! random.monad]
             [sample_0 /.random
              sample_1 /.random]
             (_.coverage [/.as_binary]
               (bit.= (/.= sample_0
                           sample_1)
                      (binary.= (!binary.value /.as_binary sample_0)
                                (!binary.value /.as_binary sample_1)))))
           (monad.let [! random.monad]
             [sample_0 /.random]
             (_.coverage [/.length]
               (n.= /.length
                    (binary.size (!binary.value /.as_binary sample_0)))))
           (<| (_.for [/.Register])
               (all _.and
                    (_.coverage [/.for /.for_wide]
                      (let [register (/08.of register)
                            slim_register (/.for register)
                            wide_register (/.for_wide register)]
                        (identical? wide_register (/.has slim_register wide_register))))
                    ))
           )))
