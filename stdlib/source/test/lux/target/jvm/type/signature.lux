... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" hash
     ["[1]T" \\test]]]
   [function
    ["?" predicate]]
   [data
    ["[0]" text]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor)]
     ["[0]" set]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [macro
    ["[0]" expansion]]
   [meta
    ["[0]" static]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" category]
    ["[0]" box]]]])

(<| (expansion.let [,*primitives (these /.boolean
                                        /.byte
                                        /.short
                                        /.int
                                        /.long
                                        /.float
                                        /.double
                                        /.char)
                    ,*simple (these /.void
                                    ,*primitives)
                    ,*boxes (these box.boolean
                                   box.byte
                                   box.short
                                   box.int
                                   box.long
                                   box.float
                                   box.double
                                   box.char)])
    (these (the .public random
             (Random (/.Signature category.Value))
             (let [primitives (is (Stack (/.Signature category.Primitive))
                                  (stack ,*primitives))
                   primitives_amount (stack.size primitives)

                   boxes (stack ,*boxes)
                   boxes_amount (stack.size boxes)]
               (all random.either
                    (do [! random.monad]
                      [choice (by ! each (n.% primitives_amount) random.natural)]
                      (random.try (in (stack.item choice primitives))))
                    (do [! random.monad]
                      [choice (by ! each (n.% boxes_amount) random.natural)
                       choice (random.try (in (stack.item choice boxes)))]
                      (in (/.class (stack) choice)))))))
    (the .public test
      Test
      (<| (_.covering /._)
          (do [! random.monad]
            [argument_0 ..random
             argument_1 ..random
             name (random.lower_cased 2)
             return (is (Random (/.Signature category.Return))
                        (all random.either
                             (in /.void)
                             ..random))])
          (_.for [/.Signature])
          (all _.and
               (_.for [/.equivalence /.=]
                      (static.when (same? /.equivalence /.=)
                        (equivalenceT.spec /.equivalence ..random)))
               (_.for [/.hash]
                      (hashT.spec /.hash ..random))

               (_.coverage [,*simple]
                 (let [options (is (Stack (/.Signature category.Return))
                                   (stack ,*simple))
                       uniques (set.of_stack /.hash options)]
                   (n.= (stack.size options)
                        (set.size uniques))))
               (_.coverage [/.var /.var_name]
                 (text.= name
                         (/.var_name (/.var name))))
               (_.coverage [/.wildcard /.signature]
                 (let [options (<| (stack#each /.signature)
                                   (is (Stack (/.Signature category.Return)))
                                   (stack ,*simple
                                          /.wildcard))
                       uniques (set.of_stack text.hash options)]
                   (n.= (stack.size options)
                        (set.size uniques))))
               
               (_.coverage [/.class]
                 (let [options (is (Stack (/.Signature category.Class))
                                   (stack#each (/.class (stack)) (stack ,*boxes)))
                       uniques (set.of_stack /.hash options)]
                   (n.= (stack.size options)
                        (set.size uniques))))
               (_.coverage [/.declaration /.as_class]
                 (let [expected (is (Stack (/.Signature category.Class))
                                    (stack#each (|>> (/.declaration (stack)) /.as_class) (stack ,*boxes)))
                       actual (is (Stack (/.Signature category.Class))
                                  (stack#each (/.class (stack)) (stack ,*boxes)))]
                   (by (stack.equivalence /.equivalence) =
                       expected
                       actual)))
               (_.coverage [/.lower]
                 (let [options (is (Stack (/.Signature category.Parameter))
                                   (stack#each (|>> (/.class (stack)) /.lower) (stack ,*boxes)))
                       uniques (set.of_stack /.hash options)]
                   (n.= (stack.size options)
                        (set.size uniques))))
               (_.coverage [/.upper]
                 (let [options (is (Stack (/.Signature category.Parameter))
                                   (stack#each (|>> (/.class (stack)) /.upper) (stack ,*boxes)))
                       uniques (set.of_stack /.hash options)]
                   (n.= (stack.size options)
                        (set.size uniques))))
               (_.coverage [/.array]
                 (and (let [options (is (Stack (/.Signature category.Array))
                                        (stack#each (|>> (/.class (stack)) /.array)
                                                    (stack ,*boxes)))
                            uniques (set.of_stack /.hash options)]
                        (n.= (stack.size options)
                             (set.size uniques)))
                      (let [single (stack#each (|>> (/.class (stack)) /.array) (stack ,*boxes))
                            double (stack#each /.array single)]
                        (not (by (stack.equivalence /.equivalence) =
                                 single
                                 double)))))
               
               (_.coverage [/.lower_prefix]
                 (text.starts_with? /.lower_prefix
                                    (/.signature (/.lower (/.class (stack) box.boolean)))))
               (_.coverage [/.upper_prefix]
                 (text.starts_with? /.upper_prefix
                                    (/.signature (/.upper (/.class (stack) box.boolean)))))
               (_.coverage [/.var_prefix]
                 (text.starts_with? /.var_prefix
                                    (/.signature (/.var name))))
               ))))
