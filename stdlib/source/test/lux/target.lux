... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [math
    ["[0]" random]]
   [test
    ["_" property (.only Test)]]
   [data
    ["[0]" text]
    [collection
     ["[0]" list]
     ["[0]" set]]]
   [math
    [number
     [/64
      ["n" natural]]]]]]
 [\\library
  ["[0]" /]])

(.`` (.`` (.using
           ["[0]" /
            (.,, (/.for /.jvm (.,, (.these ["[1][0]" jvm]))
                        /.js (.,, (.these ["[1][0]" js]))
                        /.lua (.,, (.these ["[1][0]" lua]))
                        /.python (.,, (.these ["[1][0]" python]))
                        /.ruby (.,, (.these ["[1][0]" ruby]))
                        (.,, (.these))))])))

(the .public test
  Test
  (<| (_.covering /._)
      (monad.let [! random.monad]
        [])
      (_.for [/.Target])
      (`` (all _.and
               (_.coverage [/.for]
                 (and (/.for "fake host"
                             false

                             ... else
                             true)
                      (/.for /.jvm true
                             /.js true
                             /.python true
                             /.lua true
                             /.ruby true
                             /.php true

                             ... else
                             false)))
               
               (_.coverage [/.c++ /.common_lisp /.js
                            /.jvm /.lua /.php
                            /.python /.r /.ruby]
                 (let [options (is (List /.Target)
                                   (list /.c++ /.common_lisp /.js
                                         /.jvm /.lua /.php
                                         /.python /.r /.ruby))
                       uniques (set.of_list text.hash options)]
                   (n.= (list.size options)
                        (set.size uniques))))
               
               (,, (/.for /.jvm /jvm.test
                          /.js /js.test
                          /.lua /lua.test
                          /.python /python.test
                          /.ruby /ruby.test))
               ))))
