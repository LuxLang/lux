(.using
 [lux (.except)
  ["_" test (.only Test)]
  [abstract
   ["[0]" monad (.only do)]]
  [math
   ["[0]" random (.only Random)]]
  [control
   ["[0]" try]]
  [data
   ["[0]" product]
   ["[0]" text
    ["%" format (.only format)]]
   [number
    ["n" nat]]
   [collection
    ["[0]" list ("[1]#[0]" functor mix)]
    ["[0]" dictionary (.only Dictionary)]]]
  [macro
   ["^" pattern]]]
 [\\
  ["[0]" /
   [////
    ["[0]" analysis]
    ["[0]" synthesis (.only Side Member Path Synthesis)]
    [///
     [reference
      ["[0]" variable]]]]]])

(template: (!expect <pattern> <value>)
  (case <value>
    <pattern>
    true
    
    _
    false))

(type: Context
  [#redundants Nat
   #necessary (Dictionary Nat Nat)])

(type: (Scenario a)
  (-> Context (Random [a a])))

(template [<name> <synthesis> <random>]
  [(def: (<name> context)
     (Scenario Synthesis)
     (do [! random.monad]
       [value <random>]
       (in [(<synthesis> value)
            (<synthesis> value)])))]

  [bit_scenario synthesis.bit random.bit]
  [i64_scenario synthesis.i64 (# ! each .i64 random.nat)]
  [f64_scenario synthesis.f64 random.frac]
  [text_scenario synthesis.text (random.unicode 1)]
  )

(def: (primitive_scenario context)
  (Scenario Synthesis)
  (random.either (random.either (..bit_scenario context)
                                (..i64_scenario context))
                 (random.either (..f64_scenario context)
                                (..text_scenario context))))

(def: (with_redundancy scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (do [! random.monad]
    [redundant? random.bit]
    (if redundant?
      (do !
        [let? random.bit
         [expected_input actual_input] (..primitive_scenario context)
         .let [fake_register (n.+ (the #redundants context)
                                  (dictionary.size (the #necessary context)))]
         [expected_output actual_output] (scenario (revised #redundants ++ context))]
        (in [(synthesis.branch/case [expected_input
                                     {synthesis.#Seq {synthesis.#Pop}
                                                     {synthesis.#Then expected_output}}])
             (if let?
               (synthesis.branch/let [actual_input fake_register actual_output])
               (synthesis.branch/case [actual_input
                                       {synthesis.#Seq {synthesis.#Bind fake_register}
                                                       {synthesis.#Seq {synthesis.#Pop}
                                                                       {synthesis.#Then actual_output}}}]))]))
      (scenario context))))

(def: (variant_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (do [! random.monad]
    [lefts random.nat
     right? random.bit
     [expected input] (scenario context)]
    (in [(synthesis.variant [lefts right? expected])
         (synthesis.variant [lefts right? input])])))

(def: (tuple_scenario context)
  (Scenario Synthesis)
  (let [registers (dictionary.entries (the #necessary context))]
    (# random.monad in
       [(synthesis.tuple (list#each (|>> product.left synthesis.variable/local) registers))
        (synthesis.tuple (list#each (|>> product.right synthesis.variable/local) registers))])))

(def: (structure_scenario context)
  (Scenario Synthesis)
  (random.either (..variant_scenario (..with_redundancy ..tuple_scenario) context)
                 (..tuple_scenario context)))

(def: (let_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (do random.monad
    [_ (in [])
     [expected_input actual_input] (scenario context)
     .let [real_register (dictionary.size (the #necessary context))
           fake_register (n.+ (the #redundants context)
                              (dictionary.size (the #necessary context)))]
     [expected_output actual_output] (scenario (revised #necessary (dictionary.has real_register fake_register) context))]
    (in [(synthesis.branch/let [expected_input real_register expected_output])
         (synthesis.branch/let [actual_input fake_register actual_output])])))

(def: (if_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (do random.monad
    [_ (in [])
     [expected_test actual_test] (scenario context)
     [expected_then actual_then] (scenario context)
     [expected_else actual_else] (scenario context)]
    (in [(synthesis.branch/if [expected_test
                               expected_then
                               expected_else])
         (synthesis.branch/if [actual_test
                               actual_then
                               actual_else])])))

(def: random_member
  (Random Member)
  (do random.monad
    [lefts random.nat
     right? random.bit]
    (in (if right?
          {.#Right lefts}
          {.#Left lefts}))))

(def: (get_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (do [! random.monad]
    [length (# ! each (|>> (n.% 5) ++) random.nat)
     path (random.list length ..random_member)
     [expected_record actual_record] (scenario context)]
    (in [(synthesis.branch/get [path expected_record])
         (synthesis.branch/get [path actual_record])])))

(def: random_side
  (Random Side)
  ..random_member)

(def: (path_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Path))
  (`` (all random.either
           (all random.either
                (do [! random.monad]
                  [_ (in [])
                   [expected_then actual_then] (scenario context)]
                  (in [{synthesis.#Seq {synthesis.#Pop}
                                       {synthesis.#Then expected_then}}
                       {synthesis.#Seq {synthesis.#Pop}
                                       {synthesis.#Then actual_then}}]))
                (do [! random.monad]
                  [_ (in [])
                   .let [real_register (dictionary.size (the #necessary context))
                         fake_register (n.+ (the #redundants context)
                                            (dictionary.size (the #necessary context)))]
                   [expected_then actual_then] (scenario (revised #necessary (dictionary.has real_register fake_register) context))]
                  (in [{synthesis.#Seq {synthesis.#Bind real_register}
                                       {synthesis.#Seq {synthesis.#Pop}
                                                       {synthesis.#Then expected_then}}}
                       {synthesis.#Seq {synthesis.#Bind fake_register}
                                       {synthesis.#Seq {synthesis.#Pop}
                                                       {synthesis.#Then actual_then}}}])))
           (all random.either
                (~~ (template [<tag> <random>]
                      [(do [! random.monad]
                         [test <random>
                          [expected_then actual_then] (scenario context)]
                         (in [{synthesis.#Seq {synthesis.#Test {<tag> test}}
                                              {synthesis.#Then expected_then}}
                              {synthesis.#Seq {synthesis.#Test {<tag> test}}
                                              {synthesis.#Then actual_then}}]))]

                      [synthesis.#Bit random.bit]
                      [synthesis.#I64 (# ! each .i64 random.nat)]
                      [synthesis.#F64 random.frac]
                      [synthesis.#Text (random.unicode 1)]
                      )))
           (all random.either
                (do [! random.monad]
                  [side ..random_side
                   [expected_next actual_next] (path_scenario scenario context)]
                  (in [{synthesis.#Seq {synthesis.#Access {synthesis.#Side side}}
                                       expected_next}
                       {synthesis.#Seq {synthesis.#Access {synthesis.#Side side}}
                                       actual_next}]))
                (do [! random.monad]
                  [member ..random_member
                   [expected_next actual_next] (path_scenario scenario context)]
                  (in [{synthesis.#Seq {synthesis.#Access {synthesis.#Member member}}
                                       expected_next}
                       {synthesis.#Seq {synthesis.#Access {synthesis.#Member member}}
                                       actual_next}])))
           (do [! random.monad]
             [_ (in [])
              [expected_left actual_left] (path_scenario scenario context)
              [expected_right actual_right] (path_scenario scenario context)]
             (in [{synthesis.#Alt expected_left expected_right}
                  {synthesis.#Alt actual_left actual_right}]))
           )))

(def: (case_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (do [! random.monad]
    [_ (in [])
     [expected_input actual_input] (scenario context)
     [expected_path actual_path] (..path_scenario scenario context)]
    (in [(synthesis.branch/case [expected_input expected_path])
         (synthesis.branch/case [actual_input actual_path])])))

(def: (branch_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (all random.either
       (..let_scenario scenario context)
       (..if_scenario scenario context)
       (..get_scenario scenario context)
       (..case_scenario scenario context)
       ))

(def: scope_arity 5)

(def: (scope_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (do [! random.monad]
    [_ (in [])
     .let [real_start (dictionary.size (the #necessary context))
           fake_start (n.+ (the #redundants context)
                           real_start)]
     inits (random.list ..scope_arity (scenario context))
     [expected_iteration actual_iteration] (scenario (revised #necessary
                                                              (function (_ necessary)
                                                                (list#mix (function (_ [idx _] context)
                                                                            (dictionary.has (n.+ real_start idx)
                                                                                            (n.+ fake_start idx)
                                                                                            context))
                                                                          necessary
                                                                          (list.enumeration inits)))
                                                              context))]
    (in [(synthesis.loop/scope [real_start (list#each product.left inits) expected_iteration])
         (synthesis.loop/scope [fake_start (list#each product.right inits) actual_iteration])])))

(def: (again_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (do [! random.monad]
    [_ (in [])
     resets (random.list ..scope_arity (scenario context))]
    (in [(synthesis.loop/again (list#each product.left resets))
         (synthesis.loop/again (list#each product.right resets))])))

(def: (loop_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (all random.either
       (..scope_scenario scenario context)
       (..again_scenario scenario context)
       ))

(def: (abstraction_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (do [! random.monad]
    [_ (in [])
     .let [registers (dictionary.entries (the #necessary context))
           expected_environment (list#each (|>> product.left {variable.#Local}) registers)
           actual_environment (list#each (|>> product.right {variable.#Local}) registers)]
     [expected_body actual_body] (..primitive_scenario context)]
    (in [(synthesis.function/abstraction [expected_environment 1 expected_body])
         (synthesis.function/abstraction [actual_environment 1 actual_body])])))

(def: (apply_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (do [! random.monad]
    [abstraction (# ! each (|>> synthesis.constant)
                    (random.and (random.unicode 1)
                                (random.unicode 1)))
     inputs (random.list ..scope_arity (scenario context))]
    (in [(synthesis.function/apply [abstraction (list#each product.left inputs)])
         (synthesis.function/apply [abstraction (list#each product.right inputs)])])))

(def: (function_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (all random.either
       (..abstraction_scenario scenario context)
       (..apply_scenario scenario context)
       ))

(def: (control_scenario scenario context)
  (-> (Scenario Synthesis) (Scenario Synthesis))
  (all random.either
       (..branch_scenario scenario context)
       (..loop_scenario scenario context)
       (..function_scenario scenario context)
       ))

(def: (scenario context)
  (Scenario Synthesis)
  (all random.either
       (..primitive_scenario context)
       (..structure_scenario context)
       (..control_scenario (..with_redundancy
                             (..control_scenario
                              (..with_redundancy
                                ..structure_scenario)))
                           context)
       ))

(def: default
  Context
  [#redundants 0
   #necessary (dictionary.empty n.hash)])

(def: .public test
  Test
  (<| (_.covering /._)
      (all _.and
           (do random.monad
             [[expected input] (..scenario ..default)]
             (_.coverage [/.optimization]
               (|> (/.optimization input)
                   (!expect (^.multi {try.#Success actual}
                                     (# synthesis.equivalence = expected actual))))))
           )))
