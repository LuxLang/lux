(.require
 [library
  [lux (.except)
   ["_" test (.only Test)]
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" pipe]
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    ["[0]" product]]
   [math
    ["[0]" random (.only Random)]]
   [meta
    ["[0]" configuration
     ["$[1]" \\test]]]
   ["[0]" type (.use "[1]#[0]" equivalence)
    ["[0]" check]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" // (.only)
    ["[2][0]" module]
    [//
     [phase
      ["[2][0]" extension]]
     [///
      ["[2][0]" phase (.use "[1]#[0]" functor)]]]]]])

(def .public random_state
  (Random Lux)
  (do random.monad
    [version random.nat
     host (random.lower_case 1)
     configuration ($configuration.random 5)]
    (in (//.state (//.info version host configuration)))))

(def primitive
  (Random Type)
  (do random.monad
    [name (random.lower_case 1)]
    (in {.#Primitive name (list)})))

(def .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [lux ..random_state
         .let [state [/extension.#bundle /extension.empty
                      /extension.#state lux]]
         expected ..primitive
         dummy (random.only (|>> (type#= expected) not)
                            ..primitive)
         module (random.lower_case 1)]
        (all _.and
             (_.coverage [/.expecting /.inference]
               (and (|> (/.inference expected)
                        (/.expecting expected)
                        (/module.with 0 module)
                        (/phase#each product.right)
                        (/phase.result state)
                        (pipe.case
                          {try.#Success _} true
                          {try.#Failure _} false))
                    (|> (/.inference dummy)
                        (/.expecting expected)
                        (/module.with 0 module)
                        (/phase#each product.right)
                        (/phase.result state)
                        (pipe.case
                          {try.#Success _} false
                          {try.#Failure _} true))
                    (|> (/.inference expected)
                        (/.expecting dummy)
                        (/module.with 0 module)
                        (/phase#each product.right)
                        (/phase.result state)
                        (pipe.case
                          {try.#Success _} false
                          {try.#Failure _} true))))
             (_.coverage [/.inferring]
               (|> (/.inference expected)
                   /.inferring
                   (/module.with 0 module)
                   (/phase#each product.right)
                   (/phase.result state)
                   (try#each (|>> product.left (type#= expected)))
                   (try.else false)))
             (_.coverage [/.check]
               (|> (do /phase.monad
                     [exT (/.check (do check.monad
                                     [[id type] check.existential]
                                     (in type)))]
                     (|> (/.inference exT)
                         (/.expecting exT)))
                   (/module.with 0 module)
                   (/phase#each product.right)
                   (/phase.result state)
                   (pipe.case
                     {try.#Success _} true
                     {try.#Failure _} false)))
             (_.coverage [/.existential /.existential?]
               (|> (do /phase.monad
                     [:it: /.existential]
                     (in (/.existential? :it:)))
                   (/module.with 0 module)
                   (/phase#each product.right)
                   (/phase.result state)
                   (try.else false)))
             (_.coverage [/.fresh]
               (and (|> (do /phase.monad
                          [varT (/.check (do check.monad
                                           [[id type] check.var]
                                           (in type)))]
                          (|> (/.inference expected)
                              (/.expecting varT)))
                        (/module.with 0 module)
                        (/phase#each product.right)
                        (/phase.result state)
                        (pipe.case
                          {try.#Success _} true
                          {try.#Failure _} false))
                    (|> (do /phase.monad
                          [varT (/.check (do check.monad
                                           [[id type] check.var]
                                           (in type)))]
                          (|> (/.inference expected)
                              (/.expecting varT)
                              /.fresh))
                        (/module.with 0 module)
                        (/phase#each product.right)
                        (/phase.result state)
                        (pipe.case
                          {try.#Success _} false
                          {try.#Failure _} true))))
             ))))
