(.using
 [library
  [lux (.except)
   ["_" test (.only Test)]
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["<>" parser]
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" bit]
    ["[0]" text]
    [collection
     ["[0]" list (.open: "[1]#[0]" functor)]]]
   [macro
    ["^" pattern]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]
     ["[0]" i64]
     ["[0]" frac]]]
   [meta
    ["[0]" symbol]]
   [tool
    [compiler
     [reference (.only)
      ["[0]" variable (.only Variable)]]
     [language
      [lux
       [analysis (.only Environment)]
       ["[0]" synthesis (.only Synthesis)]]]]]]]
 ["[0]" \\parser]
 ["[0]" /
  ["[1][0]" simple]
  ["[1][0]" access]])

(def: !expect
  (template (_ <pattern> <value>)
    [(case <value>
       <pattern>
       true
       
       _
       false)]))

(def: random_constant
  (Random Symbol)
  (random.and (random.unicode 1)
              (random.unicode 1)))

(def: random_variable
  (Random Variable)
  (random.or random.nat
             random.nat))

(def: random_environment
  (Random (Environment Synthesis))
  (do [! random.monad]
    [size (at ! each (n.% 5) random.nat)]
    (|> ..random_variable
        (at ! each (|>> synthesis.variable))
        (random.list size))))

(def: simple
  Test
  (`` (all _.and
           (~~ (with_template [<query> <check> <random> <synthesis> <equivalence>]
                 [(do [! random.monad]
                    [expected <random>
                     dummy (|> <random> (random.only (|>> (at <equivalence> = expected) not)))]
                    (all _.and
                         (_.coverage [<query>]
                           (|> (\\parser.result <query> (list (<synthesis> expected)))
                               (!expect (^.multi {try.#Success actual}
                                                 (at <equivalence> = expected actual)))))
                         (_.coverage [<check>]
                           (and (|> (\\parser.result (<check> expected) (list (<synthesis> expected)))
                                    (!expect {try.#Success _}))
                                (|> (\\parser.result (<check> expected) (list (<synthesis> dummy)))
                                    (!expect (^.multi {try.#Failure error}
                                                      (exception.match? \\parser.cannot_parse error))))))
                         ))]

                 [\\parser.bit \\parser.this_bit random.bit synthesis.bit bit.equivalence]
                 [\\parser.i64 \\parser.this_i64 random.i64 synthesis.i64 i64.equivalence]
                 [\\parser.f64 \\parser.this_f64 random.safe_frac synthesis.f64 frac.equivalence]
                 [\\parser.text \\parser.this_text (random.unicode 1) synthesis.text text.equivalence]
                 [\\parser.local \\parser.this_local random.nat synthesis.variable/local n.equivalence]
                 [\\parser.foreign \\parser.this_foreign random.nat synthesis.variable/foreign n.equivalence]
                 [\\parser.constant \\parser.this_constant ..random_constant synthesis.constant symbol.equivalence]
                 ))
           )))

(def: complex
  Test
  (all _.and
       (do [! random.monad]
         [expected_bit random.bit
          expected_i64 random.i64
          expected_f64 random.safe_frac
          expected_text (random.unicode 1)]
         (_.coverage [\\parser.tuple]
           (and (|> (\\parser.result (\\parser.tuple (all <>.and \\parser.bit \\parser.i64 \\parser.f64 \\parser.text))
                                     (list (synthesis.tuple (list (synthesis.bit expected_bit)
                                                                  (synthesis.i64 expected_i64)
                                                                  (synthesis.f64 expected_f64)
                                                                  (synthesis.text expected_text)))))
                    (!expect (^.multi {try.#Success [actual_bit actual_i64 actual_f64 actual_text]}
                                      (and (at bit.equivalence = expected_bit actual_bit)
                                           (at i64.equivalence = expected_i64 actual_i64)
                                           (at frac.equivalence = expected_f64 actual_f64)
                                           (at text.equivalence = expected_text actual_text)))))
                (|> (\\parser.result (\\parser.tuple (all <>.and \\parser.bit \\parser.i64 \\parser.f64 \\parser.text))
                                     (list (synthesis.text expected_text)))
                    (!expect (^.multi {try.#Failure error}
                                      (exception.match? \\parser.cannot_parse error)))))))
       (do [! random.monad]
         [arity random.nat
          expected_environment ..random_environment
          expected_body (random.unicode 1)]
         (_.coverage [\\parser.function]
           (and (|> (\\parser.result (\\parser.function arity \\parser.text)
                                     (list (synthesis.function/abstraction [expected_environment arity (synthesis.text expected_body)])))
                    (!expect (^.multi {try.#Success [actual_environment actual_body]}
                                      (and (at (list.equivalence synthesis.equivalence) =
                                               expected_environment
                                               actual_environment)
                                           (at text.equivalence = expected_body actual_body)))))
                (|> (\\parser.result (\\parser.function arity \\parser.text)
                                     (list (synthesis.text expected_body)))
                    (!expect (^.multi {try.#Failure error}
                                      (exception.match? \\parser.cannot_parse error)))))))
       (do [! random.monad]
         [arity random.nat
          expected_environment ..random_environment
          expected_body (random.unicode 1)]
         (_.coverage [\\parser.wrong_arity]
           (|> (\\parser.result (\\parser.function (++ arity) \\parser.text)
                                (list (synthesis.function/abstraction [expected_environment arity (synthesis.text expected_body)])))
               (!expect (^.multi {try.#Failure error}
                                 (exception.match? \\parser.wrong_arity error))))))
       (do [! random.monad]
         [arity (at ! each (|>> (n.% 10) ++) random.nat)
          expected_offset random.nat
          expected_inits (random.list arity random.bit)
          expected_body (random.unicode 1)]
         (_.coverage [\\parser.loop]
           (and (|> (\\parser.result (\\parser.loop (<>.many \\parser.bit) \\parser.text)
                                     (list (synthesis.loop/scope [expected_offset
                                                                  (list#each (|>> synthesis.bit) expected_inits)
                                                                  (synthesis.text expected_body)])))
                    (!expect (^.multi {try.#Success [actual_offset actual_inits actual_body]}
                                      (and (at n.equivalence = expected_offset actual_offset)
                                           (at (list.equivalence bit.equivalence) =
                                               expected_inits
                                               actual_inits)
                                           (at text.equivalence = expected_body actual_body)))))
                (|> (\\parser.result (\\parser.loop (<>.many \\parser.bit) \\parser.text)
                                     (list (synthesis.text expected_body)))
                    (!expect (^.multi {try.#Failure error}
                                      (exception.match? \\parser.cannot_parse error)))))))
       ))

(def: \\parser
  Test
  (<| (_.covering \\parser._)
      (_.for [\\parser.Parser])
      (all _.and
           (do [! random.monad]
             [expected (at ! each (|>> synthesis.i64) random.i64)]
             (_.coverage [\\parser.result \\parser.any]
               (|> (\\parser.result \\parser.any (list expected))
                   (!expect (^.multi {try.#Success actual}
                                     (at synthesis.equivalence = expected actual))))))
           (_.coverage [\\parser.empty_input]
             (|> (\\parser.result \\parser.any (list))
                 (!expect (^.multi {try.#Failure error}
                                   (exception.match? \\parser.empty_input error)))))
           (do [! random.monad]
             [expected (at ! each (|>> synthesis.i64) random.i64)]
             (_.coverage [\\parser.unconsumed_input]
               (|> (\\parser.result \\parser.any (list expected expected))
                   (!expect (^.multi {try.#Failure error}
                                     (exception.match? \\parser.unconsumed_input error))))))
           (do [! random.monad]
             [dummy (at ! each (|>> synthesis.i64) random.i64)]
             (_.coverage [\\parser.end \\parser.expected_empty_input]
               (and (|> (\\parser.result \\parser.end (list))
                        (!expect {try.#Success _}))
                    (|> (\\parser.result \\parser.end (list dummy))
                        (!expect (^.multi {try.#Failure error}
                                          (exception.match? \\parser.expected_empty_input error)))))))
           (do [! random.monad]
             [dummy (at ! each (|>> synthesis.i64) random.i64)]
             (_.coverage [\\parser.end?]
               (and (|> (\\parser.result \\parser.end? (list))
                        (!expect {try.#Success #1}))
                    (|> (\\parser.result (<>.before \\parser.any \\parser.end?) (list dummy))
                        (!expect {try.#Success #0})))))
           (_.for [\\parser.cannot_parse]
                  (all _.and
                       ..simple
                       ..complex
                       ))
           )))

(def: .public test
  Test
  (all _.and
       ..\\parser

       /simple.test
       /access.test
       ))
