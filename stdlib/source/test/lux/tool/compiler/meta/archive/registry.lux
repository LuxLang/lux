(.using
 [library
  [lux (.full)
   ["_" test (.only Test)]
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" maybe ("[1]#[0]" functor)]
    ["[0]" try ("[1]#[0]" functor)]
    [parser
     ["<[0]>" binary]]]
   [data
    ["[0]" product]
    ["[0]" text]
    [collection
     ["[0]" sequence (.only Sequence)]
     ["[0]" set (.only Set)]
     ["[0]" list ("[1]#[0]" mix functor)]]
    [format
     ["[0]" binary]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" unit]
    ["[0]" artifact (.only)
     ["[0]" category]]]]])

(template: (tagged? <tag> <it>)
  [(case <it>
     {<tag> _}
     true
     
     _
     false)])

(def: random_dependency
  (Random unit.ID)
  (all random.and
       random.nat
       random.nat
       ))

(def: (random_dependencies amount)
  (-> Nat (Random (Set unit.ID)))
  (random.set unit.hash amount ..random_dependency))

(def: .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Registry])
      (do [! random.monad]
        [expected_name (random.lower_case 5)
         mandatory? random.bit
         expected_dependencies (..random_dependencies 5)
         
         expected_amount (# ! each (n.% 10) random.nat)
         expected_names (|> (random.lower_case 1)
                            (random.set text.hash expected_amount)
                            (# ! each set.list))]
        (`` (all _.and
                 (_.coverage [/.empty]
                   (|> /.empty
                       /.artifacts
                       sequence.size
                       (n.= 0)))
                 (_.coverage [/.resource]
                   (let [[@it registry] (/.resource mandatory? expected_dependencies /.empty)]
                     (case (sequence.list (/.artifacts registry))
                       (pattern (list [artifact actual_dependencies]))
                       (and (same? @it (the artifact.#id artifact))
                            (same? mandatory? (the artifact.#mandatory? artifact))
                            (tagged? category.#Anonymous (the artifact.#category artifact))
                            (same? expected_dependencies actual_dependencies))

                       _
                       false)))
                 (~~ (template [<new> <expected>' <query> <tag> <wrong_new> <wrong_expected>']
                       [(_.coverage [<new> <query>]
                          (let [<expected> <expected>'
                                <wrong_expected> <wrong_expected>']
                            (and (let [[@it registry] (<new> <expected> mandatory? expected_dependencies /.empty)]
                                   (and (case (<query> registry)
                                          (pattern (list actual_name))
                                          (same? <expected> actual_name)

                                          _
                                          false)
                                        (case (sequence.list (/.artifacts registry))
                                          (pattern (list [artifact actual_dependencies]))
                                          (and (same? @it (the artifact.#id artifact))
                                               (same? mandatory? (the artifact.#mandatory? artifact))
                                               (case (the artifact.#category artifact)
                                                 {<tag> actual_name}
                                                 (same? <expected> actual_name)

                                                 _
                                                 false)
                                               (same? expected_dependencies actual_dependencies))

                                          _
                                          false)))
                                 (let [[@it registry] (<wrong_new> <wrong_expected> mandatory? expected_dependencies /.empty)]
                                   (case (<query> registry)
                                     (pattern (list))
                                     true

                                     _
                                     false)))))]

                       [/.definition (is category.Definition [expected_name {.#None}]) /.definitions category.#Definition /.analyser expected_name]
                       [/.analyser expected_name /.analysers category.#Analyser /.synthesizer expected_name]
                       [/.synthesizer expected_name /.synthesizers category.#Synthesizer /.generator expected_name]
                       [/.generator expected_name /.generators category.#Generator /.directive expected_name]
                       [/.directive expected_name /.directives category.#Directive /.custom expected_name]
                       [/.custom expected_name /.customs category.#Custom /.definition (is category.Definition [expected_name {.#None}])]
                       ))
                 (_.coverage [/.id]
                   (and (~~ (template [<new> <expected>' <name>]
                              [(let [<expected> <expected>'
                                     [@expected registry] (<new> <expected> mandatory? expected_dependencies /.empty)]
                                 (|> (/.id (<name> <expected>) registry)
                                     (maybe#each (same? @expected))
                                     (maybe.else false)))]

                              [/.definition (is category.Definition [expected_name {.#None}]) product.left]
                              [/.analyser expected_name |>]
                              [/.synthesizer expected_name |>]
                              [/.generator expected_name |>]
                              [/.directive expected_name |>]
                              [/.custom expected_name |>]
                              ))))
                 (_.coverage [/.artifacts]
                   (and (~~ (template [<new> <query> <equivalence> <$>]
                              [(let [expected/* (list#each <$> expected_names)
                                     [ids registry] (is [(Sequence artifact.ID) /.Registry]
                                                        (list#mix (function (_ expected [ids registry])
                                                                    (let [[@new registry] (<new> expected mandatory? expected_dependencies registry)]
                                                                      [(sequence.suffix @new ids) registry]))
                                                                  [sequence.empty /.empty]
                                                                  expected/*))
                                     it (/.artifacts registry)]
                                 (and (n.= expected_amount (sequence.size it))
                                      (list.every? (function (_ [@it [it dependencies]])
                                                     (same? @it (the artifact.#id it)))
                                                   (list.zipped_2 (sequence.list ids) (sequence.list it)))
                                      (# (list.equivalence <equivalence>) = expected/* (<query> registry))))]

                              [/.definition /.definitions category.definition_equivalence (is (-> Text category.Definition)
                                                                                              (function (_ it)
                                                                                                [it {.#None}]))]
                              [/.analyser /.analysers text.equivalence (|>>)]
                              [/.synthesizer /.synthesizers text.equivalence (|>>)]
                              [/.generator /.generators text.equivalence (|>>)]
                              [/.directive /.directives text.equivalence (|>>)]
                              [/.custom /.customs text.equivalence (|>>)]
                              ))))
                 (_.coverage [/.writer /.parser]
                   (and (~~ (template [<new> <expected>' <name>]
                              [(let [<expected> <expected>'
                                     [@expected before] (<new> <expected> mandatory? expected_dependencies /.empty)]
                                 (|> before
                                     (binary.result /.writer)
                                     (<binary>.result /.parser)
                                     (try#each (|>> (/.id (<name> <expected>))
                                                    (maybe#each (same? @expected))
                                                    (maybe.else false)))
                                     (try.else false)))]

                              [/.definition (is category.Definition [expected_name {.#None}]) product.left]
                              [/.analyser expected_name |>]
                              [/.synthesizer expected_name |>]
                              [/.generator expected_name |>]
                              [/.directive expected_name |>]
                              [/.custom expected_name |>]
                              ))))
                 )))))
