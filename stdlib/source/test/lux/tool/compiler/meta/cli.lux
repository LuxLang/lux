(.using
 [library
  [lux "*"
   ["_" test (.only Test)]
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" pipe]
    ["[0]" try ("[1]#[0]" functor)]
    ["<>" parser
     ["<[0]>" cli]]]
   [data
    ["[0]" product]
    ["[0]" text]
    [collection
     ["[0]" list ("[1]#[0]" monoid monad)]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]
   [meta
    ["[0]" symbol "_"
     ["$[1]" \\test]]
    ["[0]" configuration ("[1]#[0]" equivalence)
     ["$[1]" \\test]]]]]
 [\\library
  ["[0]" /
   ["[1][0]" compiler (.only Compiler)]]]
 ["$[0]" / "_"
  ["[1][0]" compiler]])

(def: .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Service /.service])
      (let [(open "list#[0]") (list.equivalence text.equivalence)])
      (do [! random.monad]
        [amount (# ! each (|>> (n.% 5) ++) random.nat)
         sources (random.list amount (random.lower_case 1))
         host_dependencies (random.list amount (random.lower_case 2))
         libraries (random.list amount (random.lower_case 3))
         target (random.lower_case 4)
         module (random.lower_case 5)
         compilers (random.list amount $/compiler.random)
         configuration ($configuration.random 5)
         .let [compilation' (all list#composite
                                 (list#conjoint (list#each (|>> (list "--host_dependency")) host_dependencies))
                                 (list#conjoint (list#each (|>> (list "--library")) libraries))
                                 (list#conjoint (list#each (|>> /compiler.format (list "--compiler")) compilers))
                                 (list#conjoint (list#each (|>> (list "--source")) sources))
                                 (list "--target" target
                                       "--module" module
                                       "--configuration" (configuration.format configuration)))
               export (all list#composite
                           (list#conjoint (list#each (|>> (list "--source")) sources))
                           (list "--target" target))]]
        (all _.and
             (_.for [/.Compilation]
                    (`` (all _.and
                             (~~ (template [<type> <slot> <?>]
                                   [(_.coverage [<type>]
                                      (|> (partial_list "build" compilation')
                                          (<cli>.result /.service)
                                          (try#each (|>> (pipe.case
                                                           {/.#Compilation it}
                                                           (|> it
                                                               (the <slot>)
                                                               <?>)
                                                           
                                                           _
                                                           false)))
                                          (try.else false)))]

                                   [/.Host_Dependency /.#host_dependencies (list#= host_dependencies)]
                                   [/.Library /.#libraries (list#= libraries)]
                                   [/compiler.Compiler /.#compilers (# (list.equivalence /compiler.equivalence) = compilers)]
                                   [/.Source /.#sources (list#= sources)]
                                   [/.Target /.#target (same? target)]
                                   [/.Module /.#module (same? module)]
                                   [configuration.Configuration /.#configuration (configuration#= configuration)]
                                   ))
                             )))
             (_.coverage [/.Interpretation]
               (`` (and (~~ (template [<slot> <?>]
                              [(|> (partial_list "repl" compilation')
                                   (<cli>.result /.service)
                                   (try#each (|>> (pipe.case
                                                    {/.#Interpretation it}
                                                    (|> it
                                                        (the <slot>)
                                                        <?>)
                                                    
                                                    _
                                                    false)))
                                   (try.else false))]

                              [/.#host_dependencies (list#= host_dependencies)]
                              [/.#libraries (list#= libraries)]
                              [/.#compilers (# (list.equivalence /compiler.equivalence) = compilers)]
                              [/.#sources (list#= sources)]
                              [/.#target (same? target)]
                              [/.#module (same? module)]
                              [/.#configuration (configuration#= configuration)]
                              )))))
             (_.coverage [/.Export]
               (`` (and (~~ (template [<side> <?>]
                              [(|> (partial_list "export" export)
                                   (<cli>.result /.service)
                                   (try#each (|>> (pipe.case
                                                    {/.#Export it}
                                                    (|> it
                                                        <side>
                                                        <?>)
                                                    
                                                    _
                                                    false)))
                                   (try.else false))]

                              [product.left (list#= sources)]
                              [product.right (same? target)]
                              )))))
             (_.coverage [/.target]
               (`` (and (~~ (template [<it>]
                              [(same? target (/.target <it>))]

                              [{/.#Compilation [/.#host_dependencies host_dependencies
                                                /.#libraries libraries
                                                /.#compilers compilers
                                                /.#sources sources
                                                /.#target target
                                                /.#module module
                                                /.#configuration configuration]}]
                              [{/.#Interpretation [/.#host_dependencies host_dependencies
                                                   /.#libraries libraries
                                                   /.#compilers compilers
                                                   /.#sources sources
                                                   /.#target target
                                                   /.#module module
                                                   /.#configuration configuration]}]
                              [{/.#Export [sources target]}]
                              )))))

             $/compiler.test
             ))))
