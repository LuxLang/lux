(.using
 [library
  [lux (.except)
   ["_" test (.only Test)]
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    [concurrency
     ["[0]" async (.only Async) (.open: "[1]#[0]" monad)]]]
   [data
    ["[0]" binary (.open: "[1]#[0]" equivalence)
     ["$[1]" \\test]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]
   [world
    ["[0]" file]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" //
    ["[1][0]" module]]]]
 ["$[0]" ///
  ["[1][0]" context]])

(def: .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [.let [/ "/"
               fs (file.mock /)]
         context $///context.random
         @module random.nat
         @artifact random.nat
         expected ($binary.random 1)]
        (all _.and
             (in (do [! async.monad]
                   [pre (# fs file? (/.path fs context @module @artifact))
                    _ (//module.enable! ! fs context @module)
                    write! (/.cache! fs context @module @artifact expected)
                    post (# fs file? (/.path fs context @module @artifact))
                    read! (/.cache fs context @module @artifact)]
                   (_.coverage' [/.path /.cache! /.cache]
                     (and (not pre)
                          (case write!
                            {try.#Success _} true
                            {try.#Failure _} false)
                          post
                          (case read!
                            {try.#Success actual} (binary#= expected actual)
                            {try.#Failure _} false)))))
             ))))
