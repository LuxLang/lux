(.using
 [library
  [lux (.except)
   ["_" test (.only Test)]
   [abstract
    [monad (.only do)]
    [\\specification
     ["$[0]" equivalence]
     ["$[0]" hash]]]
   [data
    ["[0]" text ("[1]#[0]" equivalence)
     ["%" format]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]
   [meta
    ["[0]" symbol ("[1]#[0]" equivalence)]]]]
 [\\library
  ["[0]" / (.only)
   ["[0]" variable (.only Register)]]]
 ["[0]" /
  ["[1][0]" variable]])

(def: constant
  (Random /.Constant)
  (all random.and
       (random.upper_case 5)
       (random.lower_case 5)
       ))

(def: .public random
  (Random /.Reference)
  (all random.or
       /variable.random
       ..constant
       ))

(def: .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Reference /.Constant])
      (do random.monad
        [expected_register random.nat
         expected_constant ..constant])
      (`` (all _.and
               (_.for [/.equivalence]
                      ($equivalence.spec /.equivalence ..random))
               (_.for [/.hash]
                      ($hash.spec /.hash ..random))

               (~~ (template [<tag>]
                     [(_.coverage [<tag>]
                        (case (<tag> expected_register)
                          (pattern (<tag> actual_register))
                          (n.= expected_register actual_register)

                          _
                          false))]

                     [/.local]
                     [/.foreign]
                     ))

               (_.coverage [/.variable /.self]
                 (and (# /.equivalence = (/.self) (/.variable (variable.self)))
                      (case (/.self)
                        (pattern (/.self))
                        true
                        
                        _
                        false)
                      (case (/.variable (variable.self))
                        (pattern (/.self))
                        true
                        
                        _
                        false)))
               (_.coverage [/.constant]
                 (case (/.constant expected_constant)
                   (pattern (/.constant actual_constant))
                   (symbol#= expected_constant actual_constant)

                   _
                   false))
               (_.coverage [/.format]
                 (and (text#= (/.format (/.local expected_register))
                              (variable.format {variable.#Local expected_register}))
                      (text#= (/.format (/.foreign expected_register))
                              (variable.format {variable.#Foreign expected_register}))
                      (text#= (/.format (/.constant expected_constant))
                              (%.symbol expected_constant))))
               
               /variable.test
               ))))
