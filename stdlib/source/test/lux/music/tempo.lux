... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" maybe]]
   [data
    [collection
     ["[0]" list]
     ["[0]" set]
     ["[0]" sequence (.only sequence)]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]
    [number
     [/64
      ["[0]" natural]]]]
   [macro
    ["[0]" expansion]
    ["[0]" template]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(expansion.let [<options> (these /.bpm_240
                                 
                                 /.allegro

                                 /.bpm_120
                                 /.bpm_96
                                 /.bpm_80
                                 /.bpm_48
                                 /.bpm_40
                                 /.bpm_32
                                 /.bpm_24
                                 )]
  (these (the .public random
           (Random /.Tempo)
           (let [options (sequence <options>)
                 amount (sequence.size options)]
             (do [! random.monad]
               [choice (by ! each (natural.% amount) random.natural)]
               (pure (maybe.trusted (sequence.item choice options))))))
         
         (the .public test
           Test
           (<| (_.covering /._)
               (do [! random.monad]
                 [expected ..random])
               (_.for [/.Tempo])
               (`` (all _.and
                        (_.coverage [<options>]
                          (let [options (is (List /.Tempo)
                                            (list <options>))
                                uniques (set.of_list natural.hash options)]
                            (natural.= (list.size options)
                                       (set.size uniques))))
                        (,, (template.with [<original> <alias>]
                              [(_.coverage [<alias>]
                                 (same? <original> <alias>))]

                              [[/.bpm_24 /.larghissimo]
                               [/.bpm_32 /.adagissimo]
                               [/.bpm_32 /.grave]
                               [/.bpm_48 /.largo]
                               [/.bpm_80 /.lento]
                               [/.bpm_96 /.moderato]
                               [/.bpm_240 /.prestissimo]]))
                        (_.coverage [/.beats_per_minute /.micro_seconds_per_beat]
                          (|> expected
                              /.beats_per_minute
                              /.micro_seconds_per_beat
                              (natural.= expected)))
                        ))))))
