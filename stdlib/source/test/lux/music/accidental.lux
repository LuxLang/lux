... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 [//
  ["[0]T" pitch]])

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [expected pitchT.random])
      (_.for [/.Accidental])
      (all _.and
           (_.coverage [/.natural]
             (n.= expected (/.natural expected)))
           (_.coverage [/.sharp /.flat]
             (and (|> expected
                      /.sharp
                      (n.= expected)
                      not)
                  (|> expected
                      /.flat
                      (n.= expected)
                      not)
                  (|> expected
                      /.sharp
                      /.flat
                      (n.= expected))
                  (|> expected
                      /.flat
                      /.sharp
                      (n.= expected))))
           (_.coverage [/.double_sharp /.double_flat]
             (and (|> expected
                      /.double_sharp
                      (n.= expected)
                      not)
                  (n.= (/.sharp (/.sharp expected))
                       (/.double_sharp expected))
                  (|> expected
                      /.double_flat
                      (n.= expected)
                      not)
                  (n.= (/.flat (/.flat expected))
                       (/.double_flat expected))
                  (|> expected
                      /.double_sharp
                      /.double_flat
                      (n.= expected))
                  (|> expected
                      /.double_flat
                      /.double_sharp
                      (n.= expected))))
           )))
