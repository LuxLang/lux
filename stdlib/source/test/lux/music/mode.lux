... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [math
    ["[0]" random (.only Random)]]
   [meta
    [macro
     ["[0]" template]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" scale (.use "[1]#[0]" equivalence)
     ["[1]T" \\test]]]]])

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [expected scaleT.random])
      (_.for [/.Mode])
      (`` (all _.and
               (_.coverage [/.next /.previous]
                 (and (|> expected
                          /.next
                          /.previous
                          (scale#= expected))
                      (|> expected
                          /.previous
                          /.next
                          (scale#= expected))))
               (_.coverage [/.of?]
                 (and (|> expected
                          /.next
                          (/.of? expected))
                      (|> expected
                          /.previous
                          (/.of? expected))))
               (,, (template.with [<mode>]
                     [(_.coverage [<mode>]
                        (/.of? scale.major <mode>))]

                     [/.ionian]
                     [/.dorian]
                     [/.phrygian]
                     [/.lydian]
                     [/.mixo_lydian]
                     [/.aeolian]
                     [/.locrian]
                     ))
               (,, (template.with [<mode>]
                     [(_.coverage [<mode>]
                        (/.of? scale.melodic_minor <mode>))]

                     [/.athenian]
                     [/.cappadocian]
                     [/.asgardian]
                     [/.pontikonisian]
                     [/.olympian]
                     [/.sisyphean]
                     [/.palamidian]
                     ))
               ))))
