... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [equivalence (.only Equivalence)]]
   [math
    ["[0]" random (.only Random)]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public (specification case
                            equivalence_of_context equivalence_of_case
                            random_context random_case)
  (for_any (_ context case)
    (-> (/.Case context case)
        (Equivalence context) (Equivalence case)
        (Random context) (Random case)
        Test))
  (do [! random.monad]
    [expected_context random_context
     expected_case random_case]
    (<| (_.for [/.Case /.case])
        (all _.and
             (_.coverage [/.when]
               (when (/.when case expected_context)
                 {.#Left actual_context}
                 (same? expected_context actual_context)

                 {.#Right actual_case}
                 (by equivalence_of_context = expected_context (/.some case actual_case))))
             (_.coverage [/.some]
               (let [expected_context (/.some case expected_case)]
                 (when (/.when case expected_context)
                   {.#Right actual_case}
                   (by equivalence_of_case = expected_case actual_case)

                   {.#Left actual_context}
                   false)))
             ))))
