... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except integer)
   [abstract
    [equality (.only Equality)]
    ["[0]" monad]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]
      ["i" integer]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public (specification view
                            equality_of_it equality_of_alternative
                            random_it random_alternative)
  (for_any (_ it alternative)
    (-> (/.View it alternative)
        (Equality it) (Equality alternative)
        (Random it) (Random alternative)
        Test))
  (monad.let [! random.monad]
    [it_0 random_it
     it_1 random_it
     
     alternative_0 random_alternative
     alternative_1 random_alternative]
    (<| (_.for [/.View /.new])
        (all _.and
             (_.coverage [/.as]
               (and (bit.= (by equality_of_it = it_0 it_1)
                           (by equality_of_alternative = (/.as view it_0) (/.as view it_1)))
                    (|> it_0
                        (/.as view)
                        (/.of view)
                        (by equality_of_it = it_0))))
             (_.coverage [/.of]
               (and (bit.= (by equality_of_alternative = alternative_0 alternative_1)
                           (by equality_of_it = (/.of view alternative_0) (/.of view alternative_1)))
                    (|> alternative_0
                        (/.of view)
                        (/.as view)
                        (by equality_of_alternative = alternative_0))))
             ))))

(the integer
  (/.View Natural Integer)
  (/.new (|>> .natural --)
         (|>> ++ .integer)))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.identity])
      (monad.let [! random.monad]
        [it random.natural
         shift random.natural])
      (all _.and
           (..specification /.identity
                            bit.equality bit.equality
                            random.bit random.bit)
           (_.coverage [/.Aspect
                        /.as_aspect /.of_aspect]
             (let [integer' (|> ..integer /.as_aspect /.of_aspect)]
               (and (i.= (/.as ..integer it)
                         (/.as integer' it))
                    (n.= (/.of ..integer (/.as ..integer it))
                         (/.of integer' (/.as integer' it))))))
           (_.coverage [/.with]
             (n.= (n.+ shift it)
                  (/.with ..integer (i.+ (.integer shift)) it)))
           )))
