... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [equivalence (.only Equivalence)]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random)]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public (specification view
                            equivalence_of_it equivalence_of_alternative
                            random_it random_alternative)
  (for_any (_ it alternative)
    (-> (/.View it alternative)
        (Equivalence it) (Equivalence alternative)
        (Random it) (Random alternative)
        Test))
  (do [! random.monad]
    [it_0 random_it
     it_1 random_it
     
     alternative_0 random_alternative
     alternative_1 random_alternative]
    (<| (_.for [/.view])
        (all _.and
             (_.coverage [/.as]
               (and (bit.= (by equivalence_of_it = it_0 it_1)
                           (by equivalence_of_alternative = (/.as view it_0) (/.as view it_1)))
                    (|> it_0
                        (/.as view)
                        (/.of view)
                        (by equivalence_of_it = it_0))))
             (_.coverage [/.of]
               (and (bit.= (by equivalence_of_alternative = alternative_0 alternative_1)
                           (by equivalence_of_it = (/.of view alternative_0) (/.of view alternative_1)))
                    (|> alternative_0
                        (/.of view)
                        (/.as view)
                        (by equivalence_of_alternative = alternative_0))))
             ))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.identity])
      (..specification /.identity
                       bit.equivalence bit.equivalence
                       random.bit random.bit)))
