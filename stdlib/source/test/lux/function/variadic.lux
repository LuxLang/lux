... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" text]
    [collection
     ["[0]" stack (.use "[1]#[0]" mix)]]]
   [math
    ["[0]" random]
    [number
     ["n" natural]]]
   [macro
    ["[0]" syntax]
    ["[0]" expansion]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the macro_error
  (syntax.macro (_ [macro <code>.any])
    (function (_ compiler)
      (when ((expansion.complete macro) compiler)
        {try.#Failure error}
        {try.#Success [compiler (stack (code.text error))]}
        
        {try.#Success _}
        {try.#Failure "OOPS!"}))))

(/.the (+ left right extra)
  (-> Natural Natural (Stack Natural) Natural)
  (stack#mix n.+ (n.+ left right) extra))

(the .public test
  Test
  (do [! random.monad]
    [p0 random.natural
     p1 random.natural
     p2 random.natural
     p3 random.natural
     p4 random.natural
     p5 random.natural]
    (<| (_.covering /._)
        (all _.and
             (_.coverage [/.the]
               (and (n.= (all n.+ p0 p1)
                         (+ p0 p1))
                    (n.= (all n.+ p0 p1 p2)
                         (+ p0 p1 p2))
                    (n.= (all n.+ p0 p1 p2 p3)
                         (+ p0 p1 p2 p3))
                    (n.= (all n.+ p0 p1 p2 p3 p4)
                         (+ p0 p1 p2 p3 p4))
                    (n.= (all n.+ p0 p1 p2 p3 p4 p5)
                         (+ p0 p1 p2 p3 p4 p5))))
             (_.coverage [/.duplicate_parameters]
               (text.contains? (its exception.#label /.duplicate_parameters)
                               (macro_error
                                (/.the .public (- _ _)
                                  (-> Natural (Stack Natural) Natural)
                                  (undefined)))))
             (_.coverage [/.must_have_rest_parameter]
               (text.contains? (its exception.#label /.must_have_rest_parameter)
                               (macro_error
                                (/.the .public (-)
                                  (-> Natural (Stack Natural) Natural)
                                  (undefined)))))
             ))))
