... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the +
  (/.-> Natural
        Natural)
  (/.new n.+ 0))

(the average
  (/.-> Natural
        Natural)
  (/.new' (function (_ [count sum])
            (n./ count sum))
          (function (_ summand [count sum])
            [(++ count) (n.+ summand sum)])
          [0 0]))

(the .public test
  Test
  (monad.let [! random.monad]
    [parameter_0 random.natural
     parameter_1 random.natural
     parameter_2 random.natural]
    (<| (_.covering /._)
        (_.for [/.-> /.new])
        (all _.and
             (_.coverage [/.of]
               (n.= (all n.+ parameter_0 parameter_1 parameter_2)
                    (/.of ..+ parameter_0 parameter_1 parameter_2)))
             (_.coverage [/.partial]
               (n.= (/.of ..+ parameter_0 parameter_1 parameter_2)
                    (/.of (/.partial ..+ parameter_0 parameter_1) parameter_2)))
             (_.coverage [/.new']
               (n.= (n./ 3 (all n.+ parameter_0 parameter_1 parameter_2))
                    (/.of ..average parameter_0 parameter_1 parameter_2)))
             ))))
