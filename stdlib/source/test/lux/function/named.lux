... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [error
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" text]
    [collection
     ["[0]" list
      ["?[1]" \\projection]]]]
   [math
    ["[0]" random]
    [number
     ["n" natural]]]
   [macro
    ["[0]" syntax]
    ["[0]" expansion]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the macro_error
  (syntax.macro (_ [macro ?list.any])
    (function (_ compiler)
      (when ((expansion.complete macro) compiler)
        {try.#Failure error}
        {try.#Success [compiler (list (code.text error))]}
        
        {try.#Success _}
        {try.#Failure "OOPS!"}))))

(/.the (+ left right)
  (-> Natural Natural
      Natural)
  (n.+ left right))

(the .public test
  Test
  (do [! random.monad]
    [p0 random.natural
     p1 random.natural]
    (<| (_.covering /._)
        (all _.and
             (_.coverage [/.the]
               (n.= (n.+ p0 p1)
                    (+ left p0
                       right p1)))
             (_.coverage [/.duplicate_parameters]
               (text.contains? (its exception.#label /.duplicate_parameters)
                               (macro_error
                                (/.the .public (- _ _)
                                  (-> Natural (List Natural) Natural)
                                  (undefined)))))
             (_.coverage [/.invalid_parameters]
               (and (text.contains? (its exception.#label /.invalid_parameters)
                                    (macro_error
                                     (+ left p0)))
                    (text.contains? (its exception.#label /.invalid_parameters)
                                    (macro_error
                                     (+ right p1)))
                    (text.contains? (its exception.#label /.invalid_parameters)
                                    (macro_error
                                     (+ left p0
                                        right p1
                                        yolo p0)))
                    (text.contains? (its exception.#label /.invalid_parameters)
                                    (macro_error
                                     (+ left p0
                                        yolo p0)))))
             ))))
