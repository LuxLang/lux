... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [control
    ["[0]" scope (.use "[1]#[0]" monad)]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the (twice it)
  (-> Natural
      Natural)
  (n.+ it it))

(the (thrice it)
  (-> Natural
      Natural)
  (all n.+ it it it))

(/.the .private (method input)
  (/.Method Natural
            Natural)
  (scope#pure (twice input)))

(the .public test
  Test
  (<| (_.covering /._)
      (monad.let [! random.monad]
        [expected random.natural])
      (_.for [/.Method /.Value])
      (all _.and
           (_.coverage [/.the /.value]
             (/.value (monad.let scope.monad
                        [actual (method expected)]
                        (pure (n.= (twice expected)
                                   actual)))))
           (_.coverage [/.let]
             (/.value (monad.let scope.monad
                        [/.let [..method (function (_ next method it)
                                           (monad.let scope.monad
                                             [it (next it)]
                                             (pure (thrice it))))]
                         actual (method expected)]
                        (pure (n.= (thrice (twice expected))
                                   actual)))))
           )))
