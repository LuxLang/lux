(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]
    ["[0]" apply
     ["[1]T" \\test]]
    [\\specification
     ["$[0]" monad]]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only IO)
   [//
    ["[0]" function]]]])

(def injection
  (Injection IO)
  (|>> /.io))

(def comparison
  (Comparison IO)
  (function (_ == left right)
    (== (/.run! left) (/.run! right))))

(def .public test
  Test
  (<| (_.covering /._)
      (_.for [/.IO])
      (do random.monad
        [sample random.nat
         exit_code random.int]
        (all _.and
             (_.for [/.functor]
                    (functorT.spec ..injection ..comparison /.functor))
             (_.for [/.apply]
                    (applyT.spec ..injection ..comparison /.apply))
             (_.for [/.monad]
                    ($monad.spec ..injection ..comparison /.monad))

             (_.coverage [/.run! /.io]
               (n.= sample
                    (/.run! (/.io sample))))
             ))))
