... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)
     ["[1]T" \\test]]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]]
   [math
    ["[0]" random]
    [number
     ["n" natural]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only IO)
   [//
    ["[0]" function]]]])

(the injection
  (Injection IO)
  (|>> /.io))

(the comparison
  (Comparison IO)
  (function (_ == left right)
    (== (/.value left) (/.value right))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.IO])
      (do random.monad
        [sample random.natural
         exit_code random.integer]
        (all _.and
             (_.for [/.functor]
                    (functorT.spec ..injection ..comparison /.functor))
             (_.for [/.monad]
                    (monadT.spec ..injection ..comparison /.monad))

             (_.coverage [/.value /.io]
               (n.= sample
                    (/.value (/.io sample))))
             (_.coverage [/.as_io]
               (n.= sample
                    (/.value (/.as_io (function.constant sample)))))
             ))))
