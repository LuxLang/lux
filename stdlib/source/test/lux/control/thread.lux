... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only)
     ["[1]T" \\test]]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only Thread)
   [//
    ["[0]" io]]]])

(the (injection value)
  (Injection (for_any (_ a !) (Thread ! a)))
  (by /.monad pure value))

(the comparison
  (Comparison (for_any (_ a !) (Thread ! a)))
  (function (_ == left right)
    (== (/.value left) (/.value right))))

(the .public test
  Test
  (monad.let random.monad
    [sample random.natural
     factor random.natural]
    (<| (_.covering /._)
        (all _.and
             (_.for [/.Thread]
                    (all _.and
                         (_.coverage [/.value]
                           (n.= sample
                                (|> sample
                                    (by /.monad pure)
                                    /.value)))
                         (_.coverage [/.io]
                           (n.= sample
                                (|> sample
                                    (by /.monad pure)
                                    /.io
                                    io.value)))
                         
                         (_.for [/.functor]
                                (functorT.spec ..injection ..comparison /.functor))
                         (_.for [/.monad]
                                (monadT.spec ..injection ..comparison /.monad))
                         ))

             (_.for [/.Box /.box]
                    (all _.and
                         (_.coverage [/.read!]
                           (n.= sample
                                (/.value (is (for_any (_ !) (Thread ! Natural))
                                             (monad.let /.monad
                                               [box (/.box sample)]
                                               (/.read! box))))))

                         (_.coverage [/.write!]
                           (n.= factor
                                (/.value (is (for_any (_ !) (Thread ! Natural))
                                             (monad.let /.monad
                                               [box (/.box sample)
                                                _ (/.write! factor box)]
                                               (/.read! box))))))

                         (_.coverage [/.update!]
                           (n.= (n.x factor sample)
                                (/.value (is (for_any (_ !)
                                               (Thread ! Natural))
                                             (monad.let /.monad
                                               [box (/.box sample)
                                                [old new] (/.update! (n.x factor) box)]
                                               (pure new))))))))
             ))))
