... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   ["[0]" debug]
   [abstract
    ["[0]" monad]]
   [control
    ["[0]" pure]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public test
  Test
  (<| (_.covering /._)
      (monad.let [! random.monad]
        [sample random.natural]
        (all _.and
             (monad.let !
               [another random.natural]
               (_.coverage [/.new]
                 (n.= (++ another)
                      (|> sample
                          (n.x 3)
                          (n.+ 4)
                          (/.new another [++])))))
             (_.coverage [/.let]
               (n.= (n.+ sample sample)
                    (|> sample
                        (/.let x [(n.+ x x)]))))
             (_.coverage [/.if]
               (text.= (if (n.= 0 sample) "zero"
                           (n.even? sample) "even"
                           "odd")
                       (|> sample
                           (/.if [(n.= 0)] [(/.new "zero" [])]
                                 [n.even?] [(/.new "even" [])]
                                 [(/.new "odd" [])]))))
             (_.coverage [/.while]
               (n.= (n.x 10 sample)
                    (|> sample
                        (/.while [(n.= (n.x 10 sample)) not]
                                 [(n.+ sample)]))))
             (_.coverage [/.with_monad]
               (n.= (++ (n.+ 4 (n.x 3 sample)))
                    (|> sample
                        (/.with_monad pure.monad
                          [(n.x 3)]
                          [(n.+ 4)]
                          [++]))))
             (_.coverage [/.exec]
               (n.= (n.x 10 sample)
                    (|> sample
                        (/.exec [%.natural (text "sample = ") debug.log!])
                        (n.x 10))))
             (_.coverage [/.tuple]
               (let [[left middle right] (|> sample
                                             (/.tuple [++]
                                                      [--]
                                                      [%.natural]))]
                 (and (n.= (++ sample) left)
                      (n.= (-- sample) middle)
                      (text.= (%.natural sample) right))))
             (_.coverage [/.when]
               (text.= (when (n.% 10 sample)
                         0 "zero"
                         1 "one"
                         2 "two"
                         3 "three"
                         4 "four"
                         5 "five"
                         6 "six"
                         7 "seven"
                         8 "eight"
                         9 "nine"
                         _ "???")
                       (|> sample
                           (n.% 10)
                           (/.when
                             0 "zero"
                             1 "one"
                             2 "two"
                             3 "three"
                             4 "four"
                             5 "five"
                             6 "six"
                             7 "seven"
                             8 "eight"
                             9 "nine"
                             _ "???"))))
             ))))
