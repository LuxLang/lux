... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    ["[0]" monad (.only do)
     ["[1]T" \\test]]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]
    ["[0]" apply
     ["[1]T" \\test]]]
   [control
    ["[0]" try (.only Try)]]
   [data
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro
     [syntax (.only syntax)]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only Projection)]])

(the (should_fail expected input)
  (All (_ a) (-> Text (Try a) Bit))
  (when input
    {try.#Failure actual}
    (text#= expected actual)
    
    _
    false))

(the (enforced? projection input)
  (All (_ s) (-> (Projection s Any) s Bit))
  (when (/.value projection input)
    {try.#Success [_ []]}
    true

    _
    false))

(the (found? projection input)
  (All (_ s) (-> (Projection s Bit) s Bit))
  (when (/.value projection input)
    {try.#Success [_ .true]}
    true

    _
    false))

(the (fails? input)
  (All (_ a) (-> (Try a) Bit))
  (when input
    {try.#Failure _}
    true

    _
    false))

(the match
  (syntax (_ [pattern <code>.any
              then <code>.any
              input <code>.any])
    (in (list (` (when (, input)
                   {try.#Success [(,' _) (, pattern)]}
                   (, then)

                   (,' _)
                   false))))))

(the combinators_0
  Test
  (do [! random.monad]
    [expected0 random.nat
     variadic (of ! each (|>> (n.max 1) (n.min 20)) random.nat)
     expected+ (random.list variadic random.nat)
     even0 (random.only n.even? random.nat)
     odd0 (random.only n.odd? random.nat)
     not0 random.bit]
    (all _.and
         (_.coverage [/.maybe]
           (and (|> (list (code.nat expected0))
                    (/.value (/.maybe <code>.nat))
                    (match {.#Some actual}
                           (n.= expected0 actual)))
                (|> (list (code.int (.int expected0)))
                    (/.value (/.maybe <code>.nat))
                    (match {.#None}
                           true))))
         (_.coverage [/.some]
           (and (|> (list#each code.nat expected+)
                    (/.value (/.some <code>.nat))
                    (match actual
                           (of (list.equivalence n.equivalence) = expected+ actual)))
                (|> (list#each (|>> .int code.int) expected+)
                    (/.value (/.some <code>.nat))
                    (match {.#End}
                           true))))
         (_.coverage [/.many]
           (and (|> (list#each code.nat expected+)
                    (/.value (/.many <code>.nat))
                    (match actual
                           (of (list.equivalence n.equivalence) = expected+ actual)))
                (|> (list (code.nat expected0))
                    (/.value (/.many <code>.nat))
                    (match (list actual)
                           (n.= expected0 actual)))
                (|> (list#each (|>> .int code.int) expected+)
                    (/.value (/.many <code>.nat))
                    fails?)))
         (_.coverage [/.only]
           (and (|> (list (code.nat even0))
                    (/.value (/.only n.even? <code>.nat))
                    (match actual (n.= even0 actual)))
                (|> (list (code.nat odd0))
                    (/.value (/.only n.even? <code>.nat))
                    fails?)))
         (_.coverage [/.and]
           (let [even (/.only n.even? <code>.nat)
                 odd (/.only n.odd? <code>.nat)]
             (and (|> (list (code.nat even0) (code.nat odd0))
                      (/.value (/.and even odd))
                      (match [left right]
                             (and (n.= even0 left)
                                  (n.= odd0 right))))
                  (|> (list (code.nat odd0) (code.nat even0))
                      (/.value (/.and even odd))
                      fails?))))
         (_.coverage [/.or]
           (let [even (/.only n.even? <code>.nat)
                 odd (/.only n.odd? <code>.nat)]
             (and (|> (list (code.nat even0))
                      (/.value (/.or even odd))
                      (match {.#Left actual} (n.= even0 actual)))
                  (|> (list (code.nat odd0))
                      (/.value (/.or even odd))
                      (match {.#Right actual} (n.= odd0 actual)))
                  (|> (list (code.bit not0))
                      (/.value (/.or even odd))
                      fails?))))
         (_.coverage [/.either]
           (let [even (/.only n.even? <code>.nat)
                 odd (/.only n.odd? <code>.nat)]
             (and (|> (list (code.nat even0))
                      (/.value (/.either even odd))
                      (match actual (n.= even0 actual)))
                  (|> (list (code.nat odd0))
                      (/.value (/.either even odd))
                      (match actual (n.= odd0 actual)))
                  (|> (list (code.bit not0))
                      (/.value (/.either even odd))
                      fails?))))
         (_.coverage [/.not]
           (and (|> (list (code.nat expected0))
                    (/.value (/.not <code>.nat))
                    fails?)
                (|> (list (code.bit not0))
                    (/.value (/.not <code>.nat))
                    (match [] true))))
         )))

(the combinators_1
  Test
  (do [! random.monad]
    [variadic (of ! each (|>> (n.max 1) (n.min 20)) random.nat)
     times (of ! each (n.% variadic) random.nat)
     expected random.nat
     wrong (|> random.nat (random.only (|>> (n.= expected) not)))
     expected+ (random.list variadic random.nat)
     separator (random.ascii 1)]
    (all _.and
         (_.coverage [/.exactly]
           (and (|> (list#each code.nat expected+)
                    (/.value (/.exactly times <code>.nat))
                    (match actual
                           (of (list.equivalence n.equivalence) =
                               (list.first times expected+)
                               actual)))
                (|> (list#each code.nat expected+)
                    (/.value (/.exactly (++ variadic) <code>.nat))
                    fails?)))
         (_.coverage [/.at_least]
           (and (|> (list#each code.nat expected+)
                    (/.value (/.at_least times <code>.nat))
                    (match actual
                           (of (list.equivalence n.equivalence) =
                               expected+
                               actual)))
                (|> (list#each code.nat expected+)
                    (/.value (/.at_least (++ variadic) <code>.nat))
                    fails?)))
         (_.coverage [/.at_most]
           (and (|> (list#each code.nat expected+)
                    (/.value (/.at_most times <code>.nat))
                    (match actual
                           (of (list.equivalence n.equivalence) =
                               (list.first times expected+)
                               actual)))
                (|> (list#each code.nat expected+)
                    (/.value (/.at_most (++ variadic) <code>.nat))
                    (match actual
                           (of (list.equivalence n.equivalence) =
                               expected+
                               actual)))))
         (_.coverage [/.between]
           (and (|> (list#each code.nat expected+)
                    (/.value (/.between times (n.- times variadic) <code>.nat))
                    (match actual
                           (of (list.equivalence n.equivalence) =
                               expected+
                               actual)))
                (|> (list#each code.nat (list.first times expected+))
                    (/.value (/.between times (n.- times variadic) <code>.nat))
                    (match actual
                           (of (list.equivalence n.equivalence) =
                               (list.first times expected+)
                               actual)))))
         (_.coverage [/.separated_by]
           (|> (list.interposed (code.text separator) (list#each code.nat expected+))
               (/.value (/.separated_by (<code>.this (code.text separator)) <code>.nat))
               (match actual
                      (of (list.equivalence n.equivalence) =
                          expected+
                          actual))))
         (_.coverage [/.remaining]
           (|> (list#each code.nat expected+)
               (/.value /.remaining)
               (match actual
                      (of (list.equivalence code.equivalence) =
                          (list#each code.nat expected+)
                          actual))))
         (_.coverage [/.else]
           (and (|> (/.value (/.else wrong (of /.monad in expected)) (list))
                    (match actual (n.= expected actual)))
                (|> (/.value (/.else expected (/.failure "yolo"))
                             (list))
                    (match actual (n.= expected actual)))
                ))
         )))

(the combinators_2
  Test
  (do random.monad
    [expected random.nat
     even (random.only n.even? random.nat)
     odd (random.only n.odd? random.nat)
     .let [nat^ <code>.nat
           even^ (/.only n.even? <code>.nat)
           odd^ (/.only n.odd? <code>.nat)]]
    (all _.and
         (_.coverage [/.rec]
           (let [projection (/.rec (function (_ self)
                                     (/.either <code>.nat
                                               (<code>.tuple self))))
                 level_0 (code.nat expected)
                 level_up (is (-> Code Code)
                              (|>> list code.tuple))]
             (and (|> (list level_0)
                      (/.value projection)
                      (match actual (n.= expected actual)))
                  (|> (list (level_up level_0))
                      (/.value projection)
                      (match actual (n.= expected actual)))
                  (|> (list (level_up (level_up level_0)))
                      (/.value projection)
                      (match actual (n.= expected actual))))))
         (_.coverage [/.after]
           (and (|> (/.value (/.after even^ <code>.nat)
                             (list (code.nat even) (code.nat expected)))
                    (match actual (n.= expected actual)))
                (|> (/.value (/.after even^ <code>.nat)
                             (list (code.nat odd) (code.nat expected)))
                    fails?)))
         (_.coverage [/.before]
           (and (|> (/.value (/.before even^ <code>.nat)
                             (list (code.nat expected) (code.nat even)))
                    (match actual (n.= expected actual)))
                (|> (/.value (/.before even^ <code>.nat)
                             (list (code.nat expected) (code.nat odd)))
                    fails?)))
         (_.coverage [/.parses?]
           (and (|> (/.value (/.parses? even^)
                             (list (code.nat even)))
                    (match verdict verdict))
                (|> (/.value (/.parses? even^)
                             (list (code.nat odd)))
                    (match verdict (not verdict)))))
         (_.coverage [/.parses]
           (and (|> (/.value (/.parses even^)
                             (list (code.nat even)))
                    (match [] true))
                (|> (/.value (/.parses even^)
                             (list (code.nat odd)))
                    fails?)))
         (_.coverage [/.speculative]
           (let [happy_path!
                 (|> (/.value (/.and (/.speculative even^) nat^)
                              (list (code.nat even)))
                     (match [speculation actual]
                            (and (n.= speculation actual)
                                 (n.= even actual))))

                 sad_path!
                 (|> (/.value (/.and (/.speculative even^) nat^)
                              (list (code.nat odd)))
                     fails?)]
             (and happy_path!
                  sad_path!)))
         (_.coverage [/.codec]
           (|> (/.value (/.codec n.decimal <code>.text)
                        (list (code.text (%.nat expected))))
               (match actual (n.= expected actual))))
         )))

(the injection
  (Injection (All (_ a i) (Projection i a)))
  (of /.monad in))

(the comparison
  (Comparison (All (_ a i) (Projection i a)))
  (function (_ == left right)
    (when [(/.value left []) (/.value right [])]
      [{try.#Success [_ left]} {try.#Success [_ right]}]
      (== left right)

      _
      false)))

(the .public test
  Test
  (do random.monad
    [expected random.nat
     failure (random.ascii 1)
     assertion (random.ascii 1)]
    (<| (_.covering /._)
        (_.for [/.Projection])
        (all _.and
             (_.for [/.functor]
                    (functorT.spec ..injection ..comparison /.functor))
             (_.for [/.apply]
                    (applyT.spec ..injection ..comparison /.apply))
             (_.for [/.monad]
                    (monadT.spec ..injection ..comparison /.monad))

             (_.coverage [/.value]
               (|> (/.value (of /.monad in expected) (list))
                   (match actual (n.= expected actual))))
             (_.coverage [/.failure]
               (|> (list)
                   (/.value (/.failure failure))
                   (should_fail failure)))
             (_.coverage [/.of_try]
               (and (|> (list)
                        (/.value (/.of_try {try.#Success expected}))
                        (match actual (n.= expected actual)))
                    (|> (list)
                        (/.value (/.of_try {try.#Failure failure}))
                        (should_fail failure))))
             (_.coverage [/.assertion]
               (and (|> (list (code.bit #1) (code.int +123))
                        (/.value (/.assertion assertion true))
                        (match [] true))
                    (|> (list (code.bit #1) (code.int +123))
                        (/.value (/.assertion assertion false))
                        fails?)))
             ..combinators_0
             ..combinators_1
             ..combinators_2
             ))))
