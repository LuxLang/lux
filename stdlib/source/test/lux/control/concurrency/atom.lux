(.using
 [library
  [lux (.except)
   ["_" test (.only Test)]
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" io]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]]]
 [\\library
  ["[0]" /]])

(def: .public test
  Test
  (<| (_.covering /._)
      (all _.and
           (do random.monad
             [expected random.nat
              .let [box (/.atom expected)]]
             (_.coverage [/.Atom /.atom /.read!]
               (io.run!
                (do io.monad
                  [actual (/.read! box)]
                  (in (same? expected actual))))))
           (do random.monad
             [target random.nat
              unknown (random.only (|>> (same? target) not) random.nat)
              expected random.nat
              .let [box (/.atom target)]]
             (_.coverage [/.compare_and_swap!]
               (io.run!
                (do io.monad
                  [swapped_unknown? (/.compare_and_swap! unknown expected box)
                   swapped_target? (/.compare_and_swap! target expected box)
                   actual (/.read! box)]
                  (in (and (not swapped_unknown?)
                           swapped_target?
                           (same? expected actual)))))))
           (do random.monad
             [init random.nat
              shift random.nat
              .let [box (/.atom init)]]
             (_.coverage [/.update!]
               (io.run!
                (do io.monad
                  [[pre post] (/.update! (n.+ shift) box)]
                  (in (and (same? init pre)
                           (n.= (n.+ shift init)
                                post)))))))
           (do random.monad
             [pre random.nat
              post random.nat
              .let [box (/.atom pre)]]
             (_.coverage [/.write!]
               (io.run!
                (do io.monad
                  [old (/.write! post box)
                   new (/.read! box)]
                  (in (and (same? pre old)
                           (same? post new)))))))
           )))
