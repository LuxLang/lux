(.require
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only Monad do)]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]
    ["[0]" apply
     ["[1]T" \\test]]
    [\\specification
     ["$[0]" monad]]]
   [control
    ["[0]" io (.only IO)]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]
    ["[0]" unit]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" atom (.only Atom atom)]
    ["[0]" async]
    ["[0]" frp (.only Channel)]]]])

(def injection
  (Injection /.STM)
  (of /.monad in))

(def comparison
  (Comparison /.STM)
  (function (_ == left right)
    (== (product.right (left (list)))
        (product.right (right (list))))))

(def .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [dummy random.nat
         expected random.nat
         iterations_per_process (|> random.nat (of ! each (n.% 100)))]
        (all _.and
             (_.for [/.functor]
                    (functorT.spec ..injection ..comparison /.functor))
             (_.for [/.apply]
                    (applyT.spec ..injection ..comparison /.apply))
             (_.for [/.monad]
                    ($monad.spec ..injection ..comparison /.monad))

             (in (do async.monad
                   [actual (/.commit! (of /.monad in expected))]
                   (unit.coverage [/.commit!]
                     (n.= expected actual))))
             (in (do async.monad
                   [actual (/.commit! (/.read (/.var expected)))]
                   (unit.coverage [/.Var /.var /.read]
                     (n.= expected actual))))
             (in (do async.monad
                   [actual (let [box (/.var dummy)]
                             (/.commit! (do /.monad
                                          [_ (/.write expected box)]
                                          (/.read box))))
                    verdict (let [box (/.var dummy)]
                              (/.commit! (do /.monad
                                           [_ (/.write expected box)
                                            actual (/.read box)]
                                           (in (n.= expected actual)))))]
                   (unit.coverage [/.write]
                     (and (n.= expected actual)
                          verdict))))
             (in (do async.monad
                   [.let [box (/.var dummy)]
                    output (/.commit! (do /.monad
                                        [_ (/.update (n.+ expected) box)]
                                        (/.read box)))]
                   (unit.coverage [/.update]
                     (n.= (n.+ expected dummy)
                          output))))
             (in (do async.monad
                   [.let [box (/.var dummy)
                          [follower sink] (io.run! (/.changes box))]
                    _ (/.commit! (/.write expected box))
                    _ (/.commit! (/.update (n.* 2) box))
                    _ (async.future (of sink close))
                    _ (/.commit! (/.update (n.* 3) box))
                    changes (frp.list follower)]
                   (unit.coverage [/.changes]
                     (of (list.equivalence n.equivalence) =
                         (list expected (n.* 2 expected))
                         changes))))
             (in (let [var (/.var 0)]
                   (do [! async.monad]
                     [_ (|> (list.repeated iterations_per_process [])
                            (list#each (function (_ _) (/.commit! (/.update ++ var))))
                            (monad.all !))
                      cummulative (/.commit! (/.read var))]
                     (unit.coverage [/.STM]
                       (n.= iterations_per_process
                            cummulative)))))
             ))))
