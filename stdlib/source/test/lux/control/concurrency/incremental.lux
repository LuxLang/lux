... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]]
   [control
    ["[0]" io]
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]]
   [math
    ["[0]" random]]
   [test
    ["_" property (.only Test)]
    ["[0]" unit]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" async]]]])

(the injection
  (Injection /.Computation)
  (|>> /.var /.mutations))

(the comparison
  (Comparison /.Computation)
  (function (_ == left right)
    (io.run!
     (do io.monad
       [left (/.value left)
        right (/.value right)]
       (in (== left right))))))

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [expected random.nat
         .let [dummy (++ expected)]

         expected_right random.safe_frac])
      (all _.and
           (_.for [/.Var]
                  (all _.and
                       (_.coverage [/.var /.mutations /.value]
                         (|> expected
                             /.var
                             /.mutations
                             /.value
                             io.run!
                             (same? expected)))
                       (_.coverage [/.mutate!]
                         (let [it (/.var dummy)]
                           (io.run! (do io.monad
                                      [before (/.value (/.mutations it))
                                       _ (/.mutate! expected it)
                                       after (/.value (/.mutations it))]
                                      (in (and (same? dummy before)
                                               (same? expected after)))))))
                       ))
           (_.for [/.Computation]
                  (all _.and
                       (_.for [/.functor]
                              (functorT.spec ..injection ..comparison /.functor))
                       
                       (_.coverage [/.or]
                         (let [left (/.var dummy)
                               right (/.var expected_right)
                               l,r (/.or (/.mutations left) (/.mutations right))]
                           (io.run! (do io.monad
                                      [_ (/.mutate! expected left)
                                       left'|right' (/.value l,r)]
                                      (in (when left'|right'
                                            {.#Left left'}
                                            (same? expected left')
                                            
                                            {.#Right right'}
                                            false))))))
                       (_.coverage [/.and]
                         (let [left (/.var dummy)
                               right (/.var expected_right)
                               l,r (/.and (/.mutations left) (/.mutations right))]
                           (io.run! (do io.monad
                                      [_ (/.mutate! expected left)
                                       [left' right'] (/.value l,r)]
                                      (in (and (same? expected left')
                                               (same? expected_right right')))))))
                       (_.coverage [/.either]
                         (let [left (/.var dummy)
                               right (/.var dummy)
                               l,r (/.either (/.mutations left) (/.mutations right))]
                           (io.run! (do io.monad
                                      [_ (/.mutate! expected right)
                                       actual (/.value l,r)]
                                      (in (same? expected actual))))))
                       ))
           )))
