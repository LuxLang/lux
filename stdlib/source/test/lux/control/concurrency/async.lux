... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)
     ["[1]T" \\test]]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]
    ["[0]" apply
     ["[1]T" \\test]]]
   [control
    ["[0]" io]]
   [math
    ["[0]" random]
    [number
     ["n" natural]
     ["i" integer]
     ["[0]" i64]]]
   [world
    [time
     ["[0]" instant]
     ["[0]" duration]]]
   [test
    ["_" property (.only Test)]
    ["[0]" unit]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" atom (.only Atom)]]]])

(the injection
  (Injection /.Async)
  /.resolved)

(the comparison
  (Comparison /.Async)
  (function (_ == left right)
    (io.value
     (do io.monad
       [?left (/.value left)
        ?right (/.value right)]
       (in (when [?left ?right]
             [{.#Some left}
              {.#Some right}]
             (== left right)
             
             _
             false))))))

(the delay
  (for .js (i64.left_shifted 4 1)
       (i64.left_shifted 3 1)))

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [waiting_time (|> random.natural (of ! each (|>> (n.% ..delay) (n.+ ..delay))))
         expected random.natural
         dummy random.natural
         .let [not_dummy (|> random.natural (random.only (|>> (n.= dummy) not)))]
         leftE not_dummy
         rightE not_dummy]
        (all _.and
             (_.for [/.functor]
                    (functorT.spec ..injection ..comparison /.functor))
             (_.for [/.apply]
                    (applyT.spec ..injection ..comparison /.apply))
             (_.for [/.monad]
                    (monadT.spec ..injection ..comparison /.monad))
             
             (in (do /.monad
                   [.let [[async resolver] (is [(/.Async Natural) (/.Resolver Natural)]
                                               (/.async []))]
                    resolved? (/.future (resolver expected))
                    actual async]
                   (unit.coverage [/.Async /.Resolver /.async]
                     (and resolved?
                          (n.= expected actual)))))
             (in (do /.monad
                   [actual (/.resolved expected)]
                   (unit.coverage [/.resolved]
                     (n.= expected actual))))
             (in (do /.monad
                   [actual (/.future (io.io expected))]
                   (unit.coverage [/.future]
                     (n.= expected actual))))
             (in (do /.monad
                   [pre (/.future instant.now)
                    actual (/.schedule! waiting_time (io.io expected))
                    post (/.future instant.now)]
                   (unit.coverage [/.schedule!]
                     (and (n.= expected actual)
                          (i.>= (.integer waiting_time)
                                (duration.millis (instant.span pre post)))))))
             (in (do /.monad
                   [pre (/.future instant.now)
                    _ (/.delay waiting_time)
                    post (/.future instant.now)]
                   (unit.coverage [/.delay]
                     (i.>= (.integer waiting_time)
                           (duration.millis (instant.span pre post))))))
             (in (do /.monad
                   [[leftA rightA] (/.and (/.future (io.io leftE))
                                          (/.future (io.io rightE)))]
                   (unit.coverage [/.and]
                     (n.= (n.+ leftE rightE)
                          (n.+ leftA rightA)))))
             (in (do /.monad
                   [pre (/.future instant.now)
                    actual (/.after waiting_time expected)
                    post (/.future instant.now)]
                   (unit.coverage [/.after]
                     (and (n.= expected actual)
                          (i.>= (.integer waiting_time)
                                (duration.millis (instant.span pre post)))))))
             (in (do /.monad
                   [?left (/.or (in leftE)
                                (/.after waiting_time dummy))
                    ?right (/.or (/.after waiting_time dummy)
                                 (in rightE))]
                   (unit.coverage [/.or]
                     (when [?left ?right]
                       [{.#Left leftA} {.#Right rightA}]
                       (n.= (n.+ leftE rightE)
                            (n.+ leftA rightA))

                       _
                       false))))
             (in (do /.monad
                   [leftA (/.either (in leftE)
                                    (/.after waiting_time dummy))
                    rightA (/.either (/.after waiting_time dummy)
                                     (in rightE))]
                   (unit.coverage [/.either]
                     (n.= (n.+ leftE rightE)
                          (n.+ leftA rightA)))))
             (in (do /.monad
                   [?actual (/.future (/.value (/.resolved expected)))
                    .let [[async resolver] (is [(/.Async Natural) (/.Resolver Natural)]
                                               (/.async []))]
                    ?never (/.future (/.value async))]
                   (unit.coverage [/.value]
                     (when [?actual ?never]
                       [{.#Some actual} {.#None}]
                       (n.= expected actual)

                       _
                       false))))
             (in (do /.monad
                   [yep (/.future (/.resolved? (/.resolved expected)))
                    .let [[async resolver] (is [(/.Async Natural) (/.Resolver Natural)]
                                               (/.async []))]
                    nope (/.future (/.resolved? async))]
                   (unit.coverage [/.resolved?]
                     (and yep
                          (not nope)))))
             (in (do /.monad
                   [?none (/.within 0 (/.after waiting_time dummy))
                    ?actual (/.within waiting_time (in expected))]
                   (unit.coverage [/.within]
                     (when [?none ?actual]
                       [{.#None} {.#Some actual}]
                       (n.= expected actual)

                       _
                       false))))
             (in (do /.monad
                   [.let [box (is (Atom Natural)
                                  (atom.atom dummy))]
                    _ (/.future (/.upon! (function (_ value)
                                           (atom.write! value box))
                                         (/.resolved expected)))
                    actual (/.future (atom.read! box))]
                   (unit.coverage [/.upon!]
                     (n.= expected actual))))
             ))))
