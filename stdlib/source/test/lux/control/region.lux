... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [functor (.only Functor)]
    [apply (.only Apply)]
    ["[0]" enum]
    ["[0]" monad (.only Monad do)
     ["[1]T" \\test]]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]
    ["[0]" apply
     ["[1]T" \\test]]]
   [control
    ["[0]" try (.only Try)]]
   [data
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [meta
    [type (.only sharing)]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only Region)
   [//
    ["[0]" thread (.only Thread)]
    ["[0]" exception (.only Exception)]]]])

(exception.def oops)

(def (success? result)
  (All (_ a) (-> (Try a) Bit))
  (when result
    {try.#Success _}
    true
    
    {try.#Failure _}
    false))

(def (throws? exception result)
  (All (_ e a) (-> (Exception e) (Try a) Bit))
  (when result
    {try.#Success _}
    false
    
    {try.#Failure error}
    (exception.is? exception error)))

(def (injection value)
  (Injection (All (_ a) (All (_ ! r) (Region r (Thread !) a))))
  (function (_ [region scope])
    (function (_ !)
      [scope
       {try.#Success value}])))

(def comparison
  (Comparison (All (_ a) (All (_ ! r) (Region r (Thread !) a))))
  (function (_ == left right)
    (when [(sharing [a]
             (is (Equivalence a)
                 ==)
             (is (Try a)
                 (thread.result (as_expected (/.run! thread.monad left)))))
           (sharing [a]
             (is (Equivalence a)
                 ==)
             (is (Try a)
                 (thread.result (as_expected (/.run! thread.monad right)))))]
      [{try.#Success left} {try.#Success right}]
      (== left right)

      _
      false)))

(def .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Region])
      (do [! random.monad]
        [expected_clean_ups (|> random.nat (of ! each (|>> (n.% 100) (n.max 1))))]
        (all _.and
             (_.for [/.functor]
                    (functorT.spec ..injection ..comparison (is (All (_ ! r)
                                                                  (Functor (Region r (thread.Thread !))))
                                                                (/.functor thread.functor))))
             (_.for [/.apply]
                    (applyT.spec ..injection ..comparison (is (All (_ ! r)
                                                                (Apply (Region r (thread.Thread !))))
                                                              (/.apply thread.monad))))
             (_.for [/.monad]
                    (monadT.spec ..injection ..comparison (is (All (_ ! r)
                                                                (Monad (Region r (thread.Thread !))))
                                                              (/.monad thread.monad))))
             
             (_.coverage [/.run!]
               (thread.result
                (do [! thread.monad]
                  [clean_up_counter (thread.box 0)
                   .let [//@ !
                         count_clean_up (function (_ value)
                                          (do !
                                            [_ (thread.update! ++ clean_up_counter)]
                                            (in {try.#Success []})))]
                   outcome (/.run! !
                                   (do [! (/.monad !)]
                                     [_ (monad.each ! (/.acquire! //@ count_clean_up)
                                                    (enum.range n.enum 1 expected_clean_ups))]
                                     (in [])))
                   actual_clean_ups (thread.read! clean_up_counter)]
                  (in (and (..success? outcome)
                           (n.= expected_clean_ups
                                actual_clean_ups))))))
             (_.coverage [/.failure]
               (thread.result
                (do [! thread.monad]
                  [clean_up_counter (thread.box 0)
                   .let [//@ !
                         count_clean_up (function (_ value)
                                          (do !
                                            [_ (thread.update! ++ clean_up_counter)]
                                            (in {try.#Success []})))]
                   outcome (/.run! !
                                   (do [! (/.monad !)]
                                     [_ (monad.each ! (/.acquire! //@ count_clean_up)
                                                    (enum.range n.enum 1 expected_clean_ups))
                                      _ (/.failure //@ (exception.error ..oops []))]
                                     (in [])))
                   actual_clean_ups (thread.read! clean_up_counter)]
                  (in (and (..throws? ..oops outcome)
                           (n.= expected_clean_ups
                                actual_clean_ups))))))
             (_.coverage [/.except]
               (thread.result
                (do [! thread.monad]
                  [clean_up_counter (thread.box 0)
                   .let [//@ !
                         count_clean_up (function (_ value)
                                          (do !
                                            [_ (thread.update! ++ clean_up_counter)]
                                            (in {try.#Success []})))]
                   outcome (/.run! !
                                   (do [! (/.monad !)]
                                     [_ (monad.each ! (/.acquire! //@ count_clean_up)
                                                    (enum.range n.enum 1 expected_clean_ups))
                                      _ (/.except //@ ..oops [])]
                                     (in [])))
                   actual_clean_ups (thread.read! clean_up_counter)]
                  (in (and (..throws? ..oops outcome)
                           (n.= expected_clean_ups
                                actual_clean_ups))))))
             (_.coverage [/.acquire! /.clean_up_error]
               (thread.result
                (do [! thread.monad]
                  [clean_up_counter (thread.box 0)
                   .let [//@ !
                         count_clean_up (function (_ value)
                                          (do !
                                            [_ (thread.update! ++ clean_up_counter)]
                                            (in (is (Try Any)
                                                    (exception.except ..oops [])))))]
                   outcome (/.run! !
                                   (do [! (/.monad !)]
                                     [_ (monad.each ! (/.acquire! //@ count_clean_up)
                                                    (enum.range n.enum 1 expected_clean_ups))]
                                     (in [])))
                   actual_clean_ups (thread.read! clean_up_counter)]
                  (in (and (or (n.= 0 expected_clean_ups)
                               (..throws? /.clean_up_error outcome))
                           (n.= expected_clean_ups
                                actual_clean_ups))))))
             (_.coverage [/.lifted]
               (thread.result
                (do [! thread.monad]
                  [clean_up_counter (thread.box 0)
                   .let [//@ !]
                   outcome (/.run! !
                                   (do (/.monad !)
                                     [_ (/.lifted //@ (thread.write! expected_clean_ups clean_up_counter))]
                                     (in [])))
                   actual_clean_ups (thread.read! clean_up_counter)]
                  (in (and (..success? outcome)
                           (n.= expected_clean_ups
                                actual_clean_ups))))))
             ))))
