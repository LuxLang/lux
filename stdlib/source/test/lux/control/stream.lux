... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [functor (.only Functor)
     [\\test (.only Comparison)]]
    ["[0]" monad (.only Monad do)
     ["[1]T" \\test]]]
   [data
    ["[0]" product]
    [collection
     ["[0]" sequence (.only sequence)]]]
   [math
    ["[0]" random]
    [number
     [/64
      ["[0]" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the (comparison yield_equivalence)
  (for_any (_ yield)
    (-> (Equivalence yield)
        (Comparison (/.Stream yield))))
  (function (_ value_equivalence reference it)
    (let [[reference_yield reference_value] (/.sequence reference)
          [it_yield it_value] (/.sequence it)]
      (and (by (sequence.equivalence yield_equivalence) = reference_yield it_yield)
           (by value_equivalence = reference_value reference_value)))))

(the .public test
  Test
  (<| (_.covering /._)
      (do random.monad
        [_0 random.natural
         _1 random.natural
         _2 random.natural])
      (_.for [/.Effect /.Stream])
      (all _.and
           (_.for [/.monad]
                  (monadT.spec (by /.monad pure) (..comparison natural.equivalence) /.monad))

           (_.coverage [/.one /.mix]
             (|> (do /.monad
                   [_ (/.one _0)
                    _ (/.one _1)
                    _ (/.one _2)]
                   (pure []))
                 (/.mix natural.+ 0)
                 product.left
                 (natural.= (all natural.+ _0 _1 _2))))
           (_.coverage [/.each]
             (|> (do /.monad
                   [_ (/.one _0)
                    _ (/.one _1)
                    _ (/.one _2)]
                   (pure []))
                 (/.each ++)
                 (/.mix natural.+ 0)
                 product.left
                 (natural.= (all natural.+ (++ _0) (++ _1) (++ _2)))))
           (_.coverage [/.only]
             (let [? (natural.> _0)
                   expected (|> (sequence _0 _1 _2)
                                (sequence.only ?)
                                (sequence.mix natural.+ 0))]
               (|> (do /.monad
                     [_ (/.one _0)
                      _ (/.one _1)
                      _ (/.one _2)]
                     (pure []))
                   (/.only ?)
                   (/.mix natural.+ 0)
                   product.left
                   (natural.= expected))))
           (_.coverage [/.sequence]
             (|> (do /.monad
                   [_ (/.one _1)

                    _ (/.one _2)
                    _ (/.one _2)]
                   (pure []))
                 /.sequence
                 product.left
                 (by (sequence.equivalence natural.equivalence) =
                     (sequence _1
                               _2 _2))))
           (_.coverage [/.many]
             (|> (do /.monad
                   [_ (/.many (list))
                    _ (/.many (list _1))
                    _ (/.many (list _2 _2))]
                   (pure []))
                 /.sequence
                 product.left
                 (by (sequence.equivalence natural.equivalence) =
                     (sequence _1
                               _2 _2))))
           (_.coverage [/.next]
             (and (when (/.next (by /.monad pure _0))
                    {.#Left it}
                    (same? _0 it)

                    else
                    false)
                  (when (/.next (/.one _0))
                    {.#Right [head tail]}
                    (same? _0 head)

                    else
                    false)))
           )))
