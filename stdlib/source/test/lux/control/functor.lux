(.module:
  [lux #*
   [control
    [monad (#+ do)]]
   data/text/format
   ["." function]
   [math
    ["r" random]]
   ["_" test (#+ Test)]]
  {1
   ["." / (#+ Functor)]})

(type: #export (Injection f)
  (All [a] (-> a (f a))))

(type: #export (Comparison f)
  (All [a]
    (-> (-> a a Bit)
        (-> (f a) (f a) Bit))))

(def: (identity injection comparison (^open "_/."))
  (All [f] (-> (Injection f) (Comparison f) (Functor f) Test))
  (do r.monad
    [sample (:: @ map injection r.nat)]
    (_.test "Identity."
            ((comparison n/=)
             (_/map function.identity sample)
             sample))))

(def: (homomorphism injection comparison (^open "_/."))
  (All [f] (-> (Injection f) (Comparison f) (Functor f) Test))
  (do r.monad
    [sample r.nat
     increase (:: @ map n/+ r.nat)]
    (_.test "Homomorphism."
            ((comparison n/=)
             (_/map increase (injection sample))
             (injection (increase sample))))))

(def: (composition injection comparison (^open "_/."))
  (All [f] (-> (Injection f) (Comparison f) (Functor f) Test))
  (do r.monad
    [sample (:: @ map injection r.nat)
     increase (:: @ map n/+ r.nat)
     decrease (:: @ map n/- r.nat)]
    (_.test "Composition."
            ((comparison n/=)
             (|> sample (_/map increase) (_/map decrease))
             (|> sample (_/map (|>> increase decrease)))))))

(def: #export (laws injection comparison functor)
  (All [f] (-> (Injection f) (Comparison f) (Functor f) Test))
  (_.context (%name (name-of /.Functor))
             ($_ _.and
                 (..identity injection comparison functor)
                 (..homomorphism injection comparison functor)
                 (..composition injection comparison functor)
                 )))
