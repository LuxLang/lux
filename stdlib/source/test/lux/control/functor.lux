(.module:
  [lux #*
   ["_" test (#+ Test)]
   data/text/format
   ["r" math/random]
   [control
    [equivalence (#+ Equivalence)]
    [monad (#+ do)]]
   ["." function]]
  {1
   ["." / (#+ Functor)]})

(type: #export (Injection f)
  (All [a] (-> a (f a))))

(type: #export (Comparison f)
  (All [a]
    (-> (Equivalence a)
        (Equivalence (f a)))))

(def: (identity injection comparison (^open "/@."))
  (All [f] (-> (Injection f) (Comparison f) (Functor f) Test))
  (do r.monad
    [sample (:: @ map injection r.nat)]
    (_.test "Identity."
            ((comparison n/=)
             (/@map function.identity sample)
             sample))))

(def: (homomorphism injection comparison (^open "/@."))
  (All [f] (-> (Injection f) (Comparison f) (Functor f) Test))
  (do r.monad
    [sample r.nat
     increase (:: @ map n/+ r.nat)]
    (_.test "Homomorphism."
            ((comparison n/=)
             (/@map increase (injection sample))
             (injection (increase sample))))))

(def: (composition injection comparison (^open "/@."))
  (All [f] (-> (Injection f) (Comparison f) (Functor f) Test))
  (do r.monad
    [sample (:: @ map injection r.nat)
     increase (:: @ map n/+ r.nat)
     decrease (:: @ map n/- r.nat)]
    (_.test "Composition."
            ((comparison n/=)
             (|> sample (/@map increase) (/@map decrease))
             (|> sample (/@map (|>> increase decrease)))))))

(def: #export (spec injection comparison functor)
  (All [f] (-> (Injection f) (Comparison f) (Functor f) Test))
  (_.context (%name (name-of /.Functor))
             ($_ _.and
                 (..identity injection comparison functor)
                 (..homomorphism injection comparison functor)
                 (..composition injection comparison functor)
                 )))
