... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)
     ["[1]T" \\test]]
    ["[0]" functor
     ["[1]T" \\test]]
    ["[0]" apply
     ["[1]T" \\test]]
    ["[0]" hash
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [control
    ["[0]" io (.use "[1]#[0]" monad)]
    ["[0]" pipe]]
   [data
    ["[0]" text]
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.use "[1]#[0]" monoid monad)]])

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [.Maybe
              .#None .#Some])
      (all _.and
           (_.for [/.equivalence]
                  (equivalenceT.spec (/.equivalence n.equivalence) (random.maybe random.natural)))
           (_.for [/.hash]
                  (|> random.natural
                      (by random.monad each (|>> {.#Some}))
                      (hashT.spec (/.hash n.hash))))
           (_.for [/.monoid]
                  (monoidT.spec (/.equivalence n.equivalence) /.monoid (random.maybe random.natural)))
           (_.for [/.functor]
                  (functorT.spec /#in /.equivalence /.functor))
           (_.for [/.apply]
                  (applyT.spec /#in /.equivalence /.apply))
           (_.for [/.monad]
                  (monadT.spec /#in /.equivalence /.monad))
           
           (do random.monad
             [left random.natural
              right random.natural
              .let [expected (n.+ left right)]]
             (let [lifted (/.lifted io.monad)]
               (_.coverage [/.With /.with /.lifted]
                 (|> (io.value (do (/.with io.monad)
                                 [a (lifted (io#in left))
                                  b (in right)]
                                 (in (n.+ a b))))
                     (pipe.when
                       {.#Some actual}
                       (n.= expected actual)

                       _
                       false)))))
           (do random.monad
             [default random.natural
              value random.natural]
             (_.coverage [/.else]
               (and (same? default (/.else default
                                           (is (Maybe Natural)
                                               {.#None})))

                    (same? value (/.else default
                                         {.#Some value})))))
           (do random.monad
             [value random.natural]
             (_.coverage [/.trusted]
               (same? value (/.trusted {.#Some value}))))
           (do random.monad
             [value random.natural]
             (_.coverage [/.list]
               (by (list.equivalence n.equivalence) =
                   (list value)
                   (/.list {.#Some value}))))
           (do random.monad
             [expected random.natural
              .let [(open "/#[0]") (/.equivalence n.equivalence)]]
             (_.coverage [/.when]
               (and (/#= {.#Some expected} (/.when true {.#Some expected}))
                    (/#= {.#None} (/.when false {.#Some expected})))))
           (do random.monad
             [expected random.natural
              alternative (|> random.natural (random.only (|>> (n.= expected) not)))]
             (_.coverage [/.or]
               (and (when (/.or {.#Some expected}
                                {.#Some alternative})
                      {.#Some actual}
                      (same? expected actual)

                      {.#None}
                      false)
                    (when (/.or {.#None}
                                {.#Some alternative})
                      {.#Some actual}
                      (same? alternative actual)

                      {.#None}
                      false))))
           )))
