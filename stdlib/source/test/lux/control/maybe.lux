... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only)
     ["[1]T" \\test]]
    ["[0]" functor
     ["[1]T" \\test]]
    ["[0]" hash
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [control
    ["[0]" io (.use "[1]:[0]" monad)]
    ["[0]" pipe]]
   [data
    ["[0]" text]
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.use "[1]:[0]" monoid monad)]])

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [.Maybe
              .#None .#Some])
      (all _.and
           (_.for [/.equivalence]
                  (equivalenceT.spec (/.equivalence n.equivalence) (random.maybe random.natural)))
           (_.for [/.hash]
                  (|> random.natural
                      (by random.monad each (|>> {.#Some}))
                      (hashT.spec (/.hash n.hash))))
           (_.for [/.monoid]
                  (monoidT.spec (/.equivalence n.equivalence) /.monoid (random.maybe random.natural)))
           (_.for [/.functor]
                  (functorT.spec /:pure /.equivalence /.functor))
           (_.for [/.monad]
                  (monadT.spec /:pure /.equivalence /.monad))
           
           (monad.let random.monad
             [left random.natural
              right random.natural
              .let [expected (n.+ left right)]]
             (let [lifted (/.lifted io.monad)]
               (_.coverage [/.With /.with /.lifted]
                 (|> (io.value (monad.let (/.with io.monad)
                                 [a (lifted (io:pure left))
                                  b (pure right)]
                                 (pure (n.+ a b))))
                     (pipe.when
                       {.#Some actual}
                       (n.= expected actual)

                       _
                       false)))))
           (monad.let random.monad
             [default random.natural
              value random.natural]
             (_.coverage [/.else]
               (and (identical? default (/.else default
                                                (is (Maybe Natural)
                                                    {.#None})))

                    (identical? value (/.else default
                                              {.#Some value})))))
           (monad.let random.monad
             [value random.natural]
             (_.coverage [/.trusted]
               (identical? value (/.trusted {.#Some value}))))
           (monad.let random.monad
             [expected random.natural
              .let [(open "/:[0]") (/.equivalence n.equivalence)]]
             (_.coverage [/.if]
               (and (/:= {.#Some expected} (/.if true {.#Some expected}))
                    (/:= {.#None} (/.if false {.#Some expected})))))
           (monad.let random.monad
             [expected random.natural
              alternative (|> random.natural (random.only (|>> (n.= expected) not)))]
             (_.coverage [/.or]
               (and (when (/.or {.#Some expected}
                                {.#Some alternative})
                      {.#Some actual}
                      (identical? expected actual)

                      {.#None}
                      false)
                    (when (/.or {.#None}
                                {.#Some alternative})
                      {.#Some actual}
                      (identical? alternative actual)

                      {.#None}
                      false))))
           (monad.let random.monad
             [expected random.natural]
             (_.coverage [/.as_list]
               (with (list.equivalence n.equivalence)
                 (and (= (list)
                         (/.as_list {.#None}))
                      (= (list expected)
                         (/.as_list {.#Some expected}))))))
           )))
