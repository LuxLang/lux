(.module:
  [lux #*
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do Monad)]
    {[0 #test]
     [/
      ["$." functor (#+ Injection Comparison)]
      ["$." apply]
      ["$." monad]
      ["$." equivalence]]}]
   [control
    pipe
    ["." io]]
   [data
    ["." text ("#@." equivalence)]
    [number
     ["n" nat]]]
   [math
    ["." random (#+ Random)]]]
  {1
   ["." / (#+ Try)]})

(def: injection
  (Injection Try)
  (|>> #/.Success))

(def: comparison
  (Comparison Try)
  (function (_ ==)
    (:: (/.equivalence ==) =)))

(def: #export (try element)
  (All [a] (-> (Random a) (Random (Try a))))
  ($_ random.or
      (random.unicode 1)
      element))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.with-cover [/.Try])
      (do random.monad
        [expected random.nat
         alternative (|> random.nat (random.filter (|>> (n.= expected) not)))
         error (random.unicode 1)
         #let [(^open "io@.") io.monad]])
      ($_ _.and
          (_.with-cover [/.equivalence]
            ($equivalence.spec (/.equivalence n.equivalence) (..try random.nat)))
          (_.with-cover [/.functor]
            ($functor.spec ..injection ..comparison /.functor))
          (_.with-cover [/.apply]
            ($apply.spec ..injection ..comparison /.apply))
          (_.with-cover [/.monad]
            ($monad.spec ..injection ..comparison /.monad))

          (_.cover [/.succeed]
                   (case (/.succeed expected)
                     (#/.Success actual)
                     (n.= expected actual)

                     _
                     false))
          (_.cover [/.fail]
                   (case (/.fail error)
                     (#/.Failure message)
                     (text@= error message)

                     _
                     false))
          (_.cover [/.assume]
                   (n.= expected
                        (/.assume (/.succeed expected))))
          (_.cover [/.to-maybe]
                   (case [(/.to-maybe (/.succeed expected))
                          (/.to-maybe (/.fail error))]
                     [(#.Some actual) #.None]
                     (n.= expected actual)

                     _
                     false))
          (_.cover [/.default]
                   (and (n.= expected
                             (/.default alternative (/.succeed expected)))
                        (n.= alternative
                             (/.default alternative (: (Try Nat)
                                                       (/.fail error))))))
          
          (_.cover [/.with /.lift]
                   (let [lift (/.lift io.monad)]
                     (|> (do (/.with io.monad)
                           [a (lift (io@wrap expected))
                            b (wrap alternative)]
                           (wrap (n.+ a b)))
                         io.run
                         (case> (#/.Success result)
                                (n.= (n.+ expected alternative)
                                     result)

                                _
                                false))))
          )))
