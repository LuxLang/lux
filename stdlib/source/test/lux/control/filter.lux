... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]
     ["[0]" sequence]]]
   [math
    ["[0]" random]
    [number
     [/64
      ["[0]" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["//" stream]]]])

(the .public test
  Test
  (<| (_.covering /._)
      (monad.let [! random.monad]
        [input (random.list 10 random.natural)])
      (_.for [/.Effect /.Filter])
      (_.for [/.stream])
      (all _.and
           (_.coverage [/.read /.yield /.monad]
             (|> (//.many input)
                 (/.stream (monad.let /.monad
                             [it /.read]
                             (/.yield it)))
                 //.sequence
                 product.left
                 (by (sequence.equivalence natural.equivalence) =
                     (sequence.of_list (list.first 1 input)))))
           (_.coverage [/.identity]
             (|> (//.many input)
                 (/.stream /.identity)
                 //.sequence
                 product.left
                 (by (sequence.equivalence natural.equivalence) =
                     (sequence.of_list input))))
           (_.coverage [/.each]
             (|> (//.many input)
                 (/.stream (/.each ++))
                 //.sequence
                 product.left
                 (by (sequence.equivalence natural.equivalence) =
                     (sequence.of_list (list#each ++ input)))))
           (_.coverage [/.only]
             (|> (//.many input)
                 (/.stream (/.only natural.even?))
                 //.sequence
                 product.left
                 (by (sequence.equivalence natural.equivalence) =
                     (sequence.of_list (list.only natural.even? input)))))
           )
      ))
