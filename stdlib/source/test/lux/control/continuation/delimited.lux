... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [data
    [collection
     ["[0]" list]]
    [text
     ["%" \\injection]]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [meta
    ["[0]" type]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" //]]])

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [elems (random.list 3 random.nat)

         left random.nat
         right random.nat])
      (all _.and
           (_.coverage [/.Static
                        /.reset /.context
                        /.shift /.exit]
             (and (let [(open "//#[0]") //.monad
                        (open "list#[0]") (list.equivalence n.equivalence)
                        visit (is (-> (List Nat)
                                      (//.Cont (List Nat) (List Nat)))
                                  (function (visit it)
                                    (when it
                                      {.#End}
                                      (//#in {.#End})

                                      {.#Item head tail}
                                      (do [! //.monad]
                                        [output (/.shift (function (_ after)
                                                           (in {.#Item head (after tail)})))]
                                        (visit output)))))]
                    (//.continued (list#= elems)
                                  (/.reset (visit elems))))
                  (same? /.reset /.context)
                  (same? /.shift /.exit)))
           (_.coverage [/.Dynamic
                        /.prompt /.control]
             (and (|> (do [! //.monad]
                        [it (/.shift (function (_ after)
                                       (do !
                                         [.let [it (after right)]]
                                         (in (n.* left it)))))]
                        (/.shift (function (_ after)
                                   (in it))))
                      (is (//.Cont Nat (/.Static Nat)))
                      /.reset
                      //.value
                      (n.= (n.* left right)))
                  (|> (do [! //.monad]
                        [it (/.control (function (_ after)
                                         (do !
                                           [it (after right)]
                                           (in (n.* left it)))))]
                        (/.control (function (_ after)
                                     (in it))))
                      (is (//.Cont Nat (/.Dynamic Nat)))
                      /.prompt
                      //.value
                      (same? right))
                  ))
           (_.coverage [/.Propagated
                        /.propagated
                        /.reset_0 /.shift_0]
             (and (|> (/.reset (is (//.Cont Nat (/.Static Nat))
                                   (do [! //.monad]
                                     [it (is (//.Cont Nat (/.Static Nat))
                                             (/.reset (/.shift (function (_ after)
                                                                 (/.shift (function (_ after)
                                                                            (in right)))))))]
                                     (in (n.* left it)))))
                      //.value
                      (n.= (n.* left right)))
                  (|> (/.reset_0 (is (//.Cont Nat (/.Propagated Nat))
                                     (do [! //.monad]
                                       [it (is (//.Cont Nat (/.Propagated Nat))
                                               (/.reset_0 (/.shift_0 (function (_ after)
                                                                       (/.shift_0 (function (_ after)
                                                                                    (in right)))))))]
                                       (in (n.* left it)))))
                      /.propagated
                      //.value
                      (n.= right))
                  ))
           )))
