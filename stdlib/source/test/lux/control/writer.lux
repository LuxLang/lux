... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [monoid (.only Monoid)]
    ["[0]" monad (.only do)
     ["[1]T" \\test]]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]
    ["[0]" apply
     ["[1]T" \\test]]]
   [control
    ["[0]" io]]
   [data
    ["[0]" text]]
   [math
    ["[0]" random]
    [number
     ["n" natural]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only Writer)]])

(the (injection monoid value)
  (for_any (_ w)
    (-> (Monoid w)
        (Injection (Writer w))))
  [(of monoid identity) value])

(the comparison
  (for_any (_ w)
    (Comparison (Writer w)))
  (function (_ == [_ left] [_ right])
    (== left right)))

(the .public test
  Test
  (do random.monad
    [trace (random.ascii 1)
     left random.natural
     right random.natural]
    (<| (_.covering /._)
        (_.for [/.Writer
                /.#trace /.#value])
        (all _.and
             (_.for [/.functor]
                    (functorT.spec (..injection text.monoid) ..comparison /.functor))
             (_.for [/.apply]
                    (applyT.spec (..injection text.monoid) ..comparison (/.apply text.monoid)))
             (_.for [/.monad]
                    (monadT.spec (..injection text.monoid) ..comparison (/.monad text.monoid)))

             (_.coverage [/.write]
               (text.= trace
                       (its /.#trace (/.write trace))))
             (_.coverage [/.With /.with /.lifted]
               (let [lifted (/.lifted text.monoid io.monad)
                     (open "io#[0]") io.monad]
                 (|> (do (/.with text.monoid io.monad)
                       [a (lifted (io#in left))
                        b (in right)]
                       (in (n.+ a b)))
                     io.value
                     (its /.#value)
                     (n.= (n.+ left right)))))
             ))))
