(.module:
  [lux #*
   ["_" test (#+ Test)]
   [abstract
    [equivalence (#+ Equivalence)]
    [monoid (#+ Monoid)]
    [monad (#+ do)]
    {[0 #test]
     [/
      ["$." functor (#+ Injection Comparison)]
      ["$." apply]
      ["$." monad]]}]
   [control
    ["." io]]
   [data
    ["." product]
    ["." text ("#;." equivalence)
     format]]
   [math
    ["r" random]]]
  {1
   ["." / (#+ Writer)]})

(def: (injection monoid value)
  (All [w] (-> (Monoid w) (Injection (Writer w))))
  [(:: monoid identity) value])

(def: comparison
  (All [w] (Comparison (Writer w)))
  (function (_ == [_ left] [_ right])
    (== left right)))

(def: #export test
  Test
  (do r.monad
    [log (r.ascii 1)
     left r.nat
     right r.nat]
    (<| (_.context (%name (name-of /.Writer)))
        ($_ _.and
            ($functor.spec (..injection text.monoid) ..comparison /.functor)
            ($apply.spec (..injection text.monoid) ..comparison (/.apply text.monoid))
            ($monad.spec (..injection text.monoid) ..comparison (/.monad text.monoid))

            (_.test "Can write any value."
                    (text;= log
                            (product.left (/.write log))))
            (let [lift (/.lift text.monoid io.monad)
                  (^open "io;.") io.monad]
              (_.test "Can add writer functionality to any monad."
                      (|> (io.run (do (/.with text.monoid io.monad)
                                    [a (lift (io;wrap left))
                                     b (wrap right)]
                                    (wrap (n/+ a b))))
                          product.right
                          (n/= (n/+ left right)))))
            ))))
