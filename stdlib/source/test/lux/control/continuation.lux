... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)
     ["[1]T" \\test]]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]
    ["[0]" apply
     ["[1]T" \\test]]]
   [data
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(def injection
  (All (_ o) (Injection (All (_ i) (/.Cont i o))))
  (|>> /.pending))

(def comparison
  (Comparison /.Cont)
  (function (_ == left right)
    (== (/.value left) (/.value right))))

(def .public test
  Test
  (<| (_.covering /._)
      (do random.monad
        [sample random.nat
         .let [(open "_#[0]") /.apply
               (open "_#[0]") /.monad]
         elems (random.list 3 random.nat)])
      (_.for [/.Cont])
      (all _.and
           (_.for [/.functor]
                  (functorT.spec ..injection ..comparison /.functor))
           (_.for [/.apply]
                  (applyT.spec ..injection ..comparison /.apply))
           (_.for [/.monad]
                  (monadT.spec ..injection ..comparison /.monad))

           (_.coverage [/.value]
             (n.= sample (/.value (_#in sample))))
           (_.coverage [/.with_current]
             (n.= (n.* 2 sample)
                  (/.value (do [! /.monad]
                             [value (/.with_current
                                      (function (_ k)
                                        (do !
                                          [temp (k sample)]
                                          ... If this code where to run,
                                          ... the output would be
                                          ... (n.* 4 sample)
                                          (k temp))))]
                             (in (n.* 2 value))))))
           (_.coverage [/.portal]
             (n.= (n.+ 100 sample)
                  (/.value (do /.monad
                             [[restart [output idx]] (/.portal [sample 0])]
                             (if (n.< 10 idx)
                               (restart [(n.+ 10 output) (++ idx)])
                               (in output))))))
           (_.coverage [/.shift /.reset]
             (let [(open "_#[0]") /.monad
                   (open "list#[0]") (list.equivalence n.equivalence)
                   visit (is (-> (List Nat)
                                 (/.Cont (List Nat) (List Nat)))
                             (function (visit xs)
                               (when xs
                                 {.#End}
                                 (_#in {.#End})

                                 {.#Item x xs'}
                                 (do [! /.monad]
                                   [output (/.shift (function (_ k)
                                                      (do !
                                                        [tail (k xs')]
                                                        (in {.#Item x tail}))))]
                                   (visit output)))))]
               (list#= elems
                       (/.value (/.reset (visit elems))))))
           (_.coverage [/.continued]
             (/.continued (same? sample)
                          (is (/.Cont Nat Bit)
                              (function (_ next)
                                (next sample)))))
           (_.coverage [/.pending]
             (/.continued (same? sample)
                          (is (/.Cont Nat Bit)
                              (/.pending sample))))
           )))
