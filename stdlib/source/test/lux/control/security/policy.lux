... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [hash (.only Hash)]
    ["[0]" monad (.only do)
     ["[1]T" \\test]]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]
    ["[0]" apply
     ["[1]T" \\test]]]
   [data
    ["[0]" text (.use "[1]#[0]" equivalence)]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [meta
    [macro
     ["^" pattern]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only Context Privacy Can_Conceal Can_Reveal Privilege Private)]])

(the (injection can_conceal)
  (All (_ label)
    (-> (Can_Conceal label) (Injection (All (_ value) (Private value label)))))
  can_conceal)

(the (comparison can_reveal)
  (All (_ label)
    (-> (Can_Reveal label) (Comparison (All (_ value) (Private value label)))))
  (function (_ == left right)
    (== (can_reveal left)
        (can_reveal right))))

(every Password
  (Private Text))

(every (Policy %)
  (Interface
   (is (Hash (Password %))
       &hash)

   (is (-> Text (Password %))
       password)

   (is (Privilege Privacy %)
       privilege)))

(the (policy _)
  (Ex (_ %) (-> Any (Policy %)))
  (/.with_policy
    (is (Context Privacy Policy)
        (function (_ (^.let privilege (open "%[0]")))
          (implementation
           (the &hash
             (implementation
              (the equivalence
                (implementation
                 (the (= reference sample)
                   (text#= (%#can_downgrade reference)
                           (%#can_downgrade sample)))))
              (the hash
                (|>> %#can_downgrade
                     (of text.hash hash)))))
           
           (the password
             %#can_upgrade)

           (the privilege
             privilege))))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Policy
              /.Can_Upgrade /.Can_Downgrade])
      (do random.monad
        [.let [policy_0 (policy [])]
         raw_password (random.ascii 10)
         .let [password (of policy_0 password raw_password)]]
        (all _.and
             (_.for [/.Privacy /.Private /.Can_Conceal /.Can_Reveal
                     /.Safety /.Safe /.Can_Trust /.Can_Distrust]
                    (all _.and
                         (_.for [/.functor]
                                (functorT.spec (..injection (of policy_0 #can_upgrade)) (..comparison (of policy_0 #can_downgrade)) /.functor))
                         (_.for [/.apply]
                                (applyT.spec (..injection (of policy_0 #can_upgrade)) (..comparison (of policy_0 #can_downgrade)) /.apply))
                         (_.for [/.monad]
                                (monadT.spec (..injection (of policy_0 #can_upgrade)) (..comparison (of policy_0 #can_downgrade)) /.monad))))

             (_.coverage [/.Privilege
                          /.#can_upgrade /.#can_downgrade

                          /.Context /.with_policy]
               (and (of policy_0 = password password)
                    (n.= (of text.hash hash raw_password)
                         (of policy_0 hash password))))
             (let [policy_1 (policy [])
                   delegate (/.delegation (of policy_0 #can_downgrade) (of policy_1 #can_upgrade))]
               (_.coverage [/.Delegation /.delegation]
                 (of policy_1 = (delegate password) (delegate password))))
             ))))
