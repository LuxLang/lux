(.module:
  [lux (#- primitive)
   ["_" test (#+ Test)]
   [abstract
    [equivalence (#+ Equivalence)]
    [predicate (#+ Predicate)]
    [monad (#+ do)]]
   [control
    [pipe (#+ case>)]
    ["." try]
    ["." exception]
    ["<>" parser]]
   [data
    ["." binary]
    ["." sum]
    ["." maybe]
    ["." bit]
    ["." name]
    ["." text ("#\." equivalence)
     ["." encoding]
     ["%" format (#+ format)]]
    ["." format #_
     ["#" binary]]
    [number
     ["." i64]
     ["n" nat]
     ["." int]
     ["." rev]
     ["." frac]]
    [collection
     ["." list]
     ["." row]
     ["." set]]]
   [macro
    ["." code]]
   ["." type]
   [math
    ["." random (#+ Random)]]]
  {1
   ["." /]})

(template: (!expect <expectation> <computation>)
  (case <computation>
    <expectation>
    true

    _
    false))

(def: segment_size 10)

(def: (utf8_conversion_does_not_alter? value)
  (Predicate Text)
  (|> value
      (\ encoding.utf8 encode)
      (\ encoding.utf8 decode)
      (case> (#try.Success converted)
             (text\= value converted)
             
             (#try.Failure error)
             false)))

(def: random_text
  (Random Text)
  (random.filter ..utf8_conversion_does_not_alter?
                 (random.unicode ..segment_size)))

(def: random_name
  (Random Name)
  (random.and ..random_text ..random_text))

(structure: location_equivalence
  (Equivalence Location)

  (def: (= [expected_module expected_line expected_column]
           [sample_module sample_line sample_column])
    (and (text\= expected_module sample_module)
         (n.= expected_line sample_line)
         (n.= expected_column sample_column))))

(def: random_location
  (Random Location)
  ($_ random.and
      ..random_text
      random.nat
      random.nat))

(def: random_code
  (Random Code)
  (random.rec
   (function (_ recur)
     (let [random_sequence (do {! random.monad}
                             [size (\ ! map (n.% 2) random.nat)]
                             (random.list size recur))]
       ($_ random.and
           ..random_location
           (: (Random (Code' (Ann Location)))
              ($_ random.or
                  random.bit
                  random.nat
                  random.int
                  random.rev
                  random.safe_frac
                  ..random_text
                  ..random_name
                  ..random_name
                  random_sequence
                  random_sequence
                  (do {! random.monad}
                    [size (\ ! map (n.% 2) random.nat)]
                    (random.list size (random.and recur recur)))
                  )))))))

(def: random_type
  (Random Type)
  (let [(^open ".") random.monad]
    ($_ random.either
        (wrap .Nat)
        (wrap .List)
        (wrap .Code)
        (wrap .Type))))

(def: size
  Test
  (<| (_.for [/.Size])
      (`` ($_ _.and
              (~~ (template [<size> <parser> <format>]
                    [(do {! random.monad}
                       [expected (\ ! map (i64.and (i64.mask <size>))
                                    random.nat)]
                       (_.cover [<size> <parser> <format>]
                                (|> (format.run <format> expected)
                                    (/.run <parser>)
                                    (!expect (^multi (#try.Success actual)
                                                     (n.= (.nat expected)
                                                          (.nat actual)))))))]

                    [/.size/8 /.bits/8 format.bits/8]
                    [/.size/16 /.bits/16 format.bits/16]
                    [/.size/32 /.bits/32 format.bits/32]
                    [/.size/64 /.bits/64 format.bits/64]
                    ))))))

(def: binary
  Test
  (`` ($_ _.and
          (~~ (template [<parser> <format>]
                [(do {! random.monad}
                   [expected (\ ! map (\ encoding.utf8 encode) (random.ascii ..segment_size))]
                   (_.cover [<parser> <format>]
                            (|> (format.run <format> expected)
                                (/.run <parser>)
                                (!expect (^multi (#try.Success actual)
                                                 (\ binary.equivalence = expected actual))))))]

                [/.binary/8 format.binary/8]
                [/.binary/16 format.binary/16]
                [/.binary/32 format.binary/32]
                [/.binary/64 format.binary/64]
                )))))

(def: utf8
  Test
  (`` ($_ _.and
          (~~ (template [<parser> <format>]
                [(do {! random.monad}
                   [expected (random.ascii ..segment_size)]
                   (_.cover [<parser> <format>]
                            (|> (format.run <format> expected)
                                (/.run <parser>)
                                (!expect (^multi (#try.Success actual)
                                                 (\ text.equivalence = expected actual))))))]

                [/.utf8/8 format.utf8/8]
                [/.utf8/16 format.utf8/16]
                [/.utf8/32 format.utf8/32]
                [/.utf8/64 format.utf8/64]
                [/.text format.text]
                )))))

(def: row
  Test
  (`` ($_ _.and
          (~~ (template [<parser> <format>]
                [(do {! random.monad}
                   [expected (random.row ..segment_size random.nat)]
                   (_.cover [<parser> <format>]
                            (|> expected
                                (format.run (<format> format.nat))
                                (/.run (<parser> /.nat))
                                (!expect (^multi (#try.Success actual)
                                                 (\ (row.equivalence n.equivalence) = expected actual))))))]

                [/.row/8 format.row/8]
                [/.row/16 format.row/16]
                [/.row/32 format.row/32]
                [/.row/64 format.row/64]
                )))))

(def: simple
  Test
  (`` ($_ _.and
          (~~ (template [<parser> <format> <random> <equivalence>]
                [(do {! random.monad}
                   [expected <random>]
                   (_.cover [<parser> <format>]
                            (|> expected
                                (format.run <format>)
                                (/.run <parser>)
                                (!expect (^multi (#try.Success actual)
                                                 (\ <equivalence> = expected actual))))))]

                [/.bit format.bit random.bit bit.equivalence]
                [/.nat format.nat random.nat n.equivalence]
                [/.int format.int random.int int.equivalence]
                [/.rev format.rev random.rev rev.equivalence]))
          (do {! random.monad}
            [expected random.frac]
            (_.cover [/.frac format.frac]
                     (|> expected
                         (format.run format.frac)
                         (/.run /.frac)
                         (!expect (^multi (#try.Success actual)
                                          (or (\ frac.equivalence = expected actual)
                                              (and (frac.not_a_number? expected)
                                                   (frac.not_a_number? actual))))))))
          (do {! random.monad}
            [expected (\ ! map (|>> (i64.and (i64.mask /.size/8))
                                    (n.max 2))
                         random.nat)]
            (_.cover [/.not_a_bit]
                     (|> expected
                         (format.run format.bits/8)
                         (/.run /.bit)
                         (!expect (^multi (#try.Failure error)
                                          (exception.match? /.not_a_bit error))))))
          )))

(def: complex
  Test
  (`` ($_ _.and
          (~~ (template [<parser> <format> <random> <equivalence>]
                [(do {! random.monad}
                   [expected <random>]
                   (_.cover [<parser> <format>]
                            (|> expected
                                (format.run <format>)
                                (/.run <parser>)
                                (!expect (^multi (#try.Success actual)
                                                 (\ <equivalence> = expected actual))))))]

                [/.location format.location random_location location_equivalence]
                [/.code format.code random_code code.equivalence]
                [/.type format.type random_type type.equivalence]
                ))
          (~~ (template [<parser_coverage> <parser> <format_coverage> <format> <random> <equivalence>]
                [(do {! random.monad}
                   [expected <random>]
                   (_.cover [<parser_coverage> <format_coverage>]
                            (|> expected
                                (format.run <format>)
                                (/.run <parser>)
                                (!expect (^multi (#try.Success actual)
                                                 (\ <equivalence> = expected actual))))))]

                [/.maybe (/.maybe /.nat) format.maybe (format.maybe format.nat) (random.maybe random.nat) (maybe.equivalence n.equivalence)]
                [/.list (/.list /.nat) format.list (format.list format.nat) (random.list ..segment_size random.nat) (list.equivalence n.equivalence)]
                [/.set (/.set n.hash /.nat) format.set (format.set format.nat) (random.set n.hash ..segment_size random.nat) set.equivalence]
                [/.name /.name format.name format.name ..random_name name.equivalence]))
          (do {! random.monad}
            [expected (\ ! map (list.repeat ..segment_size) random.nat)]
            (_.cover [/.set_elements_are_not_unique]
                     (|> expected
                         (format.run (format.list format.nat))
                         (/.run (/.set n.hash /.nat))
                         (!expect (^multi (#try.Failure error)
                                          (exception.match? /.set_elements_are_not_unique error))))))
          (do {! random.monad}
            [expected (random.or random.bit random.nat)]
            (_.cover [/.or format.or]
                     (|> expected
                         (format.run (format.or format.bit format.nat))
                         (/.run (: (/.Parser (Either Bit Nat))
                                   (/.or /.bit /.nat)))
                         (!expect (^multi (#try.Success actual)
                                          (\ (sum.equivalence bit.equivalence n.equivalence) =
                                             expected
                                             actual))))))
          (do {! random.monad}
            [tag (\ ! map (|>> (i64.and (i64.mask /.size/8))
                               (n.max 2))
                    random.nat)
             value random.bit]
            (_.cover [/.invalid_tag]
                     (|> [tag value]
                         (format.run (format.and format.bits/8 format.bit))
                         (/.run (: (/.Parser (Either Bit Nat))
                                   (/.or /.bit /.nat)))
                         (!expect (^multi (#try.Failure error)
                                          (exception.match? /.invalid_tag error))))))
          (do {! random.monad}
            [expected (random.list ..segment_size random.nat)]
            (_.cover [/.rec format.rec format.and format.any]
                     (|> expected
                         (format.run (format.rec (|>> (format.and format.nat)
                                                      (format.or format.any))))
                         (/.run (: (/.Parser (List Nat))
                                   (/.rec
                                    (function (_ recur)
                                      (/.or /.any
                                            (<>.and /.nat
                                                    recur))))))
                         (!expect (^multi (#try.Success actual)
                                          (\ (list.equivalence n.equivalence) =
                                             expected
                                             actual))))))
          )))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [/.Parser])
      (`` ($_ _.and
              (_.cover [/.run /.any
                        format.no_op format.instance]
                       (|> (format.instance format.no_op)
                           (/.run /.any)
                           (!expect (#try.Success _))))
              (do {! random.monad}
                [data (\ ! map (\ encoding.utf8 encode) (random.ascii ..segment_size))]
                (_.cover [/.binary_was_not_fully_read]
                         (|> data
                             (/.run /.any)
                             (!expect (^multi (#try.Failure error)
                                              (exception.match? /.binary_was_not_fully_read error))))))
              (do {! random.monad}
                [expected (\ ! map (\ encoding.utf8 encode) (random.ascii ..segment_size))]
                (_.cover [/.segment format.segment format.run]
                         (|> expected
                             (format.run (format.segment ..segment_size))
                             (/.run (/.segment ..segment_size))
                             (!expect (^multi (#try.Success actual)
                                              (\ binary.equivalence = expected actual))))))
              (do {! random.monad}
                [data (\ ! map (\ encoding.utf8 encode) (random.ascii ..segment_size))]
                (_.cover [/.end?]
                         (|> data
                             (/.run (do <>.monad
                                      [pre /.end?
                                       _ (/.segment ..segment_size)
                                       post /.end?]
                                      (wrap (and (not pre)
                                                 post))))
                             (!expect (#try.Success #1)))))
              (do {! random.monad}
                [to_read (\ ! map (n.% (inc ..segment_size)) random.nat)
                 data (\ ! map (\ encoding.utf8 encode) (random.ascii ..segment_size))]
                (_.cover [/.Offset /.offset]
                         (|> data
                             (/.run (do <>.monad
                                      [start /.offset
                                       _ (/.segment to_read)
                                       offset /.offset
                                       _ (/.segment (n.- to_read ..segment_size))
                                       nothing_left /.offset]
                                      (wrap (and (n.= 0 start)
                                                 (n.= to_read offset)
                                                 (n.= ..segment_size nothing_left)))))
                             (!expect (#try.Success #1)))))
              (do {! random.monad}
                [to_read (\ ! map (n.% (inc ..segment_size)) random.nat)
                 data (\ ! map (\ encoding.utf8 encode) (random.ascii ..segment_size))]
                (_.cover [/.remaining]
                         (|> data
                             (/.run (do <>.monad
                                      [_ (/.segment to_read)
                                       remaining /.remaining
                                       _ (/.segment (n.- to_read ..segment_size))
                                       nothing_left /.remaining]
                                      (wrap (and (n.= ..segment_size
                                                      (n.+ to_read remaining))
                                                 (n.= 0 nothing_left)))))
                             (!expect (#try.Success #1)))))
              ..size
              ..binary
              ..utf8
              ..row
              ..simple
              ..complex
              ))))
