(.module:
  [lux #*
   [data
    text/format
    ["." name]]
   ["M" abstract/monad (#+ Monad do)]
   ["_" test (#+ Test)]
   ["r" math/random]
   [control
    pipe
    ["p" parser]]
   [data
    ["." error]
    [number
     ["." nat ("#@." decimal)]]
    ["." text ("#@." equivalence)]
    [collection
     ["." list]]]]
  {1
   ["." /]})

(def: #export test
  Test
  (<| (_.context (name.module (name-of /._)))
      (do r.monad
        [num-args (|> r.nat (:: @ map (n/% 10)))
         #let [gen-arg (:: @ map nat@encode r.nat)]
         yes gen-arg
         #let [gen-ignore (r.filter (|>> (text@= yes) not)
                                    (r.unicode 5))]
         no gen-ignore
         pre-ignore (r.list 5 gen-ignore)
         post-ignore (r.list 5 gen-ignore)]
        ($_ _.and
            (_.test "Can read any argument."
                    (|> (/.run /.any (list yes))
                        (case> (#error.Failure _)
                               #0
                               
                               (#error.Success arg)
                               (text@= arg yes))))
            (_.test "Can test tokens."
                    (and (|> (/.run (/.this yes) (list yes))
                             (case> (#error.Failure _)
                                    #0

                                    (#error.Success _)
                                    #1))
                         (|> (/.run (/.this yes) (list no))
                             (case> (#error.Failure _)
                                    #1

                                    (#error.Success _)
                                    #0))))
            (_.test "Can use custom token parsers."
                    (|> (/.run (/.parse nat@decode) (list yes))
                        (case> (#error.Failure _)
                               #0
                               
                               (#error.Success parsed)
                               (text@= (nat@encode parsed)
                                       yes))))
            (_.test "Can query if there are any more inputs."
                    (and (|> (/.run /.end (list))
                             (case> (#error.Success []) #1 _ #0))
                         (|> (/.run (p.not /.end) (list yes))
                             (case> (#error.Success []) #0 _ #1))))
            (_.test "Can parse CLI input anywhere."
                    (|> (/.run (|> (/.somewhere (/.this yes))
                                   (p.before (p.some /.any)))
                               (list.concat (list pre-ignore (list yes) post-ignore)))
                        (case> (#error.Failure _)
                               #0

                               (#error.Success _)
                               #1)))
            ))))
