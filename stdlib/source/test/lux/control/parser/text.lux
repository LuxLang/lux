(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [abstract
     [monad (#+ do)]]
    [control
     ["." try (#+ Try)]
     ["." exception (#+ Exception)]
     ["." function]]
    [data
     ["." maybe]
     ["." text ("#\." equivalence)
      ["%" format (#+ format)]
      ["." unicode #_
       ["#" set]
       ["#/." block]]]
     [collection
      ["." set]
      ["." list ("#\." functor)]
      [tree
       ["." finger]]]]
    [math
     ["." random]
     [number (#+ hex)
      ["n" nat]]]
    [macro
     ["." code]]]]
  [\\library
   ["." /
    ["<>" //
     ["<c>" code]]]])

(template: (!expect <pattern> <value>)
  (case <value>
    <pattern>
    true
    
    _
    false))

(def: (should_fail' sample parser exception)
  (All [a e] (-> Text (/.Parser a) (Exception e) Bit))
  (case (/.run parser sample)
    (#try.Failure error)
    (exception.match? exception error)
    
    _
    false))

(def: (should_fail sample parser)
  (All [a] (-> Text (/.Parser a) Bit))
  (case (/.run parser sample)
    (#try.Failure _)
    true
    
    _
    false))

(def: (should_pass expected parser)
  (-> Text (/.Parser Text) Bit)
  (|> expected
      (/.run parser)
      (\ try.functor map (text\= expected))
      (try.default false)))

(def: (should_pass! expected parser)
  (-> Text (/.Parser /.Slice) Bit)
  (..should_pass expected (/.slice parser)))

(def: character_classes
  Test
  ($_ _.and
      (do {! random.monad}
        [offset (\ ! map (n.% 50) random.nat)
         range (\ ! map (|>> (n.% 50) (n.+ 10)) random.nat)
         #let [limit (n.+ offset range)]
         expected (\ ! map (|>> (n.% range) (n.+ offset) text.of_code) random.nat)
         out_of_range (case offset
                        0 (\ ! map (|>> (n.% 10) inc (n.+ limit) text.of_code) random.nat)
                        _ (\ ! map (|>> (n.% offset) text.of_code) random.nat))]
        (_.cover [/.range]
                 (and (..should_pass expected (/.range offset limit))
                      (..should_fail out_of_range (/.range offset limit)))))
      (do {! random.monad}
        [expected (random.char unicode.ascii/upper)
         invalid (random.only (|>> (unicode/block.within? unicode/block.basic_latin/upper) not)
                              (random.char unicode.character))]
        (_.cover [/.upper]
                 (and (..should_pass (text.of_code expected) /.upper)
                      (..should_fail (text.of_code invalid) /.upper))))
      (do {! random.monad}
        [expected (random.char unicode.ascii/lower)
         invalid (random.only (|>> (unicode/block.within? unicode/block.basic_latin/lower) not)
                              (random.char unicode.character))]
        (_.cover [/.lower]
                 (and (..should_pass (text.of_code expected) /.lower)
                      (..should_fail (text.of_code invalid) /.lower))))
      (do {! random.monad}
        [expected (\ ! map (n.% 10) random.nat)
         invalid (random.char (unicode.set [unicode/block.number_forms (list)]))]
        (_.cover [/.decimal]
                 (and (..should_pass (\ n.decimal encode expected) /.decimal)
                      (..should_fail (text.of_code invalid) /.decimal))))
      (do {! random.monad}
        [expected (\ ! map (n.% 8) random.nat)
         invalid (random.char (unicode.set [unicode/block.number_forms (list)]))]
        (_.cover [/.octal]
                 (and (..should_pass (\ n.octal encode expected) /.octal)
                      (..should_fail (text.of_code invalid) /.octal))))
      (do {! random.monad}
        [expected (\ ! map (n.% 16) random.nat)
         invalid (random.char (unicode.set [unicode/block.number_forms (list)]))]
        (_.cover [/.hexadecimal]
                 (and (..should_pass (\ n.hex encode expected) /.hexadecimal)
                      (..should_fail (text.of_code invalid) /.hexadecimal))))
      (do {! random.monad}
        [expected (random.char unicode.ascii/alpha)
         invalid (random.only (function (_ char)
                                (not (or (unicode/block.within? unicode/block.basic_latin/upper char)
                                         (unicode/block.within? unicode/block.basic_latin/lower char))))
                              (random.char unicode.character))]
        (_.cover [/.alpha]
                 (and (..should_pass (text.of_code expected) /.alpha)
                      (..should_fail (text.of_code invalid) /.alpha))))
      (do {! random.monad}
        [expected (random.char unicode.ascii/alpha_num)
         invalid (random.only (function (_ char)
                                (not (or (unicode/block.within? unicode/block.basic_latin/upper char)
                                         (unicode/block.within? unicode/block.basic_latin/lower char)
                                         (unicode/block.within? unicode/block.basic_latin/decimal char))))
                              (random.char unicode.character))]
        (_.cover [/.alpha_num]
                 (and (..should_pass (text.of_code expected) /.alpha_num)
                      (..should_fail (text.of_code invalid) /.alpha_num))))
      (do {! random.monad}
        [expected ($_ random.either
                      (wrap text.tab)
                      (wrap text.vertical_tab)
                      (wrap text.space)
                      (wrap text.new_line)
                      (wrap text.carriage_return)
                      (wrap text.form_feed))
         invalid (|> (random.unicode 1) (random.only (function (_ char)
                                                       (not (or (text\= text.tab char)
                                                                (text\= text.vertical_tab char)
                                                                (text\= text.space char)
                                                                (text\= text.new_line char)
                                                                (text\= text.carriage_return char)
                                                                (text\= text.form_feed char))))))]
        (_.cover [/.space]
                 (and (..should_pass expected /.space)
                      (..should_fail invalid /.space))))
      (do {! random.monad}
        [#let [num_options 3]
         options (|> (random.char unicode.character)
                     (random.set n.hash num_options)
                     (\ ! map (|>> set.to_list
                                   (list\map text.of_code)
                                   (text.join_with ""))))
         expected (\ ! map (function (_ value)
                             (|> options
                                 (text.nth (n.% num_options value))
                                 maybe.assume))
                     random.nat)
         invalid (random.only (function (_ char)
                                (not (text.contains? (text.of_code char) options)))
                              (random.char unicode.character))]
        (_.cover [/.one_of /.one_of! /.character_should_be]
                 (and (..should_pass (text.of_code expected) (/.one_of options))
                      (..should_fail (text.of_code invalid) (/.one_of options))
                      (..should_fail' (text.of_code invalid) (/.one_of options)
                                      /.character_should_be)

                      (..should_pass! (text.of_code expected) (/.one_of! options))
                      (..should_fail (text.of_code invalid) (/.one_of! options))
                      (..should_fail' (text.of_code invalid) (/.one_of! options)
                                      /.character_should_be)
                      )))
      (do {! random.monad}
        [#let [num_options 3]
         options (|> (random.char unicode.character)
                     (random.set n.hash num_options)
                     (\ ! map (|>> set.to_list
                                   (list\map text.of_code)
                                   (text.join_with ""))))
         invalid (\ ! map (function (_ value)
                            (|> options
                                (text.nth (n.% num_options value))
                                maybe.assume))
                    random.nat)
         expected (random.only (function (_ char)
                                 (not (text.contains? (text.of_code char) options)))
                               (random.char unicode.character))]
        (_.cover [/.none_of /.none_of! /.character_should_not_be]
                 (and (..should_pass (text.of_code expected) (/.none_of options))
                      (..should_fail (text.of_code invalid) (/.none_of options))
                      (..should_fail' (text.of_code invalid) (/.none_of options)
                                      /.character_should_not_be)

                      (..should_pass! (text.of_code expected) (/.none_of! options))
                      (..should_fail (text.of_code invalid) (/.none_of! options))
                      (..should_fail' (text.of_code invalid) (/.none_of! options)
                                      /.character_should_not_be)
                      )))
      ))

(def: runs
  Test
  (let [octal! (/.one_of! "01234567")]
    ($_ _.and
        (do {! random.monad}
          [left (\ ! map (|>> (n.% 8) (\ n.octal encode)) random.nat)
           right (\ ! map (|>> (n.% 8) (\ n.octal encode)) random.nat)
           #let [expected (format left right)]
           invalid (|> random.nat
                       (\ ! map (n.% 16))
                       (random.only (n.>= 8))
                       (\ ! map (\ n.hex encode)))]
          (_.cover [/.many /.many!]
                   (and (..should_pass expected (/.many /.octal))
                        (..should_fail invalid (/.many /.octal))

                        (..should_pass! expected (/.many! octal!)))))
        (do {! random.monad}
          [left (\ ! map (|>> (n.% 8) (\ n.octal encode)) random.nat)
           right (\ ! map (|>> (n.% 8) (\ n.octal encode)) random.nat)
           #let [expected (format left right)]
           invalid (|> random.nat
                       (\ ! map (n.% 16))
                       (random.only (n.>= 8))
                       (\ ! map (\ n.hex encode)))]
          (_.cover [/.some /.some!]
                   (and (..should_pass expected (/.some /.octal))
                        (..should_pass "" (/.some /.octal))
                        (..should_fail invalid (/.some /.octal))

                        (..should_pass! expected (/.some! octal!))
                        (..should_pass! "" (/.some! octal!)))))
        (do {! random.monad}
          [#let [octal (\ ! map (|>> (n.% 8) (\ n.octal encode)) random.nat)]
           first octal
           second octal
           third octal]
          (_.cover [/.exactly /.exactly!]
                   (and (..should_pass (format first second) (/.exactly 2 /.octal))
                        (..should_fail (format first second third) (/.exactly 2 /.octal))
                        (..should_fail (format first) (/.exactly 2 /.octal))

                        (..should_pass! (format first second) (/.exactly! 2 octal!))
                        (..should_fail (format first second third) (/.exactly! 2 octal!))
                        (..should_fail (format first) (/.exactly! 2 octal!)))))
        (do {! random.monad}
          [#let [octal (\ ! map (|>> (n.% 8) (\ n.octal encode)) random.nat)]
           first octal
           second octal
           third octal]
          (_.cover [/.at_most /.at_most!]
                   (and (..should_pass (format first second) (/.at_most 2 /.octal))
                        (..should_pass (format first) (/.at_most 2 /.octal))
                        (..should_fail (format first second third) (/.at_most 2 /.octal))

                        (..should_pass! (format first second) (/.at_most! 2 octal!))
                        (..should_pass! (format first) (/.at_most! 2 octal!))
                        (..should_fail (format first second third) (/.at_most! 2 octal!)))))
        (do {! random.monad}
          [#let [octal (\ ! map (|>> (n.% 8) (\ n.octal encode)) random.nat)]
           first octal
           second octal
           third octal]
          (_.cover [/.at_least /.at_least!]
                   (and (..should_pass (format first second) (/.at_least 2 /.octal))
                        (..should_pass (format first second third) (/.at_least 2 /.octal))
                        (..should_fail (format first) (/.at_least 2 /.octal))

                        (..should_pass! (format first second) (/.at_least! 2 octal!))
                        (..should_pass! (format first second third) (/.at_least! 2 octal!))
                        (..should_fail (format first) (/.at_least! 2 octal!)))))
        (do {! random.monad}
          [#let [octal (\ ! map (|>> (n.% 8) (\ n.octal encode)) random.nat)]
           first octal
           second octal
           third octal]
          (_.cover [/.between /.between!]
                   (and (..should_pass (format first second) (/.between 2 3 /.octal))
                        (..should_pass (format first second third) (/.between 2 3 /.octal))
                        (..should_fail (format first) (/.between 2 3 /.octal))

                        (..should_pass! (format first second) (/.between! 2 3 octal!))
                        (..should_pass! (format first second third) (/.between! 2 3 octal!))
                        (..should_fail (format first) (/.between! 2 3 octal!)))))
        )))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [/.Parser])
      ($_ _.and
          (do {! random.monad}
            [sample (random.unicode 1)]
            (_.cover [/.run /.end!]
                     (and (|> (/.run /.end!
                                     "")
                              (!expect (#try.Success _)))
                          (|> (/.run /.end!
                                     sample)
                              (!expect (#try.Failure _))))))
          (do {! random.monad}
            [#let [size 10]
             expected (random.unicode size)
             dummy (|> (random.unicode size)
                       (random.only (|>> (text\= expected) not)))]
            (_.cover [/.this /.cannot_match]
                     (and (|> (/.run (/.this expected)
                                     expected)
                              (!expect (#try.Success [])))
                          (|> (/.run (/.this expected)
                                     dummy)
                              (!expect (^multi (#try.Failure error)
                                               (exception.match? /.cannot_match error)))))))
          (_.cover [/.Slice /.slice /.cannot_slice]
                   (|> ""
                       (/.run (/.slice /.any!))
                       (!expect (^multi (#try.Failure error)
                                        (exception.match? /.cannot_slice error)))))
          (do {! random.monad}
            [expected (random.unicode 1)]
            (_.cover [/.any /.any!]
                     (and (..should_pass expected /.any)
                          (..should_fail "" /.any)

                          (..should_pass! expected /.any!)
                          (..should_fail "" /.any!))))
          (do {! random.monad}
            [expected (random.unicode 1)]
            (_.cover [/.peek /.cannot_parse]
                     (and (..should_pass expected (<>.before /.any /.peek))
                          (|> ""
                              (/.run (<>.before /.any /.peek))
                              (!expect (^multi (#try.Failure error)
                                               (exception.match? /.cannot_parse error)))))))
          (do {! random.monad}
            [dummy (random.unicode 1)]
            (_.cover [/.unconsumed_input]
                     (|> (format dummy dummy)
                         (/.run /.any)
                         (!expect (^multi (#try.Failure error)
                                          (exception.match? /.unconsumed_input error))))))
          (do {! random.monad}
            [sample (random.unicode 1)]
            (_.cover [/.Offset /.offset]
                     (|> sample
                         (/.run (do <>.monad
                                  [pre /.offset
                                   _ /.any
                                   post /.offset]
                                  (wrap [pre post])))
                         (!expect (#try.Success [0 1])))))
          (do {! random.monad}
            [left (random.unicode 1)
             right (random.unicode 1)
             #let [input (format left right)]]
            (_.cover [/.get_input]
                     (|> input
                         (/.run (do <>.monad
                                  [pre /.get_input
                                   _ /.any
                                   post /.get_input
                                   _ /.any]
                                  (wrap (and (text\= input pre)
                                             (text\= right post)))))
                         (!expect (#try.Success #1)))))
          (do {! random.monad}
            [left (random.unicode 1)
             right (random.unicode 1)
             expected (random.only (|>> (text\= right) not)
                                   (random.unicode 1))]
            (_.cover [/.enclosed]
                     (|> (format left expected right)
                         (/.run (/.enclosed [left right] (/.this expected)))
                         (!expect (#try.Success _)))))
          (do {! random.monad}
            [in (random.unicode 1)
             out (random.unicode 1)]
            (_.cover [/.local]
                     (|> out
                         (/.run (do <>.monad
                                  [_ (/.local in (/.this in))]
                                  (/.this out)))
                         (!expect (#try.Success _)))))
          (do {! random.monad}
            [expected (\ ! map (|>> (n.% 8) (\ n.octal encode)) random.nat)]
            (_.cover [/.embed]
                     (|> (list (code.text expected))
                         (<c>.run (/.embed /.octal <c>.text))
                         (!expect (^multi (#try.Success actual)
                                          (text\= expected actual))))))
          (do {! random.monad}
            [invalid (random.ascii/upper 1)
             expected (random.only (|>> (unicode/block.within? unicode/block.basic_latin/upper)
                                        not)
                                   (random.char unicode.character))
             #let [upper! (/.one_of! "ABCDEFGHIJKLMNOPQRSTUVWXYZ")]]
            (_.cover [/.not /.not! /.expected_to_fail]
                     (and (..should_pass (text.of_code expected) (/.not /.upper))
                          (|> invalid
                              (/.run (/.not /.upper))
                              (!expect (^multi (#try.Failure error)
                                               (exception.match? /.expected_to_fail error))))

                          (..should_pass! (text.of_code expected) (/.not! upper!))
                          (|> invalid
                              (/.run (/.not! upper!))
                              (!expect (^multi (#try.Failure error)
                                               (exception.match? /.expected_to_fail error)))))))
          (do {! random.monad}
            [upper (random.ascii/upper 1)
             lower (random.ascii/lower 1)
             invalid (random.only (function (_ char)
                                    (not (or (unicode/block.within? unicode/block.basic_latin/upper char)
                                             (unicode/block.within? unicode/block.basic_latin/lower char))))
                                  (random.char unicode.character))
             #let [upper! (/.one_of! "ABCDEFGHIJKLMNOPQRSTUVWXYZ")
                   lower! (/.one_of! "abcdefghijklmnopqrstuvwxyz")]]
            (_.cover [/.and /.and!]
                     (and (..should_pass (format upper lower) (/.and /.upper /.lower))
                          (..should_fail (format (text.of_code invalid) lower) (/.and /.upper /.lower))
                          (..should_fail (format upper (text.of_code invalid)) (/.and /.upper /.lower))

                          (..should_pass! (format upper lower) (/.and! upper! lower!))
                          (..should_fail (format (text.of_code invalid) lower) (/.and! upper! lower!))
                          (..should_fail (format upper (text.of_code invalid)) (/.and! upper! lower!)))))
          (do {! random.monad}
            [expected (random.unicode 1)
             invalid (random.unicode 1)]
            (_.cover [/.satisfies /.character_does_not_satisfy_predicate]
                     (and (..should_pass expected (/.satisfies (function.constant true)))
                          (..should_fail' invalid (/.satisfies (function.constant false))
                                          /.character_does_not_satisfy_predicate))))
          ..character_classes
          ..runs
          )))
