(.module:
  [lux #*
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do)]]
   [control
    ["." function]
    ["." try]
    ["<>" parser]]
   [data
    ["." bit]
    ["." name]
    ["." text]
    [collection
     ["." list]]]
   [macro
    ["." code]]
   [math
    ["." random (#+ Random)]
    [number
     ["." nat]
     ["." int]
     ["." rev]
     ["." frac]]]]
  {1
   ["." /]})

(template: (!expect <pattern> <value>)
  (case <value>
    <pattern>
    true
    
    _
    false))

(def: random_name
  (Random Name)
  (random.and (random.unicode 1)
              (random.unicode 1)))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [/.Parser])
      (`` ($_ _.and
              (do {! random.monad}
                [expected (\ ! map code.bit random.bit)]
                (_.cover [/.run]
                         (and (|> (/.run /.any (list expected))
                                  (!expect (#try.Success _)))
                              (|> (/.run /.any (list))
                                  (!expect (#try.Failure _))))))
              (~~ (template [<query> <check> <random> <code> <equivalence>]
                    [(do {! random.monad}
                       [expected <random>
                        dummy (|> <random> (random.filter (|>> (\ <equivalence> = expected) not)))]
                       ($_ _.and
                           (_.cover [<query>]
                                    (|> (/.run <query> (list (<code> expected)))
                                        (!expect (^multi (#try.Success actual)
                                                         (\ <equivalence> = expected actual)))))
                           (_.cover [<check>]
                                    (and (|> (/.run (<check> expected) (list (<code> expected)))
                                             (!expect (#try.Success [])))
                                         (|> (/.run (<check> expected) (list (<code> dummy)))
                                             (!expect (#try.Failure _)))))
                           ))]

                    [/.any /.this! (\ ! map code.bit random.bit) function.identity code.equivalence]
                    [/.bit /.bit! random.bit code.bit bit.equivalence]
                    [/.nat /.nat! random.nat code.nat nat.equivalence]
                    [/.int /.int! random.int code.int int.equivalence]
                    [/.rev /.rev! random.rev code.rev rev.equivalence]
                    [/.frac /.frac! random.safe_frac code.frac frac.equivalence]
                    [/.text /.text! (random.unicode 1) code.text text.equivalence]
                    [/.identifier /.identifier! ..random_name code.identifier name.equivalence]
                    [/.tag /.tag! ..random_name code.tag name.equivalence]
                    [/.local_identifier /.local_identifier! (random.unicode 1) code.local_identifier text.equivalence]
                    [/.local_tag /.local_tag! (random.unicode 1) code.local_tag text.equivalence]
                    ))
              (~~ (template [<query> <code>]
                    [(do {! random.monad}
                       [expected_left random.nat
                        expected_right random.int]
                       (_.cover [<query>]
                                (|> (/.run (<query> (<>.and /.nat /.int))
                                           (list (<code> (list (code.nat expected_left)
                                                               (code.int expected_right)))))
                                    (!expect (^multi (#try.Success [actual_left actual_right])
                                                     (and (\ nat.equivalence = expected_left actual_left)
                                                          (\ int.equivalence = expected_right actual_right)))))))]

                    [/.form code.form]
                    [/.tuple code.tuple]
                    ))
              (do {! random.monad}
                [expected_left random.nat
                 expected_right random.int]
                (_.cover [/.record]
                         (|> (/.run (/.record (<>.and /.nat /.int))
                                    (list (code.record (list [(code.nat expected_left)
                                                              (code.int expected_right)]))))
                             (!expect (^multi (#try.Success [actual_left actual_right])
                                              (and (\ nat.equivalence = expected_left actual_left)
                                                   (\ int.equivalence = expected_right actual_right)))))))
              (do {! random.monad}
                [expected_local random.nat
                 expected_global random.int]
                (_.cover [/.local]
                         (|> (/.run (<>.and (/.local (list (code.nat expected_local)) /.nat)
                                            /.int)
                                    (list (code.int expected_global)))
                             (!expect (^multi (#try.Success [actual_local actual_global])
                                              (and (\ nat.equivalence = expected_local actual_local)
                                                   (\ int.equivalence = expected_global actual_global)))))))
              (do {! random.monad}
                [dummy (\ ! map code.bit random.bit)]
                (_.cover [/.end?]
                         (|> (/.run (do <>.monad
                                      [pre /.end?
                                       _ /.any
                                       post /.end?]
                                      (wrap (and (not pre)
                                                 post)))
                                    (list dummy))
                             (!expect (^multi (#try.Success verdict)
                                              verdict)))))
              (do {! random.monad}
                [dummy (\ ! map code.bit random.bit)]
                (_.cover [/.end!]
                         (and (|> (/.run /.end! (list))
                                  (!expect (#try.Success [])))
                              (|> (/.run /.end! (list dummy))
                                  (!expect (#try.Failure _))))))
              ))))
