(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [abstract
     [monad (#+ do)]]
    [control
     ["." try]
     ["." exception]]
    [data
     ["." text ("#\." equivalence)]
     [collection
      ["." dictionary]]]
    [math
     ["." random]
     [number
      ["n" nat]]]]]
  [\\library
   ["." /
    ["/#" // ("#\." monad)]]])

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [/.Environment /.Parser])
      ($_ _.and
          (_.cover [/.empty]
                   (dictionary.empty? /.empty))
          (do random.monad
            [expected random.nat]
            (_.cover [/.run]
                     (|> (/.run (//\wrap expected) /.empty)
                         (\ try.functor map (n.= expected))
                         (try.default false))))
          (do random.monad
            [property (random.ascii/alpha 1)
             expected (random.ascii/alpha 1)]
            (_.cover [/.Property /.property]
                     (|> /.empty
                         (dictionary.put property expected)
                         (/.run (/.property property))
                         (\ try.functor map (text\= expected))
                         (try.default false))))
          (do random.monad
            [property (random.ascii/alpha 1)]
            (_.cover [/.unknown]
                     (case (/.run (/.property property) /.empty)
                       (#try.Success _)
                       false
                       
                       (#try.Failure error)
                       (exception.match? /.unknown error))))
          )))
