(.module:
  [lux #*
   [control
    [monad (#+ do)]]
   [data
    [text
     format]]
   ["." function]
   [math
    ["r" random]]
   ["_" test (#+ Test)]]
  {1
   ["." / (#+ Apply)]}
  [//
   [functor (#+ Injection Comparison)]])

(def: (identity injection comparison (^open "_;."))
  (All [f] (-> (Injection f) (Comparison f) (Apply f) Test))
  (do r.monad
    [sample (:: @ map injection r.nat)]
    (_.test "Identity."
            ((comparison n/=)
             (_;apply (injection function.identity) sample)
             sample))))

(def: (homomorphism injection comparison (^open "_;."))
  (All [f] (-> (Injection f) (Comparison f) (Apply f) Test))
  (do r.monad
    [sample r.nat
     increase (:: @ map n/+ r.nat)]
    (_.test "Homomorphism."
            ((comparison n/=)
             (_;apply (injection increase) (injection sample))
             (injection (increase sample))))))

(def: (interchange injection comparison (^open "_;."))
  (All [f] (-> (Injection f) (Comparison f) (Apply f) Test))
  (do r.monad
    [sample r.nat
     increase (:: @ map n/+ r.nat)]
    (_.test "Interchange."
            ((comparison n/=)
             (_;apply (injection increase) (injection sample))
             (_;apply (injection (function (_ f) (f sample))) (injection increase))))))

(def: (composition injection comparison (^open "_;."))
  (All [f] (-> (Injection f) (Comparison f) (Apply f) Test))
  (do r.monad
    [sample r.nat
     increase (:: @ map n/+ r.nat)
     decrease (:: @ map n/- r.nat)]
    (_.test "Composition."
            ((comparison n/=)
             (_$ _;apply
                 (injection function.compose)
                 (injection increase)
                 (injection decrease)
                 (injection sample))
             ($_ _;apply
                 (injection increase)
                 (injection decrease)
                 (injection sample))))))

(def: #export (laws injection comparison apply)
  (All [f] (-> (Injection f) (Comparison f) (Apply f) Test))
  (_.context (%name (name-of /.Apply))
             ($_ _.and
                 (..identity injection comparison apply)
                 (..homomorphism injection comparison apply)
                 (..interchange injection comparison apply)
                 (..composition injection comparison apply)
                 )))
