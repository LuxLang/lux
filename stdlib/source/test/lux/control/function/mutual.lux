(.module:
  [lux #*
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do)]]
   [data
    ["." bit ("#\." equivalence)]
    [text
     ["%" format (#+ format)]]]
   [math
    ["." random (#+ Random)]
    [number
     ["n" nat]]]]
  [\\
   ["." /]])

(def: test_let
  Test
  (do {! random.monad}
    [sample (\ ! map (n.% 10) random.nat)
     #let [expected (n.even? sample)]]
    (<| (_.cover [/.let])
        (/.let [(even? number)
                (-> Nat Bit)
                (case number
                  0 true
                  _ (odd? (dec number)))

                (odd? number)
                (-> Nat Bit)
                (case number
                  0 false
                  _ (even? (dec number)))]
          (and (bit\= expected (even? sample))
               (bit\= (not expected) (odd? sample)))))))

(/.def:
  [(even? number)
   (-> Nat Bit)
   (case number
     0 true
     _ (odd? (dec number)))]

  [(odd? number)
   (-> Nat Bit)
   (case number
     0 false
     _ (even? (dec number)))])

(def: test_def
  Test
  (do {! random.monad}
    [sample (\ ! map (n.% 10) random.nat)
     #let [expected (n.even? sample)]]
    (<| (_.cover [/.def:])
        (and (bit\= expected (..even? sample))
             (bit\= (not expected) (..odd? sample))))))

(def: #export test
  Test
  (<| (_.covering /._)
      ($_ _.and
          ..test_let
          ..test_def
          )))
