... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]
    ["[0]" monoid
     ["[1]T" \\test]]
    [functor
     ["[0]" contravariant
      ["[1]T" \\test]]]]
   [control
    ["[0]" function]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(def (multiple? factor)
  (-> Nat (/.Predicate Nat))
  (when factor
    0 (function.constant false)
    _ (|>> (n.% factor) (n.= 0))))

(def .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [sample random.nat
         samples (random.list 10 random.nat)
         .let [equivalence (is (Equivalence (/.Predicate Nat))
                               (implementation
                                (def (= left right)
                                  (bit#= (left sample)
                                         (right sample)))))]])
      (_.for [/.Predicate])
      (all _.and
           (_.for [/.functor]
                  (contravariantT.spec equivalence (multiple? 2) /.functor))
           (let [generator (is (Random (/.Predicate Nat))
                               (|> random.nat
                                   (random.only (|>> (n.= 0) not))
                                   (of ! each multiple?)))]
             (all _.and
                  (_.for [/.union]
                         (monoidT.spec equivalence /.union generator))
                  (_.for [/.intersection]
                         (monoidT.spec equivalence /.intersection generator))))
           
           (_.coverage [/.none]
             (bit#= false (/.none sample)))
           (_.coverage [/.all]
             (bit#= true (/.all sample)))
           (_.coverage [/.or]
             (bit#= (/.all sample)
                    ((/.or /.none /.all) sample)))
           (_.coverage [/.and]
             (bit#= (/.none sample)
                    ((/.and /.none /.all) sample)))
           (_.coverage [/.complement]
             (and (not (bit#= (/.none sample)
                              ((/.complement /.none) sample)))
                  (not (bit#= (/.all sample)
                              ((/.complement /.all) sample)))))
           (_.coverage [/.difference]
             (let [/2? (multiple? 2)
                   /3? (multiple? 3)]
               (bit#= (and (/2? sample)
                           (not (/3? sample)))
                      ((/.difference /3? /2?) sample))))
           (_.coverage [/.rec]
             (let [even? (multiple? 2)
                   any_even? (is (/.Predicate (List Nat))
                                 (/.rec (function (_ again)
                                          (function (_ values)
                                            (when values
                                              {.#End}
                                              false

                                              {.#Item head tail}
                                              (or (even? head)
                                                  (again tail)))))))]
               (bit#= (list.any? even? samples)
                      (any_even? samples))))
           )))
