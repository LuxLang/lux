... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" scope]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(def (twice it)
  (-> Nat
      Nat)
  (n.+ it it))

(def (thrice it)
  (-> Nat
      Nat)
  (all n.+ it it it))

(/.def .private (method input)
  (/.Method Nat
            Nat)
  (twice input))

(def .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [expected random.nat])
      (_.for [/.Method /.Value])
      (all _.and
           (_.coverage [/.value /.def]
             (/.value (do scope.monad
                        [actual (method expected)]
                        (in (n.= (twice expected)
                                 actual)))))
           (_.coverage [/.let]
             (/.value (do scope.monad
                        [/.let [..method (function (_ next method it)
                                           (thrice (next it)))]
                         actual (method expected)]
                        (in (n.= (thrice (twice expected))
                                 actual)))))
           )))
