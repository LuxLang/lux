... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]]
   [data
    ["[0]" identity (.only Identity)]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(def .public test
  Test
  (do random.monad
    [sample random.nat]
    (<| (_.covering /._)
        (all _.and
             (_.coverage [/.be]
               (n.= (++ sample)
                    (is (Identity Nat)
                        (/.be identity.comonad
                          [value (out sample)]
                          (out (++ value))))))
             ))))

(def .public (spec injection comparison it)
  (All (_ !)
    (-> (Injection !) (Comparison !) (/.CoMonad !)
        Test))
  (<| (_.for [/.CoMonad])
      (do [! random.monad]
        [.let [(open "/#[0]") it]
         sample random.nat
         increase (of ! each (function (_ diff)
                               (|>> /#out (n.+ diff)))
                      random.nat)
         decrease (of ! each (function (_ diff)
                               (|>> /#out (n.- diff)))
                      random.nat)
         morphism (of ! each (function (_ diff)
                               (|>> /#out (n.+ diff)))
                      random.nat)
         .let [start (injection sample)
               == (comparison n.=)]])
      (all _.and
           (_.for [/.functor]
                  (functorT.spec injection comparison (the /.functor it)))

           (_.coverage [/.disjoint /.out]
             (let [left_identity!
                   (n.= (morphism start)
                        (|> start /#disjoint (/#each morphism) /#out))
                   
                   right_identity!
                   (== start
                       (|> start /#disjoint (/#each /#out)))
                   
                   associativity!
                   (== (|> start /#disjoint (/#each (|>> /#disjoint (/#each increase) decrease)))
                       (|> start /#disjoint (/#each increase) /#disjoint (/#each decrease)))]
               (and left_identity!
                    right_identity!
                    associativity!)))
           )))
