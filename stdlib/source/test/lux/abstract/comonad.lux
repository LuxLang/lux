... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]]
   [control
    ["[0]" pure (.only Pure)]]
   [math
    ["[0]" random]
    [number
     ["n" natural]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public test
  Test
  (do random.monad
    [sample random.natural]
    (<| (_.covering /._)
        (all _.and
             (_.coverage [/.be]
               (n.= (++ sample)
                    (is (Pure Natural)
                        (/.be pure.comonad
                          [value (out sample)]
                          (out (++ value))))))
             ))))

(the .public (spec injection comparison it)
  (for_any (_ !)
    (-> (Injection !) (Comparison !) (/.CoMonad !)
        Test))
  (<| (_.for [/.CoMonad])
      (do [! random.monad]
        [.let [(open "/#[0]") it]
         sample random.natural
         increase (of ! each (function (_ diff)
                               (|>> /#out (n.+ diff)))
                      random.natural)
         decrease (of ! each (function (_ diff)
                               (|>> /#out (n.- diff)))
                      random.natural)
         morphism (of ! each (function (_ diff)
                               (|>> /#out (n.+ diff)))
                      random.natural)
         .let [start (injection sample)
               == (comparison n.=)]])
      (all _.and
           (_.for [/.functor]
                  (functorT.spec injection comparison (its /.functor it)))

           (_.coverage [/.disjoint /.out]
             (let [left_identity!
                   (n.= (morphism start)
                        (|> start /#disjoint (/#each morphism) /#out))
                   
                   right_identity!
                   (== start
                       (|> start /#disjoint (/#each /#out)))
                   
                   associativity!
                   (== (|> start /#disjoint (/#each (|>> /#disjoint (/#each increase) decrease)))
                       (|> start /#disjoint (/#each increase) /#disjoint (/#each decrease)))]
               (and left_identity!
                    right_identity!
                    associativity!)))
           )))
