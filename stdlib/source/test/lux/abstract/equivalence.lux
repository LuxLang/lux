... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Variant Record)
   [abstract
    ["[0]" monad]
    [functor
     ["[0]" contravariant
      ["[1]T" \\test]]]]
   [control
    ["[0]" maybe]]
   [data
    ["[0]" any]
    ["[0]" text]
    [collection
     ["[0]" list]]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]
      ["i" integer]]]]
   [test
    ["_" property (.only Test)]]
   [macro
    ["[0]" template]]]]
 ["[0]" \\polytypic]
 [\\library
  ["[0]" /]])

... https://en.wikipedia.org/wiki/Commutative_property
(template.the .public (commutativity ,+
                                     ,=
                                     ,0 ,1)
  [(,= (,+ ,0 ,1)
       (,+ ,1 ,0))])

... https://en.wikipedia.org/wiki/Reflexive_relation
(template.the .public (reflexivity ,=
                                   ,0)
  [(,= ,0 ,0)])

... https://en.wikipedia.org/wiki/Transitive_relation
(template.the .public (transitivity ,=
                                    ,0 ,1 ,2)
  [(|> (and (,= ,0 ,1)
            (,= ,1 ,2))
       (bit.implies (,= ,0 ,2)))])

... https://en.wikipedia.org/wiki/Equality_(mathematics)#Basic_properties
(template.the .public (specification ,implementation ,=
                                     ,random)
  [(monad.let [! random.monad]
     [.let [random ,random]
      ,0 random
      ,1 random
      ,2 random]
     (all _.and
          (_.coverage [,=]
            (and (..reflexivity ,=
                                ,0)
                 (..commutativity ,=
                                  bit.=
                                  ,0 ,1)
                 (..transitivity ,=
                                 ,0 ,1 ,2)))
          (<| (_.for [,implementation])
              (_.coverage [/.Equivalence
                           /.=]
                (same? ,=
                       (by ,implementation =))))))])

(the .public (spec (open "/#[0]") random)
  (for_any (_ it)
    (-> (/.Equivalence it) (Random it)
        Test))
  (monad.let random.monad
    [left random
     right random]
    (<| (_.for [/.Equivalence])
        (_.coverage [/.=]
          (let [reflexivity!
                (/#= left left)

                symmetry!
                (bit.= (/#= left right)
                       (/#= right left))]
            (and reflexivity!
                 symmetry!
                 ))))))

(every Variant
  (.Variant
   {#Case_0 Bit}
   {#Case_1 Integer}
   {#Case_2 Decimal}))

(every Recursive
  (Rec Recursive
    (.Variant
     {#Number Decimal}
     {#Addition Decimal Recursive})))

(every Record
  (.Record
   [#bit Bit
    #integer Integer
    #decimal Decimal
    #text Text
    #maybe (Maybe Integer)
    #list (List Integer)
    #variant Variant
    #tuple [Integer Decimal Text]
    #recursive Recursive]))

(the random_recursive
  (Random Recursive)
  (random.rec (function (_ random_recursive)
                (random.or random.unit_decimal
                           (random.and random.unit_decimal
                                       random_recursive)))))

(the random
  (Random Record)
  (monad.let [! random.monad]
    [size (by ! each (n.% 2) random.natural)
     .let [random_integer (is (Random Integer)
                              (by ! each
                                  (|>> i.absolute
                                       (i.% +1,000,000))
                                  random.integer))]]
    (all random.and
         random.bit
         random_integer
         random.unit_decimal
         (random.unicode size)
         (random.maybe random_integer)
         (random.list size random_integer)
         (all random.or
              random.bit
              random_integer
              random.unit_decimal)
         (all random.and
              random_integer
              random.unit_decimal
              (random.unicode size))
         random_recursive
         )))

(the equivalence
  (/.Equivalence ..Record)
  (\\polytypic.equivalence ..Record))

(the \\polytypic
  Test
  (<| (_.covering \\polytypic._)
      (_.for [\\polytypic.equivalence]
             (..spec ..equivalence ..random))))

(the .public test
  Test
  (monad.let random.monad
    [leftN random.natural
     rightN random.natural
     leftI random.integer
     rightI random.integer
     sample random.natural
     different (|> random.natural (random.only (|>> (n.= sample) not)))
     .let [equivalence (is (/.Equivalence (/.Equivalence Natural))
                           (implementation
                            (the (= left right)
                              (and (bit.= (by left = leftN leftN)
                                          (by right = leftN leftN))
                                   (bit.= (by left = rightN rightN)
                                          (by right = rightN rightN))
                                   (bit.= (by left = leftN rightN)
                                          (by right = leftN rightN))))))]]
    (<| (_.covering /._)
        (all _.and
             (_.for [/.functor]
                    (contravariantT.spec equivalence n.equivalence /.functor))

             (_.coverage [/.class]
               (bit.= (by n.equivalence = leftN rightN)
                      ((/.class n.equivalence leftN) rightN)))
             (_.coverage [/.sum]
               (let [both (/.sum n.equivalence i.equivalence)]
                 (and (bit.= (by both = {.#Left leftN} {.#Left rightN})
                             (by n.equivalence = leftN rightN))
                      (bit.= (by both = {.#Right leftI} {.#Right rightI})
                             (by i.equivalence = leftI rightI)))))
             (_.coverage [/.product]
               (let [both (/.product n.equivalence i.equivalence)]
                 (bit.= (by both = [leftN leftI] [rightN rightI])
                        (and (by n.equivalence = leftN rightN)
                             (by i.equivalence = leftI rightI)))))
             (_.coverage [/.recursive]
               (let [equivalence (is (/.Equivalence (Stack Natural))
                                     (/.recursive
                                      (|>> (/.product n.equivalence)
                                           (/.sum any.equivalence))))]
                 (and (by equivalence = (stack sample sample) (stack sample sample))
                      (not (by equivalence = (stack sample sample) (stack sample)))
                      (not (by equivalence = (stack sample sample) (stack different different))))))

             ..\\polytypic
             ))))
