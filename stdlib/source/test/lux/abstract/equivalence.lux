... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Variant Record)
   [abstract
    [monad (.only do)]
    [functor
     ["[0]" contravariant
      ["[1]T" \\test]]]]
   [control
    ["[0]" maybe]]
   [data
    ["[0]" any]
    ["[0]" bit]
    ["[0]" text]
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]
     ["i" int]]]
   [test
    ["_" property (.only Test)]]]]
 ["[0]" \\polytypic]
 [\\library
  ["[0]" /]])

(the .public (spec (open "/#[0]") random)
  (for_any (_ of)
    (-> (/.Equivalence of) (Random of)
        Test))
  (do random.monad
    [left random
     right random]
    (<| (_.for [/.Equivalence])
        (_.coverage [/.=]
          (let [reflexivity!
                (/#= left left)

                symmetry!
                (bit.= (/#= left right)
                       (/#= right left))]
            (and reflexivity!
                 symmetry!
                 ))))))

(every Variant
  (.Variant
   {#Case_0 Bit}
   {#Case_1 Int}
   {#Case_2 Dec}))

(every Recursive
  (Rec Recursive
    (.Variant
     {#Number Dec}
     {#Addition Dec Recursive})))

(every Record
  (.Record
   [#bit Bit
    #int Int
    #dec Dec
    #text Text
    #maybe (Maybe Int)
    #list (List Int)
    #variant Variant
    #tuple [Int Dec Text]
    #recursive Recursive]))

(the random_recursive
  (Random Recursive)
  (random.rec (function (_ random_recursive)
                (random.or random.safe_dec
                           (random.and random.safe_dec
                                       random_recursive)))))

(the random
  (Random Record)
  (do [! random.monad]
    [size (of ! each (n.% 2) random.nat)
     .let [random_int (is (Random Int)
                          (of ! each (|>> i.abs (i.% +1,000,000)) random.int))]]
    (all random.and
         random.bit
         random_int
         random.safe_dec
         (random.unicode size)
         (random.maybe random_int)
         (random.list size random_int)
         (all random.or
              random.bit
              random_int
              random.safe_dec)
         (all random.and
              random_int
              random.safe_dec
              (random.unicode size))
         random_recursive
         )))

(the equivalence
  (/.Equivalence ..Record)
  (\\polytypic.equivalence ..Record))

(the \\polytypic
  Test
  (<| (_.covering \\polytypic._)
      (_.for [\\polytypic.equivalence]
             (..spec ..equivalence ..random))))

(the .public test
  Test
  (do random.monad
    [leftN random.nat
     rightN random.nat
     leftI random.int
     rightI random.int
     sample random.nat
     different (|> random.nat (random.only (|>> (n.= sample) not)))
     .let [equivalence (is (/.Equivalence (/.Equivalence Nat))
                           (implementation
                            (the (= left right)
                              (and (bit.= (of left = leftN leftN)
                                          (of right = leftN leftN))
                                   (bit.= (of left = rightN rightN)
                                          (of right = rightN rightN))
                                   (bit.= (of left = leftN rightN)
                                          (of right = leftN rightN))))))]]
    (<| (_.covering /._)
        (all _.and
             (_.for [/.functor]
                    (contravariantT.spec equivalence n.equivalence /.functor))
             (_.coverage [/.recursive]
               (let [equivalence (is (/.Equivalence (List Nat))
                                     (/.recursive
                                      (|>> (/.product n.equivalence)
                                           (/.sum any.equivalence))))]
                 (and (of equivalence = (list sample sample) (list sample sample))
                      (not (of equivalence = (list sample sample) (list sample)))
                      (not (of equivalence = (list sample sample) (list different different))))))

             ..\\polytypic
             ))))
