... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Variant Record)
   [abstract
    [monad (.only do)]
    [functor
     ["[0]" contravariant
      ["[1]T" \\test]]]]
   [control
    ["[0]" maybe]]
   [data
    ["[0]" any]
    ["[0]" bit]
    ["[0]" text]
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]
     ["i" integer]]]
   [test
    ["_" property (.only Test)]]]]
 ["[0]" \\polytypic]
 [\\library
  ["[0]" /]])

(the .public (spec (open "/#[0]") random)
  (for_any (_ it)
    (-> (/.Equivalence it) (Random it)
        Test))
  (do random.monad
    [left random
     right random]
    (<| (_.for [/.Equivalence])
        (_.coverage [/.=]
          (let [reflexivity!
                (/#= left left)

                symmetry!
                (bit.= (/#= left right)
                       (/#= right left))]
            (and reflexivity!
                 symmetry!
                 ))))))

(every Variant
  (.Variant
   {#Case_0 Bit}
   {#Case_1 Integer}
   {#Case_2 Decimal}))

(every Recursive
  (Rec Recursive
    (.Variant
     {#Number Decimal}
     {#Addition Decimal Recursive})))

(every Record
  (.Record
   [#bit Bit
    #integer Integer
    #decimal Decimal
    #text Text
    #maybe (Maybe Integer)
    #list (List Integer)
    #variant Variant
    #tuple [Integer Decimal Text]
    #recursive Recursive]))

(the random_recursive
  (Random Recursive)
  (random.rec (function (_ random_recursive)
                (random.or random.unit_decimal
                           (random.and random.unit_decimal
                                       random_recursive)))))

(the random
  (Random Record)
  (do [! random.monad]
    [size (by ! each (n.% 2) random.natural)
     .let [random_integer (is (Random Integer)
                              (by ! each
                                  (|>> i.absolute
                                       (i.% +1,000,000))
                                  random.integer))]]
    (all random.and
         random.bit
         random_integer
         random.unit_decimal
         (random.unicode size)
         (random.maybe random_integer)
         (random.list size random_integer)
         (all random.or
              random.bit
              random_integer
              random.unit_decimal)
         (all random.and
              random_integer
              random.unit_decimal
              (random.unicode size))
         random_recursive
         )))

(the equivalence
  (/.Equivalence ..Record)
  (\\polytypic.equivalence ..Record))

(the \\polytypic
  Test
  (<| (_.covering \\polytypic._)
      (_.for [\\polytypic.equivalence]
             (..spec ..equivalence ..random))))

(the .public test
  Test
  (do random.monad
    [leftN random.natural
     rightN random.natural
     leftI random.integer
     rightI random.integer
     sample random.natural
     different (|> random.natural (random.only (|>> (n.= sample) not)))
     .let [equivalence (is (/.Equivalence (/.Equivalence Natural))
                           (implementation
                            (the (= left right)
                              (and (bit.= (by left = leftN leftN)
                                          (by right = leftN leftN))
                                   (bit.= (by left = rightN rightN)
                                          (by right = rightN rightN))
                                   (bit.= (by left = leftN rightN)
                                          (by right = leftN rightN))))))]]
    (<| (_.covering /._)
        (all _.and
             (_.for [/.functor]
                    (contravariantT.spec equivalence n.equivalence /.functor))

             (_.coverage [/.sum]
               (let [both (/.sum n.equivalence i.equivalence)]
                 (and (bit.= (by both = {.#Left leftN} {.#Left rightN})
                             (by n.equivalence = leftN rightN))
                      (bit.= (by both = {.#Right leftI} {.#Right rightI})
                             (by i.equivalence = leftI rightI)))))
             (_.coverage [/.product]
               (let [both (/.product n.equivalence i.equivalence)]
                 (bit.= (by both = [leftN leftI] [rightN rightI])
                        (and (by n.equivalence = leftN rightN)
                             (by i.equivalence = leftI rightI)))))
             (_.coverage [/.recursive]
               (let [equivalence (is (/.Equivalence (Stack Natural))
                                     (/.recursive
                                      (|>> (/.product n.equivalence)
                                           (/.sum any.equivalence))))]
                 (and (by equivalence = (stack sample sample) (stack sample sample))
                      (not (by equivalence = (stack sample sample) (stack sample)))
                      (not (by equivalence = (stack sample sample) (stack different different))))))

             ..\\polytypic
             ))))
