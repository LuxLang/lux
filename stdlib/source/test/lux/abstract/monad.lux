... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [functor
     [\\test (.only Injection Comparison)]]]
   [control
    ["[0]" pure (.only Pure)]
    ["[0]" io]
    [concurrency
     ["[0]" atom (.only Atom)]]]
   [data
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix)]]
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    ["[0]" random]
    [number
     ["n" natural]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only do)]]
 ["[0]" /
  ["[1][0]" free]
  ["[1][0]" indexed]])

(the .public (spec injection comparison (open "/#[0]"))
  (for_any (_ !)
    (-> (Injection !) (Comparison !) (/.Monad !)
        Test))
  (<| (_.for [/.Monad])
      (do [! random.monad]
        [sample random.natural
         increase (by ! each (function (_ diff)
                               (|>> (n.+ diff) /#in))
                      random.natural)
         decrease (by ! each (function (_ diff)
                               (|>> (n.- diff) /#in))
                      random.natural)
         morphism (by ! each (function (_ diff)
                               (|>> (n.+ diff) /#in))
                      random.natural)
         .let [left_identity!
               ((comparison n.=)
                (|> (injection sample) (/#each morphism) /#conjoint)
                (morphism sample))

               right_identity!
               ((comparison n.=)
                (|> (injection sample) (/#each /#in) /#conjoint)
                (injection sample))

               associativity!
               ((comparison n.=)
                (|> (injection sample) (/#each increase) /#conjoint (/#each decrease) /#conjoint)
                (|> (injection sample) (/#each (|>> increase (/#each decrease) /#conjoint)) /#conjoint))]])
      (_.coverage [/.functor /.in /.conjoint]
        (and left_identity!
             right_identity!
             associativity!))))

(the .public test
  Test
  (do random.monad
    [mono random.natural
     poly (random.list 10 random.natural)
     before (random.lower_cased 1)
     after (random.upper_cased 1)]
    (<| (_.covering /._)
        (all _.and
             (_.coverage [/.do]
               (n.= (++ mono)
                    (is (Pure Natural)
                        (/.do pure.monad
                          [sample (in mono)]
                          (in (++ sample))))))
             (_.coverage [/.then]
               (n.= (++ mono)
                    (is (Pure Natural)
                        (/.then pure.monad
                                (|>> ++ (by pure.monad in))
                                (by pure.monad in mono)))))
             (_.coverage [/.all]
               (by (list.equivalence n.equivalence) =
                   (list#each ++ poly)
                   (|> poly
                       (list#each (|>> ++ (by pure.monad in)))
                       (is (List (Pure Natural)))
                       (/.all pure.monad)
                       (is (Pure (List Natural))))))
             (_.coverage [/.each]
               (by (list.equivalence n.equivalence) =
                   (list#each ++ poly)
                   (|> poly
                       (/.each pure.monad (|>> ++ (by pure.monad in)))
                       (is (Pure (List Natural))))))
             (_.coverage [/.only]
               (by (list.equivalence n.equivalence) =
                   (list.only n.even? poly)
                   (|> poly
                       (/.only pure.monad (|>> n.even? (by pure.monad in)))
                       (is (Pure (List Natural))))))
             (_.coverage [/.mix]
               (n.= (list#mix n.+ 0 poly)
                    (|> poly
                        (/.mix pure.monad
                               (function (_ part whole)
                                 (by pure.monad in
                                     (n.+ part whole)))
                               0)
                        (is (Pure Natural)))))
             (_.coverage [/.deferred]
               (|> (let [output (is (Atom Text)
                                    (atom.atom ""))]
                     (/.do [! io.monad]
                       [_ (do !
                            [/.deferred [! (atom.update! (text.suffix after) output)]]
                            (atom.update! (text.suffix before) output))]
                       (atom.read! output)))
                   io.value
                   (text.= (%.message before after))))

             (/free.test ..spec)
             /indexed.test
             ))))
