... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [functor
     [\\test (.only Injection Comparison)]]]
   [concurrency
    ["[0]" atom (.only Atom)]]
   [control
    ["[0]" pure (.only Pure)]
    ["[0]" io]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 ["[0]" /
  ["[1][0]" free]
  ["[1][0]" indexed]])

(the .public (spec injection comparison (open "/:[0]"))
  (for_any (_ !)
    (-> (Injection !) (Comparison !) (/.Monad !)
        Test))
  (<| (_.for [/.Monad])
      (/.let [! random.monad]
        [sample random.natural
         increase (by ! each (function (_ diff)
                               (|>> (n.+ diff) /:pure))
                      random.natural)
         decrease (by ! each (function (_ diff)
                               (|>> (n.- diff) /:pure))
                      random.natural)
         morphism (by ! each (function (_ diff)
                               (|>> (n.+ diff) /:pure))
                      random.natural)
         .let [left_identity!
               ((comparison n.=)
                (|> (injection sample) (/:each morphism) /:conjoint)
                (morphism sample))

               right_identity!
               ((comparison n.=)
                (|> (injection sample) (/:each /:pure) /:conjoint)
                (injection sample))

               associativity!
               ((comparison n.=)
                (|> (injection sample) (/:each increase) /:conjoint (/:each decrease) /:conjoint)
                (|> (injection sample) (/:each (|>> increase (/:each decrease) /:conjoint)) /:conjoint))]])
      (_.coverage [/.functor /.pure /.conjoint]
        (and left_identity!
             right_identity!
             associativity!))))

(the .public test
  Test
  (/.let random.monad
    [mono random.natural
     before (random.lower_cased 1)
     after (random.upper_cased 1)

     expected random.natural
     .let [not_expected (++ expected)]]
    (<| (_.covering /._)
        (all _.and
             (_.coverage [/.let]
               (n.= (++ mono)
                    (is (Pure Natural)
                        (/.let pure.monad
                          [sample (pure mono)]
                          (pure (++ sample))))))
             (_.coverage [/.then]
               (n.= (++ mono)
                    (is (Pure Natural)
                        (/.then pure.monad
                                (|>> ++ (by pure.monad pure))
                                (by pure.monad pure mono)))))
             (_.coverage [/.deferred]
               (|> (let [output (is (Atom Text)
                                    (atom.atom ""))]
                     (/.let [! io.monad]
                       [_ (/.let !
                            [/.deferred [! (atom.update! (text.suffix after) output)]]
                            (atom.update! (text.suffix before) output))]
                       (atom.read! output)))
                   io.value
                   (text.= (text before after))))
             (_.coverage [/.if]
               (and (identical? expected
                                (/.let [! pure.monad]
                                  []
                                  (/.if ! (pure true)
                                        (pure expected)
                                        (pure not_expected))))
                    (identical? expected
                                (/.let [! pure.monad]
                                  []
                                  (/.if ! (pure false)
                                        (pure not_expected)
                                        (pure expected))))))
             (_.coverage [/.when]
               (and (identical? expected
                                (/.let [! pure.monad]
                                  []
                                  (/.when ! (pure true)
                                    .true
                                    (pure expected)
                                    
                                    .false
                                    (pure not_expected))))
                    (identical? expected
                                (/.let [! pure.monad]
                                  []
                                  (/.when ! (pure .false)
                                    .true
                                    (pure not_expected)
                                    
                                    .false
                                    (pure expected))))))

             (/free.test ..spec)
             /indexed.test
             ))))
