(.require
 [library
  [lux (.except)
   [abstract
    [functor
     [\\test (.only Injection Comparison)]]]
   [data
    ["[0]" identity (.only Identity)]
    [collection
     ["[0]" list (.use "[1]#[0]" functor mix)]]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only do)
   ["[0]" free]]]
 ["[0]" /
  ["[1][0]" free]
  ["[1][0]" indexed]])

(def (left_identity injection comparison (open "_//[0]"))
  (All (_ !)
    (-> (Injection !) (Comparison !) (/.Monad !)
        Test))
  (do [! random.monad]
    [sample random.nat
     morphism (of ! each (function (_ diff)
                           (|>> (n.+ diff) _//in))
                  random.nat)]
    (_.test "Left identity."
      ((comparison n.=)
       (|> (injection sample) (_//each morphism) _//conjoint)
       (morphism sample)))))

(def (right_identity injection comparison (open "_//[0]"))
  (All (_ !)
    (-> (Injection !) (Comparison !) (/.Monad !)
        Test))
  (do random.monad
    [sample random.nat]
    (_.test "Right identity."
      ((comparison n.=)
       (|> (injection sample) (_//each _//in) _//conjoint)
       (injection sample)))))

(def (associativity injection comparison (open "_//[0]"))
  (All (_ !)
    (-> (Injection !) (Comparison !) (/.Monad !)
        Test))
  (do [! random.monad]
    [sample random.nat
     increase (of ! each (function (_ diff)
                           (|>> (n.+ diff) _//in))
                  random.nat)
     decrease (of ! each (function (_ diff)
                           (|>> (n.- diff) _//in))
                  random.nat)]
    (_.test "Associativity."
      ((comparison n.=)
       (|> (injection sample) (_//each increase) _//conjoint (_//each decrease) _//conjoint)
       (|> (injection sample) (_//each (|>> increase (_//each decrease) _//conjoint)) _//conjoint)))))

(def .public (spec injection comparison monad)
  (All (_ !)
    (-> (Injection !) (Comparison !) (/.Monad !)
        Test))
  (<| (_.for [/.Monad])
      (all _.and
           (..left_identity injection comparison monad)
           (..right_identity injection comparison monad)
           (..associativity injection comparison monad)
           )))

(def .public test
  Test
  (do random.monad
    [mono random.nat
     poly (random.list 10 random.nat)]
    (<| (_.covering /._)
        (all _.and
             (_.coverage [/.do]
               (n.= (++ mono)
                    (is (Identity Nat)
                        (/.do identity.monad
                          [sample (in mono)]
                          (in (++ sample))))))
             (_.coverage [/.then]
               (n.= (++ mono)
                    (is (Identity Nat)
                        (/.then identity.monad
                                (|>> ++ (of identity.monad in))
                                (of identity.monad in mono)))))
             (_.coverage [/.all]
               (of (list.equivalence n.equivalence) =
                   (list#each ++ poly)
                   (|> poly
                       (list#each (|>> ++ (of identity.monad in)))
                       (is (List (Identity Nat)))
                       (/.all identity.monad)
                       (is (Identity (List Nat))))))
             (_.coverage [/.each]
               (of (list.equivalence n.equivalence) =
                   (list#each ++ poly)
                   (|> poly
                       (/.each identity.monad (|>> ++ (of identity.monad in)))
                       (is (Identity (List Nat))))))
             (_.coverage [/.only]
               (of (list.equivalence n.equivalence) =
                   (list.only n.even? poly)
                   (|> poly
                       (/.only identity.monad (|>> n.even? (of identity.monad in)))
                       (is (Identity (List Nat))))))
             (_.coverage [/.mix]
               (n.= (list#mix n.+ 0 poly)
                    (|> poly
                        (/.mix identity.monad
                               (function (_ part whole)
                                 (of identity.monad in
                                     (n.+ part whole)))
                               0)
                        (is (Identity Nat)))))

             /free.test
             (_.for [free.monad]
                    (..spec /free.injection /free.comparison (is (/.Monad (free.Free List))
                                                                 (free.monad list.functor))))
             
             /indexed.test
             ))))
