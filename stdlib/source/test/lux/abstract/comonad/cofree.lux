(.module:
  [library
   [lux "*"
    ["_" test {"+" Test}]
    [abstract
     [functor {"+" Functor}]
     [comonad {"+" CoMonad}]
     [\\specification
      ["$[0]" functor {"+" Injection Comparison}]
      ["$[0]" comonad]]]
    [control
     ["//" continuation]]
    [data
     [collection
      ["[0]" list]
      ["[0]" stream {"+" Stream} ("[1]#[0]" comonad)]]]
    [math
     ["[0]" random]]]]
  [\\library
   ["[0]" /]])

(def: (injection value)
  (Injection (/.CoFree Stream))
  [value (stream#each injection (stream.repeated value))])

(def: (interpret [head tail])
  (All (_ a) (-> (/.CoFree Stream a) (Stream a)))
  (|> tail
      (stream#each (# (/.comonad stream.functor) out))
      [head]
      //.pending))

(def: comparison
  (Comparison (/.CoFree Stream))
  (function (_ == left right)
    (# (list.equivalence ==) =
       (stream.first 100 (..interpret left))
       (stream.first 100 (..interpret right)))))

(def: .public test
  Test
  (<| (_.covering /._)
      (_.for [/.CoFree])
      ($_ _.and
          (_.for [/.functor]
                 ($functor.spec ..injection ..comparison (: (Functor (/.CoFree Stream))
                                                            (/.functor stream.functor))))
          (_.for [/.comonad]
                 ($comonad.spec ..injection ..comparison (: (CoMonad (/.CoFree Stream))
                                                            (/.comonad stream.functor))))
          )))
