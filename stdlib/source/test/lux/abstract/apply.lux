... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" maybe]
    ["[0]" function]]
   [data
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [meta
    ["[0]" type]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 [//
  ["[0]T" functor (.only Injection Comparison)]])

(the .public test
  Test
  (do random.monad
    [left random.natural
     right random.natural]
    (<| (_.covering /._)
        (all _.and
             (_.coverage [/.composite]
               (let [expected (n.+ left right)]
                 (when (by (/.composite maybe.monad maybe.apply list.apply) on
                           {.#Some (list right)}
                           {.#Some (list (n.+ left))})
                   {.#Some (list actual)}
                   (n.= expected actual)

                   _
                   false)))
             ))))

(the .public (spec injection comparison it)
  (for_any (_ !)
    (-> (Injection !) (Comparison !) (/.Apply !)
        Test))
  (<| (_.for [/.Apply])
      (type.let [:$/1: (-> Natural Natural)])
      (do [! random.monad]
        [sample random.natural
         increase (is (Random :$/1:)
                      (by ! each n.+ random.natural))
         decrease (is (Random :$/1:)
                      (by ! each n.- random.natural))])
      (all _.and
           (_.for [/.functor]
                  (functorT.spec injection comparison (its /.functor it)))

           (_.coverage [/.on]
             (let [(open "/#[0]") it

                   identity!
                   ((comparison n.=)
                    (/#on (injection sample)
                          (injection function.identity))
                    (injection sample))

                   homomorphism!
                   ((comparison n.=)
                    (/#on (injection sample) (injection increase))
                    (injection (increase sample)))
                   
                   interchange!
                   ((comparison n.=) (/#on (injection sample) (injection increase))
                    (/#on (injection increase) (injection (is (-> (-> Natural Natural)
                                                                  Natural)
                                                              (function (_ f) (f sample))))))
                   
                   composition!
                   ((comparison n.=)
                    (|> (injection (is (-> :$/1: :$/1: :$/1:)
                                       function.composite))
                        (/#on (injection increase))
                        (/#on (injection decrease))
                        (/#on (injection sample)))
                    (/#on (/#on (injection sample)
                                (injection increase))
                          (injection decrease)))]
               (and identity!
                    homomorphism!
                    interchange!
                    composition!)))
           )))
