... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]
    ["[0]" order
     ["[1]T" \\test]]]
   [error
    ["[0]" try (.use "[1]:[0]" functor)]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]:[0]" mix)]]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]
   [aspect
    ["[0]" view]]]]
 [\\library
  ["[0]" /]])

(the .public test
  Test
  (let [limit (is (Random Natural)
                  (by random.monad each (n.% 20) random.natural))]
    (monad.let random.monad
      [start limit
       end limit
       .let [[start end] (if (n.< end start)
                             [start end]
                             [end start])
             range (/.range n.enum start end)]]
      (<| (_.covering /._)
          (all _.and
               (_.coverage [/.range]
                 (let [expected_size (|> end (n.- start) ++)
                       expected_start? (|> range (list.item 0) (try:each (n.= start)) (try.else false))
                       expected_end? (|> range list.in_reverse (list.item 0) (try:each (n.= end)) (try.else false))
                       can_be_backwards? (by (list.equivalence n.equivalence) =
                                             (/.range n.enum start end)
                                             (list.in_reverse (/.range n.enum end start)))
                       every_element_is_a_successor? (when (view.as list.stack range)
                                                       {.:Top head tail}
                                                       (|> (list:mix (function (_ next [verdict prev])
                                                                       [(and verdict
                                                                             (n.= next (by n.enum succ prev)))
                                                                        next])
                                                                     [true head]
                                                                     (view.of list.stack tail))
                                                           product.left)
                                                       
                                                       {.:Empty}
                                                       false)]
                   (and (n.= expected_size (list.size range))
                        expected_start?
                        expected_end?
                        can_be_backwards?
                        every_element_is_a_successor?)))
               )))))

(the .public (spec (open "/:[0]") random)
  (for_any (_ it)
    (-> (/.Enum it) (Random it)
        Test))
  (monad.let random.monad
    [sample random]
    (<| (_.for [/.Enum])
        (all _.and
             (_.for [/.order]
                    (orderT.spec /:order random))
             
             (_.coverage [/.succ /.pred]
               (and (/:= (|> sample /:succ /:pred)
                         sample)
                    (/:= (|> sample /:pred /:succ)
                         sample)
                    (not (/:= (/:succ sample)
                              sample))
                    (not (/:= (/:pred sample)
                              sample))))
             ))))
