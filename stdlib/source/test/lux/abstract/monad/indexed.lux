... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(every (Effect input output value)
  (-> input
      [output value]))

(the monad
  (/.Monad Effect)
  (implementation
   (the (pure it)
     (function (_ input)
       [input it]))

   (the (then $ @)
     (function (_ pre)
       (let [[interim input] (@ pre)]
         (($ input) interim))))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Monad])
      (monad.let random.monad
        [left random.natural
         right random.natural
         .let [expected (n.+ left right)]])
      (all _.and
           (_.coverage [/.let
                        /.pure /.then]
             (let [it (is (Effect [] [] Natural)
                          (/.let ..monad
                            [left' (pure left)
                             right' (pure right)]
                            (pure (n.+ left right))))
                   [_ actual] (it [])]
               (n.= expected actual)))
           )))
