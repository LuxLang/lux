(.module:
  [lux #*
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do)]]
   [data
    ["." maybe]
    [number
     ["n" nat]]
    [collection
     ["." list]]]
   [math
    ["." random]]]
  {1
   ["." / (#+ Functor)]})

(def: #export test
  Test
  (do random.monad
    [left random.nat
     right random.nat
     shift random.nat]
    (<| (_.covering /._)
        ($_ _.and
            (_.cover [/.Or /.sum]
                     (and (case (\ (/.sum maybe.functor list.functor) map
                                   (n.+ shift)
                                   (#.Left (#.Some left)))
                            (#.Left (#.Some actual))
                            (n.= (n.+ shift left) actual)

                            _
                            false)
                          (case (\ (/.sum maybe.functor list.functor) map
                                   (n.+ shift)
                                   (#.Right (list right)))
                            (^ (#.Right (list actual)))
                            (n.= (n.+ shift right) actual)

                            _
                            false)))
            (_.cover [/.And /.product]
                     (case (\ (/.product maybe.functor list.functor) map
                              (n.+ shift)
                              [(#.Some left) (list right)])
                       (^ [(#.Some actualL) (list actualR)])
                       (and (n.= (n.+ shift left) actualL)
                            (n.= (n.+ shift right) actualR))

                       _
                       false))
            (_.cover [/.Then /.compose]
                     (case (\ (/.compose maybe.functor list.functor) map
                              (n.+ shift)
                              (#.Some (list left)))
                       (^ (#.Some (list actual)))
                       (n.= (n.+ shift left) actual)

                       _
                       false))
            ))))
