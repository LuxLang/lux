... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["[0]" natural]
     ["[0]" integer]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    [equivalence (.only Equivalence)]]]])

(the .public test
  Test
  (do random.monad
    [naturalL random.natural
     naturalR random.natural
     integerL random.integer
     integerR random.integer]
    (<| (_.covering /._)
        (all _.and
             (_.coverage [/.and]
               (let [[naturalLR integerLR] (by (/.and natural.addition integer.multiplication) composite
                                               [naturalL integerL] [naturalR integerR])]
                 (and (natural.= (by natural.addition composite naturalL naturalR)
                                 naturalLR)
                      (integer.= (by integer.multiplication composite integerL integerR)
                                 integerLR))))
             ))))

(the .public (spec (open "/#[0]") (open "/#[0]") random)
  (for_any (_ of)
    (-> (Equivalence of) (/.Monoid of) (Random of)
        Test))
  (do random.monad
    [sample random
     left random
     mid random
     right random]
    (<| (_.for [/.Monoid])
        (all _.and
             (_.coverage [/.identity]
               (let [left_identity!
                     (/#= sample
                          (/#composite /#identity sample))
                     
                     right_identity!
                     (/#= sample
                          (/#composite sample /#identity))]
                 (and left_identity!
                      right_identity!)))
             (_.coverage [/.composite]
               (let [associativity!
                     (/#= (/#composite left (/#composite mid right))
                          (/#composite (/#composite left mid) right))]
                 (and associativity!)))
             ))))
