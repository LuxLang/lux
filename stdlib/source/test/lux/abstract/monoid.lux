... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["[0]" natural]
     ["[0]" int]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    [equivalence (.only Equivalence)]]]])

(the .public test
  Test
  (do random.monad
    [naturalL random.natural
     naturalR random.natural
     intL random.int
     intR random.int]
    (<| (_.covering /._)
        (all _.and
             (_.coverage [/.and]
               (let [[naturalLR intLR] (of (/.and natural.addition int.multiplication) composite
                                           [naturalL intL] [naturalR intR])]
                 (and (natural.= (of natural.addition composite naturalL naturalR)
                                 naturalLR)
                      (int.= (of int.multiplication composite intL intR)
                             intLR))))
             ))))

(the .public (spec (open "/#[0]") (open "/#[0]") random)
  (for_any (_ of)
    (-> (Equivalence of) (/.Monoid of) (Random of)
        Test))
  (do random.monad
    [sample random
     left random
     mid random
     right random]
    (<| (_.for [/.Monoid])
        (all _.and
             (_.coverage [/.identity]
               (let [left_identity!
                     (/#= sample
                          (/#composite /#identity sample))
                     
                     right_identity!
                     (/#= sample
                          (/#composite sample /#identity))]
                 (and left_identity!
                      right_identity!)))
             (_.coverage [/.composite]
               (let [associativity!
                     (/#= (/#composite left (/#composite mid right))
                          (/#composite (/#composite left mid) right))]
                 (and associativity!)))
             ))))
