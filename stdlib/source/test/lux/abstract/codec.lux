(.module:
  [lux #*
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do)]]
   [control
    ["." try]]
   [data
    ["." bit ("#\." equivalence)]
    [format
     ["." json (#+ JSON)]]]
   [math
    ["." random (#+ Random)]]]
  {1
   ["." / (#+ Codec)
    [//
     [equivalence (#+ Equivalence)]]]})

(def: json
  (Codec JSON Bit)
  (let [field "value"]
    (structure
     (def: encode
       (|>> #json.Boolean
            [field]
            list
            (json.object)))
     (def: decode
       (json.get-boolean field)))))

(def: codec
  (Codec Text Bit)
  (/.compose json.codec ..json))

(def: #export test
  Test
  (do random.monad
    [expected random.bit]
    (<| (_.covering /._)
        (_.cover [/.compose]
                 (case (|> expected (\ ..codec encode) (\ ..codec decode))
                   (#try.Success actual)
                   (bit\= expected actual)
                   
                   (#try.Failure error)
                   false)))))
