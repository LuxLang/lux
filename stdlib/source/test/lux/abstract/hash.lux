... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]
    [functor
     ["[0]" contravariant
      ["[1]T" \\test]]]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["[0]" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only Hash)
   [//
    [equivalence (.only Equivalence)]]]])

(the .public (spec (open "/#[0]") random)
  (for_any (_ it)
    (-> (/.Hash it) (Random it)
        Test))
  (do random.monad
    [parameter random
     subject random]
    (all _.and
         (_.for [/.equivalence]
                (equivalenceT.spec /#equivalence random))
         (_.coverage [/.Hash /.hash]
           (if (/#= parameter subject)
               (natural.= (/#hash parameter) (/#hash subject))
               true))
         )))

(the .public test
  Test
  (do random.monad
    [leftN random.natural
     rightN random.natural
     .let [hash (is (Equivalence (/.Hash Natural))
                    (implementation
                     (the (= (open "left#[0]") (open "right#[0]"))
                       (and (bit.= (left#= (left#hash leftN) (left#hash leftN))
                                   (right#= (right#hash leftN) (right#hash leftN)))
                            (bit.= (left#= (left#hash rightN) (left#hash rightN))
                                   (right#= (right#hash rightN) (right#hash rightN)))
                            (bit.= (left#= (left#hash leftN) (left#hash rightN))
                                   (right#= (right#hash leftN) (right#hash rightN)))))))]]
    (<| (_.covering /._)
        (all _.and
             (_.for [/.functor]
                    (contravariantT.spec hash natural.hash /.functor))
             ))))
