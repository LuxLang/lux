(.using
 [library
  [lux "*"
   ["_" test {"+" Test}]
   [abstract
    [monad {"+" do}]]
   [control
    ["[0]" io]]
   [math
    ["[0]" random]]]]
 [\\library
  ["[0]" /]]
 ["$[0]" / "_"
  ["[1][0]" export]])

(/.import: (os/getenv [/.String] "io" "?" /.String))

(def: .public test
  Test
  (do [! random.monad]
    [boolean random.bit
     integer random.int
     float random.frac
     string (random.lower_case 1)]
    (<| (_.covering /._)
        (`` (all _.and
                 (~~ (template [<type> <sample>]
                       [(_.cover [<type>]
                                 (exec
                                   (is <type> <sample>)
                                   true))]

                       [/.Boolean boolean]
                       [/.Integer integer]
                       [/.Float float]
                       [/.String string]
                       ))
                 (_.for [/.Object]
                        (all _.and
                             (~~ (template [<type>]
                                   [(_.cover [<type>]
                                             (exec
                                               (|> []
                                                   (as <type>)
                                                   (is (Ex (_ a) (/.Object a))))
                                               true))]

                                   [/.Nil]
                                   [/.Table]
                                   ))))
                 (_.cover [/.Function /.function]
                          (exec
                            (|> (/.function (_ [input/0 Nat])
                                  Int
                                  (.int input/0))
                                (is /.Function)
                                (is (Ex (_ a) (/.Object a))))
                            true))
                 (_.cover [/.import:]
                          (case (io.run! (..os/getenv string))
                            {.#Some _} true
                            {.#None} true))

                 $/export.test
                 )))))
