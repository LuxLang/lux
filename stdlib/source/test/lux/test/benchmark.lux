... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" order]]
   [control
    ["[0]" io (.only IO)]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]
     ["f" frac]]]
   [world
    [time
     ["[0]" duration]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(def (fibonacci it)
  (-> Nat
      Nat)
  (when it
    0 0
    1 1
    _ (n.+ (fibonacci (n.- 1 it))
           (fibonacci (n.- 2 it)))))

(def .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [times (of ! each (n.% 10) random.nat)

         .let [computation (is (IO Nat)
                               (io.io (fibonacci 32)))]])
      (all _.and
           (_.coverage [/.time]
             (io.run!
              (do io.monad
                [duration (/.time computation)]
                (in (duration.positive? duration)))))
           (<| (_.for [/.Benchmark
                       /.#times /.#minimum /.#maximum /.#average])
               (all _.and
                    (_.coverage [/.test]
                      (io.run!
                       (do io.monad
                         [it (/.test times computation)
                          .let [performed_exact_number_of_times!
                                (n.= times (the /.#times it))]]
                         (in (and performed_exact_number_of_times!
                                  (when times
                                    0 (and (duration.neutral? (the /.#minimum it))
                                           (duration.neutral? (the /.#maximum it))
                                           (duration.neutral? (the /.#average it)))
                                    _ (and (duration.positive? (the /.#minimum it))
                                           (duration.positive? (the /.#maximum it))
                                           (duration.positive? (the /.#average it))

                                           (order.<= duration.order
                                                     (the /.#maximum it)
                                                     (the /.#minimum it))
                                           (order.<= duration.order
                                                     (the /.#maximum it)
                                                     (the /.#average it))
                                           (order.>= duration.order
                                                     (the /.#minimum it)
                                                     (the /.#average it)))))))))
                    ))
           )))
