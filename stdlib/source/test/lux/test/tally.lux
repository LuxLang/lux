... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [data
    [collection
     ["[0]" set]]]
   [math
    ["[0]" random]
    [number
     ["n" natural]]]
   [meta
    ["[0]" symbol]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 [///
  [meta
   ["[0]T" symbol]]])

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Tally])
      (do [! random.monad]
        [left (symbolT.random 1 1)
         right (symbolT.random 2 2)
         expected_successes random.natural
         expected_failures random.natural])
      (all _.and
           (_.coverage [/.empty]
             (and (n.= 0 (its /.#successes /.empty))
                  (n.= 0 (its /.#failures /.empty))
                  (n.= 0 (set.size (its /.#expected /.empty)))
                  (n.= 0 (set.size (its /.#actual /.empty)))))
           (_.coverage [/.success /.#successes]
             (and (n.= 1 (its /.#successes /.success))
                  (n.= 0 (its /.#failures /.success))
                  (n.= 0 (set.size (its /.#expected /.success)))
                  (n.= 0 (set.size (its /.#actual /.success)))))
           (_.coverage [/.failure /.#failures]
             (and (n.= 0 (its /.#successes /.failure))
                  (n.= 1 (its /.#failures /.failure))
                  (n.= 0 (set.size (its /.#expected /.failure)))
                  (n.= 0 (set.size (its /.#actual /.failure)))))
           (_.coverage [/.failed?]
             (and (/.failed? /.failure)
                  (not (/.failed? /.success))))
           (_.coverage [/.and /.#expected /.#actual]
             (and (let [it (/.and /.success /.success)]
                    (and (n.= 2 (its /.#successes it))
                         (n.= 0 (its /.#failures it))))
                  (let [it (/.and /.failure /.failure)]
                    (and (n.= 0 (its /.#successes it))
                         (n.= 2 (its /.#failures it))))
                  (let [it (/.and /.success /.failure)]
                    (and (n.= 1 (its /.#successes it))
                         (n.= 1 (its /.#failures it))))
                  (let [custom [/.#successes expected_successes
                                /.#failures expected_failures
                                /.#expected (set.of_list symbol.hash (list left))
                                /.#actual (set.of_list symbol.hash (list right))]]
                    (and (let [it (/.and /.success custom)]
                           (and (n.= (++ expected_successes) (its /.#successes it))
                                (n.= expected_failures (its /.#failures it))
                                (set.= (its /.#expected custom) (its /.#expected it))
                                (set.= (its /.#actual custom) (its /.#actual it))))
                         (let [it (/.and custom /.failure)]
                           (and (n.= expected_successes (its /.#successes it))
                                (n.= (++ expected_failures) (its /.#failures it))
                                (set.= (its /.#expected custom) (its /.#expected it))
                                (set.= (its /.#actual custom) (its /.#actual it))))))))
           )))
