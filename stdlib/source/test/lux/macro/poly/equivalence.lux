(.module:
  [lux #*
   [control
    [monad (#+ do Monad)]
    [equivalence (#+ Equivalence)]]
   [data
    ["." bit]
    ["." maybe]
    [number
     ["." int ("#/." number)]]
    ["." text
     format]
    [collection
     ["." list]]]
   [math
    ["r" random]]
   ["." macro
    [poly (#+ derived:)
     ["&" equivalence]]]]
  lux/test)

(type: Variant
  (#Case0 Bit)
  (#Case1 Int)
  (#Case2 Frac))

(type: #rec Recursive
  (#Number Frac)
  (#Addition Frac Recursive))

(type: Record
  {#bit Bit
   #int Int
   #frac Frac
   #text Text
   #maybe (Maybe Int)
   #list (List Int)
   #variant Variant
   #tuple [Int Frac Text]
   #recursive Recursive})

(def: gen-recursive
  (r.Random Recursive)
  (r.rec (function (_ gen-recursive)
           (r.or r.frac
                 (r.and r.frac gen-recursive)))))

(def: gen-record
  (r.Random Record)
  (do r.monad
    [size (:: @ map (n/% 2) r.nat)
     #let [gen-int (|> r.int (:: @ map (|>> int/abs (i/% +1_000_000))))]]
    ($_ r.and
        r.bit
        gen-int
        r.frac
        (r.unicode size)
        (r.maybe gen-int)
        (r.list size gen-int)
        ($_ r.or r.bit gen-int r.frac)
        ($_ r.and gen-int r.frac (r.unicode size))
        gen-recursive)))

(derived: (&.Equivalence<?> Record))

(context: "Equivalence polytypism"
  (<| (times 100)
      (do @
        [sample gen-record
         #let [(^open "&/.") ..equivalence]]
        (test "Every instance equals itself."
              (&/= sample sample)))))
