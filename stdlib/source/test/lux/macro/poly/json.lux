(.module:
  [lux #*
   ["%" data/text/format (#+ format)]
   ["_" test (#+ Test)]
   [abstract
    codec
    [monad (#+ do)]
    [equivalence (#+ Equivalence)]
    {[0 #test]
     [/
      ["$." equivalence]
      ["$." codec]]}]
   [control
    pipe
    ["p" parser
     ## TODO: Get rid of this import ASAP
     [json (#+)]]]
   [data
    ["." bit]
    ["." maybe]
    ["." text]
    [number
     ["." frac]]
    [format
     [json (#+)]]
    [collection
     [row (#+ row)]
     ["d" dictionary]
     ["." list]]]
   [macro
    [poly (#+ derived:)]
    ["." poly/equivalence]]
   [type
    ["." unit]]
   [math
    ["r" random (#+ Random)]]
   [time
    ["ti" instant]
    ["tda" date]
    ## ["tdu" duration]
    ]]
  [test
   [lux
    [time
     ["_." instant]
     ## ["_." duration]
     ["_." date]]]]
  {1
   ["." /]}
  )

(type: Variant
  (#Bit Bit)
  (#Text Text)
  (#Frac Frac))

(type: #rec Recursive
  (#Number Frac)
  (#Addition Frac Recursive))

(type: Record
  {#bit Bit
   #frac Frac
   #text Text
   #maybe (Maybe Frac)
   #list (List Frac)
   #dictionary (d.Dictionary Text Frac)
   #variant Variant
   #tuple [Bit Frac Text]
   #recursive Recursive
   ## #instant ti.Instant
   ## #duration tdu.Duration
   #date tda.Date
   #grams (unit.Qty unit.Gram)
   })

(def: gen-recursive
  (Random Recursive)
  (r.rec (function (_ gen-recursive)
           (r.or r.frac
                 (r.and r.frac gen-recursive)))))

(derived: recursive-equivalence (poly/equivalence.equivalence Recursive))

(def: qty
  (All [unit] (Random (unit.Qty unit)))
  (|> r.int (:: r.monad map unit.in)))

(def: gen-record
  (Random Record)
  (do r.monad
    [size (:: @ map (n/% 2) r.nat)]
    ($_ r.and
        r.bit
        r.frac
        (r.unicode size)
        (r.maybe r.frac)
        (r.list size r.frac)
        (r.dictionary text.hash size (r.unicode size) r.frac)
        ($_ r.or r.bit (r.unicode size) r.frac)
        ($_ r.and r.bit r.frac (r.unicode size))
        ..gen-recursive
        ## _instant.instant
        ## _duration.duration
        _date.date
        ..qty
        )))

(derived: equivalence (poly/equivalence.equivalence Record))
(derived: codec (/.codec Record))

(def: #export test
  Test
  (<| (_.context (%.name (name-of /._)))
      (<| (_.seed 14562075782602945288)
          ($codec.spec ..equivalence ..codec gen-record))))
