... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" maybe]]
   [error
    ["[0]" try (.use "[1]#[0]" functor)]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the export
  (Random [(Maybe Code) Natural])
  (all random.and
       (|> random.bit
           (by random.monad each code.bit)
           random.maybe)
       random.natural))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Policy])
      (do random.monad
        [[expected_export_policy expected_un_exported] ..export]
        (_.coverage [/.with /.default]
          (|> (when expected_export_policy
                {.#Some expected_export_policy}
                (stack expected_export_policy (code.natural expected_un_exported))

                {.#None}
                (stack (code.natural expected_un_exported)))
              (<code>.value (/.with <code>.natural))
              (try#each (function (_ [actual_export_policy actual_un_exported])
                          (|> expected_export_policy
                              (maybe.else /.default)
                              (code.= actual_export_policy)
                              (and (n.= expected_un_exported actual_un_exported)))))
              (try.else false))))))
