... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [data
    [collection
     ["[0]" list
      ["?[1]" \\projection]]]]
   [error
    ["[0]" try]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [meta
    ["[0]" static]
    ["[0]" code
     ["<[1]>" \\projection]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public random
  (Random /.Declaration)
  (let [word (random.alphabetic 10)]
    (all random.and
         word
         (do [! random.monad]
           [size (by ! each (n.% 10) random.natural)]
           (random.list size word))
         )))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Declaration
              /.#name /.#arguments])
      (all _.and
           (_.for [/.equivalence /.=]
                  (static.when (same? /.equivalence /.=)
                    (equivalenceT.spec /.equivalence ..random)))

           (do random.monad
             [expected ..random]
             (_.coverage [/.as_code /.of_code]
               (when (?list.value (list (/.as_code expected))
                                  /.of_code)
                 {try.#Failure _}
                 false
                 
                 {try.#Success actual}
                 (by /.equivalence = expected actual)))))))
