... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [control
    ["<>" projection]
    ["[0]" try]]
   [math
    ["[0]" random (.only Random)]]
   [meta
    ["[0]" static]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 ["$[0]" ////
  [meta
   ["[1][0]" code]]])

(the .public random
  (Random /.Check)
  (all random.and
       $////code.random
       $////code.random
       ))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Check
              /.#type /.#value])
      (all _.and
           (_.for [/.equivalence /.=]
                  (static.when (same? /.equivalence /.=)
                    (equivalenceT.spec /.equivalence ..random)))

           (do random.monad
             [[type value] ..random]
             (_.coverage [/.injection /.projection]
               (when (<code>.value /.projection
                                   (list (/.injection [/.#type type
                                                       /.#value value])))
                 {try.#Failure _}
                 false
                 
                 {try.#Success check}
                 (and (code.= type (its /.#type check))
                      (code.= value (its /.#value check)))))))))
