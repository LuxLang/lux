(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [abstract
     [monad (#+ do)]
     [\\spec
      ["$." equivalence]]]
    [control
     ["." try]
     ["<>" parser
      ["<.>" code]]]
    [math
     ["." random (#+ Random)]]
    [macro
     ["." code ("#\." equivalence)]]]]
  [\\library
   ["." /]]
  ["$." /// #_
   ["#." code]])

(def: #export random
  (Random /.Check)
  ($_ random.and
      $///code.random
      $///code.random
      ))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [/.Check])
      ($_ _.and
          (_.for [/.equivalence]
                 ($equivalence.spec /.equivalence ..random))

          (do random.monad
            [[type value] ..random]
            (_.cover [/.format /.parser]
                     (case (<code>.run /.parser
                                       (list (/.format {#/.type type
                                                        #/.value value})))
                       (#try.Failure _)
                       false
                       
                       (#try.Success check)
                       (and (code\= type (get@ #/.type check))
                            (code\= value (get@ #/.value check)))))))))
