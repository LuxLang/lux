(.using
 [library
  [lux (.except)
   ["_" test (.only Test)]
   [abstract
    [monad (.only do)]
    [\\specification
     ["$[0]" equivalence]]]
   [control
    ["[0]" try]
    ["<>" parser (.only)
     ["<[0]>" code]]]
   [math
    ["[0]" random (.only Random)]]
   [macro
    ["[0]" code (.open: "[1]#[0]" equivalence)]]]]
 [\\library
  ["[0]" /]]
 ["$[0]" ///
  ["[1][0]" code]])

(def: .public random
  (Random /.Check)
  (all random.and
       $///code.random
       $///code.random
       ))

(def: .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Check])
      (all _.and
           (_.for [/.equivalence]
                  ($equivalence.spec /.equivalence ..random))

           (do random.monad
             [[type value] ..random]
             (_.coverage [/.format /.parser]
               (case (<code>.result /.parser
                                    (list (/.format [/.#type type
                                                     /.#value value])))
                 {try.#Failure _}
                 false
                 
                 {try.#Success check}
                 (and (code#= type (the /.#type check))
                      (code#= value (the /.#value check)))))))))
