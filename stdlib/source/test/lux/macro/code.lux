(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [abstract
     [monad (#+ do)]
     [\\spec
      ["$." equivalence]]]
    [control
     ["." try (#+ Try)]]
    [data
     ["." product]
     ["." text]
     [collection
      ["." list ("#\." functor)]]]
    [math
     ["." random (#+ Random) ("#\." monad)]
     [number
      ["n" nat]]]
    [meta
     ["." location]]
    [tool
     [compiler
      [language
       [lux
        ["." syntax]]]]]]]
  [\\library
   ["." /]])

(def: random_text
  (Random Text)
  (random.ascii/alpha 10))

(def: random_name
  (Random Name)
  (random.and ..random_text ..random_text))

(def: (random_sequence random)
  (All [a] (-> (Random a) (Random (List a))))
  (do {! random.monad}
    [size (|> random.nat (\ ! map (n.% 3)))]
    (random.list size random)))

(def: (random_record random)
  (All [a] (-> (Random a) (Random (List [a a]))))
  (do {! random.monad}
    [size (|> random.nat (\ ! map (n.% 3)))]
    (random.list size (random.and random random))))

(def: #export random
  (Random Code)
  (random.rec
   (function (_ random)
     ($_ random.either
         (random\map /.bit random.bit)
         (random\map /.nat random.nat)
         (random\map /.int random.int)
         (random\map /.rev random.rev)
         (random\map /.frac random.safe_frac)
         (random\map /.text ..random_text)
         (random\map /.identifier ..random_name)
         (random\map /.tag ..random_name)
         (random\map /.form (..random_sequence random))
         (random\map /.tuple (..random_sequence random))
         (random\map /.record (..random_record random))
         ))))

(def: (read source_code)
  (-> Text (Try Code))
  (let [parse (syntax.parse ""
                            syntax.no_aliases
                            (text.size source_code))
        start (: Source
                 [location.dummy 0 source_code])]
    (case (parse start)
      (#.Left [end error])
      (#try.Failure error)
      
      (#.Right [end lux_code])
      (#try.Success lux_code))))

(def: (replace_simulation [original substitute])
  (-> [Code Code] (Random [Code Code]))
  (random.rec
   (function (_ replace_simulation)
     (let [for_sequence (: (-> (-> (List Code) Code) (Random [Code Code]))
                           (function (_ to_code)
                             (do {! random.monad}
                               [parts (..random_sequence replace_simulation)]
                               (wrap [(to_code (list\map product.left parts))
                                      (to_code (list\map product.right parts))]))))]
       ($_ random.either
           (random\wrap [original substitute])
           (do {! random.monad}
             [sample (random.filter (|>> (\ /.equivalence = original) not)
                                    ($_ random.either
                                        (random\map /.bit random.bit)
                                        (random\map /.nat random.nat)
                                        (random\map /.int random.int)
                                        (random\map /.rev random.rev)
                                        (random\map /.frac random.safe_frac)
                                        (random\map /.text ..random_text)
                                        (random\map /.identifier ..random_name)
                                        (random\map /.tag ..random_name)))]
             (wrap [sample sample]))
           (for_sequence /.form)
           (for_sequence /.tuple)
           (do {! random.monad}
             [parts (..random_sequence replace_simulation)]
             (wrap [(/.record (let [parts' (list\map product.left parts)]
                                (list.zip/2 parts' parts')))
                    (/.record (let [parts' (list\map product.right parts)]
                                (list.zip/2 parts' parts')))]))
           )))))

(def: #export test
  Test
  (<| (_.covering /._)
      ($_ _.and
          (_.for [/.equivalence]
                 ($equivalence.spec /.equivalence ..random))
          
          (_.for [/.format]
                 (`` ($_ _.and
                         (~~ (template [<coverage> <random> <tag>]
                               [(do {! random.monad}
                                  [expected <random>]
                                  (_.cover [<coverage>]
                                           (and (case (..read (/.format (<coverage> expected)))
                                                  (#try.Success actual)
                                                  (\ /.equivalence =
                                                     actual
                                                     (<coverage> expected))
                                                  
                                                  (#try.Failure error)
                                                  false)
                                                (\ /.equivalence =
                                                   [location.dummy (<tag> expected)]
                                                   (<coverage> expected)))))]

                               [/.bit random.bit #.Bit]
                               [/.nat random.nat #.Nat]
                               [/.int random.int #.Int]
                               [/.rev random.rev #.Rev]
                               [/.frac random.safe_frac #.Frac]
                               [/.text ..random_text #.Text]
                               [/.tag ..random_name #.Tag]
                               [/.identifier ..random_name #.Identifier]
                               [/.form (..random_sequence ..random) #.Form]
                               [/.tuple (..random_sequence ..random) #.Tuple]
                               [/.record (..random_record ..random) #.Record]))
                         (~~ (template [<coverage> <random> <tag>]
                               [(do {! random.monad}
                                  [expected <random>]
                                  (_.cover [<coverage>]
                                           (and (case (..read (/.format (<coverage> expected)))
                                                  (#try.Success actual)
                                                  (\ /.equivalence =
                                                     actual
                                                     (<coverage> expected))
                                                  
                                                  (#try.Failure error)
                                                  false)
                                                (\ /.equivalence =
                                                   [location.dummy (<tag> ["" expected])]
                                                   (<coverage> expected)))
                                           ))]

                               [/.local_tag        ..random_text #.Tag]
                               [/.local_identifier ..random_text #.Identifier]
                               )))))
          (do {! random.monad}
            [[original substitute] (random.filter (function (_ [original substitute])
                                                    (not (\ /.equivalence = original substitute)))
                                                  (random.and ..random ..random))
             [sample expected] (random.filter (function (_ [sample expected])
                                                (not (\ /.equivalence = sample expected)))
                                              (..replace_simulation [original substitute]))]
            (_.cover [/.replace]
                     (\ /.equivalence =
                        expected
                        (/.replace original substitute sample))))
          )))
