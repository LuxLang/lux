(.module:
  [lux #*
   ["%" data/text/format (#+ format)]
   [abstract/monad (#+ do)]
   ["r" math/random (#+ Random)]
   ["_" test (#+ Test)]
   [data
    ["." text ("#@." equivalence)]
    [number
     ["i" int]
     ["f" frac]]]]
  {1
   ["." /]})

(def: #export test
  Test
  (<| (_.context (%.name (name-of /._)))
      (do r.monad
        [bit r.bit
         nat r.nat
         int r.int
         rev r.rev
         above (:: @ map (i.% +100) r.int)
         below (:: @ map (i.% +100) r.int)
         #let [frac (|> below
                        (i./ +100)
                        i.frac
                        (f.+ (i.frac above))
                        (f.* -1.0))]
         text (r.ascii 10)
         short (r.ascii/alpha 10)
         module (r.ascii/alpha 10)
         #let [name [module short]]]
        (`` ($_ _.and
                (~~ (template [<desc> <code> <text>]
                      [(let [code <code>]
                         (_.test (format "Can produce " <desc> " code node.")
                                 (and (text@= <text> (/.to-text code))
                                      (:: /.equivalence = code code))))]

                      ["bit"              (/.bit bit)                                 (%.bit bit)]
                      ["nat"              (/.nat nat)                                 (%.nat nat)]
                      ["int"              (/.int int)                                 (%.int int)]
                      ["rev"              (/.rev rev)                                 (%.rev rev)]
                      ["frac"             (/.frac frac)                               (%.frac frac)]
                      ["text"             (/.text text)                               (%.text text)]
                      ["local-ltag"       (/.local-tag short)                         (format "#" short)]
                      ["lag"              (/.tag [module short])                      (format "#" (%.name name))]
                      ["local-identifier" (/.local-identifier short)                  short]
                      ["identifier"       (/.identifier [module short])               (%.name name)]
                      ["form"             (/.form (list (/.bit bit) (/.int int)))     (format "(" (%.bit bit) " " (%.int int) ")")]
                      ["tuple"            (/.tuple (list (/.bit bit) (/.int int)))    (format "[" (%.bit bit) " " (%.int int) "]")]
                      ["record"           (/.record (list [(/.bit bit) (/.int int)])) (format "{" (%.bit bit) " " (%.int int) "}")]
                      )))))))
