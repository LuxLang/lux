... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [data
    ["[0]" bit]
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [meta
    [macro
     ["[0]" template]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" //]]])

(the every?
  (/.Algorithm List)
  (is (/.Algorithm (//.Recursive
                    (for_any (_ recursion)
                      (//.Sum (//.Constant [])
                              (//.Product //.Variable recursion)))))
      (/.recursive
       (function (_ it)
         (/.sum /.constant
                (/.product /.variable
                           it))))))

(the .public test
  Test
  (do [! random.monad]
    [.let [natural (is (Random Natural)
                       (of ! each (n.% 1,000,000) random.natural))
           even (is (Random Natural)
                    (of ! each (n.* 2) natural))
           odd (is (Random Natural)
                   (of ! each (n.+ 1) even))]
     even_0 even
     even_1 even
     odd_0 odd]
    (<| (_.covering /._)
        (_.for [/.Algorithm])
        (all _.and
             (_.coverage [/.constant /.variable /.recursive
                          /.sum /.product]
               (`` (and (,, (template.with [,list]
                              [(let [it (is (List Natural)
                                            ,list)]
                                 (bit.= (list.every? n.odd? it)
                                        (..every? n.odd? it)))]

                              [(list even_0 even_1 odd_0)]
                              [(list even_0 odd_0 even_1)]
                              [(list odd_0 even_0 even_1)]
                              [(list even_0 even_1 even_0)]
                              )))))
             ))))
