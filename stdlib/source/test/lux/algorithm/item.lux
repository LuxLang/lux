... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]]
   [data
    [collection
     ["[0]" stack]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" //]]])

(the item
  (/.Algorithm Stack)
  (is (/.Algorithm (//.Recursive
                    (for_any (_ recursion)
                      (//.Sum (//.Constant [])
                              (//.Product //.Variable recursion)))))
      (/.recursive
       (function (_ it)
         (/.sum /.constant
                (/.product /.variable
                           it))))))

(the .public test
  Test
  (do [! random.monad]
    [arity (by ! each (n.% 10) random.natural)
     items (random.stack arity random.natural)]
    (<| (_.covering /._)
        (_.for [/.Algorithm])
        (all _.and
             (_.coverage [/.constant /.variable /.recursive
                          /.sum /.product]
               (let [= (try.equivalence n.equivalence)]
                 (stack.every? (function (_ index)
                                 (= (stack.item index items)
                                    (..item index items)))
                               (stack.indices arity))))
             (_.coverage [/.invalid]
               (when (..item arity items)
                 {try.#Failure /.invalid}
                 true

                 else
                 false))
             ))))
