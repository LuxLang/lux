(.module:
  [lux #*
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do)]]
   [control
    ["." try]]
   [data
    ["." text ("#\." equivalence)]]
   [math
    ["." random (#+ Random)]
    [number
     ["." nat]
     ["." frac]]]]
  {1
   ["." /]})

(/.import: Uint8Array)

## On Nashorn
(/.import: java/lang/String
  ["#::."
   (new [Uint8Array /.String])
   (getBytes [/.String] Uint8Array)])

## On Node
(/.import: Buffer
  ["#::."
   (#static from [/.String /.String] Buffer)
   (toString [/.String] /.String)])

## On the browser
(/.import: TextEncoder
  ["#::."
   (new [/.String])
   (encode [/.String] Uint8Array)])

(/.import: TextDecoder
  ["#::."
   (new [/.String])
   (decode [Uint8Array] /.String)])

(def: #export test
  Test
  (do {! random.monad}
    [boolean random.bit
     number (\ ! map (|>> (nat.% 100) nat.frac) random.nat)
     string (random.ascii 5)
     function (\ ! map (function (_ shift)
                         (: (-> Nat Nat)
                            (nat.+ shift)))
                 random.nat)
     ## I64s get compiled as JavaScript objects with a specific structure.
     object random.nat]
    (<| (_.covering /._)
        ($_ _.and
            (_.cover [/.on_browser? /.on_node_js? /.on_nashorn?]
                     (or /.on_nashorn?
                         /.on_node_js?
                         /.on_browser?))
            (_.cover [/.type_of]
                     (and (text\= "boolean" (/.type_of boolean))
                          (text\= "number" (/.type_of number))
                          (text\= "string" (/.type_of string))
                          (text\= "function" (/.type_of function))
                          (text\= "object" (/.type_of object))))
            (_.cover [/.try]
                     (case (/.try (error! string))
                       (#try.Success _)
                       false
                       
                       (#try.Failure error)
                       (text\= string error)))
            (_.cover [/.import:]
                     (let [encoding "utf8"]
                       (text\= string
                               (cond /.on_nashorn?
                                     (let [binary (java/lang/String::getBytes [encoding] (:coerce java/lang/String string))]
                                       (|> (java/lang/String::new [binary encoding])
                                           (:coerce Text)))
                                     
                                     /.on_node_js?
                                     (|> (Buffer::from [string encoding])
                                         (Buffer::toString [encoding]))
                                     
                                     ## On the browser
                                     (let [binary (|> (TextEncoder::new [encoding])
                                                      (TextEncoder::encode [string]))]
                                       (|> (TextDecoder::new [encoding])
                                           (TextDecoder::decode [binary])))
                                     ))))
            ))))
