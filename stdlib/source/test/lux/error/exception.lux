... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" try (.only Try)]]]])

(/.the an_exception)
(/.the another_exception)

(the label "YOLO")
(/.the (custom_exception value)
  (/.Exception Natural)
  (/.report (list [label (%.natural value)])))

(the .public test
  Test
  (monad.let [! random.monad]
    [expected random.natural
     wrong (|> random.natural (random.only (|>> (n.= expected) not)))
     assertion_succeeded? random.bit
     .let [report_element (by ! each %.natural random.natural)]
     field0 report_element
     value0 report_element
     field1 report_element
     value1 report_element]
    (<| (_.covering /._)
        (_.for [/.Exception
                /.#constructor /.#label])
        (all _.and
             (_.coverage [/.except]
               (when (/.except ..an_exception [])
                 {try.#Success _} false
                 {try.#Failure _} true))
             (_.coverage [/.error]
               (when (/.except ..an_exception [])
                 {try.#Success _}
                 false
                 
                 {try.#Failure message}
                 (text.= message (/.error ..an_exception []))))
             (_.coverage [/.is?]
               (/.is? ..an_exception
                      (/.error ..an_exception [])))
             (_.coverage [/.assertion]
               (when (/.assertion ..an_exception [] assertion_succeeded?)
                 {try.#Success _}
                 assertion_succeeded?
                 
                 {try.#Failure message}
                 (and (not assertion_succeeded?)
                      (text.= message (/.error ..an_exception [])))))
             (_.coverage [/.when]
               (and (n.= expected
                         (|> (/.except ..an_exception [])
                             (/.when ..an_exception (function (_ ex) expected))
                             (/.else (function (_ ex) wrong))))
                    (n.= expected
                         (|> (/.except ..another_exception [])
                             (/.when ..an_exception (function (_ ex) wrong))
                             (/.when ..another_exception (function (_ ex) expected))
                             (/.else (function (_ ex) wrong))))))
             (_.coverage [/.else]
               (n.= expected
                    (|> (/.except ..another_exception [])
                        (/.when ..an_exception (function (_ ex) wrong))
                        (/.else (function (_ ex) expected)))))
             (_.coverage [/.report]
               (let [report (/.report (list [field0 value0]
                                            [field1 value1]))]
                 (and (text.contains? field0 report)
                      (text.contains? value0 report)
                      (text.contains? field1 report)
                      (text.contains? value1 report))))
             (_.coverage [/.listing]
               (let [enumeration (/.listing %.text (list field0 value0 field1 value1))]
                 (and (text.contains? field0 enumeration)
                      (text.contains? value0 enumeration)
                      (text.contains? field1 enumeration)
                      (text.contains? value1 enumeration))))
             (_.coverage [/.with]
               (and (when (/.with ..an_exception [] {try.#Success expected})
                      {try.#Success actual} (n.= expected actual)
                      {try.#Failure _} false)
                    (when (/.with ..an_exception [] {try.#Failure ""})
                      {try.#Success _} false
                      {try.#Failure message} (text.= message (/.error ..an_exception [])))
                    (when (/.with ..an_exception []
                            (is (Try Natural)
                                (/.except ..another_exception [])))
                      {try.#Success _}
                      false
                      
                      {try.#Failure message}
                      (and (text.contains? (/.error ..an_exception []) message)
                           (text.contains? (/.error ..another_exception []) message)))))
             (_.coverage [/.the]
               (when (/.except ..custom_exception [expected])
                 {try.#Success _}
                 false
                 
                 {try.#Failure message}
                 (and (text.contains? ..label message)
                      (text.contains? (%.natural expected) message))))
             ))))
