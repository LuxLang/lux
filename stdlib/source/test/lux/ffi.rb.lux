... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" io]]
   [math
    ["[0]" random]]
   [macro
    ["[0]" template]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 ["$[0]" /
  ["[1][0]" export]])

(/.import File
          "[1]::[0]"
          ("static" SEPARATOR /.String))

(the .public test
  Test
  (do [! random.monad]
    [boolean random.bit
     integer random.integer
     float random.decimal
     string (random.lower_cased 1)]
    (<| (_.covering /._)
        (`` (all _.and
                 (,, (template.with [<type> <sample>]
                       [(_.coverage [<type>]
                          (exec
                            (is <type> <sample>)
                            true))]

                       [/.Boolean boolean]
                       [/.Integer integer]
                       [/.Float float]
                       [/.String string]
                       ))
                 (_.for [/.Object]
                        (all _.and
                             (,, (template.with [<type>]
                                   [(_.coverage [<type>]
                                      (exec
                                        (|> []
                                            (as (-> Any <type>))
                                            (is (for_some (_ a) (/.Object a))))
                                        true))]

                                   [/.Nil]
                                   [/.Function]
                                   ))))
                 (_.coverage [/.import]
                   (same? (io.value (..File::SEPARATOR))
                          (io.value (..File::SEPARATOR))))
                 (_.coverage [/.nil?]
                   (and (not (/.nil? boolean))
                        (not (/.nil? integer))
                        (not (/.nil? float))
                        (not (/.nil? string))))
                 (_.coverage [/.nil]
                   (/.nil? (/.nil [])))
                 
                 $/export.test
                 )))))
