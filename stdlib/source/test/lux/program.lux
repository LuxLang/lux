(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [abstract
     [monad (#+ do)]]
    [control
     ["." io]
     ["." try]
     ["<>" parser
      ["<.>" code]
      ["<.>" cli]]]
    [data
     ["." text]
     [collection
      ["." list]]]
    [macro
     [syntax (#+ syntax:)]]
    [math
     ["." random]]]]
  [\\library
   ["." /]])

(syntax: (actual_program {actual_program (<| <code>.form
                                             (<>.after (<code>.text! "lux def program"))
                                             <code>.any)})
  (in (list actual_program)))

(def: .public test
  Test
  (<| (_.covering /._)
      (do random.monad
        [inputs (random.list 5 (random.ascii/upper 5))]
        (_.cover [/.program:]
                 (let [(^open "list\.") (list.equivalence text.equivalence)]
                   (and (with_expansions [<program> (/.program: all_arguments
                                                      (io.io all_arguments))]
                          (let [outcome ((: (-> (List Text) (io.IO Any))
                                            (..actual_program <program>))
                                         inputs)]
                            (is? (: Any inputs) (io.run! outcome))))
                        (with_expansions [<program> (/.program: [arg/0 arg/1 arg/2 arg/3 arg/4]
                                                      (io.io (list arg/4 arg/3 arg/2 arg/1 arg/0)))]
                          (let [outcome ((: (-> (List Text) (io.IO Any))
                                            (..actual_program <program>))
                                         inputs)]
                            (list\= (list.reversed inputs)
                                    (:as (List Text) (io.run! outcome)))))
                        (with_expansions [<program> (/.program: [{all_arguments (<>.many <cli>.any)}]
                                                      (io.io all_arguments))]
                          (let [outcome ((: (-> (List Text) (io.IO Any))
                                            (..actual_program <program>))
                                         inputs)]
                            (list\= inputs
                                    (:as (List Text) (io.run! outcome)))))
                        (with_expansions [<program> (/.program: [arg/0 arg/1 arg/2 arg/3]
                                                      (io.io []))]
                          (case (try ((: (-> (List Text) (io.IO Any))
                                         (..actual_program <program>))
                                      inputs))
                            (#try.Success _)
                            false
                            
                            (#try.Failure _)
                            true))))))))
