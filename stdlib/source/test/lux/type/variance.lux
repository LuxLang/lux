... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [math
    ["[0]" random (.only Random)]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" // (.only)
    ["[1][0]" check]]]])

(every Super
  (for_some (_ sub)
    [Text sub]))

(every Sub
  (Super Bit))

(the .public test
  Test
  (<| (_.covering /._)
      (do random.monad
        [expected random.natural])
      (all _.and
           (_.coverage [/.Co]
             (and (//check.< (type (/.Co Super)) (type (/.Co Sub)))
                  (not (//check.< (type (/.Co Sub)) (type (/.Co Super))))))
           (_.coverage [/.Contra]
             (and (//check.< (type (/.Contra Sub)) (type (/.Contra Super)))
                  (not (//check.< (type (/.Contra Super)) (type (/.Contra Sub))))))
           (_.coverage [/.In]
             (and (//check.< (type (/.In Super)) (type (/.In Super)))
                  (//check.< (type (/.In Sub)) (type (/.In Sub)))
                  (not (//check.< (type (/.In Sub)) (type (/.In Super))))
                  (not (//check.< (type (/.In Super)) (type (/.In Sub))))))
           (_.for [/.Mutable]
                  (all _.and
                       (_.coverage [/.write /.read]
                         (|> (/.write expected)
                             (is (/.Mutable I64 Natural))
                             /.read
                             (same? (.i64 expected))))
                       (_.coverage [/.Read_Only /.read_only]
                         (|> (/.write expected)
                             (is (/.Mutable I64 Natural))
                             /.read_only
                             (is (/.Read_Only I64))
                             /.read
                             (same? (.i64 expected))))
                       (_.coverage [/.Write_Only /.write_only]
                         (|> (/.write expected)
                             (is (/.Mutable I64 Natural))
                             /.write_only
                             (is (/.Write_Only Natural))
                             /.read
                             (is Any)
                             (same? (as Any expected))))
                       ))
           )))
