(.module:
  [lux #*
   ["%" data/text/format (#+ format)]
   ["r" math/random (#+ Random)]
   ["_" test (#+ Test)]
   [abstract
    [monad
     [indexed (#+ do)]]]
   [control
    ["." io]]
   [data
    [number
     ["n" nat]]]]
  {1
   ["." / (#+ Res)]})

(def: #export test
  Test
  (<| (_.context (%.name (name_of /._)))
      ($_ _.and
          (_.test "Can produce and consume keys in an ordered manner."
                  (<| (n.= (n.+ 123 456))
                      io.run
                      /.run_sync
                      (do /.sync
                        [res|left (/.ordered_sync 123)
                         res|right (/.ordered_sync 456)
                         right (/.read_sync res|right)
                         left (/.read_sync res|left)]
                        (wrap (n.+ left right)))))
          (_.test "Can exchange commutative keys."
                  (<| (n.= (n.+ 123 456))
                      io.run
                      /.run_sync
                      (do /.sync
                        [res|left (/.commutative_sync 123)
                         res|right (/.commutative_sync 456)
                         _ (/.exchange_sync [1 0])
                         left (/.read_sync res|left)
                         right (/.read_sync res|right)]
                        (wrap (n.+ left right)))))
          (_.test "Can group and un-group keys."
                  (<| (n.= (n.+ 123 456))
                      io.run
                      /.run_sync
                      (do /.sync
                        [res|left (/.commutative_sync 123)
                         res|right (/.commutative_sync 456)
                         _ (/.group_sync 2)
                         _ (/.un_group_sync 2)
                         right (/.read_sync res|right)
                         left (/.read_sync res|left)]
                        (wrap (n.+ left right)))))
          )))
