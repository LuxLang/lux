... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception]]
   [data
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["[0]" natural]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public (test random_type)
  (-> (Random Type)
      Test)
  (<| (_.covering /._)
      (do [! random.monad]
        [expected_0 random_type
         expected_1 random_type
         variable_0 random.natural
         .let [variable_1 (++ variable_0)
               variable_2 (++ variable_1)]])
      (all _.and
           (<| (_.for [/.Binding])
               (all _.and
                    (_.coverage [/.un_bound]
                      (when (/.un_bound)
                        (/.un_bound)
                        true

                        else
                        false))
                    (_.coverage [/.bound]
                      (when (/.bound expected_0)
                        (/.bound actual)
                        (same? expected_0 actual)

                        else
                        false))
                    ))
           (<| (_.for [/.Environment])
               (all _.and
                    (_.coverage [/.empty]
                      (when /.empty
                        (list)
                        true

                        else
                        false))
                    (_.coverage [/.has]
                      (when (/.has variable_0 /.empty)
                        (list)
                        false

                        else
                        true))
                    (<| (_.for [/.Query])
                        (all _.and
                             (_.coverage [/.its]
                               (when (|> /.empty
                                         (/.has variable_0)
                                         (/.its variable_0))
                                 {try.#Success (/.un_bound)}
                                 true

                                 else
                                 false))
                             (_.coverage [/.not_known]
                               (when (|> /.empty
                                         (/.its variable_0))
                                 {try.#Failure error}
                                 (exception.is? /.not_known error)

                                 else
                                 false))
                             ))
                    (<| (_.for [/.Change])
                        (all _.and
                             (_.coverage [/.new]
                               (when (do try.monad
                                       [it (|> /.empty
                                               (/.has variable_0)
                                               (/.new variable_0 expected_0))]
                                       (/.its variable_0 it))
                                 {try.#Success (/.bound actual)}
                                 (same? expected_0 actual)

                                 else
                                 false))
                             (_.coverage [/.cannot_bind_again]
                               (when (do try.monad
                                       [it (|> /.empty
                                               (/.has variable_0)
                                               (/.new variable_0 expected_0))]
                                       (/.new variable_0 expected_0 it))
                                 {try.#Failure error}
                                 (exception.is? /.cannot_bind_again error)

                                 else
                                 false))
                             (_.coverage [/.type]
                               (<| (try.else false)
                                   (do try.monad
                                     [it (|> /.empty
                                             (/.has variable_0)
                                             (/.new variable_0 expected_0))
                                      actual (/.type variable_0 it)]
                                     (in (same? expected_0 actual)))))
                             (_.coverage [/.changed]
                               (<| (try.else false)
                                   (do try.monad
                                     [it (|> /.empty
                                             (/.has variable_0)
                                             (/.new variable_0 expected_0))
                                      it (/.changed variable_0 (/.bound expected_1) it)
                                      actual (/.type variable_0 it)]
                                     (in (same? expected_1 actual)))))
                             ))
                    ))
           (<| (_.for [/.Ring])
               (let [(open "list#[0]") (list.equivalence natural.equivalence)])
               (all _.and
                    (_.coverage [/.ring]
                      (<| (try.else false)
                          (do try.monad
                            [it (|> /.empty
                                    (/.has variable_0)
                                    (/.ring variable_0))]
                            (in (list#= (list variable_0) it)))))
                    (_.coverage [/.two]
                      (<| (try.else false)
                          (do try.monad
                            [it (|> /.empty
                                    (/.has variable_0)
                                    (/.has variable_1)
                                    (/.two variable_0 variable_1))
                             ring_01 (/.ring variable_0 it)
                             ring_10 (/.ring variable_1 it)]
                            (in (and (list#= (list variable_1 variable_0) ring_01)
                                     (list#= (list variable_0 variable_1) ring_10))))))
                    (_.coverage [/.binding]
                      (and (when (do try.monad
                                   [it (|> /.empty
                                           (/.has variable_0)
                                           (/.has variable_1)
                                           (/.two variable_0 variable_1))]
                                   (/.binding variable_0 it))
                             {try.#Success (/.un_bound)}
                             true

                             else
                             false)
                           (when (do try.monad
                                   [it (|> /.empty
                                           (/.has variable_0)
                                           (/.changed variable_0 (/.bound expected_0)))]
                                   (/.binding variable_0 it))
                             {try.#Success (/.bound actual)}
                             (same? expected_0 actual)

                             else
                             false)))
                    (_.coverage [/.not_bound]
                      (when (do try.monad
                              [it (|> /.empty
                                      (/.has variable_0)
                                      (/.has variable_1)
                                      (/.two variable_0 variable_1))]
                              (/.type variable_0 it))
                        {try.#Failure error}
                        (exception.is? /.not_bound error)

                        else
                        false))
                    (_.coverage [/.three]
                      (<| (try.else false)
                          (do try.monad
                            [it (|> /.empty
                                    (/.has variable_0)
                                    (/.has variable_1)
                                    (/.has variable_2)
                                    (/.two variable_0 variable_1))
                             it (/.three variable_2 variable_1 variable_0 it)
                             ring_021 (/.ring variable_0 it)
                             ring_210 (/.ring variable_2 it)
                             ring_102 (/.ring variable_1 it)]
                            (in (and (list#= (list variable_1 variable_2 variable_0) ring_021)
                                     (list#= (list variable_0 variable_1 variable_2) ring_210)
                                     (list#= (list variable_2 variable_0 variable_1) ring_102))))))
                    ))
           )))
