... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [error
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]]]]
   ["[0]" macro (.only)
    ["[0]" syntax]
    ["[0]" template]]
   ["[0]" meta (.only)
    ["[0]" name]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(template.with_locals ['Foo 'Bar]
  (these (template.with [<syntax> <meta>]
           [(the <syntax>
              (syntax.macro (_ [])
                (do meta.monad
                  [frame <meta>]
                  (pure (list (` (name (, (code.name (its /.#name frame))))))))))]

           [[current /.current]
            [specific (/.specific (`` (name (,, (template.name [.._] ['Foo])))))]])

         (/.every ('Foo a)
           Text

           (/.every ('Bar a)
             Natural

             (the .public test
               Test
               (<| (_.covering /._)
                   (_.for [/.every])
                   (do random.monad
                     [expected_foo (random.lower_cased 5)
                      expected_bar random.natural]
                     (all _.and
                          (_.coverage [/.of]
                            (and (exec (is ('Foo Text)
                                           (/.of 'Foo expected_foo))
                                   true)
                                 (exec (is ('Bar Text)
                                           (/.of expected_bar))
                                   true)))
                          (_.coverage [/.as]
                            (and (|> expected_foo
                                     (/.of 'Foo)
                                     (is ('Foo Bit))
                                     (/.as 'Foo)
                                     (text.= expected_foo))
                                 (|> (/.of expected_bar)
                                     (is ('Bar Bit))
                                     /.as
                                     (n.= expected_bar))))
                          (_.coverage [/.transmutation]
                            (and (exec (|> expected_foo
                                           (/.of 'Foo)
                                           (is ('Foo .Macro))
                                           (/.transmutation 'Foo)
                                           (is ('Foo .Lux)))
                                   true)
                                 (exec (|> (/.of expected_bar)
                                           (is ('Bar .Macro))
                                           /.transmutation
                                           (is ('Bar .Lux)))
                                   true)))
                          (_.for [/.Frame
                                  /.#name /.#type_vars /.#abstraction /.#reification]
                                 (all _.and
                                      (_.coverage [/.current]
                                        (name.= (`` (name (,, (template.name [.._] ['Bar]))))
                                                (..current)))
                                      (_.coverage [/.specific]
                                        (name.= (`` (name (,, (template.name [.._] ['Foo]))))
                                                (..specific)))
                                      ))
                          ))))))))
