... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [error
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Dynamic])
      (do random.monad
        [expected random.natural]
        (all _.and
             (_.coverage [/.dynamic /.static]
               (when (/.static Natural (/.dynamic expected))
                 {try.#Success actual}
                 (n.= expected actual)
                 
                 {try.#Failure _}
                 false))
             (_.coverage [/.wrong_type]
               (when (/.static Text (/.dynamic expected))
                 {try.#Success actual}
                 false
                 
                 {try.#Failure error}
                 (exception.is? /.wrong_type error)))
             (_.coverage [/.as_text]
               (when (/.as_text (/.dynamic expected))
                 {try.#Success actual}
                 (text.= (%.natural expected) actual)
                 
                 {try.#Failure _}
                 false))
             ))))
