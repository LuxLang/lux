... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [function
    [predicate (.only Predicate)]]
   [data
    ["[0]" maybe (.use "[1]:[0]" monad)]
    [collection
     ["[0]" list (.use "[1]:[0]" functor)]]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the _refiner
  (/.refiner (n.> 123)))

(the _type
  (/.type _refiner))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Refined])
      (monad.let [! random.monad]
        [raw random.natural
         modulus (by ! each (|>> (n.% 10) (n.+ 2)) random.natural)
         .let [predicate (is (Predicate Natural)
                             (|>> (n.% modulus) (n.= 0)))]
         total_raws (by ! each (|>> (n.% 20) ++) random.natural)
         raws (random.list total_raws random.natural)]
        (all _.and
             (_.for [/.Refiner]
                    (all _.and
                         (_.coverage [/.refiner]
                           (when (/.refiner predicate raw)
                             {.:Some refined}
                             (predicate raw)
                             
                             {.:None}
                             (not (predicate raw))))
                         (_.coverage [/.predicate]
                           (|> (/.refiner predicate modulus)
                               (maybe:each (|>> /.predicate (identical? predicate)))
                               (maybe.else false)))
                         ))
             (_.coverage [/.value]
               (|> (/.refiner predicate modulus)
                   (maybe:each (|>> /.value (n.= modulus)))
                   (maybe.else false)))
             (_.coverage [/.lifted]
               (and (|> (/.refiner predicate modulus)
                        (maybe:each (/.lifted (n.+ modulus)))
                        maybe:conjoint
                        (maybe:each (|>> /.value (n.= (n.+ modulus modulus))))
                        (maybe.else false))
                    (|> (/.refiner predicate modulus)
                        (maybe:each (/.lifted (n.+ (++ modulus))))
                        maybe:conjoint
                        (maybe:each (|>> /.value (n.= (n.+ modulus (++ modulus)))))
                        (maybe.else false)
                        not)))
             (_.coverage [/.only]
               (let [expected (list.only predicate raws)
                     actual (/.only (/.refiner predicate) raws)]
                 (and (n.= (list.size expected)
                           (list.size actual))
                      (by (list.equivalence n.equivalence) =
                          expected
                          (list:each /.value actual)))))
             (_.coverage [/.partition]
               (let [expected (list.only predicate raws)
                     [alternative actual] (/.partition (/.refiner predicate) raws)]
                 (and (n.= (list.size expected)
                           (list.size actual))
                      (n.= (n.- (list.size expected) total_raws)
                           (list.size alternative))
                      (by (list.equivalence n.equivalence) =
                          expected
                          (list:each /.value actual)))))
             (_.coverage [/.type]
               (exec (is (Maybe .._type)
                         (.._refiner raw))
                 true))
             (monad.let random.monad
               [predicate (random.either (pure n.even?)
                                         (pure n.odd?))
                .let [refiner (/.refiner predicate)]
                sample (/.random refiner random.natural)]
               (_.coverage [/.random]
                 (and (identical? predicate (/.predicate sample))
                      (predicate (/.value sample)))))
             ))))
