... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [control
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [format
     ["md" markdown]]]
   [macro
    ["[0]" syntax]
    ["[0]" template]
    ["[0]" expansion]]
   ["[0]" meta (.only)
    ["[0]" static]
    ["[0]" name]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 ["[0]" /
  ["[1][0]" remember]])

(the macro_error
  (syntax.macro (_ [macro <code>.any])
    (function (_ compiler)
      {try.#Success [compiler (list (code.bit (when ((expansion.complete macro) compiler)
                                                {try.#Failure error}
                                                true
                                                
                                                {try.#Success _}
                                                false)))]})))

(the description
  (syntax.macro (_ [])
    (by meta.monad each
        (|>> %.natural code.text list)
        meta.seed)))

(expansion.let ['definition_description' (..description)
                'module_description' (..description)
                'default (..description)
                'default (template.name ['default])
                'default+ (template.name [.._] ['default])

                'alias (template.name ['alias])
                'alias+ (template.name [.._] ['alias])

                expected_example (static.random_natural)
                expected_comment (static.random_natural)
                expected_deprecated (static.random_natural)]
  (these (the .public 'default
           Natural
           123)

         (alias ['alias]
                'default)

         (the .public test
           Test
           (<| (_.covering /._)
               (let [module (`` (/.module .._
                                          (,, (template.text ['module_description']))))
                     definition (`` (/.definition 'default+
                                      'definition_description'
                                      (/.example (' expected_example))
                                      (/.comment (,, (template.text [expected_comment])))
                                      (/.deprecated (,, (template.text [expected_deprecated])))))
                     alias (`` (/.alias 'alias+))

                     full_documentation (/.markdown (list module
                                                          definition
                                                          alias))])
               (_.for [/.Documentation
                       /.#Definition /.#Module])
               (all _.and
                    (_.for [/.markdown]
                           (all _.and
                                (_.for [/.Module
                                        /.#module /.#description /.#dependees /.#coverage]
                                       (_.coverage [/.module]
                                         (and (let [[expected _] (name .._)]
                                                (text.contains? expected
                                                                full_documentation))
                                              (text.contains? (template.text ['module_description'])
                                                              full_documentation))))
                                (_.for [/.Definition
                                        /.#global /.#documentation]
                                       (all _.and
                                            (_.coverage [/.definition]
                                              (and (text.contains? (template.text ['definition_description'])
                                                                   full_documentation)
                                                   (text.contains? (template.text ['default+])
                                                                   full_documentation)))
                                            (_.coverage [/.example]
                                              (text.contains? (template.text [expected_example])
                                                              full_documentation))
                                            (_.coverage [/.comment]
                                              (text.contains? (template.text [expected_comment])
                                                              full_documentation))
                                            (_.coverage [/.deprecated]
                                              (text.contains? (template.text [expected_deprecated])
                                                              full_documentation))
                                            ))
                                (_.coverage [/.alias]
                                  (text.contains? (template.text ['alias+])
                                                  full_documentation))
                                ))
                    (_.coverage [/.unqualified_name]
                      (`` (and (,, (template.with [<example>]
                                     [(macro_error <example>)]
                                     
                                     [(/.definition 'default)]
                                     [(/.definition 'default
                                        (,, (template.text ['definition_description'])))]
                                     )))))

                    /remember.test
                    )))))
