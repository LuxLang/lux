... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [control
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\format]]
    [format
     ["md" markdown]]]
   ["[0]" meta (.only)
    ["[0]" static]
    ["[0]" symbol]
    ["[0]" code (.only)
     ["<[1]>" \\parser]]
    [macro
     [syntax (.only syntax)]
     ["[0]" template]
     ["[0]" expansion]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 ["[0]" /
  ["[1][0]" remember]])

(def macro_error
  (syntax (_ [macro <code>.any])
    (function (_ compiler)
      {try.#Success [compiler (list (code.bit (when ((expansion.complete macro) compiler)
                                                {try.#Failure error}
                                                true
                                                
                                                {try.#Success _}
                                                false)))]})))

(def description
  (syntax (_ [])
    (of meta.monad each
        (|>> %.nat code.text list)
        meta.seed)))

(with_expansions ['definition_description' (..description)
                  'module_description' (..description)
                  'default (..description)
                  'default (template.symbol ['default])
                  'default+ (template.symbol [.._] ['default])

                  'alias (template.symbol ['alias])
                  'alias+ (template.symbol [.._] ['alias])

                  expected_example (static.random_nat)
                  expected_comment (static.random_nat)
                  expected_deprecated (static.random_nat)]
  (these (def .public 'default
           Nat
           123)

         (alias ['alias]
                'default)

         (def .public test
           Test
           (<| (_.covering /._)
               (let [module (`` (/.module .._
                                          (,, (template.text ['module_description']))))
                     definition (`` (/.definition 'default+
                                      'definition_description'
                                      (/.example (' expected_example))
                                      (/.comment (,, (template.text [expected_comment])))
                                      (/.deprecated (,, (template.text [expected_deprecated])))))
                     alias (`` (/.alias 'alias+))

                     full_documentation (/.markdown (list module
                                                          definition
                                                          alias))])
               (_.for [/.Documentation
                       /.#Definition /.#Module])
               (all _.and
                    (_.for [/.markdown]
                           (all _.and
                                (_.for [/.Module
                                        /.#module /.#description /.#dependees /.#coverage]
                                       (_.coverage [/.module]
                                         (and (let [[expected _] (symbol .._)]
                                                (text.contains? expected
                                                                full_documentation))
                                              (text.contains? (template.text ['module_description'])
                                                              full_documentation))))
                                (_.for [/.Definition
                                        /.#global /.#documentation]
                                       (all _.and
                                            (_.coverage [/.definition]
                                              (and (text.contains? (template.text ['definition_description'])
                                                                   full_documentation)
                                                   (text.contains? (template.text ['default+])
                                                                   full_documentation)))
                                            (_.coverage [/.example]
                                              (text.contains? (template.text [expected_example])
                                                              full_documentation))
                                            (_.coverage [/.comment]
                                              (text.contains? (template.text [expected_comment])
                                                              full_documentation))
                                            (_.coverage [/.deprecated]
                                              (text.contains? (template.text [expected_deprecated])
                                                              full_documentation))
                                            ))
                                (_.coverage [/.alias]
                                  (text.contains? (template.text ['alias+])
                                                  full_documentation))
                                ))
                    (_.coverage [/.unqualified_symbol]
                      (`` (and (,, (with_template [<example>]
                                     [(macro_error <example>)]
                                     
                                     [(/.definition 'default)]
                                     [(/.definition 'default
                                        (,, (template.text ['definition_description'])))]
                                     )))))

                    /remember.test
                    )))))
