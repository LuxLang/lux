(.module:
  [lux #*
   ["%" data/text/format (#+ format)]
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do Monad)]
    {[0 #test]
     [/
      ["$." equivalence]
      ["$." order]
      ["$." enum]
      ["$." codec]]}]
   [control
    pipe]
   [data
    ["." text]
    [number
     ["i" int]]]
   [math
    ["r" random (#+ Random)]]
   [time
    ["@d" duration]
    ["@date" date]]]
  [//
   ["_." duration]]
  {1
   ["." / (#+ Instant)]})

(def: boundary Int +99,999,999,999,999)

(def: #export instant
  (Random Instant)
  (:: r.monad map (|>> (i.% boundary) /.from-millis) r.int))

(def: #export test
  Test
  (<| (_.context (%.name (name-of /._)))
      ($_ _.and
          ($equivalence.spec /.equivalence ..instant)
          ($order.spec /.order ..instant)
          ($enum.spec /.enum ..instant)
          ## TODO; Uncomment ASAP
          ## ($codec.spec /.equivalence /.codec ..instant)

          (do r.monad
            [millis r.int]
            (_.test "Can convert from/to milliseconds."
                    (|> millis /.from-millis /.to-millis (i.= millis))))
          (do r.monad
            [sample instant
             span _duration.duration
             #let [(^open "@/.") /.equivalence
                   (^open "@d/.") @d.equivalence]]
            ($_ _.and
                (_.test "The span of a instant and itself has an empty duration."
                        (|> sample (/.span sample) (@d/= @d.empty)))
                (_.test "Can shift a instant by a duration."
                        (|> sample (/.shift span) (/.span sample) (@d/= span)))
                (_.test "Can obtain the time-span between the epoch and an instant."
                        (|> sample /.relative /.absolute (@/= sample)))
                (_.test "All instants are relative to the epoch."
                        (|> /.epoch (/.shift (/.relative sample)) (@/= sample)))))
          )))
