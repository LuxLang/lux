... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [math
    [number
     ["n" nat]]]
   [meta
    ["[0]" static]
    [macro
     ["[0]" expansion]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" //]]])

(expansion.let [<nat> (static.random_nat)]
  (these (/.export
           (the nullary
             Nat
             <nat>)
           (the unary
             (-> Nat Nat)
             (|>> (n.+ <nat>)))
           (the CONSTANT
             Nat
             <nat>)
           (the $global
             (-> Nat Nat)
             (|>> (n.+ <nat>))))

         (//.import (nullary [] Nat))
         (//.import (unary [Nat] Nat))
         (//.import (CONSTANT Nat))
         (//.import ($global (-> Nat Nat)))

         (the .public test
           Test
           (<| (_.covering /._)
               (all _.and
                    (_.coverage [/.export]
                      (and (n.= <nat> (..nullary []))
                           (n.= (n.+ <nat> <nat>) (..unary <nat>))
                           (n.= <nat> (..CONSTANT))
                           (n.= (n.+ <nat> <nat>) ((..$global) <nat>))))
                    ))))
  )
