... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [data
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]
    [number
     [/64
      ["n" natural]
      ["d" decimal]
      ["r" revolution]
      ["[0]" integer]]]]
   [macro
    ["[0]" template]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" rgb]
    ["[0]" hsl]
    ["[0]" hsb]]]])

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [expected_hue (by ! each (|>> d.abs (d.% +0.9) (d.+ +0.05))
                          random.unit_decimal)
         .let [expected_saturation +0.5]
         variations (by ! each (|>> (n.% 3) (n.+ 2)) random.natural)
         .let [max_spread (d./ (|> variations ++ .integer integer.decimal)
                               +1.0)
               min_spread (d./ +2.0 max_spread)
               spread_space (d.- min_spread max_spread)]
         spread (by ! each (|>> d.abs (d.% spread_space) (d.+ min_spread))
                    random.unit_decimal)])
      (`` (all _.and
               (,, (template.with [<palette>]
                     [(_.coverage [<palette>]
                        (let [expected (hsb.rgb (hsb.hsb expected_hue expected_saturation +0.5))
                              [c0 c1 c2] (<palette> expected)]
                          (and (rgb.= expected c0)
                               (not (rgb.= expected c1))
                               (not (rgb.= expected c2)))))]

                     [/.triad]
                     [/.clash]
                     [/.split_complement]))
               (,, (template.with [<palette>]
                     [(_.coverage [<palette>]
                        (let [expected (hsb.rgb (hsb.hsb expected_hue expected_saturation +0.5))
                              [c0 c1 c2 c3] (<palette> expected)]
                          (and (rgb.= expected c0)
                               (not (rgb.= expected c1))
                               (not (rgb.= expected c2))
                               (not (rgb.= expected c3)))))]

                     [/.square]
                     [/.tetradic]))
               (_.for [/.Spread /.Scheme]
                      (all _.and
                           (,, (template.with [expected_brightness <palette>]
                                 [(_.coverage [<palette>]
                                    (let [expected (hsb.rgb (hsb.hsb expected_hue
                                                                     expected_saturation
                                                                     expected_brightness))
                                          palette (<palette> spread variations expected)]
                                      (and (n.= variations (list.size palette))
                                           (not (list.any? (rgb.= expected) palette)))))]
                                 [+1.0 /.analogous]
                                 [+0.5 /.monochromatic]
                                 ))
                           ))
               ))))
