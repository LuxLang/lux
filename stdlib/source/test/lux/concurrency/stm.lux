... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)
     ["[1]T" \\test]]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]]
   [control
    ["[0]" io (.only IO)]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    ["[0]" random]
    [number
     ["n" natural]]]
   [test
    ["_" property (.only Test)]
    ["[0]" unit]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" atom (.only Atom atom)]
    ["[0]" future]
    ["[0]" frp (.only Channel)]]]])

(the injection
  (Injection /.STM)
  (by /.monad in))

(the comparison
  (Comparison /.STM)
  (function (_ == left right)
    (== (product.right (left (stack)))
        (product.right (right (stack))))))

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [dummy random.natural
         expected random.natural
         iterations_per_process (|> random.natural (by ! each (n.% 100)))]
        (all _.and
             (_.for [/.functor]
                    (functorT.spec ..injection ..comparison /.functor))
             (_.for [/.monad]
                    (monadT.spec ..injection ..comparison /.monad))

             (in (do future.monad
                   [actual (/.commit! (by /.monad in expected))]
                   (unit.coverage [/.commit!]
                     (n.= expected actual))))
             (in (do future.monad
                   [actual (/.commit! (/.read (/.var expected)))]
                   (unit.coverage [/.Var /.var /.read]
                     (n.= expected actual))))
             (in (do future.monad
                   [actual (let [box (/.var dummy)]
                             (/.commit! (do /.monad
                                          [_ (/.write expected box)]
                                          (/.read box))))
                    verdict (let [box (/.var dummy)]
                              (/.commit! (do /.monad
                                           [_ (/.write expected box)
                                            actual (/.read box)]
                                           (in (n.= expected actual)))))]
                   (unit.coverage [/.write]
                     (and (n.= expected actual)
                          verdict))))
             (in (do future.monad
                   [.let [box (/.var dummy)]
                    output (/.commit! (do /.monad
                                        [_ (/.update (n.+ expected) box)]
                                        (/.read box)))]
                   (unit.coverage [/.update]
                     (n.= (n.+ expected dummy)
                          output))))
             (in (do future.monad
                   [.let [box (/.var dummy)
                          [follower sink] (io.value (/.changes box))]
                    _ (/.commit! (/.write expected box))
                    _ (/.commit! (/.update (n.* 2) box))
                    _ (future.soon (by sink close))
                    _ (/.commit! (/.update (n.* 3) box))
                    changes (frp.as_list follower)]
                   (unit.coverage [/.changes]
                     (by (list.equivalence n.equivalence) =
                         (list expected (n.* 2 expected))
                         changes))))
             (in (let [var (/.var 0)]
                   (do [! future.monad]
                     [_ (|> (list.repeated iterations_per_process [])
                            (list#each (function (_ _) (/.commit! (/.update ++ var))))
                            (list.all' !))
                      cummulative (/.commit! (/.read var))]
                     (unit.coverage [/.STM]
                       (n.= iterations_per_process
                            cummulative)))))
             ))))
