... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only)
     ["[1]T" \\test]]
    ["[0]" functor
     ["[1]T" \\test (.only Injection Comparison)]]]
   [control
    ["[0]" io (.only IO)]]
   [data
    ["[0]" product]
    [collection
     ["[0]" list (.use "[1]:[0]" functor)]]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]
    ["[0]" unit]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" atom (.only Atom atom)]
    ["[0]" future]
    ["[0]" frp (.only Channel)]]]])

(the injection
  (Injection /.STM)
  (by /.monad pure))

(the comparison
  (Comparison /.STM)
  (function (_ == left right)
    (== (product.right (left (stack)))
        (product.right (right (stack))))))

(the .public test
  Test
  (<| (_.covering /._)
      (monad.let [! random.monad]
        [dummy random.natural
         expected random.natural
         iterations_per_process (|> random.natural (by ! each (n.% 100)))]
        (all _.and
             (_.for [/.functor]
                    (functorT.spec ..injection ..comparison /.functor))
             (_.for [/.monad]
                    (monadT.spec ..injection ..comparison /.monad))

             (pure (monad.let future.monad
                     [actual (/.commit! (by /.monad pure expected))]
                     (unit.coverage [/.commit!]
                       (n.= expected actual))))
             (pure (monad.let future.monad
                     [actual (/.commit! (/.read (/.var expected)))]
                     (unit.coverage [/.Var /.var /.read]
                       (n.= expected actual))))
             (pure (monad.let future.monad
                     [actual (let [box (/.var dummy)]
                               (/.commit! (monad.let /.monad
                                            [_ (/.write expected box)]
                                            (/.read box))))
                      verdict (let [box (/.var dummy)]
                                (/.commit! (monad.let /.monad
                                             [_ (/.write expected box)
                                              actual (/.read box)]
                                             (pure (n.= expected actual)))))]
                     (unit.coverage [/.write]
                       (and (n.= expected actual)
                            verdict))))
             (pure (monad.let future.monad
                     [.let [box (/.var dummy)]
                      output (/.commit! (monad.let /.monad
                                          [_ (/.update (n.+ expected) box)]
                                          (/.read box)))]
                     (unit.coverage [/.update]
                       (n.= (n.+ expected dummy)
                            output))))
             (pure (monad.let future.monad
                     [.let [box (/.var dummy)
                            [follower sink] (io.value (/.changes box))]
                      _ (/.commit! (/.write expected box))
                      _ (/.commit! (/.update (n.x 2) box))
                      _ (future.soon (by sink close))
                      _ (/.commit! (/.update (n.x 3) box))
                      changes (frp.as_list follower)]
                     (unit.coverage [/.changes]
                       (by (list.equality n.equality) =
                           (list expected (n.x 2 expected))
                           changes))))
             (pure (let [var (/.var 0)]
                     (monad.let [! future.monad]
                       [_ (|> (list.repeated iterations_per_process [])
                              (list:each (function (_ _) (/.commit! (/.update ++ var))))
                              (list.complete !))
                        cummulative (/.commit! (/.read var))]
                       (unit.coverage [/.STM]
                         (n.= iterations_per_process
                              cummulative)))))
             ))))
