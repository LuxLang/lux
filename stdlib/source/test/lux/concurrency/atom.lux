... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [control
    ["[0]" io]]
   [math
    ["[0]" random]
    [number
     [/64
      ["n" natural]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public test
  Test
  (<| (_.covering /._)
      (all _.and
           (monad.let random.monad
             [expected random.natural
              .let [box (/.atom expected)]]
             (_.coverage [/.Atom /.atom /.read!]
               (io.value
                (monad.let io.monad
                  [actual (/.read! box)]
                  (pure (same? expected actual))))))
           (monad.let random.monad
             [target random.natural
              unknown (random.only (|>> (same? target) not) random.natural)
              expected random.natural
              .let [box (/.atom target)]]
             (_.coverage [/.compare_and_swap!]
               (io.value
                (monad.let io.monad
                  [swapped_unknown? (/.compare_and_swap! unknown expected box)
                   swapped_target? (/.compare_and_swap! target expected box)
                   actual (/.read! box)]
                  (pure (and (not swapped_unknown?)
                             swapped_target?
                             (same? expected actual)))))))
           (monad.let random.monad
             [init random.natural
              shift random.natural
              .let [box (/.atom init)]]
             (_.coverage [/.update!]
               (io.value
                (monad.let io.monad
                  [[pre post] (/.update! (n.+ shift) box)]
                  (pure (and (same? init pre)
                             (n.= (n.+ shift init)
                                  post)))))))
           (monad.let random.monad
             [pre random.natural
              post random.natural
              .let [box (/.atom pre)]]
             (_.coverage [/.write!]
               (io.value
                (monad.let io.monad
                  [old (/.write! post box)
                   new (/.read! box)]
                  (pure (and (same? pre old)
                             (same? post new)))))))
           )))
