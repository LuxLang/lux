... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    [concurrency
     ["[0]" async (.only Async) (.use "[1]#[0]" monad)]]]
   [data
    ["[0]" binary (.use "[1]#[0]" equivalence)
     ["$[1]" \\test]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]
   [world
    ["[0]" file]]
   [test
    ["[0]" unit]
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" //
    ["[1][0]" module]]]]
 ["$[0]" ///
  ["[1][0]" context]])

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [context $///context.random
         .let [/ "/"
               fs (file.mock /)]
         @module random.nat
         @artifact random.nat
         expected ($binary.random 1)]
        (all _.and
             (in (do [! async.monad]
                   [pre (of fs file? (/.path fs context @module @artifact))
                    _ (//module.enable! ! fs @module)
                    write! (/.cache! fs context @module @artifact expected)
                    post (of fs file? (/.path fs context @module @artifact))
                    read! (/.cache fs context @module @artifact)]
                   (unit.coverage [/.path /.cache! /.cache]
                     (and (not pre)
                          (when write!
                            {try.#Success _} true
                            {try.#Failure _} false)
                          post
                          (when read!
                            {try.#Success actual} (binary#= expected actual)
                            {try.#Failure _} false)))))
             ))))
