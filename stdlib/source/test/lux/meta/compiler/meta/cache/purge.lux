... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [concurrency
    ["[0]" async]]
   [control
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    ["[0]" binary
     ["$[1]" \\test]]
    [collection
     ["[0]" dictionary]
     ["[0]" sequence]
     ["[0]" set]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [world
    ["[0]" file]]
   [test
    ["[0]" unit]
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" //
    ["[1][0]" module]
    ["[0]" dependency
     ["[1]" module]]
    ["/[1]" //
     ["[0]" archive (.only)
      ["[0]" registry]
      ["[0]" module (.only)
       ["$[1]" \\test]
       ["[0]" descriptor (.only)
        ["$[1]" \\test]]]]
     ["/[1]" //]]]]])

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [descriptor ($descriptor.random 0)
         source_code (random.upper_cased 1)
         name/0 (random.lower_cased 1)
         module/0 ($module.random random.natural)
         content/0 ($binary.random 1)
         name/1 (random.lower_cased 2)
         module/1 (|> ($module.random random.natural)
                      (by ! each (has [module.#descriptor descriptor.#references]
                                      (set.of_list text.hash (list name/0)))))
         content/1 ($binary.random 2)
         .let [id/0 (its module.#id module/0)
               id/1 (its module.#id module/1)
               input [////.#module (its descriptor.#name descriptor)
                      ////.#file (its descriptor.#file descriptor)
                      ////.#hash (its descriptor.#hash descriptor)
                      ////.#code source_code]
               / "/"
               fs (file.mock /)]]
        (all _.and
             (_.for [/.Cache]
                    (all _.and
                         (_.coverage [/.valid?]
                           (and (/.valid? descriptor input)
                                (not (/.valid? descriptor (has ////.#module source_code input)))
                                (not (/.valid? descriptor (has ////.#file source_code input)))
                                (not (/.valid? descriptor (revised ////.#hash ++ input)))))
                         ))
             (_.for [/.Purge]
                    (all _.and
                         (_.coverage [/.purge]
                           (and (dictionary.empty? (/.purge (list) (list)))
                                (let [order (is (dependency.Order Natural)
                                                (list [name/0 id/0
                                                       [archive.#module module/0
                                                        archive.#output (sequence.sequence)
                                                        archive.#registry registry.empty]]))]
                                  (and (let [cache (is (List /.Cache)
                                                       (list [true name/0 id/0 module/0 registry.empty]))]
                                         (dictionary.empty? (/.purge cache order)))
                                       (let [cache (is (List /.Cache)
                                                       (list [false name/0 id/0 module/0 registry.empty]))]
                                         (dictionary.key? (/.purge cache order) name/0))))
                                (let [order (is (dependency.Order Natural)
                                                (list [name/0 id/0
                                                       [archive.#module module/0
                                                        archive.#output (sequence.sequence)
                                                        archive.#registry registry.empty]]
                                                      [name/1 id/1
                                                       [archive.#module module/1
                                                        archive.#output (sequence.sequence)
                                                        archive.#registry registry.empty]]))]
                                  (and (let [cache (is (List /.Cache)
                                                       (list [true name/0 id/0 module/0 registry.empty]
                                                             [true name/1 id/1 module/1 registry.empty]))
                                             purge (/.purge cache order)]
                                         (dictionary.empty? purge))
                                       (let [cache (is (List /.Cache)
                                                       (list [true name/0 id/0 module/0 registry.empty]
                                                             [false name/1 id/1 module/1 registry.empty]))
                                             purge (/.purge cache order)]
                                         (and (not (dictionary.key? (/.purge cache order) name/0))
                                              (dictionary.key? (/.purge cache order) name/1)))
                                       (let [cache (is (List /.Cache)
                                                       (list [false name/0 id/0 module/0 registry.empty]
                                                             [true name/1 id/1 module/1 registry.empty]))
                                             purge (/.purge cache order)]
                                         (and (dictionary.key? (/.purge cache order) name/0)
                                              (dictionary.key? (/.purge cache order) name/1)))))))
                         (in (do [! async.monad]
                               [_ (//module.enable! ! fs id/0)
                                .let [dir (//module.path fs id/0)
                                      file/0 (%.message dir / name/0)
                                      file/1 (%.message dir / name/1)]
                                _ (by fs write file/0 content/0)
                                _ (by fs write file/1 content/1)
                                pre (by fs directory_files dir)
                                _ (/.purge! fs id/0)
                                post (by fs directory_files dir)]
                               (unit.coverage [/.purge!]
                                 (<| (try.else false)
                                     (do try.monad
                                       [pre pre]
                                       (in (and (by set.equivalence =
                                                    (set.of_list text.hash pre)
                                                    (set.of_list text.hash (list file/0 file/1)))
                                                (when post
                                                  {try.#Failure error}
                                                  (exception.is? file.cannot_find_directory error)

                                                  success
                                                  false))))))))
                         ))
             ))))
