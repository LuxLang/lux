(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception]
    [concurrency
     ["[0]" async (.only Async) (.use "[1]#[0]" monad)]]]
   [math
    ["[0]" random]]
   [world
    ["[0]" file]]
   [test
    ["[0]" unit]
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 ["$[0]" ///
  ["[1][0]" context]])

(`` (def (bad it)
      (-> (file.System Async) (file.System Async))
      (implementation
       (,, (with_template [<name>]
             [(def <name>
                (at it <name>))]

             [separator]
             [file?]
             [directory?]
             [modify]
             [write]
             [append]
             [move]
             [directory_files]
             [sub_directories]
             [file_size]
             [last_modified]
             [can_execute?]
             [read]
             [delete]
             ))

       (def (make_directory path)
         (async#in {try.#Failure ""}))
       )))

(def .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [context $///context.random
         @module random.nat]
        (all _.and
             (in (do [! async.monad]
                   [.let [/ "/"
                          fs (file.mock /)]
                    pre/0 (at fs directory? (/.path fs context @module))
                    pre/1 (/.enabled? fs context @module)
                    outcome (/.enable! ! fs context @module)
                    post/0 (at fs directory? (/.path fs context @module))
                    post/1 (/.enabled? fs context @module)]
                   (unit.coverage [/.path /.enabled? /.enable!]
                     (and (not pre/0)
                          (not pre/1)
                          
                          (case outcome
                            {try.#Success _} true
                            {try.#Failure _} false)
                          
                          post/0
                          post/1))))
             (in (do [! async.monad]
                   [.let [/ "/"
                          fs (file.mock /)]
                    pre/0 (at fs directory? (/.path fs context @module))
                    pre/1 (/.enabled? fs context @module)
                    outcome (/.enable! ! (..bad fs) context @module)
                    post/0 (at fs directory? (/.path fs context @module))
                    post/1 (/.enabled? fs context @module)]
                   (unit.coverage [/.cannot_enable]
                     (and (not pre/0)
                          (not pre/1)
                          
                          (case outcome
                            {try.#Success _}
                            false
                            
                            {try.#Failure error}
                            (exception.match? /.cannot_enable error))
                          
                          (not post/0)
                          (not post/1)))))
             ))))
