... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [data
    ["[0]" text]
    [collection
     ["[0]" set]
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(def .public random
  (Random /.Context)
  (do [! random.monad]
    [context (all random.either
                  (in /.js)
                  (in /.jvm)
                  (in /.lua)
                  (in /.python)
                  (in /.ruby))]
    (of ! each context (random.lower_cased 1))))

(def .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Extension

              /.Context
              /.#host /.#host_module_extension /.#target /.#artifact_extension])
      (do [! random.monad]
        [target (random.lower_cased 1)]
        (all _.and
             (_.coverage [/.js /.jvm /.lua /.python /.ruby /.c++]
               (let [contexts (list (/.js target)
                                    (/.jvm target)
                                    (/.lua target)
                                    (/.python target)
                                    (/.ruby target)
                                    (/.c++ target))
                     maximum (list.size contexts)]
                 (`` (and (,, (with_template [<amount> <slot>]
                                [(|> contexts
                                     (list#each (the <slot>))
                                     (set.of_list text.hash)
                                     set.size
                                     (n.= <amount>))]

                                [maximum /.#host]
                                [maximum /.#host_module_extension]
                                [maximum /.#artifact_extension]
                                [1 /.#target]
                                ))))))
             ))))
