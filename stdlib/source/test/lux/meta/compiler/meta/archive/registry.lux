(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" maybe (.use "[1]#[0]" functor)]
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    ["[0]" product]
    ["[0]" text]
    ["[0]" binary
     ["[1]" \\format]
     ["<[1]>" \\parser]]
    [collection
     ["[0]" sequence (.only Sequence)]
     ["[0]" set (.only Set)]
     ["[0]" list (.use "[1]#[0]" mix functor)]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" unit]
    ["[0]" artifact (.only)
     ["[0]" category]]]]])

(def tagged?
  (template (_ <tag> <it>)
    [(when <it>
       {<tag> _}
       true
       
       _
       false)]))

(def random_dependency
  (Random unit.ID)
  (all random.and
       random.nat
       random.nat
       ))

(def (random_dependencies amount)
  (-> Nat (Random (Set unit.ID)))
  (random.set unit.hash amount ..random_dependency))

(def .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Registry])
      (do [! random.monad]
        [expected_name (random.lower_cased 5)
         mandatory? random.bit
         expected_dependencies (..random_dependencies 5)
         
         expected_amount (at ! each (n.% 10) random.nat)
         expected_names (|> (random.lower_cased 1)
                            (random.set text.hash expected_amount)
                            (at ! each set.list))]
        (`` (all _.and
                 (_.coverage [/.empty]
                   (|> /.empty
                       /.artifacts
                       sequence.size
                       (n.= 0)))
                 (_.coverage [/.resource]
                   (let [[@it registry] (/.resource mandatory? expected_dependencies /.empty)]
                     (when (sequence.list (/.artifacts registry))
                       (list [artifact actual_dependencies])
                       (and (same? @it (the artifact.#id artifact))
                            (same? mandatory? (the artifact.#mandatory? artifact))
                            (tagged? category.#Anonymous (the artifact.#category artifact))
                            (same? expected_dependencies actual_dependencies))

                       _
                       false)))
                 (,, (with_template [<new> <expected>' <query> <tag> <wrong_new> <wrong_expected>']
                       [(_.coverage [<new> <query>]
                          (let [<expected> <expected>'
                                <wrong_expected> <wrong_expected>']
                            (and (let [[@it registry] (<new> <expected> mandatory? expected_dependencies /.empty)]
                                   (and (when (<query> registry)
                                          (list actual_name)
                                          (same? <expected> actual_name)

                                          _
                                          false)
                                        (when (sequence.list (/.artifacts registry))
                                          (list [artifact actual_dependencies])
                                          (and (same? @it (the artifact.#id artifact))
                                               (same? mandatory? (the artifact.#mandatory? artifact))
                                               (when (the artifact.#category artifact)
                                                 {<tag> actual_name}
                                                 (same? <expected> actual_name)

                                                 _
                                                 false)
                                               (same? expected_dependencies actual_dependencies))

                                          _
                                          false)))
                                 (let [[@it registry] (<wrong_new> <wrong_expected> mandatory? expected_dependencies /.empty)]
                                   (when (<query> registry)
                                     (list)
                                     true

                                     _
                                     false)))))]

                       [/.definition (is category.Definition [expected_name {.#None}]) /.definitions category.#Definition /.analyser expected_name]
                       [/.analyser expected_name /.analysers category.#Analyser /.synthesizer expected_name]
                       [/.synthesizer expected_name /.synthesizers category.#Synthesizer /.translator expected_name]
                       [/.translator expected_name /.translators category.#Translator /.declaration expected_name]
                       [/.declaration expected_name /.declarations category.#Declaration /.custom expected_name]
                       [/.custom expected_name /.customs category.#Custom /.definition (is category.Definition [expected_name {.#None}])]
                       ))
                 (_.coverage [/.id]
                   (and (,, (with_template [<new> <expected>' <name>]
                              [(let [<expected> <expected>'
                                     [@expected registry] (<new> <expected> mandatory? expected_dependencies /.empty)]
                                 (|> (/.id (<name> <expected>) registry)
                                     (maybe#each (same? @expected))
                                     (maybe.else false)))]

                              [/.definition (is category.Definition [expected_name {.#None}]) product.left]
                              [/.analyser expected_name |>]
                              [/.synthesizer expected_name |>]
                              [/.translator expected_name |>]
                              [/.declaration expected_name |>]
                              [/.custom expected_name |>]
                              ))))
                 (_.coverage [/.artifacts]
                   (and (,, (with_template [<new> <query> <equivalence> <$>]
                              [(let [expected/* (list#each <$> expected_names)
                                     [ids registry] (is [(Sequence artifact.ID) /.Registry]
                                                        (list#mix (function (_ expected [ids registry])
                                                                    (let [[@new registry] (<new> expected mandatory? expected_dependencies registry)]
                                                                      [(sequence.suffix @new ids) registry]))
                                                                  [sequence.empty /.empty]
                                                                  expected/*))
                                     it (/.artifacts registry)]
                                 (and (n.= expected_amount (sequence.size it))
                                      (list.every? (function (_ [@it [it dependencies]])
                                                     (same? @it (the artifact.#id it)))
                                                   (list.zipped_2 (sequence.list ids) (sequence.list it)))
                                      (at (list.equivalence <equivalence>) = expected/* (<query> registry))))]

                              [/.definition /.definitions category.definition_equivalence (is (-> Text category.Definition)
                                                                                              (function (_ it)
                                                                                                [it {.#None}]))]
                              [/.analyser /.analysers text.equivalence (|>>)]
                              [/.synthesizer /.synthesizers text.equivalence (|>>)]
                              [/.translator /.translators text.equivalence (|>>)]
                              [/.declaration /.declarations text.equivalence (|>>)]
                              [/.custom /.customs text.equivalence (|>>)]
                              ))))
                 (_.coverage [/.format /.parser]
                   (and (,, (with_template [<new> <expected>' <name>]
                              [(let [<expected> <expected>'
                                     [@expected before] (<new> <expected> mandatory? expected_dependencies /.empty)]
                                 (|> before
                                     (binary.result /.format)
                                     (<binary>.result /.parser)
                                     (try#each (|>> (/.id (<name> <expected>))
                                                    (maybe#each (same? @expected))
                                                    (maybe.else false)))
                                     (try.else false)))]

                              [/.definition (is category.Definition [expected_name {.#None}]) product.left]
                              [/.analyser expected_name |>]
                              [/.synthesizer expected_name |>]
                              [/.translator expected_name |>]
                              [/.declaration expected_name |>]
                              [/.custom expected_name |>]
                              ))))
                 )))))
