... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [control
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text (.use "[1]#[0]" equivalence)]
    ["[0]" binary
     ["[1]F" \\injection]
     ["<[1]>" \\projection]]]
   [math
    ["[0]" random (.only Random)]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 ["[0]" ////
  ["[1][0]" version]])

(the .public random
  (Random /.Signature)
  (all random.and
       (all random.and
            (random.upper_cased 1)
            (random.lower_cased 1))
       ////version.random
       ))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Signature
              /.#name /.#version])
      (all _.and
           (_.for [/.equivalence]
                  (equivalenceT.spec /.equivalence ..random))
           
           (do random.monad
             [left ..random
              right ..random]
             (_.coverage [/.description]
               (bit#= (of /.equivalence = left right)
                      (text#= (/.description left) (/.description right)))))
           (do random.monad
             [expected ..random]
             (_.coverage [/.injection /.projection]
               (|> expected
                   (binaryF.value /.injection)
                   (<binary>.value /.projection)
                   (try#each (of /.equivalence = expected))
                   (try.else false))))
           )))
