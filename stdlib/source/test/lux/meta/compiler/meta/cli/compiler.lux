(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [\\specification
     ["$[0]" equivalence]]]
   [control
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    ["[0]" text
     ["<[1]>" \\parser]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]
   [meta
    ["[0]" symbol
     ["$[1]" \\test]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(def .public random
  (Random /.Compiler)
  (do [! random.monad]
    [definition ($symbol.random 1 1)
     amount (at ! each (n.% 5) random.nat)
     parameters (random.list amount (random.lower_case 2))]
    (in [/.#definition definition
         /.#parameters parameters])))

(def .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Compiler])
      (do [! random.monad]
        [expected ..random]
        (all _.and
             (_.for [/.equivalence]
                    ($equivalence.spec /.equivalence ..random))

             (_.coverage [/.format /.parser]
               (|> expected
                   /.format
                   (<text>.result /.parser)
                   (try#each (at /.equivalence = expected))
                   (try.else false)))
             ))))
