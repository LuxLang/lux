(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" io]
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" text (.use "[1]#[0]" equivalence)]]
   [math
    ["[0]" random (.only Random)]]
   [meta
    [compiler
     [target
      [jvm
       ["[0]" bytecode]]]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(def .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [.let [$unit [0 0]]
         expected (random.upper_cased 1)])
      (all _.and
           (_.coverage [/.host]
             (io.run! (do io.monad
                        [[class_loader host] /.host]
                        (in (when (of host evaluate $unit [{.#None} (bytecode.string expected)])
                              {try.#Success actual}
                              (text#= expected (as Text actual))
                              
                              {try.#Failure error}
                              false)))))
           (_.coverage [/.invalid_value]
             (io.run! (do io.monad
                        [[class_loader host] /.host]
                        (in (when (of host evaluate $unit [{.#None} bytecode.aconst_null])
                              {try.#Success _}
                              false
                              
                              {try.#Failure error}
                              (exception.match? /.invalid_value error))))))
           (_.coverage [/.cannot_load]
             (io.run! (do io.monad
                        [[class_loader host] /.host]
                        (in (when (of host evaluate $unit [{.#None} (all bytecode.composite
                                                                         bytecode.lconst_0
                                                                         bytecode.lconst_0
                                                                         bytecode.ldiv
                                                                         bytecode.pop2
                                                                         (bytecode.string expected)
                                                                         )])
                              {try.#Success _}
                              false
                              
                              {try.#Failure error}
                              (exception.match? /.cannot_load error))))))
           )))
