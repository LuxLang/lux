... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" pipe]
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    ["[0]" product]]
   [math
    ["[0]" random (.only Random)]]
   [meta
    ["[0]" configuration
     ["$[1]" \\test]]
    ["[0]" type (.only)
     ["[0]" check]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" // (.only)
    ["[2][0]" module]
    [//
     [phase
      ["[2][0]" extension]]
     [///
      ["[2][0]" phase (.use "[1]#[0]" functor)]]]]]])

(the .public random_state
  (Random Lux)
  (do random.monad
    [version random.nat
     host (random.lower_cased 1)
     configuration ($configuration.random 5)]
    (in (//.state (//.info version host configuration)))))

(the nominal
  (Random Type)
  (do random.monad
    [name (random.lower_cased 1)]
    (in {.#Nominal name (list)})))

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [lux ..random_state
         .let [state [/extension.#bundle /extension.empty
                      /extension.#state lux]]
         expected ..nominal
         dummy (random.only (|>> (type.= expected) not)
                            ..nominal)
         module (random.lower_cased 1)]
        (all _.and
             (_.coverage [/.expecting /.inference]
               (and (|> (/.inference expected)
                        (/.expecting expected)
                        (/module.with 0 module)
                        (/phase#each product.right)
                        (/phase.value state)
                        (pipe.when
                          {try.#Success _} true
                          {try.#Failure _} false))
                    (|> (/.inference dummy)
                        (/.expecting expected)
                        (/module.with 0 module)
                        (/phase#each product.right)
                        (/phase.value state)
                        (pipe.when
                          {try.#Success _} false
                          {try.#Failure _} true))
                    (|> (/.inference expected)
                        (/.expecting dummy)
                        (/module.with 0 module)
                        (/phase#each product.right)
                        (/phase.value state)
                        (pipe.when
                          {try.#Success _} false
                          {try.#Failure _} true))))
             (_.coverage [/.inferring]
               (|> (/.inference expected)
                   /.inferring
                   (/module.with 0 module)
                   (/phase#each product.right)
                   (/phase.value state)
                   (try#each (|>> product.left (type.= expected)))
                   (try.else false)))
             (_.coverage [/.check]
               (|> (do /phase.monad
                     [exT (/.check (do check.monad
                                     [[id type] check.existential]
                                     (in type)))]
                     (|> (/.inference exT)
                         (/.expecting exT)))
                   (/module.with 0 module)
                   (/phase#each product.right)
                   (/phase.value state)
                   (pipe.when
                     {try.#Success _} true
                     {try.#Failure _} false)))
             (_.coverage [/.existential /.existential?]
               (|> (do /phase.monad
                     [:it: /.existential]
                     (in (/.existential? :it:)))
                   (/module.with 0 module)
                   (/phase#each product.right)
                   (/phase.value state)
                   (try.else false)))
             (_.coverage [/.fresh]
               (and (|> (do /phase.monad
                          [varT (/.check (do check.monad
                                           [[id type] check.var]
                                           (in type)))]
                          (|> (/.inference expected)
                              (/.expecting varT)))
                        (/module.with 0 module)
                        (/phase#each product.right)
                        (/phase.value state)
                        (pipe.when
                          {try.#Success _} true
                          {try.#Failure _} false))
                    (|> (do /phase.monad
                          [varT (/.check (do check.monad
                                           [[id type] check.var]
                                           (in type)))]
                          (|> (/.inference expected)
                              (/.expecting varT)
                              /.fresh))
                        (/module.with 0 module)
                        (/phase#each product.right)
                        (/phase.value state)
                        (pipe.when
                          {try.#Success _} false
                          {try.#Failure _} true))))
             ))))
