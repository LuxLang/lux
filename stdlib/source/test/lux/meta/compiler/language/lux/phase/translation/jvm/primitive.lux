... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" io]
    ["[0]" try]]
   [data
    ["[0]" bit]
    ["[0]" text]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["[0]" integer]
     ["[0]" decimal]]]
   [meta
    [macro
     ["[0]" template]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" host]]]])

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [expected_bit random.bit
         expected_i64 random.i64
         expected_f64 random.unit_decimal
         expected_text (random.lower_cased 1)])
      (`` (all _.and
               (,, (template.with [<constructor> <expected> <type> <=>]
                     [(_.coverage [<constructor>]
                        (io.value (do io.monad
                                    [[class_loader host] host.host]
                                    (in (when (of host evaluate [{.#None} (<constructor> <expected>)])
                                          {try.#Success actual}
                                          (<=> <expected> (as <type> actual))
                                          
                                          {try.#Failure error}
                                          false)))))]

                     [/.bit expected_bit Bit bit.=]
                     [/.i64 expected_i64 Integer integer.=]
                     [/.f64 expected_f64 Decimal decimal.=]
                     [/.text expected_text Text text.=]
                     ))
               ))))
