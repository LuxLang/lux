(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [\\specification
     ["$[0]" equivalence]]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text (.use "[1]#[0]" equivalence)]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number
     ["f" frac]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 ["[0]" //
  ["[1][0]" simple]
  ["[1][0]" complex]])

(def .public random
  (Random /.Pattern)
  (random.rec
   (function (_ random)
     (all random.or
          //simple.random
          (//complex.random 4 random)
          random.nat
          ))))

(def .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Pattern])
      (do random.monad
        [expected_register random.nat
         expected_bit random.bit
         expected_nat random.nat
         expected_int random.int
         expected_rev random.rev
         expected_frac random.frac
         expected_text (random.lower_case 2)

         expected_lefts random.nat
         expected_right? random.bit

         left ..random
         right ..random])
      (`` (all _.and
               (_.for [/.equivalence]
                      ($equivalence.spec /.equivalence ..random))
               
               (_.coverage [/.format]
                 (bit#= (at /.equivalence = left right)
                        (text#= (/.format left) (/.format right))))
               (_.coverage [/.unit]
                 (case (/.unit)
                   (/.unit)
                   true

                   _
                   false))
               (,, (with_template [<tag> <value>]
                     [(_.coverage [<tag>]
                        (case (<tag> <value>)
                          (<tag> actual)
                          (same? <value> actual)

                          _
                          false))]

                     [/.bind expected_register]
                     [/.bit expected_bit]
                     [/.nat expected_nat]
                     [/.int expected_int]
                     [/.rev expected_rev]
                     [/.frac expected_frac]
                     [/.text expected_text]
                     ))
               (_.coverage [/.variant]
                 (case (/.variant [expected_lefts expected_right? (/.text expected_text)])
                   (/.variant [actual_lefts actual_right? (/.text actual_text)])
                   (and (same? expected_lefts actual_lefts)
                        (same? expected_right? actual_right?)
                        (same? expected_text actual_text))

                   _
                   false))
               (_.coverage [/.tuple]
                 (case (/.tuple (list (/.bit expected_bit)
                                      (/.nat expected_nat)
                                      (/.int expected_int)
                                      (/.rev expected_rev)
                                      (/.frac expected_frac)
                                      (/.text expected_text)))
                   (/.tuple (list (/.bit actual_bit)
                                  (/.nat actual_nat)
                                  (/.int actual_int)
                                  (/.rev actual_rev)
                                  (/.frac actual_frac)
                                  (/.text actual_text)))
                   (and (same? expected_bit actual_bit)
                        (same? expected_nat actual_nat)
                        (same? expected_int actual_int)
                        (same? expected_rev actual_rev)
                        (same? expected_frac actual_frac)
                        (same? expected_text actual_text))

                   _
                   false))
               ))))
