... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [control
    ["<>" projection]
    ["[0]" pipe]
    ["[0]" try]
    ["[0]" exception]
    ["[0]" function]
    ["[0]" maybe]]
   [data
    ["[0]" bit]
    ["[0]" text (.only)]
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number
     ["n" natural]
     ["i" integer]
     ["d" decimal]
     ["r" revolution]]]
   [meta
    ["[0]" static]
    ["[0]" symbol]
    [macro
     ["[0]" template]
     ["[0]" expansion]]
    ["[0]" configuration
     ["$[1]" \\test]]
    [compiler
     [reference (.only Constant)
      [variable (.only)]]]]
   [test
    ["_" property (.only Test)]]]]
 ["[0]" \\projection]
 [\\library
  ["[0]" / (.only)
   [////
    ["[0]" phase]
    ["[0]" version]]]]
 ["[0]" /
  ["[1][0]" complex]
  ["[1][0]" inference]
  ["[1][0]" macro]
  ["[1][0]" module]
  ["[1][0]" pattern]
  ["[1][0]" scope]
  ["[1][0]" simple]
  ["[1][0]" type]
  ["[1][0]" coverage]
  [////
   ["[1][0]" reference (.only)
    ["[2][0]" variable]]
   [///
    [meta
     ["[1][0]" symbol]
     ["[0]" location (.only)
      ["[2][1]" /]
      ["[1]" \\library]]]]]])

(the !expect
  (template.macro (_ <expectation> <computation>)
    [(when <computation>
       <expectation>
       true

       _
       false)]))

(the constant
  (Random Constant)
  (random.and (random.unicode 10)
              (random.unicode 10)))

(the \\projection
  Test
  (<| (_.covering \\projection._)
      (_.for [\\projection.Projection])
      (do [! random.monad]
        []
        (`` (all _.and
                 (do [! random.monad]
                   [expected (of ! each (|>> /.bit) random.bit)]
                   (_.coverage [\\projection.value \\projection.any]
                     (|> (list expected)
                         (\\projection.value \\projection.any)
                         (pipe.when
                           {try.#Success actual}
                           (of /.equivalence = expected actual)

                           {try.#Failure _}
                           false))))
                 (,, (template.with [<query> <check> <random> <analysis> <=>]
                       [(do [! random.monad]
                          [expected <random>]
                          (_.coverage [<query>]
                            (|> (list (<analysis> expected))
                                (\\projection.value <query>)
                                (pipe.when
                                  {try.#Success actual}
                                  (<=> expected actual)

                                  {try.#Failure _}
                                  false))))
                        (do [! random.monad]
                          [expected <random>]
                          (_.coverage [<check>]
                            (|> (list (<analysis> expected))
                                (\\projection.value (<check> expected))
                                (!expect {try.#Success _}))))]
                       
                       [\\projection.bit \\projection.this_bit random.bit /.bit bit.=]
                       [\\projection.natural \\projection.this_natural random.natural /.natural n.=]
                       [\\projection.integer \\projection.this_integer random.integer /.integer i.=]
                       [\\projection.decimal \\projection.this_decimal random.unit_decimal /.decimal d.=]
                       [\\projection.revolution \\projection.this_revolution random.revolution /.revolution r.=]
                       [\\projection.text \\projection.this_text (random.unicode 10) /.text text.=]
                       [\\projection.local \\projection.this_local random.natural /.local n.=]
                       [\\projection.foreign \\projection.this_foreign random.natural /.foreign n.=]
                       [\\projection.constant \\projection.this_constant ..constant /.constant symbol.=]
                       ))
                 (do [! random.monad]
                   [expected random.bit]
                   (_.coverage [\\projection.tuple]
                     (|> (list (/.tuple (list (/.bit expected))))
                         (\\projection.value (\\projection.tuple \\projection.bit))
                         (pipe.when
                           {try.#Success actual}
                           (bit.= expected actual)

                           {try.#Failure _}
                           false))))
                 (do [! random.monad]
                   [dummy random.bit]
                   (_.coverage [\\projection.end?]
                     (and (|> (\\projection.value \\projection.end? (list))
                              (!expect {try.#Success .true}))
                          (|> (\\projection.value (do <>.monad
                                                    [verdict \\projection.end?
                                                     _ \\projection.bit]
                                                    (in verdict))
                                                  (list (/.bit dummy)))
                              (!expect {try.#Success .false})))))
                 (do [! random.monad]
                   [dummy random.bit]
                   (_.coverage [\\projection.end]
                     (and (|> (\\projection.value \\projection.end (list))
                              (!expect {try.#Success _}))
                          (|> (\\projection.value \\projection.end (list (/.bit dummy)))
                              (!expect {try.#Failure _})))))
                 (do [! random.monad]
                   [expected random.bit]
                   (_.coverage [\\projection.cannot_parse]
                     (and (|> (list (/.bit expected))
                              (\\projection.value \\projection.natural)
                              (pipe.when
                                {try.#Success _}
                                false

                                {try.#Failure error}
                                (exception.is? \\projection.cannot_parse error)))
                          (|> (list)
                              (\\projection.value \\projection.bit)
                              (pipe.when
                                {try.#Success _}
                                false

                                {try.#Failure error}
                                (exception.is? \\projection.cannot_parse error))))))
                 (do [! random.monad]
                   [expected random.bit]
                   (_.coverage [\\projection.unconsumed_input]
                     (|> (list (/.bit expected) (/.bit expected))
                         (\\projection.value \\projection.bit)
                         (pipe.when
                           {try.#Success _}
                           false

                           {try.#Failure error}
                           (exception.is? \\projection.unconsumed_input error)))))
                 )))))

(the (random_branch random)
  (for_any (_ a)
    (-> (Random a)
        (Random (/.Branch' a))))
  (all random.and
       /pattern.random
       random
       ))

(the (random_match multiplicity random)
  (for_any (_ a)
    (-> Natural (Random a)
        (Random (/.Match' a))))
  (all random.and
       (..random_branch random)
       (random.list multiplicity (..random_branch random))
       ))

(the .public (random multiplicity)
  (-> Natural (Random /.Analysis))
  (<| random.rec
      (function (_ random))
      (let [random|when (all random.and
                             random
                             (..random_match multiplicity random)
                             )
            random|function (all random.and
                                 (random.list multiplicity random)
                                 random
                                 )
            random|apply (all random.and
                              random
                              random
                              )
            random|extension (all random.and
                                  (random.lower_cased 1)
                                  (random.list multiplicity random)
                                  )])
      (all random.or
           /simple.random
           (/complex.random multiplicity random)
           /reference.random
           random|when
           random|function
           random|apply
           random|extension
           )))

(the test|simple
  Test
  (do random.monad
    [bit random.bit
     natural random.natural
     integer random.integer
     revolution random.revolution
     decimal random.decimal
     text (random.lower_cased 1)]
    (`` (all _.and
             (_.coverage [/.unit]
               (when (/.unit)
                 (/.unit)
                 true
                 
                 _
                 false))
             (,, (template.with [<tag> <expected>]
                   [(_.coverage [<tag>]
                      (when (<tag> <expected>)
                        (<tag> actual)
                        (same? <expected> actual)
                        
                        _
                        false))]

                   [/.bit bit]
                   [/.natural natural]
                   [/.integer integer]
                   [/.revolution revolution]
                   [/.decimal decimal]
                   [/.text text]))
             ))))

(the test|complex
  Test
  (do random.monad
    [expected_left (..random 2)
     expected_right (..random 2)
     expected_lefts random.natural
     expected_right? random.bit]
    (all _.and
         (_.coverage [/.variant]
           (let [expected (if expected_right?
                            expected_right
                            expected_left)]
             (when (/.variant [expected_lefts expected_right? expected])
               (/.variant [actual_lefts actual_right? actual])
               (and (same? expected_lefts actual_lefts)
                    (same? expected_right? actual_right?)
                    (same? expected actual))

               _
               false)))
         (_.coverage [/.tuple]
           (when (/.tuple (list expected_left expected_right))
             (/.tuple (list actual_left actual_right))
             (and (same? expected_left actual_left)
                  (same? expected_right actual_right))

             _
             false))
         )))

(the test|reference
  Test
  (do random.monad
    [expected_register random.natural
     expected_constant (/symbol.random 1 1)
     expected_variable /variable.random]
    (`` (all _.and
             (,, (template.with [<tag> <expected>]
                   [(_.coverage [<tag>]
                      (when (<tag> <expected>)
                        (<tag> actual)
                        (same? <expected> actual)

                        _
                        false))]

                   [/.local expected_register]
                   [/.foreign expected_register]
                   [/.constant expected_constant]
                   [/.variable expected_variable]
                   ))
             ))))

(the tagged?
  (template.macro (_ <tag> <it>)
    [(when <it>
       {<tag> _}
       true

       _
       false)]))

(the test|reification
  Test
  (do random.monad
    [expected_abstraction (random.only (|>> (..tagged? /.#Apply) not)
                                       (..random 2))
     expected_parameter/0 (..random 2)
     expected_parameter/1 (..random 2)]
    (all _.and
         (_.coverage [/.reified /.reification]
           (when (|> [expected_abstraction (list expected_parameter/0 expected_parameter/1)]
                     /.reified
                     /.reification)
             [actual_abstraction (list actual_parameter/0 actual_parameter/1)]
             (and (same? expected_abstraction actual_abstraction)
                  (same? expected_parameter/0 actual_parameter/0)
                  (same? expected_parameter/1 actual_parameter/1))

             _
             false))
         (_.coverage [/.no_op]
           (when (/.no_op expected_parameter/0)
             (/.no_op actual)
             (same? expected_parameter/0 actual)

             _
             false))
         )))

(the test|when
  Test
  (do random.monad
    [expected_input (..random 2)
     expected_match (random_match 2 (..random 2))]
    (all _.and
         (_.coverage [/.when]
           (when (/.when [expected_input expected_match])
             (/.when [actual_input actual_match])
             (and (same? expected_input actual_input)
                  (same? expected_match actual_match))

             _
             false))
         )))

(expansion.let [<id> (static.random_natural)
                <exception> (template.symbol ["exception_" <id>])]
  (these (exception.the <exception>)

         (the test|phase
           Test
           (do random.monad
             [version/0 random.natural
              host/0 (random.lower_cased 5)
              version/1 random.natural
              host/1 (random.lower_cased 5)
              expected_error (random.lower_cased 10)
              location/0 /location.random
              location/1 /location.random
              configuration ($configuration.random 5)
              .let [state/0 (has .#location location/0
                                 (/.state (/.info version/0 host/0 configuration)))
                    state/1 (has .#location location/1
                                 (/.state (/.info version/1 host/1 configuration)))]]
             (all _.and
                  (_.coverage [/.set_state]
                    (|> (do phase.monad
                          [pre (phase.read function.identity)
                           _ (/.set_state state/1)
                           post (phase.read function.identity)]
                          (in (and (same? state/0 pre)
                                   (same? state/1 post))))
                        (phase.value state/0)
                        (try.else false)))
                  (_.coverage [/.failure]
                    (|> (/.failure expected_error)
                        (phase.value state/0)
                        (pipe.when
                          {try.#Failure actual_error}
                          (and (text.contains? expected_error actual_error)
                               (text.contains? (location.as_text location/0) actual_error))

                          _
                          false)))
                  (_.coverage [/.except]
                    (|> (/.except <exception> [])
                        (phase.value state/0)
                        (pipe.when
                          {try.#Failure actual_error}
                          (and (text.contains? (exception.error <exception> []) actual_error)
                               (text.contains? (location.as_text location/0) actual_error))

                          _
                          false)))
                  (_.coverage [/.with_exception]
                    (|> (/.failure expected_error)
                        (/.with_exception <exception> [])
                        (phase.value state/0)
                        (pipe.when
                          {try.#Failure actual_error}
                          (and (text.contains? expected_error actual_error)
                               (text.contains? (exception.error <exception> []) actual_error)
                               (text.contains? (location.as_text location/0) actual_error))

                          _
                          false)))
                  (_.coverage [/.assertion]
                    (and (|> (/.assertion <exception> [] false)
                             (phase.value state/0)
                             (pipe.when
                               {try.#Failure actual_error}
                               (and (text.contains? (exception.error <exception> []) actual_error)
                                    (text.contains? (location.as_text location/0) actual_error))

                               _
                               false))
                         (|> (/.assertion <exception> [] true)
                             (phase.value state/0)
                             (pipe.when
                               {try.#Success _}
                               true

                               _
                               false))))
                  )))))

(the test|state
  Test
  (do random.monad
    [version random.natural
     host (random.lower_cased 5)

     expected_module (random.lower_cased 10)
     dummy_module (random.lower_cased 11)

     location /location.random
     
     expected_file (random.lower_cased 12)
     expected_code (random.lower_cased 13)

     configuration ($configuration.random 5)
     .let [state (has .#location location
                      (/.state (/.info version host configuration)))]]
    (all _.and
         (_.coverage [/.info]
           (let [it (/.info version host configuration)]
             (and (text.= (version.injection version)
                          (its .#version it))
                  (same? host
                         (its .#target it))
                  (..tagged? .#Build (its .#mode it))
                  (same? configuration (its .#configuration it)))))
         (_.coverage [/.state]
           (let [info (/.info version host configuration)
                 it (/.state info)]
             (and (same? info
                         (its .#info it))
                  (same? location.dummy
                         (its .#location it))
                  (..tagged? .#None (its .#current_module it))
                  (..tagged? .#None (its .#expected it))
                  (list.empty? (its .#modules it))
                  (list.empty? (its .#scopes it))
                  (list.empty? (its [.#type_context .#var_bindings] it))
                  (when (its .#source it)
                    [location 0 ""]
                    (same? location.dummy location)

                    _
                    false))))
         (_.coverage [/.set_current_module]
           (|> (do phase.monad
                 [_ (/.set_current_module expected_module)]
                 (phase.read (|>> (its .#current_module) (maybe.else ""))))
               (phase.value state)
               (pipe.when
                 {try.#Success actual}
                 (same? expected_module actual)

                 _
                 false)))
         (_.coverage [/.with_current_module]
           (let [current_module (phase.read (|>> (its .#current_module) (maybe.else "")))]
             (|> (do phase.monad
                   [_ (/.set_current_module expected_module)
                    pre current_module
                    mid (/.with_current_module dummy_module
                          current_module)
                    post current_module]
                   (in (and (same? expected_module pre)
                            (same? dummy_module mid)
                            (same? expected_module post))))
                 (phase.value state)
                 (try.else false))))
         (_.coverage [/.location /.set_location]
           (let [expected (/.location expected_file)]
             (|> (do phase.monad
                   [_ (/.set_location expected)]
                   (phase.read (its .#location)))
                 (phase.value state)
                 (pipe.when
                   {try.#Success actual}
                   (same? expected actual)

                   _
                   false))))
         (_.coverage [/.with_location]
           (let [expected (/.location expected_file)
                 dummy (/.location expected_code)
                 location (phase.read (its .#location))]
             (|> (do phase.monad
                   [_ (/.set_location expected)
                    pre location
                    mid (/.with_location dummy
                          location)
                    post location]
                   (in (and (same? expected pre)
                            (same? dummy mid)
                            (same? expected post))))
                 (phase.value state)
                 (try.else false))))
         (_.coverage [/.source /.set_source_code]
           (let [expected (/.source expected_file expected_code)]
             (|> (do phase.monad
                   [_ (/.set_source_code expected)]
                   (phase.read (its .#source)))
                 (phase.value state)
                 (pipe.when
                   {try.#Success actual}
                   (same? expected actual)

                   _
                   false))))
         (_.coverage [/.with_source_code]
           (let [expected (/.source expected_file expected_code)
                 dummy (/.source expected_code expected_file)
                 source (phase.read (its .#source))]
             (|> (do phase.monad
                   [_ (/.set_source_code expected)
                    pre source
                    mid (/.with_source_code dummy
                          source)
                    post source]
                   (in (and (same? expected pre)
                            (same? dummy mid)
                            (same? expected post))))
                 (phase.value state)
                 (try.else false))))
         )))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Analysis])
      (do random.monad
        [left (..random 2)
         right (..random 2)]
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec /.equivalence (..random 2)))
             
             ..test|simple
             ..test|complex
             ..test|reference
             (_.for [/.Reification]
                    ..test|reification)
             (_.for [/.Branch /.Branch' /.Match /.Match']
                    ..test|when)
             (_.for [/.Operation /.Phase /.Handler /.Bundle]
                    ..test|phase)
             (_.for [/.State]
                    ..test|state)
             (_.coverage [/.injection]
               (bit.= (of /.equivalence = left right)
                      (text.= (/.injection left) (/.injection right))))

             /complex.test
             /inference.test
             /macro.test
             /module.test
             /pattern.test
             /scope.test
             /simple.test
             /type.test
             /coverage.test

             ..\\projection
             ))))
