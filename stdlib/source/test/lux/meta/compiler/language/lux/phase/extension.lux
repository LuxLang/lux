... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" hash
     ["[1]T" \\test]]]
   [control
    ["[0]" pipe]
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\injection (.only format)]]
    [collection
     ["[0]" dictionary]
     ["[0]" list (.use "[1]#[0]" mix)]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]
     ["i" int]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [/////
    ["[0]" phase]
    [meta
     ["[0]" archive]]]]])

(the .public random
  (Random (/.Extension Nat))
  (all random.and
       (random.lower_cased 5)
       (random.list 2 random.nat)
       ))

(the test|state
  Test
  (do [! random.monad]
    [state random.int
     dummy (random.only (|>> (i.= state) not)
                        random.int)]
    (all _.and
         (_.coverage [/.read]
           (|> (is (/.Operation Int Nat Nat Text)
                   (/.read %.int))
               (of phase.functor each (text#= (%.int state)))
               (phase.value [/.#bundle /.empty
                             /.#state state])
               (try.else false)))
         (_.coverage [/.update]
           (|> (is (/.Operation Int Nat Nat Text)
                   (do phase.monad
                     [_ (/.update ++)]
                     (/.read %.int)))
               (of phase.functor each (text#= (%.int (++ state))))
               (phase.value [/.#bundle /.empty
                             /.#state state])
               (try.else false)))
         (_.coverage [/.temporary]
           (|> (is (/.Operation Int Nat Nat Text)
                   (do phase.monad
                     [|state'| (/.temporary ++ (/.read %.int))
                      |state| (/.read %.int)]
                     (in (format |state'| " " |state|))))
               (of phase.functor each (text#= (format (%.int (++ state)) " " (%.int state))))
               (phase.value [/.#bundle /.empty
                             /.#state state])
               (try.else false)))
         (_.coverage [/.localized]
           (|> (is (/.Operation Int Nat Nat Text)
                   (do phase.monad
                     [|state| (/.localized %.int
                                           (function (_ _ old) (++ old))
                                           (text.enclosed ["<" ">"])
                                           (/.read %.int))
                      |state'| (/.read %.int)]
                     (in (format |state'| " " |state|))))
               (of phase.functor each (text#= (format (%.int (i.+ +2 state))
                                                      " " (%.int (i.+ +1 state)))))
               (phase.value [/.#bundle /.empty
                             /.#state state])
               (try.else false))))
    ))

(the extender
  /.Extender
  (|>> as_expected))

(the handler/0
  (/.Handler Int Nat Nat)
  (function (_ @self phase archive inputs)
    (of phase.monad in (list#mix n.+ 0 inputs))))

(the handler/1
  (/.Handler Int Nat Nat)
  (function (_ @self phase archive inputs)
    (of phase.monad in (list#mix n.* 1 inputs))))

(the test|name
  Test
  (do [! random.monad]
    [state random.int
     extension (random.lower_cased 1)
     left random.nat
     right random.nat]
    (all _.and
         (_.coverage [/.cannot_overwrite]
           (|> (do phase.monad
                 [_ (/.install extender extension handler/0)]
                 (/.install extender extension handler/1))
               (phase.value [/.#bundle /.empty
                             /.#state state])
               (pipe.when
                 {try.#Failure error}
                 (exception.is? /.cannot_overwrite error)

                 _
                 false)))
         (_.coverage [/.unknown]
           (|> (/.apply archive.empty (function (_ archive input)
                                        (of phase.monad in (++ input)))
                        [extension (list left right)])
               (phase.value [/.#bundle /.empty
                             /.#state state])
               (pipe.when
                 {try.#Failure error}
                 (exception.is? /.unknown error)

                 _
                 false)))
         )))

(the test|bundle
  Test
  (let [phase (is (/.Phase Int Nat Nat)
                  (function (_ archive input)
                    (of phase.monad in (++ input))))]
    (do [! random.monad]
      [state random.int

       extension (random.lower_cased 1)
       left random.nat
       right random.nat]
      (all _.and
           (_.coverage [/.empty]
             (dictionary.empty? /.empty))
           (<| (_.for [/.Extender /.Handler])
               (all _.and
                    (_.coverage [/.install /.apply]
                      (|> (do phase.monad
                            [_ (/.install extender extension handler/0)]
                            (/.apply archive.empty phase [extension (list left right)]))
                          (of phase.functor each (n.= (n.+ left right)))
                          (phase.value [/.#bundle /.empty
                                        /.#state state])
                          (try.else false)))
                    (_.coverage [/.Phase]
                      (let [handler (is (/.Handler Int Nat Nat)
                                        (function (_ @self phase archive inputs)
                                          (let [! phase.monad]
                                            (|> inputs
                                                (monad.each ! (phase archive))
                                                (of ! each (list#mix n.+ 0))))))]
                        (|> (do phase.monad
                              [_ (/.install extender extension handler)]
                              (/.apply archive.empty phase [extension (list left right)]))
                            (of phase.functor each (n.= (n.+ (++ left) (++ right))))
                            (phase.value [/.#bundle /.empty
                                          /.#state state])
                            (try.else false))))
                    (_.coverage [/.with]
                      (|> (do phase.monad
                            [_ (/.with extender (dictionary.of_list text.hash (list [extension handler/1])))]
                            (/.apply archive.empty (function (_ archive input)
                                                     (of phase.monad in (++ input)))
                                     [extension (list left right)]))
                          (of phase.functor each (n.= (n.* left right)))
                          (phase.value [/.#bundle /.empty
                                        /.#state state])
                          (try.else false)))
                    (_.for [/.Name]
                           ..test|name)
                    ))
           ))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Extension])
      (do [! random.monad]
        [state random.int
         dummy (random.only (|>> (i.= state) not)
                            random.int)
         expected random.nat
         expected_error (random.lower_cased 1)]
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec (/.equivalence n.equivalence) ..random))
             (_.for [/.hash]
                    (hashT.spec (/.hash n.hash) ..random))
             
             (<| (_.for [/.Operation])
                 (all _.and
                      (_.coverage [/.lifted]
                        (and (|> (is (/.Operation Int Nat Nat Nat)
                                     (/.lifted (do phase.monad
                                                 []
                                                 (in expected))))
                                 (of phase.functor each (same? expected))
                                 (phase.value [/.#bundle /.empty
                                               /.#state state])
                                 (try.else false))
                             (|> (is (/.Operation Int Nat Nat Nat)
                                     (/.lifted (phase.of_try {try.#Failure expected_error})))
                                 (phase.value [/.#bundle /.empty
                                               /.#state state])
                                 (pipe.when
                                   {try.#Failure actual_error}
                                   (same? expected_error actual_error)

                                   _
                                   false))))
                      (_.coverage [/.up]
                        (|> (do phase.monad
                              []
                              (in expected))
                            (is (/.Operation Int Nat Nat Nat))
                            /.up
                            (is (phase.Operation Int Nat))
                            (of phase.functor each (same? expected))
                            (phase.value state)
                            (try.else false)))
                      ))
             (_.for [/.State]
                    ..test|state)
             (_.for [/.Bundle]
                    ..test|bundle)
             ))))
