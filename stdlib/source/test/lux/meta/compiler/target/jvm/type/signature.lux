... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" hash
     ["[1]T" \\test]]]
   [control
    [function
     ["?" predicate]]]
   [data
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]
     ["[0]" set]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [macro
    ["[0]" expansion]]
   [meta
    ["[0]" static]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" category]
    ["[0]" box]]]])

(<| (expansion.let [,*primitives (these /.boolean
                                        /.byte
                                        /.short
                                        /.int
                                        /.long
                                        /.float
                                        /.double
                                        /.char)
                    ,*simple (these /.void
                                    ,*primitives)
                    ,*boxes (these box.boolean
                                   box.byte
                                   box.short
                                   box.int
                                   box.long
                                   box.float
                                   box.double
                                   box.char)])
    (these (the .public random
             (Random (/.Signature category.Value))
             (let [primitives (is (List (/.Signature category.Primitive))
                                  (list ,*primitives))
                   primitives_amount (list.size primitives)

                   boxes (list ,*boxes)
                   boxes_amount (list.size boxes)]
               (all random.either
                    (do [! random.monad]
                      [choice (by ! each (n.% primitives_amount) random.natural)]
                      (random.try (in (list.item choice primitives))))
                    (do [! random.monad]
                      [choice (by ! each (n.% boxes_amount) random.natural)
                       choice (random.try (in (list.item choice boxes)))]
                      (in (/.class (list) choice)))))))
    (the .public test
      Test
      (<| (_.covering /._)
          (do [! random.monad]
            [argument_0 ..random
             argument_1 ..random
             name (random.lower_cased 2)
             return (is (Random (/.Signature category.Return))
                        (all random.either
                             (in /.void)
                             ..random))])
          (_.for [/.Signature])
          (all _.and
               (_.for [/.equivalence /.=]
                      (static.when (same? /.equivalence /.=)
                        (equivalenceT.spec /.equivalence ..random)))
               (_.for [/.hash]
                      (hashT.spec /.hash ..random))

               (_.coverage [,*simple]
                 (let [options (is (List (/.Signature category.Return))
                                   (list ,*simple))
                       uniques (set.of_list /.hash options)]
                   (n.= (list.size options)
                        (set.size uniques))))
               (_.coverage [/.var /.var_name]
                 (text.= name
                         (/.var_name (/.var name))))
               (_.coverage [/.wildcard /.signature]
                 (let [options (<| (list#each /.signature)
                                   (is (List (/.Signature category.Return)))
                                   (list ,*simple
                                         /.wildcard))
                       uniques (set.of_list text.hash options)]
                   (n.= (list.size options)
                        (set.size uniques))))
               
               (_.coverage [/.class]
                 (let [options (is (List (/.Signature category.Class))
                                   (list#each (/.class (list)) (list ,*boxes)))
                       uniques (set.of_list /.hash options)]
                   (n.= (list.size options)
                        (set.size uniques))))
               (_.coverage [/.declaration /.as_class]
                 (let [expected (is (List (/.Signature category.Class))
                                    (list#each (|>> (/.declaration (list)) /.as_class) (list ,*boxes)))
                       actual (is (List (/.Signature category.Class))
                                  (list#each (/.class (list)) (list ,*boxes)))]
                   (by (list.equivalence /.equivalence) =
                       expected
                       actual)))
               (_.coverage [/.lower]
                 (let [options (is (List (/.Signature category.Parameter))
                                   (list#each (|>> (/.class (list)) /.lower) (list ,*boxes)))
                       uniques (set.of_list /.hash options)]
                   (n.= (list.size options)
                        (set.size uniques))))
               (_.coverage [/.upper]
                 (let [options (is (List (/.Signature category.Parameter))
                                   (list#each (|>> (/.class (list)) /.upper) (list ,*boxes)))
                       uniques (set.of_list /.hash options)]
                   (n.= (list.size options)
                        (set.size uniques))))
               (_.coverage [/.array]
                 (and (let [options (is (List (/.Signature category.Array))
                                        (list#each (|>> (/.class (list)) /.array)
                                                   (list ,*boxes)))
                            uniques (set.of_list /.hash options)]
                        (n.= (list.size options)
                             (set.size uniques)))
                      (let [single (list#each (|>> (/.class (list)) /.array) (list ,*boxes))
                            double (list#each /.array single)]
                        (not (by (list.equivalence /.equivalence) =
                                 single
                                 double)))))
               
               (_.coverage [/.lower_prefix]
                 (text.starts_with? /.lower_prefix
                                    (/.signature (/.lower (/.class (list) box.boolean)))))
               (_.coverage [/.upper_prefix]
                 (text.starts_with? /.upper_prefix
                                    (/.signature (/.upper (/.class (list) box.boolean)))))
               (_.coverage [/.var_prefix]
                 (text.starts_with? /.var_prefix
                                    (/.signature (/.var name))))
               ))))
