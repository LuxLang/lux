... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [control
    ["[0]" try]]
   [data
    ["[0]" bit]
    ["[0]" text]
    ["[0]" binary (.only)
     ["![1]" \\injection]]]
   [math
    ["[0]" random]
    [number
     ["n" natural]]]
   [meta
    ["[0]" static]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["[0]" frame (.only)
    ["[2][1]" \\test]]]])

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [before /.random
         datum frame.random])
      (_.for [/.Stack /.random])
      (all _.and
           (_.for [/.equivalence /.=]
                  (static.when (same? /.equivalence /.=)
                    (equivalenceT.spec /.equivalence /.random)))
           
           (_.coverage [/.empty /.catch]
             (not (/.= /.empty /.catch)))
           (_.coverage [/.push /.pop]
             (<| (try.else false)
                 (do try.monad
                   [stack (/.push datum before)
                    .let [push_matters!
                          (not (/.= before stack))]
                    stack (/.pop datum stack)
                    .let [pop_matters!
                          (/.= before stack)]]
                   (in (and push_matters!
                            pop_matters!)))))
           (_.coverage [/.max]
             (<| (try.else false)
                 (do try.monad
                   [after (/.push datum before)]
                   (in (same? after (/.max after before))))))
           (do [! random.monad]
             [sample_0 /.random
              sample_1 /.random]
             (_.coverage [/.as_binary]
               (bit.= (/.= sample_0
                           sample_1)
                      (binary.= (!binary.value /.as_binary sample_0)
                                (!binary.value /.as_binary sample_1)))))
           (do [! random.monad]
             [sample_0 /.random]
             (_.coverage [/.length]
               (n.= /.length
                    (binary.size (!binary.value /.as_binary sample_0)))))
           (do [! random.monad]
             [sample_0 /.random
              sample_1 /.random]
             (_.coverage [/.as_text]
               (bit.= (/.= sample_0
                           sample_1)
                      (text.= (/.as_text sample_0)
                              (/.as_text sample_1)))))

           /frame.test
           )))
