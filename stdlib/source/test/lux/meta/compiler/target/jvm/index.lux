... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [data
    ["[0]" binary (.only)
     ["![1]" \\injection]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]
    [number
     ["n" natural]]]
   [meta
    ["[0]" static]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 [//
  [encoding
   ["[0]" unsigned]]])

(the .public random
  (for_any (_ of)
    (Random (/.Index of)))
  (random#each /.index
               unsigned.random_u2))

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [expected unsigned.random_u2])
      (_.for [/.Index])
      (all _.and
           (_.for [/.equivalence /.=]
                  (static.when (same? /.equivalence /.=)
                    (equivalenceT.spec /.equivalence ..random)))
           
           (_.coverage [/.index /.value]
             (|> expected
                 /.index
                 /.value
                 (same? expected)))
           (_.coverage [/.injection /.length]
             (n.= /.length (binary.size (!binary.value /.injection (/.index expected)))))
           )))
