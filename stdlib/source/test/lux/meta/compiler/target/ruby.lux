... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   ["[0]" ffi]
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [control
    ["[0]" pipe]
    ["[0]" maybe (.use "[1]#[0]" functor)]
    ["[0]" try (.only Try) (.use "[1]#[0]" functor)]]
   [function
    ["[0]" predicate]]
   [data
    ["[0]" bit]
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor)]
     ["[0]" set]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number (.only hex)
     ["n" natural]
     ["i" integer]
     ["d" decimal]
     ["[0]" i64]]]
   [macro
    ["[0]" template]]
   [meta
    [compiler
     [meta
      ["[0]" packager
       ["[1]" ruby]]]]]
   [world
    ["[0]" file]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.use "[1]#[0]" equivalence)]])

(ffi.import (eval [Text] "try" "?" Any))

(the (expression ??? it)
  (-> (-> Any Bit) /.Expression Bit)
  (|> it
      /.code
      ..eval
      (try#each (|>> (maybe#each ???)
                     (maybe.else false)))
      (try.else false)))

(the nil
  (-> /.Expression Bit)
  (|>> /.code
       ..eval
       (try#each (|>> (pipe.when
                        {.#None} true
                        {.#Some _} false)))
       (try.else false)))

(the test|literal
  Test
  (do [! random.monad]
    [bool random.bit
     float random.decimal
     int random.integer
     string (random.upper_cased 5)]
    (all _.and
         (_.coverage [/.nil]
           (..nil /.nil))
         (_.coverage [/.bool]
           (expression (|>> (as Bit) (bit.= bool))
                       (/.bool bool)))
         (_.coverage [/.int]
           (expression (|>> (as Integer) (i.= int))
                       (/.int int)))
         (_.coverage [/.float]
           (expression (|>> (as Decimal) (d.= float))
                       (/.float float)))
         (_.coverage [/.string]
           (expression (|>> (as Text) (text.= string))
                       (/.string string)))
         (_.coverage [/.symbol]
           (expression (|>> (as Text) (text.= string))
                       (/.do "id2name" (stack) {.#None} (/.symbol string))))
         )))

(the test|bool
  Test
  (do [! random.monad]
    [left random.bit
     right random.bit]
    (`` (all _.and
             (,, (template.with [</> <lux>]
                   [(_.coverage [</>]
                      (let [expected (<lux> left right)]
                        (expression (|>> (as Bit) (bit.= expected))
                                    (</> (/.bool left) (/.bool right)))))]

                   [/.or .or]
                   [/.and .and]
                   ))
             (_.coverage [/.not]
               (expression (|>> (as Bit) (bit.= (not left)))
                           (/.not (/.bool left))))
             ))))

(the test|float
  Test
  (do [! random.monad]
    [parameter (random.only (|>> (d.= +0.0) not)
                            random.unit_decimal)
     subject random.unit_decimal]
    (`` (all _.and
             (,, (template.with [</> <lux> <pre>]
                   [(_.coverage [</>]
                      (let [expected (<lux> (<pre> parameter) (<pre> subject))]
                        (expression (|>> (as Decimal) (d.= expected))
                                    (</> (/.float (<pre> parameter)) (/.float (<pre> subject))))))]

                   [/.+ d.+ |>]
                   [/.- d.- |>]
                   [/.* d.* |>]
                   [/./ d./ |>]
                   [/.% d.mod |>]
                   [/.pow d.pow d.abs]
                   ))
             (,, (template.with [</> <lux>]
                   [(_.coverage [</>]
                      (let [expected (<lux> parameter subject)]
                        (expression (|>> (as Bit) (bit.= expected))
                                    (</> (/.float parameter) (/.float subject)))))]

                   [/.<  d.<]
                   [/.<= d.<=]
                   [/.>  d.>]
                   [/.>= d.>=]
                   [/.=  d.=]
                   ))
             ))))

(the int_16
  (-> Integer Integer)
  (i64.and (-- (i64.left_shifted 15 1))))

(the test|int
  Test
  (do [! random.monad]
    [left random.integer
     right random.integer

     i16 (by ! each ..int_16 random.integer)
     shift (by ! each (n.% 16) random.natural)]
    (`` (all _.and
             (,, (template.with [</> <lux>]
                   [(_.coverage [</>]
                      (let [expected (<lux> left right)]
                        (expression (|>> (as Decimal) d.integer (i.= expected))
                                    (</> (/.int left) (/.int right)))))]

                   [/.bit_or i64.or]
                   [/.bit_xor i64.xor]
                   [/.bit_and i64.and]
                   ))
             (_.coverage [/.bit_not]
               (expression (|>> (as Integer) (i.= (i64.not left)))
                           (/.bit_not (/.int left))))
             (_.coverage [/.opposite]
               (expression (|>> (as Integer) (i.= (i.* -1 left)))
                           (/.opposite (/.int left))))
             (_.coverage [/.bit_shl]
               (let [expected (i64.left_shifted shift i16)]
                 (expression (|>> (as Decimal) d.integer (i.= expected))
                             (/.bit_shl (/.int (.integer shift))
                                        (/.int i16)))))
             (_.coverage [/.bit_shr]
               (let [expected (i.right_shifted shift i16)]
                 (expression (|>> (as Decimal) d.integer (i.= expected))
                             (/.bit_shr (/.int (.integer shift))
                                        (/.int i16)))))
             ))))

(the test|array
  Test
  (do [! random.monad]
    [size (by ! each (|>> (n.% 10) ++) random.natural)
     index (by ! each (n.% size) random.natural)
     items (random.stack size random.unit_decimal)
     .let [expected (|> items
                        (stack.item index)
                        (try.else d.not_a_number))]
     from (by ! each (n.% size) random.natural)
     plus (by ! each (n.% (n.- from size)) random.natural)
     .let [to (/.int (.integer (n.+ plus from)))
           from (/.int (.integer from))]]
    (all _.and
         (_.coverage [/.array /.item]
           (and (expression (|>> (as Decimal) (d.= expected))
                            (/.item (/.int (.integer index))
                                    (/.array (stack#each /.float items))))
                (expression (|>> (as Bit))
                            (|> (/.array (stack#each /.float items))
                                (/.item (/.int (.integer size)))
                                (/.= /.nil)))))
         (_.coverage [/.array_range]
           (expression (|>> (as Integer) (i.= (.integer (++ plus))))
                       (|> (/.array (stack#each /.float items))
                           (/.array_range from to)
                           (/.its "length"))))
         )))

(the test|hash
  Test
  (do [! random.monad]
    [expected random.unit_decimal
     field (random.upper_cased 5)
     dummy (random.only (|>> (text.= field) not)
                        (random.upper_cased 5))
     .let [field (/.string field)
           dummy (/.string dummy)]]
    (all _.and
         (_.coverage [/.hash]
           (and (expression (|>> (as Decimal) (d.= expected))
                            (/.item field (/.hash (stack [field (/.float expected)]))))
                (expression (|>> (as Bit))
                            (|> (/.hash (stack [field (/.float expected)]))
                                (/.item dummy)
                                (/.= /.nil)))))
         )))

(the test|object
  Test
  (do [! random.monad]
    [size (by ! each (|>> (n.% 10) ++) random.natural)
     index (by ! each (n.% size) random.natural)
     items (random.stack size random.unit_decimal)
     $class (by ! each (|>> %.natural (text "class_") /.local)
                random.natural)
     $sub_class (by ! each (|>> %.natural (text "sub_class_") /.local)
                    random.natural)
     $method/0 (by ! each (|>> %.natural (text "method_") /.local)
                   random.natural)
     $method/1 (|> random.natural
                   (by ! each (|>> %.natural (text "method_") /.local))
                   (random.only (|>> (/#= $method/0) not)))
     $arg/0 (by ! each (|>> %.natural (text "arg_") /.local)
                random.natural)
     $state (by ! each (|>> %.natural (text "instance_") /.instance)
                random.natural)
     single random.unit_decimal
     .let [double (/.function $method/0 (stack $arg/0)
                    (/.return (/.+ $arg/0 $arg/0)))]]
    (all _.and
         (_.coverage [/.its]
           (expression (|>> (as Integer) (i.= (.integer size)))
                       (|> (/.array (stack#each /.float items))
                           (/.its "length"))))
         (_.coverage [/.do]
           (expression (let [expected (|> items
                                          (stack.item index)
                                          (try.else d.not_a_number))]
                         (|>> (as Decimal) (d.= expected)))
                       (|> (/.array (stack#each /.float items))
                           (/.do "at" (stack (/.int (.integer index))) {.#None}))))
         (_.coverage [/.class]
           (expression (|>> (as Decimal) (d.= (d.+ single single)))
                       (|> (all /.then
                                (/.; (/.set (stack $class) (/.class [/.#parameters (stack)
                                                                     /.#body double])))
                                (/.return (|> $class
                                              (/.new (stack) {.#None})
                                              (/.do (/.code $method/0) (stack (/.float single)) {.#None}))))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.new /.initialize]
           (expression (|>> (as Decimal) (d.= single))
                       (|> (all /.then
                                (/.; (/.set (stack $class) (/.class [/.#parameters (stack)
                                                                     /.#body (all /.then
                                                                                  (/.function /.initialize (stack $arg/0)
                                                                                    (/.; (/.set (stack $state) $arg/0)))
                                                                                  (/.function $method/0 (stack)
                                                                                    (/.return $state))
                                                                                  )])))
                                (/.return (|> $class
                                              (/.new (stack (/.float single)) {.#None})
                                              (/.do (/.code $method/0) (stack) {.#None}))))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.alias_method/2]
           (expression (|>> (as Decimal) (d.= (d.+ single single)))
                       (|> (all /.then
                                (/.; (/.set (stack $class) (/.class [/.#parameters (stack)
                                                                     /.#body (all /.then
                                                                                  double
                                                                                  (/.; (/.alias_method/2 (/.string (/.code $method/1))
                                                                                                         (/.string (/.code $method/0)))))])))
                                (/.return (|> $class
                                              (/.new (stack) {.#None})
                                              (/.do (/.code $method/1) (stack (/.float single)) {.#None}))))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.for [/.module]
                (all _.and
                     (_.coverage [/.include/1]
                       (expression (|>> (as Decimal) (d.= (d.+ single single)))
                                   (|> (all /.then
                                            (/.; (/.set (stack $class) (/.module [/.#parameters (stack)
                                                                                  /.#body double])))
                                            (/.; (/.set (stack $sub_class) (/.class [/.#parameters (stack)
                                                                                     /.#body (/.; (/.include/1 $class))])))
                                            (/.return (|> $sub_class
                                                          (/.new (stack) {.#None})
                                                          (/.do (/.code $method/0) (stack (/.float single)) {.#None}))))
                                       [(stack)] (/.lambda {.#None})
                                       (/.apply_lambda (stack)))))
                     (_.coverage [/.extend/1]
                       (expression (|>> (as Decimal) (d.= (d.+ single single)))
                                   (|> (all /.then
                                            (/.; (/.set (stack $class) (/.module [/.#parameters (stack)
                                                                                  /.#body double])))
                                            (/.; (/.set (stack $sub_class) (/.class [/.#parameters (stack)
                                                                                     /.#body (/.; (/.extend/1 $class))])))
                                            (/.return (|> $sub_class
                                                          (/.do (/.code $method/0) (stack (/.float single)) {.#None}))))
                                       [(stack)] (/.lambda {.#None})
                                       (/.apply_lambda (stack)))))
                     ))
         )))

(the test|io
  Test
  (<| (do [! random.monad]
        [left (random.upper_cased 5)
         right (random.upper_cased 5)
         $old (by ! each /.local (random.upper_cased 1))
         $new (by ! each /.local (random.upper_cased 2))
         $it (by ! each /.local (random.upper_cased 3))
         .let [expected (text left right)]])
      (all _.and
           (_.for [/.stdout]
                  (all _.and
                       (_.coverage [/.print/1]
                         (expression (|>> (as Text) (text.= expected))
                                     (|> (all /.then
                                              (/.; (/.require/1 (/.string "stringio")))
                                              (/.; (/.set (stack $old) /.stdout))
                                              (/.; (/.set (stack $new) (/.new (stack) {.#None} (/.manual "StringIO"))))
                                              (/.; (/.set (stack /.stdout) $new))
                                              (/.; (/.print/1 (/.string left)))
                                              (/.; (/.print/1 (/.string right)))
                                              (/.; (/.set (stack /.stdout) $old))
                                              (/.return (/.its "string" $new)))
                                         [(stack)] (/.lambda {.#None})
                                         (/.apply_lambda (stack)))))
                       (_.coverage [/.print/2]
                         (expression (|>> (as Text) (text.= expected))
                                     (|> (all /.then
                                              (/.; (/.require/1 (/.string "stringio")))
                                              (/.; (/.set (stack $old) /.stdout))
                                              (/.; (/.set (stack $new) (/.new (stack) {.#None} (/.manual "StringIO"))))
                                              (/.; (/.set (stack /.stdout) $new))
                                              (/.; (/.print/2 (/.string left) (/.string right)))
                                              (/.; (/.set (stack /.stdout) $old))
                                              (/.return (/.its "string" $new)))
                                         [(stack)] (/.lambda {.#None})
                                         (/.apply_lambda (stack)))))
                       ))
           (_.for [/.stdin]
                  (all _.and
                       (_.coverage [/.gets/0]
                         (expression (|>> (as Text) (text.= (text left text.\n)))
                                     (|> (all /.then
                                              (/.; (/.require/1 (/.string "stringio")))
                                              (/.; (/.set (stack $old) /.stdin))
                                              (/.; (/.set (stack /.stdin) (/.new (stack (/.string (text left text.\n))) {.#None}
                                                                                 (/.manual "StringIO"))))
                                              (/.; (/.set (stack $it) /.gets/0))
                                              (/.; (/.set (stack /.stdin) $old))
                                              (/.return $it))
                                         [(stack)] (/.lambda {.#None})
                                         (/.apply_lambda (stack)))))
                       (_.coverage [/.last_string_read]
                         (expression (|>> (as Text) (text.= (text right text.\n)))
                                     (|> (all /.then
                                              (/.; (/.require/1 (/.string "stringio")))
                                              (/.; (/.set (stack $old) /.stdin))
                                              (/.; (/.set (stack /.stdin) (/.new (stack (/.string (text right text.\n))) {.#None}
                                                                                 (/.manual "StringIO"))))
                                              (/.; (/.set (stack $it) /.gets/0))
                                              (/.; (/.set (stack /.stdin) $old))
                                              (/.return /.last_string_read))
                                         [(stack)] (/.lambda {.#None})
                                         (/.apply_lambda (stack)))))
                       (_.coverage [/.last_line_number_read]
                         (expression (|>> (as Natural) (n.= 2))
                                     /.last_line_number_read))
                       ))
           )))

(the test|computation
  Test
  (do [! random.monad]
    [test random.bit
     then random.unit_decimal
     else random.unit_decimal

     bool random.bit
     float random.decimal
     string (random.upper_cased 5)

     comment (random.upper_cased 10)]
    (all _.and
         ..test|bool
         ..test|float
         ..test|int
         ..test|array
         ..test|hash
         ..test|object
         ..test|io
         (_.coverage [/.?]
           (let [expected (if test then else)]
             (expression (|>> (as Decimal) (d.= expected))
                         (/.? (/.bool test)
                              (/.float then)
                              (/.float else)))))
         (_.coverage [/.comment]
           (expression (|>> (as Decimal) (d.= then))
                       (/.comment comment
                         (/.float then))))
         )))

(the test|global
  Test
  (do [! random.monad]
    [float/0 random.unit_decimal
     $global (by ! each /.global (random.lower_cased 10))
     pattern (by ! each /.string (random.lower_cased 11))]
    (all _.and
         (_.coverage [/.global]
           (expression (|>> (as Text) (text.= "global-variable"))
                       (|> (all /.then
                                (/.; (/.set (stack $global) (/.float float/0)))
                                (/.return (/.defined?/1 $global)))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.script_name]
           (expression (let [file (text (by file.default delimiter) packager.main_file)]
                         (|>> (as Text)
                              (text.ends_with? file)))
                       /.script_name))
         (_.coverage [/.input_record_delimiter]
           (expression (|>> (as Text)
                            (text.= text.\n))
                       /.input_record_delimiter))
         (_.coverage [/.output_record_delimiter]
           (..nil /.output_record_delimiter))
         (_.coverage [/.process_id]
           (expression (|>> (as Natural) (n.= 0) not)
                       /.process_id))
         (_.coverage [/.case_insensitivity_flag]
           (expression (|>> (as Bit) (bit.= false))
                       /.case_insensitivity_flag))
         (_.coverage [/.command_line_arguments]
           (expression (|>> (as Integer) (i.= +0))
                       (/.its "length" /.command_line_arguments)))
         (_.coverage [/.last_string_matched]
           (expression (|>> (as Bit))
                       (|> (all /.then
                                (/.; (|> (/.manual "Regexp")
                                         (/.new (stack pattern) {.#None})
                                         (/.do "match" (stack pattern) {.#None})))
                                (/.return (/.= pattern /.last_string_matched)))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.last_regexp_match]
           (expression (|>> (as Bit))
                       (|> (/.return (|> (/.manual "Regexp")
                                         (/.new (stack pattern) {.#None})
                                         (/.do "match" (stack pattern) {.#None})
                                         (/.= /.last_regexp_match)))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         )))

(the test|local_var
  Test
  (do [! random.monad]
    [float/0 random.unit_decimal
     $foreign (by ! each /.local (random.lower_cased 10))]
    (all _.and
         (_.coverage [/.local]
           (expression (|>> (as Decimal) (d.= (d.+ float/0 float/0)))
                       (|> (/.return (/.+ $foreign $foreign))
                           [(stack $foreign)] (/.lambda {.#None})
                           (/.apply_lambda (stack (/.float float/0))))))
         (_.coverage [/.set]
           (expression (|>> (as Decimal) (d.= (d.+ float/0 float/0)))
                       (|> (all /.then
                                (/.; (/.set (stack $foreign) (/.float float/0)))
                                (/.return (/.+ $foreign $foreign)))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         )))

(the test|instance_var
  Test
  (do [! random.monad]
    [float/0 random.unit_decimal
     instance (by ! each (|>> %.natural (text "instance_"))
                  random.natural)
     .let [$instance (/.instance instance)]
     $method (by ! each (|>> %.natural (text "method_") /.local)
                 random.natural)
     $class (by ! each (|>> %.natural (text "class_") /.local)
                random.natural)
     $object (by ! each (|>> %.natural (text "object_") /.local)
                 random.natural)]
    (all _.and
         (_.coverage [/.instance]
           (expression (|>> (as Decimal) (d.= float/0))
                       (|> (all /.then
                                (/.; (/.set (stack $class) (/.class [/.#parameters (stack)
                                                                     /.#body (all /.then
                                                                                  (/.function /.initialize (stack)
                                                                                    (/.; (/.set (stack $instance) (/.float float/0))))
                                                                                  (/.function $method (stack)
                                                                                    (/.return $instance))
                                                                                  )])))
                                (/.return (|> $class
                                              (/.new (stack) {.#None})
                                              (/.do (/.code $method) (stack) {.#None}))))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.attr_reader/*]
           (expression (|>> (as Decimal) (d.= float/0))
                       (|> (all /.then
                                (/.; (/.set (stack $class) (/.class [/.#parameters (stack)
                                                                     /.#body (all /.then
                                                                                  (/.attr_reader/* (stack instance))
                                                                                  (/.function /.initialize (stack)
                                                                                    (/.; (/.set (stack $instance) (/.float float/0))))
                                                                                  )])))
                                (/.return (|> $class
                                              (/.new (stack) {.#None})
                                              (/.its instance))))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.attr_writer/*]
           (expression (|>> (as Decimal) (d.= float/0))
                       (|> (all /.then
                                (/.; (/.set (stack $class) (/.class [/.#parameters (stack)
                                                                     /.#body (all /.then
                                                                                  (/.attr_writer/* (stack instance))
                                                                                  (/.function $method (stack)
                                                                                    (/.return $instance))
                                                                                  )])))
                                (/.; (/.set (stack $object) (|> $class
                                                                (/.new (stack) {.#None}))))
                                (/.; (/.set (stack (/.its instance $object)) (/.float float/0)))
                                (/.return (|>  $object
                                               (/.do (/.code $method) (stack) {.#None}))))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.attr_accessor/*]
           (expression (|>> (as Decimal) (d.= float/0))
                       (|> (all /.then
                                (/.; (/.set (stack $class) (/.class [/.#parameters (stack)
                                                                     /.#body (/.attr_accessor/* (stack instance))])))
                                (/.; (/.set (stack $object) (|> $class
                                                                (/.new (stack) {.#None}))))
                                (/.; (/.set (stack (/.its instance $object)) (/.float float/0)))
                                (/.return (/.its instance $object)))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         )))

(the test|static_var
  Test
  (do [! random.monad]
    [int/0 (by ! each (|>> (n.% 10) ++ .integer)
               random.natural)
     $static (by ! each (|>> %.natural (text "static_") /.static)
                 random.natural)
     $arg (by ! each (|>> %.natural /.local)
              random.natural)
     $method (by ! each (|>> %.natural (text "method_") /.local)
                 random.natural)
     $class (by ! each (|>> %.natural (text "class_") /.local)
                random.natural)]
    (all _.and
         (_.coverage [/.static /.class_variable_set /.class_variable_get]
           (expression (|>> (as Integer) (i.= int/0))
                       (|> (all /.then
                                (/.; (/.set (stack $class) (/.class [/.#parameters (stack)
                                                                     /.#body (/.function $method (stack)
                                                                               (/.return (/.int +0)))])))
                                (/.; (/.class_variable_set $static (/.int int/0) $class))
                                (/.return (/.class_variable_get $static $class)))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         )))

(the test|variadic
  Test
  (do [! random.monad]
    [$inputs (by ! each /.local (random.lower_cased 10))
     arity (by ! each (n.% 10) random.natural)
     vals (|> random.integer
              (by ! each /.int)
              (random.stack arity))
     keys (|> (random.lower_cased 1)
              (random.set text.hash arity)
              (by ! each (|>> set.stack (stack#each /.string))))]
    (all _.and
         (<| (_.for [/.LVar*])
             (all _.and
                  (_.coverage [/.variadic]
                    (expression (|>> (as Integer) .natural (n.= arity))
                                (|> (/.return (/.its "length" $inputs))
                                    [(stack (/.variadic $inputs))] (/.lambda {.#None})
                                    (/.apply_lambda vals))))
                  (_.coverage [/.splat]
                    (expression (|>> (as Integer) .natural (n.= arity))
                                (|> (/.return (/.its "length" (/.array (stack (/.splat $inputs)))))
                                    [(stack (/.variadic $inputs))] (/.lambda {.#None})
                                    (/.apply_lambda vals))))))
         (<| (_.for [/.LVar**])
             (_.coverage [/.variadic_kv /.double_splat]
               (expression (|>> (as Integer) .natural (n.= arity))
                           (|> (/.return (/.its "length" $inputs))
                               [(stack (/.variadic_kv $inputs))] (/.lambda {.#None})
                               (/.apply_lambda (stack (/.double_splat (/.hash (stack.zipped_2 keys vals)))))))))
         )))

(the test|var
  Test
  (do [! random.monad]
    [float/0 random.unit_decimal
     $foreign (by ! each /.local (random.lower_cased 10))

     $constant (by ! each /.constant (random.lower_cased 10))]
    (all _.and
         (_.coverage [/.defined?/1]
           (and (expression (|>> (as Bit))
                            (|> (/.defined?/1 $foreign)
                                (/.= /.nil)))
                (expression (|>> (as Text) (text.= "local-variable"))
                            (|> (all /.then
                                     (/.; (/.set (stack $foreign) (/.float float/0)))
                                     (/.return (/.defined?/1 $foreign)))
                                [(stack)] (/.lambda {.#None})
                                (/.apply_lambda (stack))))))
         (_.for [/.CVar]
                (_.coverage [/.constant]
                  (expression (|>> (as Text) (text.= "constant"))
                              (|> (all /.then
                                       (/.; (/.set (stack $constant) (/.float float/0)))
                                       (/.return (/.defined?/1 $constant)))
                                  [(stack)] (/.lambda {.#None})
                                  (/.apply_lambda (stack))))))
         (_.for [/.GVar]
                ..test|global)
         (_.for [/.LVar]
                ..test|local_var)
         (_.for [/.IVar]
                ..test|instance_var)
         (_.for [/.SVar]
                ..test|static_var)
         ..test|variadic
         )))

(the test|location
  Test
  (do [! random.monad]
    [float/0 random.unit_decimal
     $foreign (by ! each /.local (random.lower_cased 10))
     field (by ! each /.string (random.upper_cased 10))]
    (all _.and
         (<| (_.for [/.Var])
             ..test|var)
         (_.coverage [/.Access]
           (and (expression (|>> (as Decimal) (d.= (d.+ float/0 float/0)))
                            (let [@ (/.item (/.int +0) $foreign)]
                              (|> (all /.then
                                       (/.; (/.set (stack $foreign) (/.array (stack $foreign))))
                                       (/.; (/.set (stack @) (/.+ @ @)))
                                       (/.return @))
                                  [(stack $foreign)] (/.lambda {.#None})
                                  (/.apply_lambda (stack (/.float float/0))))))
                (expression (|>> (as Decimal) (d.= (d.+ float/0 float/0)))
                            (let [@ (/.item field $foreign)]
                              (|> (all /.then
                                       (/.; (/.set (stack $foreign) (/.hash (stack [field $foreign]))))
                                       (/.; (/.set (stack @) (/.+ @ @)))
                                       (/.return @))
                                  [(stack $foreign)] (/.lambda {.#None})
                                  (/.apply_lambda (stack (/.float float/0))))))
                ))
         )))

(the test|expression
  Test
  (do [! random.monad]
    [dummy random.unit_decimal
     expected random.unit_decimal]
    (`` (all _.and
             (_.for [/.Literal]
                    ..test|literal)
             (_.for [/.Computation]
                    ..test|computation)
             (_.for [/.Location]
                    ..test|location)
             ))))

(the test|label
  Test
  (do [! random.monad]
    [input (by ! each ..int_16 random.integer)

     full_inner_iterations (by ! each (|>> (n.% 20) ++) random.natural)
     expected_inner_iterations (by ! each (n.% full_inner_iterations) random.natural)

     full_outer_iterations (by ! each (|>> (n.% 10) ++) random.natural)
     expected_outer_iterations (by ! each (n.% full_outer_iterations) random.natural)

     .let [$input (/.local "input")
           $output (/.local "output")
           $inner_index (/.local "inner_index")
           $outer_index (/.local "outer_index")]]
    (all _.and
         (_.coverage [/.break]
           (let [expected (i.* (.integer expected_inner_iterations) input)]
             (expression (|>> (as Decimal) d.integer (i.= expected))
                         (|> (all /.then
                                  (/.; (/.set (stack $inner_index) (/.int +0)))
                                  (/.; (/.set (stack $output) (/.int +0)))
                                  (/.while (/.< (/.int (.integer full_inner_iterations)) $inner_index)
                                           (all /.then
                                                (/.when (/.= (/.int (.integer expected_inner_iterations)) $inner_index)
                                                  /.break)
                                                (/.; (/.set (stack $output) (/.+ $input $output)))
                                                (/.; (/.set (stack $inner_index) (/.+ (/.int +1) $inner_index)))
                                                ))
                                  (/.return $output))
                             [(stack $input)] (/.lambda {.#None})
                             (/.apply_lambda (stack (/.int input)))))))
         (_.coverage [/.next]
           (let [expected (i.* (.integer (n.- expected_inner_iterations full_inner_iterations)) input)]
             (expression (|>> (as Decimal) d.integer (i.= expected))
                         (|> (all /.then
                                  (/.; (/.set (stack $inner_index) (/.int +0)))
                                  (/.; (/.set (stack $output) (/.int +0)))
                                  (/.while (/.< (/.int (.integer full_inner_iterations)) $inner_index)
                                           (all /.then
                                                (/.; (/.set (stack $inner_index) (/.+ (/.int +1) $inner_index)))
                                                (/.when (/.<= (/.int (.integer expected_inner_iterations)) $inner_index)
                                                  /.next)
                                                (/.; (/.set (stack $output) (/.+ $input $output)))
                                                ))
                                  (/.return $output))
                             [(stack $input)] (/.lambda {.#None})
                             (/.apply_lambda (stack (/.int input)))))))
         (_.coverage [/.redo]
           (let [expected (i.* (.integer (n.- expected_inner_iterations full_inner_iterations)) input)]
             (expression (|>> (as Decimal) d.integer (i.= expected))
                         (|> (all /.then
                                  (/.; (/.set (stack $inner_index) (/.int +0)))
                                  (/.; (/.set (stack $output) (/.int +0)))
                                  (/.while (/.< (/.int (.integer full_inner_iterations)) $inner_index)
                                           (all /.then
                                                (/.; (/.set (stack $inner_index) (/.+ (/.int +1) $inner_index)))
                                                (/.when (/.<= (/.int (.integer expected_inner_iterations)) $inner_index)
                                                  /.redo)
                                                (/.; (/.set (stack $output) (/.+ $input $output)))
                                                ))
                                  (/.return $output))
                             [(stack $input)] (/.lambda {.#None})
                             (/.apply_lambda (stack (/.int input)))))))
         )))

(the test|loop
  Test
  (do [! random.monad]
    [input (by ! each (i.right_shifted 32) random.integer)
     iterations (by ! each (n.% 10) random.natural)
     .let [$input (/.local "input")
           $output (/.local "output")
           $index (/.local "index")
           expected (i.* (.integer iterations) input)]]
    (all _.and
         (_.coverage [/.while]
           (expression (|>> (as Integer) (i.= expected))
                       (|> (all /.then
                                (/.; (/.set (stack $index) (/.int +0)))
                                (/.; (/.set (stack $output) (/.int +0)))
                                (/.while (/.< (/.int (.integer iterations)) $index)
                                         (all /.then
                                              (/.; (/.set (stack $output) (/.+ $input $output)))
                                              (/.; (/.set (stack $index) (/.+ (/.int +1) $index)))
                                              ))
                                (/.return $output))
                           [(stack $input)] (/.lambda {.#None})
                           (/.apply_lambda (stack (/.int input))))))
         (_.coverage [/.for_in]
           (expression (|>> (as Integer) (i.= expected))
                       (|> (all /.then
                                (/.; (/.set (stack $output) (/.int +0)))
                                (/.for_in $index (/.array (stack.repeated iterations (/.int input)))
                                          (/.; (/.set (stack $output) (/.+ $index $output))))
                                (/.return $output))
                           [(stack $input)] (/.lambda {.#None})
                           (/.apply_lambda (stack (/.int input))))))
         ..test|label
         )))

(the random_tag
  (Random Integer)
  (random#each (i64.and (hex "FF,FF,FF,FF"))
               random.integer))

(the test|exception
  Test
  (do [! random.monad]
    [expected random.unit_decimal
     dummy (random.only (|>> (d.= expected) not)
                        random.unit_decimal)
     error (random.lower_cased 10)
     $ex (by ! each /.local (random.lower_cased 10))

     expected_tag ..random_tag
     dummy_tag (random.only (|>> (i.= expected_tag) not)
                            ..random_tag)
     .let [expected_tag (/.int expected_tag)
           dummy_tag (/.int dummy_tag)]]
    (all _.and
         (_.coverage [/.begin]
           (expression (|>> (as Decimal) (d.= expected))
                       (|> (/.begin (/.return (/.float expected))
                                    (stack [(stack) $ex (/.return (/.float dummy))]))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.Rescue /.throw/1]
           (expression (|>> (as Decimal) (d.= expected))
                       (|> (/.begin (all /.then
                                         (/.throw/1 (/.string error))
                                         (/.return (/.float dummy)))
                                    (stack [(stack) $ex (/.return (/.float expected))]))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.raise]
           (expression (|>> (as Decimal) (d.= expected))
                       (|> (/.begin (all /.then
                                         (/.; (/.raise (/.string error)))
                                         (/.return (/.float dummy)))
                                    (stack [(stack) $ex (/.return (/.float expected))]))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.catch /.throw/2]
           (and (expression (|>> (as Decimal) (d.= expected))
                            (<| (/.apply_lambda (stack))
                                (/.lambda {.#None}) [(stack)]
                                /.return
                                (/.catch expected_tag) [(stack)]
                                (/.throw/2 expected_tag (/.float expected))))
                (expression (|>> (as Decimal) (d.= expected))
                            (<| (/.apply_lambda (stack))
                                (/.lambda {.#None}) [(stack)]
                                /.return
                                (/.catch expected_tag) [(stack)]
                                /.; (/.catch dummy_tag) [(stack)]
                                (/.throw/2 expected_tag (/.float expected))))
                (expression (|>> (as Decimal) (d.= expected))
                            (<| (/.apply_lambda (stack))
                                (/.lambda {.#None}) [(stack)]
                                /.return
                                (/.catch dummy_tag) [(stack)]
                                /.; (/.catch expected_tag) [(stack)]
                                (/.throw/2 expected_tag (/.float expected))))))
         (_.coverage [/.latest_error_message]
           (expression (|>> (as Text) (text.= error))
                       (|> (/.begin (all /.then
                                         (/.; (/.raise (/.string error)))
                                         (/.return (/.float dummy)))
                                    (stack [(stack) $ex (/.return (/.its "message" /.latest_error_message))]))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.latest_error_location]
           (and (|> (/.return /.latest_error_location)
                    [(stack)] (/.lambda {.#None})
                    (/.apply_lambda (stack))
                    ..nil)
                (expression (|>> (as Bit) (bit.= true))
                            (|> (/.begin (all /.then
                                              (/.; (/.raise (/.string error)))
                                              (/.return (/.float dummy)))
                                         (stack [(stack) $ex (/.return (all /.and
                                                                            (/.do "kind_of?" (stack (is /.CVar (/.manual "Array"))) {.#None} /.latest_error_location)
                                                                            (/.> (/.int +0) (/.its "length" /.latest_error_location))))]))
                                [(stack)] (/.lambda {.#None})
                                (/.apply_lambda (stack))))))
         )))

(the test|function
  Test
  (do [! random.monad]
    [iterations (by ! each (n.% 10) random.natural)
     $self (by ! each /.local (random.lower_cased 1))
     field (random.lower_cased 3)
     $class (by ! each /.local (random.upper_cased 4))

     float/0 random.unit_decimal
     float/1 random.unit_decimal
     float/2 random.unit_decimal
     $arg/0 (by ! each /.local (random.lower_cased 10))
     $arg/1 (by ! each /.local (random.lower_cased 11))
     $arg/2 (by ! each /.local (random.lower_cased 12))]
    (all _.and
         (_.coverage [/.lambda /.return]
           (and (expression (|>> (as Decimal) (d.= float/0))
                            (|> (/.return (/.float float/0))
                                [(stack)] (/.lambda {.#None})
                                (/.apply_lambda (stack))))
                (expression (|>> (as Decimal) d.natural (n.= iterations))
                            (|> (/.return (/.? (/.< (/.int (.integer iterations)) $arg/0)
                                               (/.apply_lambda (stack (/.+ (/.int +1) $arg/0)) $self)
                                               $arg/0))
                                [(stack $arg/0)] (/.lambda {.#Some $self})
                                (/.apply_lambda (stack (/.int +0)))))))
         (_.coverage [/.apply_lambda]
           (expression (|>> (as Decimal) (d.= (all d.+ float/0 float/1 float/2)))
                       (|> (/.return (all /.+ $arg/0 $arg/1 $arg/2))
                           [(stack $arg/0 $arg/1 $arg/2)] (/.lambda {.#None})
                           (/.apply_lambda (stack (/.float float/0) (/.float float/1) (/.float float/2))))))
         (_.coverage [/.function]
           (expression (|>> (as Decimal) d.natural (n.= iterations))
                       (|> (all /.then
                                (/.function $self (stack $arg/0)
                                  (/.return (/.? (/.< (/.int (.integer iterations)) $arg/0)
                                                 (/.apply (stack (/.+ (/.int +1) $arg/0)) {.#None} $self)
                                                 $arg/0)))
                                (/.return (/.apply (stack (/.int +0)) {.#None} $self)))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.apply]
           (expression (|>> (as Decimal) (d.= (all d.+ float/0 float/1 float/2)))
                       (|> (all /.then
                                (/.function $self (stack $arg/0 $arg/1 $arg/2)
                                  (/.return (all /.+ $arg/0 $arg/1 $arg/2)))
                                (/.return (/.apply (stack (/.float float/0) (/.float float/1) (/.float float/2)) {.#None} $self)))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         )))

(the test|branching
  Test
  (do [! random.monad]
    [float/0 random.unit_decimal
     float/1 random.unit_decimal
     float/2 random.unit_decimal
     arg/0 (random.lower_cased 10)
     arg/1 (random.only (|>> (text.= arg/0) not)
                        (random.lower_cased 10))
     arg/2 (random.only (predicate.and (|>> (text.= arg/0) not)
                                       (|>> (text.= arg/1) not))
                        (random.lower_cased 10))
     .let [$arg/0 (/.local arg/0)
           $arg/1 (/.local arg/1)
           $arg/2 (/.local arg/2)]
     ??? random.bit]
    (all _.and
         (_.coverage [/.if]
           (expression (|>> (as Decimal) (d.= (if ??? float/0 float/1)))
                       (|> (/.if (/.bool ???)
                             (/.return (/.float float/0))
                             (/.return (/.float float/1)))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         (_.coverage [/.when]
           (expression (|>> (as Decimal) (d.= (if ??? float/0 float/1)))
                       (|> (all /.then
                                (/.when (/.bool ???)
                                  (/.return (/.float float/0)))
                                (/.return (/.float float/1)))
                           [(stack)] (/.lambda {.#None})
                           (/.apply_lambda (stack)))))
         )))

(the test|statement
  Test
  (do [! random.monad]
    [float/0 random.unit_decimal
     float/1 random.unit_decimal
     float/2 random.unit_decimal
     $arg/0 (by ! each /.local (random.lower_cased 10))
     $arg/1 (by ! each /.local (random.lower_cased 11))
     $arg/2 (by ! each /.local (random.lower_cased 12))
     expected (by ! each (|>> %.integer (text.replaced "+" ""))
                  random.integer)]
    (all _.and
         (_.coverage [/.;]
           (expression (|>> (as Decimal) (d.= float/0))
                       (|> (all /.then
                                (/.; (/.+ $arg/0 $arg/0))
                                (/.return $arg/0))
                           [(stack $arg/0)] (/.lambda {.#None})
                           (/.apply_lambda (stack (/.float float/0))))))
         (_.coverage [/.then]
           (expression (|>> (as Decimal) (d.= float/0))
                       (|> (all /.then
                                (/.return $arg/0)
                                (/.return $arg/1))
                           [(stack $arg/0 $arg/1)] (/.lambda {.#None})
                           (/.apply_lambda (stack (/.float float/0) (/.float float/1))))))
         (_.coverage [/.require/1]
           (let [$JSON (is /.CVar (/.manual "JSON"))]
             (expression (|>> (as Text) (text.= expected))
                         (|> (all /.then
                                  (/.; (/.require/1 (/.string "json")))
                                  (/.return (let [json (/.do "parse" (stack $arg/0) {.#None} $JSON)]
                                              (/.do "generate" (stack json) {.#None} $JSON))))
                             [(stack $arg/0)] (/.lambda {.#None})
                             (/.apply_lambda (stack (/.string expected)))))))
         ..test|exception
         ..test|branching
         ..test|loop
         (_.for [/.Block]
                ..test|function)
         )))

(the random_expression
  (Random /.Expression)
  (let [literal (is (Random /.Literal)
                    (all random.either
                         (random#each /.bool random.bit)
                         (random#each /.float random.decimal)
                         (random#each /.int random.integer)
                         (random#each /.string (random.lower_cased 5))
                         ))]
    (all random.either
         literal
         )))

(the .public test
  Test
  (do [! random.monad]
    [expected ..random_expression]
    (<| (_.covering /._)
        (_.for [/.Code])
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec /.equivalence ..random_expression))
             
             (_.coverage [/.code /.manual]
               (|> (/.manual (/.code expected))
                   (is /.Expression)
                   (/#= expected)))
             (_.for [/.Expression]
                    ..test|expression)
             (_.for [/.Statement]
                    ..test|statement)
             ))))
