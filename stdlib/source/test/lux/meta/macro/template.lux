... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception]]
   [data
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["[0]" nat]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\parser]]
    [macro
     [syntax (.only syntax)]
     ["[0]" expansion]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(/.let [(!pow/2 <scalar>)
        [(nat.* <scalar> <scalar>)]]
  (the pow/2
    (-> Nat Nat)
    (|>> !pow/2)))

(the macro_error
  (syntax (_ [macro <code>.any])
    (function (_ compiler)
      (when ((expansion.complete macro) compiler)
        {try.#Failure error}
        {try.#Success [compiler (list (code.text error))]}
        
        {try.#Success _}
        {try.#Failure "OOPS!"}))))

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [left random.nat
         mid random.nat
         right random.nat]
        (with_expansions [<module> (these [.5 -4 +3 2 #1 #0 c b "a"])
                          <module>' ".5-4+32#1#0cba"
                          <short> (these ["a" b c #0 #1 2 +3 -4 .5])
                          <short>' "abc#0#12+3-4.5"]
          (all _.and
               (_.coverage [/.spliced]
                 (of (list.equivalence nat.equivalence) =
                     (list left mid right)
                     (`` (list (,, (/.spliced [left mid right]))))))
               (_.coverage [/.amount]
                 (when (/.amount [left mid right])
                   3 true
                   _ false))
               (_.coverage [/.text]
                 (when (/.text <short>)
                   <short>' true
                   _ false))
               (_.coverage [/.symbol]
                 (and (when (`` (symbol (,, (/.symbol <short>))))
                        ["" <short>'] true
                        _ false)
                      (when (`` (symbol (,, (/.symbol <module> <short>))))
                        [<module>' <short>'] true
                        _ false)
                      ))
               (_.coverage [/.with_locals]
                 (/.with_locals [var0 var1]
                   (let [var0 left
                         var1 right]
                     (and (nat.= left var0)
                          (nat.= right var1)))))
               (do !
                 [scalar random.nat]
                 (_.coverage [/.let]
                   (let [can_use_with_statements!
                         (nat.= (all nat.* scalar scalar)
                                (..pow/2 scalar))]
                     (and can_use_with_statements!
                          (/.let [(pow/3 <scalar>)
                                  [(all nat.* <scalar> <scalar> <scalar>)]

                                  (pow/9 <scalar>)
                                  [(pow/3 (pow/3 <scalar>))]]
                            (let [can_use_with_expressions!
                                  (nat.= (all nat.* scalar scalar scalar)
                                         (pow/3 scalar))

                                  can_refer!
                                  (nat.= (all nat.*
                                              scalar scalar scalar
                                              scalar scalar scalar
                                              scalar scalar scalar)
                                         (pow/9 scalar))

                                  can_shadow!
                                  (let [pow/3 (function (_ scalar)
                                                (all nat.+ scalar scalar scalar))]
                                    (nat.= (all nat.+ scalar scalar scalar)
                                           (pow/3 scalar)))]
                              (and can_use_with_expressions!
                                   can_refer!
                                   can_shadow!)))
                          ))))
               (_.coverage [/.irregular_arguments]
                 (/.let [(arity/3 <0> <1> <2>)
                         [""]]
                   (exception.is? /.irregular_arguments
                                  (macro_error (arity/3 "a" "b")))))
               )))
      ))
