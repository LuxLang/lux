... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" bit]
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["[0]" natural]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro
     ["[0]" syntax]
     ["[0]" expansion]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(/.let [(!pow/2 <scalar>)
        [(natural.* <scalar> <scalar>)]]
  (the pow/2
    (-> Natural Natural)
    (|>> !pow/2)))

(the macro_error
  (syntax.macro (_ [macro <code>.any])
    (function (_ compiler)
      (when ((expansion.complete macro) compiler)
        {try.#Failure error}
        {try.#Success [compiler (list (code.text error))]}
        
        {try.#Success _}
        {try.#Failure "OOPS!"}))))

(the !n/+
  (/.macro (_ <left> <right>)
    [(natural.+ <left> <right>)]))

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [left random.natural
         mid random.natural
         right random.natural]
        (expansion.let [<module> (these [.5 -4 +3 2 #1 #0 c b "a"])
                        <module>' ".5-4+32#1#0cba"
                        <short> (these ["a" b c #0 #1 2 +3 -4 .5])
                        <short>' "abc#0#12+3-4.5"]
          (all _.and
               (do random.monad
                 [left random.natural
                  right random.natural]
                 (_.coverage [/.macro]
                   (natural.= (natural.+ left right)
                              (!n/+ left right))))
               (_.coverage [/.with]
                 (let [bits (`` (list (,, (/.with [_]
                                            [true]

                                            [0] [1] [2]
                                            ))))]
                   (and (natural.= 3 (list.size bits))
                        (list.every? (bit.= true) bits))))
               (_.coverage [/.spliced]
                 (of (list.equivalence natural.equivalence) =
                     (list left mid right)
                     (`` (list (,, (/.spliced [left mid right]))))))
               (_.coverage [/.amount]
                 (when (/.amount [left mid right])
                   3 true
                   _ false))
               (_.coverage [/.text]
                 (when (/.text <short>)
                   <short>' true
                   _ false))
               (_.coverage [/.symbol]
                 (and (when (`` (symbol (,, (/.symbol <short>))))
                        ["" <short>'] true
                        _ false)
                      (when (`` (symbol (,, (/.symbol <module> <short>))))
                        [<module>' <short>'] true
                        _ false)
                      ))
               (_.coverage [/.with_locals]
                 (/.with_locals [var0 var1]
                   (let [var0 left
                         var1 right]
                     (and (natural.= left var0)
                          (natural.= right var1)))))
               (do !
                 [scalar random.natural]
                 (_.coverage [/.let]
                   (let [can_use_with_statements!
                         (natural.= (all natural.* scalar scalar)
                                    (..pow/2 scalar))]
                     (and can_use_with_statements!
                          (/.let [(pow/3 <scalar>)
                                  [(all natural.* <scalar> <scalar> <scalar>)]

                                  (pow/9 <scalar>)
                                  [(pow/3 (pow/3 <scalar>))]]
                            (let [can_use_with_expressions!
                                  (natural.= (all natural.* scalar scalar scalar)
                                             (pow/3 scalar))

                                  can_refer!
                                  (natural.= (all natural.*
                                                  scalar scalar scalar
                                                  scalar scalar scalar
                                                  scalar scalar scalar)
                                             (pow/9 scalar))

                                  can_shadow!
                                  (let [pow/3 (function (_ scalar)
                                                (all natural.+ scalar scalar scalar))]
                                    (natural.= (all natural.+ scalar scalar scalar)
                                               (pow/3 scalar)))]
                              (and can_use_with_expressions!
                                   can_refer!
                                   can_shadow!)))
                          ))))
               (_.coverage [/.irregular_arguments]
                 (/.let [(arity/3 <0> <1> <2>)
                         [""]]
                   (exception.is? /.irregular_arguments
                                  (macro_error (arity/3 "a" "b")))))
               )))
      ))
