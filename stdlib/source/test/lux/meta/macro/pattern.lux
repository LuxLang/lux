(.require
 [library
  [lux (.except)
   ["_" test (.only Test)]
   [abstract
    [monad (.only do)]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]
     ["i" int]
     ["f" frac]]]
   [meta
    ["[0]" code]]]]
 [\\library
  ["[0]" /]])

(type (Pair l r)
  (Record
   [#left l
    #right r]))

(def !pair
  (template (_ <left> <right>)
    [[..#left <left>
      ..#right <right>]]))

(def .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [expected_nat (at ! each (n.% 1) random.nat)
         expected_int (at ! each (i.% +1) random.int)
         expected_rev (random.either (in .5)
                                     (in .25))
         expected_frac (random.either (in +0.5)
                                      (in +1.25))
         expected_text (random.either (in "+0.5")
                                      (in "+1.25"))]
        (all _.and
             (do [! random.monad]
               [sample (at ! each (n.% 5) random.nat)]
               (_.coverage [/.with_template]
                 (case sample
                   (/.with_template [<case>]
                     [<case> true])
                   ([0] [1] [2] [3] [4])

                   _
                   false)))
             (_.coverage [/.or]
               (and (/.case expected_rev
                      (/.or .5 .25) true
                      _ false)
                    (/.case expected_frac
                      (/.or +0.5 +1.25) true
                      _ false)
                    (/.case expected_text
                      (/.or "+0.5" "+1.25") true
                      _ false)))
             (_.coverage [/.let]
               (let [expected_pair (is (Pair Nat Int)
                                       [..#left expected_nat ..#right expected_int])]
                 (/.case expected_pair
                   (/.let actual_pair (/.pattern (!pair actual_left actual_right)))
                   (and (/.same? expected_pair actual_pair)
                        (/.same? expected_nat actual_left)
                        (/.same? expected_int actual_right)))))
             (_.coverage [/.multi]
               (let [expected_pair (is (Pair Nat Int)
                                       [..#left expected_nat ..#right expected_int])]
                 (and (/.case expected_pair
                        (/.multi (/.pattern (!pair 0 actual_right))
                                 [actual_right
                                  +0])
                        true

                        _
                        false)
                      (/.case expected_pair
                        (/.multi (/.pattern (!pair 0 actual_right))
                                 (i.= +0 actual_right))
                        true

                        _
                        false))))
             (_.coverage [/.|>]
               (case expected_frac
                 (/.|> actual_frac [(f.* +2.0) (f.* +2.0)])
                 (f.= (f.* +4.0 expected_frac)
                      actual_frac)))
             (_.coverage [/.`]
               (case (code.text expected_text)
                 (/.` "+0.5") true
                 (/.` "+1.25") true
                 _ false))
             ))))
