(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [abstract
     [monad (#+ do)]
     [\\specification
      ["$." equivalence]]]
    [data
     ["." text]]
    [math
     ["." random (#+ Random)]]]]
  [\\library
   ["." /]]
  ["$." /// #_
   [macro
    ["#." code]]])

(def: #export random
  (Random Location)
  ($_ random.and
      (random.ascii/alpha 10)
      random.nat
      random.nat
      ))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [.Location])
      ($_ _.and
          (_.for [/.equivalence]
                 ($equivalence.spec /.equivalence ..random))

          (_.cover [/.here]
                   (not (\ /.equivalence = (/.here) (/.here))))
          (do random.monad
            [location ..random
             error (random.ascii/alpha 10)]
            (_.cover [/.format /.with]
                     (let [located_error (/.with location error)]
                       (and (text.contains? (/.format location)
                                            located_error)
                            (text.contains? error
                                            located_error)))))
          (do random.monad
            [[location _] $///code.random]
            (_.cover [/.dummy]
                     (\ /.equivalence = /.dummy location)))
          )))
