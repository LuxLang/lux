(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [equivalence (.only Equivalence)]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["i" int]
     ["[0]" ratio (.use "[1]#[0]" equivalence)]]]
   ["[0]" meta (.only)
    ["[0]" code]
    [macro
     [syntax (.only syntax)]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" //]]])

(def natural
  (syntax (_ [])
    (at meta.monad each
        (|>> code.nat list)
        meta.seed)))

(with_expansions [<from> (..natural)
                  <to> (..natural)]
  (def how (/.scale [ratio.#denominator <from> ratio.#numerator <to>]))
  (def How (/.type how))

  (def how::from <from>)
  (def how::to <to>)
  )

(def .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Scale])
      (do [! random.monad]
        [small (|> random.int
                   (at ! each (i.% +1,000))
                   (at ! each (at //.meter in)))
         large (|> random.int
                   (at ! each (i.% +1,000))
                   (at ! each (i.* +1,000,000,000))
                   (at ! each (at //.meter in)))
         .let [(open "meter#[0]") (is (Equivalence (//.Measure Any //.Meter))
                                      //.equivalence)]
         unscaled (|> random.int
                      (at ! each (i.% +1,000))
                      (at ! each (i.* (.int how::to)))
                      (at ! each (at //.meter in)))]
        (`` (all _.and
                 (,, (with_template [<type> <scale>]
                       [(_.coverage [<type> <scale>]
                          (|> small
                              (at <scale> up)
                              (is (//.Measure <type> //.Meter))
                              (at <scale> down)
                              (is (//.Measure Any //.Meter))
                              (meter#= small)))]

                       [/.Kilo /.kilo]
                       [/.Mega /.mega]
                       [/.Giga /.giga]
                       ))
                 (,, (with_template [<type> <scale>]
                       [(_.coverage [<type> <scale>]
                          (|> large
                              (at <scale> up)
                              (is (//.Measure <type> //.Meter))
                              (at <scale> down)
                              (is (//.Measure Any //.Meter))
                              (meter#= large)))]

                       [/.Milli /.milli]
                       [/.Micro /.micro]
                       [/.Nano /.nano]
                       ))
                 (_.coverage [/.re_scaled]
                   (|> large (is (//.Measure Any //.Meter))
                       (at /.kilo up) (is (//.Measure /.Kilo //.Meter))
                       (/.re_scaled /.kilo /.milli) (is (//.Measure /.Milli //.Meter))
                       (/.re_scaled /.milli /.kilo) (is (//.Measure /.Kilo //.Meter))
                       (at /.kilo down) (is (//.Measure Any //.Meter))
                       (meter#= large)))
                 (_.coverage [/.scale /.type]
                   (and (|> unscaled
                            (at ..how up)
                            (is (//.Measure How //.Meter))
                            (at ..how down)
                            (meter#= unscaled))
                        (ratio#= [ratio.#denominator ..how::from
                                  ratio.#numerator ..how::to]
                                 (at ..how ratio))))
                 )))))
