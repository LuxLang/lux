... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\format (.only format)]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(def .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Dynamic])
      (do random.monad
        [expected random.nat]
        (all _.and
             (_.coverage [/.dynamic /.static]
               (when (/.static Nat (/.dynamic expected))
                 {try.#Success actual}
                 (n.= expected actual)
                 
                 {try.#Failure _}
                 false))
             (_.coverage [/.wrong_type]
               (when (/.static Text (/.dynamic expected))
                 {try.#Success actual}
                 false
                 
                 {try.#Failure error}
                 (exception.match? /.wrong_type error)))
             (_.coverage [/.format]
               (when (/.format (/.dynamic expected))
                 {try.#Success actual}
                 (text#= (%.nat expected) actual)
                 
                 {try.#Failure _}
                 false))
             ))))
