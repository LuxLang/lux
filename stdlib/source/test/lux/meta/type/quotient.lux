... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public (random class super)
  (for_any (_ t c %)
    (-> (/.Class t c %) (Random t)
        (Random (/.Quotient t c %))))
  (of random.monad each (/.quotient class) super))

(the mod_10_class
  (/.class (|>> (n.% 10) %.natural)))

(the Mod_10
  (/.type ..mod_10_class))

(the .public test
  Test
  (<| (_.covering /._)
      (do random.monad
        [modulus (random.only (n.> 0) random.natural)
         .let [class (is (-> Natural Text)
                         (|>> (n.% modulus) %.natural))]
         value random.natural]
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec (/.equivalence text.equivalence)
                                       (..random (/.class class) random.natural)))

             (_.for [/.Class]
                    (_.coverage [/.class]
                      (same? (is Any class)
                             (is Any (/.class class)))))
             (_.for [/.Quotient]
                    (all _.and
                         (_.coverage [/.quotient /.value /.label]
                           (let [quotient (/.quotient (/.class class) value)]
                             (and (same? value
                                         (/.value quotient))
                                  (text.= (class value)
                                          (/.label quotient)))))
                         (_.coverage [/.type]
                           (exec
                             (is ..Mod_10
                                 (/.quotient ..mod_10_class value))
                             true))
                         ))
             ))))
