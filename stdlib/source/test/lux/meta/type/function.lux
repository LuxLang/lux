... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]]
   [meta
    ["[0]" static]
    [macro
     ["[0]" expansion]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" //]]])

(the (general _ _)
  (for_any (_ input output)
    (-> input input
        output))
  (undefined))

(the .public test
  Test
  (<| (_.covering /._)
      (expansion.let [,random (these (static.random //.code
                                                    (all random.either
                                                         (random#in Bit)
                                                         (random#in Natural)
                                                         (random#in Integer)
                                                         (random#in Rev)
                                                         (random#in Decimal)
                                                         (random#in Rev))))
                      ,input ,random
                      ,output ,random])
      (all _.and
           (_.coverage [/.domain]
             (//.= (Tuple ,input ,input)
                   (/.domain ..general [,input ,output])))
           (_.coverage [/.image]
             (//.= ,output
                   (/.image ..general [,input ,output])))
           )
      ))
