... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [math
    ["[0]" random (.only Random)]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" // (.only)
    ["[1][0]" check]]]])

(every Super
  (Ex (_ sub) [Text sub]))

(every Sub
  (Super Bit))

(the .public test
  Test
  (<| (_.covering /._)
      (do random.monad
        [expected random.nat])
      (all _.and
           (_.coverage [/.Co]
             (and (//check.subsumes? (type (/.Co Super)) (type (/.Co Sub)))
                  (not (//check.subsumes? (type (/.Co Sub)) (type (/.Co Super))))))
           (_.coverage [/.Contra]
             (and (//check.subsumes? (type (/.Contra Sub)) (type (/.Contra Super)))
                  (not (//check.subsumes? (type (/.Contra Super)) (type (/.Contra Sub))))))
           (_.coverage [/.In]
             (and (//check.subsumes? (type (/.In Super)) (type (/.In Super)))
                  (//check.subsumes? (type (/.In Sub)) (type (/.In Sub)))
                  (not (//check.subsumes? (type (/.In Sub)) (type (/.In Super))))
                  (not (//check.subsumes? (type (/.In Super)) (type (/.In Sub))))))
           (_.for [/.Mutable]
                  (all _.and
                       (_.coverage [/.write /.read]
                         (|> (/.write expected)
                             (is (/.Mutable I64 Nat))
                             /.read
                             (same? (.i64 expected))))
                       (_.coverage [/.Read_Only /.read_only]
                         (|> (/.write expected)
                             (is (/.Mutable I64 Nat))
                             /.read_only
                             (is (/.Read_Only I64))
                             /.read
                             (same? (.i64 expected))))
                       (_.coverage [/.Write_Only /.write_only]
                         (|> (/.write expected)
                             (is (/.Mutable I64 Nat))
                             /.write_only
                             (is (/.Write_Only Nat))
                             /.read
                             (is Any)
                             (same? (as Any expected))))
                       ))
           )))
