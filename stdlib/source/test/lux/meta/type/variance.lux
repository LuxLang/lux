... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [math
    ["[0]" random (.only Random)]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" // (.only)
    ["[1][0]" check]]]])

(type Super
  (Ex (_ sub) [Text sub]))

(type Sub
  (Super Bit))

(def .public test
  Test
  (<| (_.covering /._)
      (do random.monad
        [expected random.nat])
      (all _.and
           (_.coverage [/.Co]
             (and (//check.subsumes? (//.literal (/.Co Super)) (//.literal (/.Co Sub)))
                  (not (//check.subsumes? (//.literal (/.Co Sub)) (//.literal (/.Co Super))))))
           (_.coverage [/.Contra]
             (and (//check.subsumes? (//.literal (/.Contra Sub)) (//.literal (/.Contra Super)))
                  (not (//check.subsumes? (//.literal (/.Contra Super)) (//.literal (/.Contra Sub))))))
           (_.coverage [/.In]
             (and (//check.subsumes? (//.literal (/.In Super)) (//.literal (/.In Super)))
                  (//check.subsumes? (//.literal (/.In Sub)) (//.literal (/.In Sub)))
                  (not (//check.subsumes? (//.literal (/.In Sub)) (//.literal (/.In Super))))
                  (not (//check.subsumes? (//.literal (/.In Super)) (//.literal (/.In Sub))))))
           (_.for [/.Mutable]
                  (all _.and
                       (_.coverage [/.write /.read]
                         (|> (/.write expected)
                             (is (/.Mutable I64 Nat))
                             /.read
                             (same? (.i64 expected))))
                       (_.coverage [/.Read_Only /.read_only]
                         (|> (/.write expected)
                             (is (/.Mutable I64 Nat))
                             /.read_only
                             (is (/.Read_Only I64))
                             /.read
                             (same? (.i64 expected))))
                       (_.coverage [/.Write_Only /.write_only]
                         (|> (/.write expected)
                             (is (/.Mutable I64 Nat))
                             /.write_only
                             (is (/.Write_Only Nat))
                             /.read
                             (is Any)
                             (same? (as Any expected))))
                       ))
           )))
