... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" text]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro
     ["[0]" syntax]
     ["[0]" expansion]
     ["[0]" template]]]
   [test
    ["_" property (.only Test)]]
   [world
    [time
     ["[0]" instant (.only Instant)]
     ["[0]" duration (.only Duration)]]]]]
 [\\library
  ["[0]" /]])

(/.type .public Mortal
        [@birth Instant
         @life_span (Maybe Duration)])

(/.type Human
        Mortal
        [@name Text])

(/.type (TransHuman id)
        Human
        [@id id])

(the macro_error
  (syntax.macro (_ [macro <code>.any])
    (function (_ compiler)
      (when ((expansion.complete macro) compiler)
        {try.#Failure error}
        {try.#Success [compiler (list (code.text error))]}
        
        {try.#Success _}
        {try.#Failure "OOPS!"}))))

(template.with_locals [<slot>]
  (the repeat_slot_error
    (macro_error
     (/.type Bad_Row
             [<slot> Bit
              <slot> Natural]))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Row
              /.#content /.#extra])
      (do [! random.monad]
        [dummy_birth instant.random
         expected_birth (random.only (|>> (same? dummy_birth) not)
                                     instant.random)
         expected_life_span (random.maybe duration.random)

         dummy_name (random.upper_cased 1)
         expected_name (random.upper_cased 2)

         dummy_id random.natural
         expected_id (random.only (|>> (n.= dummy_id) not) random.natural)])
      (all _.and
           (_.coverage [/.type /.row]
             (exec
               (is Mortal
                   (/.row [@birth expected_birth
                           @life_span expected_life_span]))
               (is Human
                   (/.row [@name expected_name
                           @birth expected_birth
                           @life_span expected_life_span]))
               (is (TransHuman Natural)
                   (/.row [@id expected_id
                           @name expected_name
                           @birth expected_birth
                           @life_span expected_life_span]))
               true))
           (_.coverage [/.cannot_repeat_slot_names]
             (text.contains? (its exception.#label /.cannot_repeat_slot_names)
                             repeat_slot_error))
           (_.coverage [/.missing_level]
             (and (|> (/.row [@name expected_name])
                      macro_error
                      (text.contains? (its exception.#label /.missing_level)))
                  (|> (/.row [@id expected_id])
                      macro_error
                      (text.contains? (its exception.#label /.missing_level)))
                  (|> (/.row [@id expected_id
                              @name expected_name])
                      macro_error
                      (text.contains? (its exception.#label /.missing_level)))
                  (|> (/.row [@id expected_id
                              @birth expected_birth
                              @life_span expected_life_span])
                      macro_error
                      (text.contains? (its exception.#label /.missing_level)))))
           (_.for [/.Slot /.slot]
                  (all _.and
                       (_.coverage [/.its]
                         (and (|> (/.row [@birth expected_birth
                                          @life_span expected_life_span])
                                  (is (Mortal Any))
                                  (/.its @birth)
                                  (same? expected_birth))
                              (|> (/.row [@name expected_name
                                          @birth expected_birth
                                          @life_span expected_life_span])
                                  (is Human)
                                  (/.its @name)
                                  (same? expected_name))
                              (|> (/.row [@id expected_id
                                          @name expected_name
                                          @birth expected_birth
                                          @life_span expected_life_span])
                                  (is (TransHuman Natural))
                                  (/.its @id)
                                  (same? expected_id))))
                       (_.coverage [/.has]
                         (and (|> (/.row [@birth dummy_birth
                                          @life_span expected_life_span])
                                  (is (Mortal Any))
                                  (/.has @birth expected_birth)
                                  (/.its @birth)
                                  (same? expected_birth))
                              (|> (/.row [@name dummy_name
                                          @birth expected_birth
                                          @life_span expected_life_span])
                                  (is (Human Any))
                                  (/.has @name expected_name)
                                  (/.its @name)
                                  (same? expected_name))
                              (|> (/.row [@id dummy_id
                                          @name expected_name
                                          @birth expected_birth
                                          @life_span expected_life_span])
                                  (is (TransHuman Natural Any))
                                  (/.has @id expected_id)
                                  (/.its @id)
                                  (same? expected_id))))
                       (_.coverage [/.revised]
                         (and (|> (/.row [@birth dummy_birth
                                          @life_span expected_life_span])
                                  (is (Mortal Any))
                                  (/.revised @birth (function (_ _) expected_birth))
                                  (/.its @birth)
                                  (same? expected_birth))
                              (|> (/.row [@name dummy_name
                                          @birth expected_birth
                                          @life_span expected_life_span])
                                  (is (Human Any))
                                  (/.revised @name (function (_ _) expected_name))
                                  (/.its @name)
                                  (same? expected_name))
                              (|> (/.row [@id dummy_id
                                          @name expected_name
                                          @birth expected_birth
                                          @life_span expected_life_span])
                                  (is (TransHuman Natural Any))
                                  (/.revised @id (function (_ _) expected_id))
                                  (/.its @id)
                                  (same? expected_id))))
                       ))
           )))
