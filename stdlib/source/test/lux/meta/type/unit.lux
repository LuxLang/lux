(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [equivalence (.only Equivalence)]
    [\\specification
     ["$[0]" equivalence]
     ["$[0]" order]
     ["$[0]" enum]]]
   [data
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\format]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["i" int]]]
   [meta
    ["[0]" static]
    ["[0]" code]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]]
 ["[0]" /
  ["[1][0]" scale]])

(with_template [<name> <type> <unit>]
  [(def (<name> range)
     (-> Nat (Random (/.Measure Any <type>)))
     (|> random.int
         (of random.monad each (i.% (.int range)))
         (random.only (|>> (i.= +0) not))
         (of random.monad each (of <unit> in))))]

  [meter /.Meter /.meter]
  [second /.Second /.second]
  )

(def polymorphism
  Test
  (all _.and
       (_.for [/.equivalence]
              ($equivalence.spec /.equivalence (..meter 1,000)))
       (_.for [/.order]
              ($order.spec /.order (..meter 1,000)))
       (_.for [/.enum]
              ($enum.spec /.enum (..meter 1,000)))
       ))

(with_expansions [<descriptor> (static.random code.text
                                              (random.upper_cased 1))]
  (def what (/.unit <descriptor>))
  (def What (/.type what))

  (def unit
    Test
    (do random.monad
      [expected random.int]
      (_.for [/.Unit]
             (`` (all _.and
                      (,, (with_template [<type> <unit>]
                            [(_.coverage [<type> <unit>]
                               (and (|> expected
                                        (of <unit> in)
                                        (of <unit> out)
                                        (i.= expected))
                                    (let [it (|> expected
                                                 (of <unit> in)
                                                 (of <unit> format))]
                                      (and (not (text#= (%.int expected) it))
                                           (text.starts_with? (%.int expected) it)))))]

                            [/.Gram /.gram]
                            [/.Meter /.meter]
                            [/.Litre /.litre]
                            [/.Second /.second]
                            ))
                      (_.coverage [/.measure /.number]
                        (|> expected
                            /.measure
                            /.number
                            (i.= expected)))
                      (_.coverage [/.unit /.type]
                        (and (|> expected
                                 (of ..what in)
                                 (is (/.Measure Any What))
                                 (of ..what out)
                                 (i.= expected))
                             (let [it (|> expected
                                          (of ..what in)
                                          (of ..what format))]
                               (and (text.contains? (%.int expected) it)
                                    (text.contains? <descriptor> it)))))
                      ))))))

(def arithmetic
  Test
  (do random.monad
    [.let [zero (of /.meter in +0)
           (open "meter#[0]") (is (Equivalence (/.Measure Any /.Meter))
                                  /.equivalence)]
     left (random.only (|>> (meter#= zero) not) (..meter 1,000))
     right (..meter 1,000)
     extra (..second 1,000)]
    (`` (all _.and
             (,, (with_template [<q> <i>]
                   [(_.coverage [<q>]
                      (i.= (<i> (of /.meter out left) (of /.meter out right))
                           (of /.meter out (<q> left right))))]

                   [/.+ i.+]
                   [/.- i.-]
                   ))
             (_.coverage [/.*]
               (let [expected (i.* (of /.meter out left) (of /.meter out right))
                     actual (/.number (is (/.Measure Any [/.Meter /.Meter])
                                          (/.* left right)))]
                 (i.= expected actual)))
             (_.coverage [/./]
               (|> right
                   (/.* left)
                   (/./ left)
                   (meter#= right)))
             ))))

(def .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Measure])
      (all _.and
           ..polymorphism
           ..unit
           ..arithmetic

           /scale.test
           )))
