... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    ["[0]" bit]
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]
    [number
     ["n" nat]]]
   ["[0]" meta (.only)
    ["[0]" static]
    ["[0]" location]
    ["[0]" symbol]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["^" pattern]
   ["[0]" syntax]
   ["[0]" template]
   ["[0]" expansion]]]
 ["[0]" /
  ["[1][0]" context]
  ["[1][0]" expansion]
  ["[1][0]" local]
  ["[1][0]" syntax]
  ["[1][0]" template]
  ["[1][0]" pattern]
  ["[1][0]" vocabulary]])

(the !expect
  (template.macro (_ <pattern> <value>)
    [(when <value>
       <pattern> true
       _ false)]))

(the !global
  (template.macro (_ <definition>)
    [(is [Text [Bit .Global]]
         [(template.text [<definition>]) [true {.#Definition [.Macro <definition>]}]])]))

(the pow/2
  (syntax.macro (_ [number <code>.any])
    (in (list (` (n.* (, number) (, number)))))))

(the pow/4
  (syntax.macro (_ [number <code>.any])
    (in (list (` (..pow/2 (..pow/2 (, number))))))))

(the repeated
  (syntax.macro (_ [times <code>.nat
                    token <code>.any])
    (in (list.repeated times token))))

(the fresh_symbol
  (syntax.macro (_ [])
    (do meta.monad
      [g!fresh (/.symbol "fresh")]
      (in (list g!fresh)))))

(the random_lux
  (Random [Nat Text .Lux])
  (do [! random.monad]
    [seed random.nat
     symbol_prefix (random.upper_cased 1)
     .let [macro_module (symbol.module (symbol expansion._))
           current_module (symbol.module (symbol .._))]]
    (in [seed
         symbol_prefix
         [.#info            [.#target ""
                             .#version ""
                             .#mode {.#Build}
                             .#configuration (list)]
          .#source          [location.dummy 0 ""]
          .#location         location.dummy
          .#current_module  {.#Some current_module}
          .#modules         (list [macro_module
                                   [.#module_hash    0
                                    .#module_aliases (list)
                                    .#definitions    (is (List [Text [Bit .Global]])
                                                         (list (!global expansion.log_single!)
                                                               (!global expansion.log_complete!)
                                                               (!global expansion.log_total!)))
                                    .#imports        (list)
                                    .#module_state   {.#Active}]]
                                  [current_module
                                   [.#module_hash    0
                                    .#module_aliases (list)
                                    .#definitions    (is (List [Text [Bit .Global]])
                                                         (list (!global ..pow/2)
                                                               (!global ..pow/4)
                                                               (!global ..repeated)))
                                    .#imports        (list)
                                    .#module_state   {.#Active}]])
          .#scopes          (list)
          .#type_context    [.#ex_counter 0
                             .#var_counter 0
                             .#var_bindings (list)]
          .#expected        {.#None}
          .#seed            seed
          .#scope_type_vars (list)
          .#extensions      []
          .#eval            (as (-> Type Code (Meta Any)) [])
          .#host            []]])))

(the iterated
  (syntax.macro (_ [cycle <code>.nat
                    it <code>.any])
    (in (list (when cycle
                0 it
                _ (` (..iterated (, (code.nat (-- cycle))) (, it))))))))

(the test|expansion
  Test
  (do [! random.monad]
    [[seed symbol_prefix lux] ..random_lux

     pow/1 (of ! each code.nat random.nat)

     repetitions (of ! each (n.% 10) random.nat)
     .let [single_expansion (` (..pow/2 (..pow/2 (, pow/1))))
           expansion (` (n.* (..pow/2 (, pow/1))
                             (..pow/2 (, pow/1))))
           full_expansion (` (n.* (n.* (, pow/1) (, pow/1))
                                  (n.* (, pow/1) (, pow/1))))]]
    (`` (all _.and
             ... (,, (template.with [<expander> <logger> <expansion>]
             ...       [(_.coverage [<expander>]
             ...          (|> (<expander> (` (..pow/4 (, pow/1))))
             ...              (meta.value lux)
             ...              (try#each (of (list.equivalence code.equivalence) =
             ...                            (list <expansion>)))
             ...              (try.else false)))

             ...        (_.coverage [<logger>]
             ...          (and (|> (expansion.single (` (<logger> "omit" (..pow/4 (, pow/1)))))
             ...                   (meta.value lux)
             ...                   (try#each (of (list.equivalence code.equivalence) = (list)))
             ...                   (try.else false))
             ...               (|> (expansion.single (` (<logger> (..pow/4 (, pow/1)))))
             ...                   (meta.value lux)
             ...                   (try#each (of (list.equivalence code.equivalence) = (list <expansion>)))
             ...                   (try.else false))))]

             ...       [expansion.single   expansion.log_single!   single_expansion]
             ...       [expansion.complete expansion.log_complete! expansion]
             ...       [expansion.total    expansion.log_total!    full_expansion]
             ...       ))
             ... (_.coverage [expansion.one]
             ...   (bit.= (not (n.= 1 repetitions))
             ...          (|> (expansion.one (` (..repeated (, (code.nat repetitions)) (, pow/1))))
             ...              (meta.value lux)
             ...              (!expect {try.#Failure _}))))
             (_.coverage [/.final]
               (expansion.let [<expected> (static.random_nat)
                               <cycles> (static.random code.nat
                                                       (random#each (|>> (n.% 5) ++) random.nat))
                               <actual> (/.final (..iterated <cycles> <expected>))]
                 (when (' <actual>)
                   [_ {.#Nat actual}]
                   (n.= <expected> actual)

                   _
                   false)))
             (_.coverage [/.times]
               (expansion.let [<expected> (static.random_nat)
                               <max> (static.random code.nat
                                                    (random#each (|>> (n.% 10) (n.+ 2)) random.nat))
                               <cycles> (static.random code.nat
                                                       (random#each (|>> (n.% <max>) ++) random.nat))
                               <actual> (/.times <cycles> (..iterated <max> <expected>))]
                 (let [expected_remaining (n.- <cycles> <max>)]
                   (when (` <actual>)
                     (^.` (..iterated (^., [_ {.#Nat actual_remaining}]) (^., [_ {.#Nat actual}])))
                     (and (n.= expected_remaining actual_remaining)
                          (n.= <expected> actual))

                     _
                     false))))
             ))))

(the sum
  (macro (_ tokens)
    (loop (again [tokens tokens
                  output 0])
      (when tokens
        {.#End}
        (of meta.monad in (list (code.nat output)))
        
        {.#Item [_ {.#Nat head}] tail}
        (again tail (n.+ head output))

        _
        (meta.failure "")))))

(the sum'
  (macro (_ tokens lux)
    ((/.function ..sum) tokens lux)))

(the .public test
  Test
  (<| (_.covering /._)
      (all _.and
           (do [! random.monad]
             [[seed symbol_prefix lux] ..random_lux]
             (all _.and
                  (_.coverage [/.symbol]
                    (|> (/.symbol symbol_prefix)
                        (of meta.monad each code.as_text)
                        (meta.value lux)
                        (!expect (^.multi {try.#Success actual_symbol}
                                          (and (text.contains? symbol_prefix actual_symbol)
                                               (text.contains? (%.nat seed) actual_symbol))))))
                  (_.coverage [/.with_symbols]
                    (expansion.let [<expected> (fresh_symbol)]
                      (|> (/.with_symbols [<expected>]
                            (of meta.monad in <expected>))
                          (meta.value lux)
                          (!expect (^.multi {try.#Success [_ {.#Symbol ["" actual]}]}
                                            (text.contains? (template.text [<expected>])
                                                            actual))))))
                  (_.coverage [/.function]
                    (expansion.let [n/0 (static.random_nat)
                                    n/1 (static.random_nat)
                                    n/1 (static.random_nat)]
                      (n.= (..sum n/0 n/1 n/1)
                           (..sum' n/0 n/1 n/1))))
                  (_.coverage [/.macro]
                    (|> ..sum
                        /.function
                        /.macro
                        (is Macro)
                        (same? ..sum)))
                  ))

           ..test|expansion

           /context.test
           /expansion.test
           /local.test
           /syntax.test
           /template.test
           /pattern.test
           /vocabulary.test
           )))
