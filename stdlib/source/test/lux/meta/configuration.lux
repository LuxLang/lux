... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" monoid
     ["[1]T" \\test]]]
   [control
    ["<>" parser]
    ["[0]" try (.use "[1]#[0]" functor)]
    ["[0]" exception]]
   [data
    ["[0]" text (.only)
     ["<[1]>" \\parser]]
    [collection
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]]
   ["[0]" meta (.only)
    ["[0]" code (.only)
     ["<[1]>" \\parser]]
    [macro
     [syntax (.only syntax)]
     ["[0]" expansion]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public (random amount)
  (-> Nat (Random /.Configuration))
  (when amount
    0 (random#in /.empty)
    _ (do [! random.monad]
        [feature (random.upper_cased amount)
         value (random.lower_cased amount)]
        (of ! each (|>> (list.partial [feature value]))
            (random (-- amount))))))

(the failure
  (syntax (_ [it <code>.any])
    (function (_ lux)
      (when (expansion.complete it lux)
        {try.#Failure error}
        {try.#Success [lux (list (code.text error))]}
        
        {try.#Success _}
        {try.#Failure ""}))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Configuration])
      (do [! random.monad]
        [expected (..random 5)]
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec /.equivalence (..random 5)))
             (_.for [/.monoid]
                    (monoidT.spec /.equivalence /.monoid (..random 5)))

             (_.coverage [/.empty]
               (list.empty? /.empty))
             (_.coverage [/.format /.parser]
               (|> expected
                   /.format
                   (<text>.value /.parser)
                   (try#each (of /.equivalence = expected))
                   (try.else false)))
             (_.coverage [/.for]
               (and (and (/.for ["left" "<<<"
                                 "right" ">>>"]
                                true
                                ... else
                                false)
                         (/.for ["left" "<<<"]
                                true
                                ... else
                                false)
                         (/.for ["right" ">>>"]
                                true
                                ... else
                                false))
                    (and (/.for ["yolo" ""]
                                false
                                ... else
                                true)
                         (/.for ["left" "yolo"]
                                false
                                ... else
                                true))))
             (_.coverage [/.invalid]
               (and (text.contains? (its exception.#label /.invalid)
                                    (..failure (/.for)))
                    (text.contains? (its exception.#label /.invalid)
                                    (..failure (/.for ["left" "yolo"]
                                                      ... else
                                                      false)))))
             ))))
