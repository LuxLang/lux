... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except local global)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [control
    ["<>" projection]
    ["[0]" function]
    ["[0]" try (.only Try)]]
   [data
    ["[0]" bit]
    ["[0]" product]
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number
     ["[0]" nat]
     ["[0]" int]
     ["[0]" rev]
     ["[0]" dec]]]
   [meta
    ["[0]" symbol]
    ["[0]" location]
    [macro
     ["^" pattern]
     ["[0]" template]]
    [compiler
     [language
      [lux
       ["[0]" syntax]]]]]
   [test
    ["_" property (.only Test)]]]]
 ["[0]" \\projection]
 [\\library
  ["[0]" /]])

(the !expect
  (template.macro (_ <pattern> <value>)
    [(when <value>
       <pattern>
       true
       
       _
       false)]))

(the local
  (Random Text)
  (random.lower_cased 1))

(the global
  (Random Symbol)
  (all random.and
       (random.lower_cased 1)
       (random.lower_cased 1)
       ))

(the any_symbol
  (Random Symbol)
  (all random.either
       (random#each (|>> [""])
                    ..local)
       ..global
       ))

(the \\projection
  Test
  (<| (_.covering \\projection._)
      (_.for [\\projection.Projection])
      (`` (all _.and
               (do [! random.monad]
                 [expected (of ! each /.bit random.bit)]
                 (_.coverage [\\projection.value]
                   (and (|> (\\projection.value \\projection.any (list expected))
                            (!expect {try.#Success _}))
                        (|> (\\projection.value \\projection.any (list))
                            (!expect {try.#Failure _})))))
               (,, (template.with [<query> <check> <random> <code> <equivalence>]
                     [(do [! random.monad]
                        [expected <random>
                         dummy (|> <random> (random.only (|>> (of <equivalence> = expected) not)))]
                        (all _.and
                             (_.coverage [<query>]
                               (|> (\\projection.value <query> (list (<code> expected)))
                                   (!expect (^.multi {try.#Success actual}
                                                     (of <equivalence> = expected actual)))))
                             (_.coverage [<check>]
                               (and (|> (\\projection.value (<check> expected) (list (<code> expected)))
                                        (!expect {try.#Success []}))
                                    (|> (\\projection.value (<check> expected) (list (<code> dummy)))
                                        (!expect {try.#Failure _}))))
                             ))]

                     [\\projection.any \\projection.this (of ! each /.bit random.bit) function.identity /.equivalence]
                     [\\projection.bit \\projection.this_bit random.bit /.bit bit.equivalence]
                     [\\projection.nat \\projection.this_nat random.nat /.nat nat.equivalence]
                     [\\projection.int \\projection.this_int random.int /.int int.equivalence]
                     [\\projection.rev \\projection.this_rev random.rev /.rev rev.equivalence]
                     [\\projection.dec \\projection.this_dec random.safe_dec /.dec dec.equivalence]
                     [\\projection.text \\projection.this_text (random.unicode 1) /.text text.equivalence]
                     [\\projection.local \\projection.this_local ..local /.local text.equivalence]
                     [\\projection.global \\projection.this_global ..global /.symbol symbol.equivalence]
                     [\\projection.symbol \\projection.this_symbol ..any_symbol /.symbol symbol.equivalence]
                     ))
               (,, (template.with [<query> <code>]
                     [(do [! random.monad]
                        [expected_left random.nat
                         expected_right random.int]
                        (_.coverage [<query>]
                          (|> (\\projection.value (<query> (<>.and \\projection.nat \\projection.int))
                                                  (list (<code> (list (/.nat expected_left)
                                                                      (/.int expected_right)))))
                              (!expect (^.multi {try.#Success [actual_left actual_right]}
                                                (and (of nat.equivalence = expected_left actual_left)
                                                     (of int.equivalence = expected_right actual_right)))))))]

                     [\\projection.form /.form]
                     [\\projection.variant /.variant]
                     [\\projection.tuple /.tuple]
                     ))
               (do [! random.monad]
                 [expected_local random.nat
                  expected_global random.int]
                 (_.coverage [\\projection.locally]
                   (|> (\\projection.value (<>.and (\\projection.locally (list (/.nat expected_local)) \\projection.nat)
                                                   \\projection.int)
                                           (list (/.int expected_global)))
                       (!expect (^.multi {try.#Success [actual_local actual_global]}
                                         (and (of nat.equivalence = expected_local actual_local)
                                              (of int.equivalence = expected_global actual_global)))))))
               (do [! random.monad]
                 [dummy (of ! each /.bit random.bit)]
                 (_.coverage [\\projection.end?]
                   (|> (\\projection.value (do <>.monad
                                             [pre \\projection.end?
                                              _ \\projection.any
                                              post \\projection.end?]
                                             (in (and (not pre)
                                                      post)))
                                           (list dummy))
                       (!expect (^.multi {try.#Success verdict}
                                         verdict)))))
               (do [! random.monad]
                 [dummy (of ! each /.bit random.bit)]
                 (_.coverage [\\projection.end]
                   (and (|> (\\projection.value \\projection.end (list))
                            (!expect {try.#Success []}))
                        (|> (\\projection.value \\projection.end (list dummy))
                            (!expect {try.#Failure _})))))
               (do [! random.monad]
                 [expected (of ! each /.bit random.bit)]
                 (_.coverage [\\projection.next]
                   (|> (\\projection.value (do <>.monad
                                             [pre \\projection.next
                                              post \\projection.any]
                                             (in (and (same? expected pre)
                                                      (same? pre post))))
                                           (list expected))
                       (!expect {try.#Success _}))))
               (do [! random.monad]
                 [expected (of ! each /.bit random.bit)]
                 (_.coverage [\\projection.not]
                   (and (|> (\\projection.value (\\projection.not \\projection.nat) (list expected))
                            (!expect (^.multi {try.#Success actual}
                                              (same? expected actual))))
                        (|> (\\projection.value (\\projection.not \\projection.bit) (list expected))
                            (!expect {try.#Failure _})))))
               ))))

(the random_text
  (Random Text)
  (random.alphabetic 10))

(the random_symbol
  (Random Symbol)
  (random.and ..random_text ..random_text))

(the (random_sequence random)
  (for_any (_ a)
    (-> (Random a)
        (Random (List a))))
  (do [! random.monad]
    [size (|> random.nat (of ! each (nat.% 3)))]
    (random.list size random)))

(the .public random
  (Random Code)
  (random.rec
   (function (_ random)
     (all random.either
          (random#each /.bit random.bit)
          (random#each /.nat random.nat)
          (random#each /.int random.int)
          (random#each /.rev random.rev)
          (random#each /.dec random.safe_dec)
          (random#each /.text ..random_text)
          (random#each /.symbol ..random_symbol)
          (random#each /.form (..random_sequence random))
          (random#each /.variant (..random_sequence random))
          (random#each /.tuple (..random_sequence random))
          ))))

(the (read source_code)
  (-> Text (Try Code))
  (let [parse (syntax.parse ""
                            syntax.no_aliases
                            (text.size source_code))
        start (is Source
                  [location.dummy 0 source_code])]
    (when (parse start)
      {.#Left [end error]}
      {try.#Failure error}
      
      {.#Right [end lux_code]}
      {try.#Success lux_code})))

(the (replacement_simulation [original substitute])
  (-> [Code Code] (Random [Code Code]))
  (random.rec
   (function (_ replacement_simulation)
     (let [for_sequence (is (-> (-> (List Code) Code) (Random [Code Code]))
                            (function (_ to_code)
                              (random.only (|>> product.left (of /.equivalence = original) not)
                                           (do [! random.monad]
                                             [parts (..random_sequence replacement_simulation)]
                                             (in [(to_code (list#each product.left parts))
                                                  (to_code (list#each product.right parts))])))))]
       (all random.either
            (random#in [original substitute])
            (do [! random.monad]
              [sample (random.only (|>> (of /.equivalence = original) not)
                                   (all random.either
                                        (random#each /.bit random.bit)
                                        (random#each /.nat random.nat)
                                        (random#each /.int random.int)
                                        (random#each /.rev random.rev)
                                        (random#each /.dec random.safe_dec)
                                        (random#each /.text ..random_text)
                                        (random#each /.symbol ..random_symbol)))]
              (in [sample sample]))
            (for_sequence /.form)
            (for_sequence /.variant)
            (for_sequence /.tuple)
            )))))

(the for_format
  Test
  (`` (all _.and
           (,, (template.with [<coverage> <random> <tag>]
                 [(do [! random.monad]
                    [expected <random>]
                    (_.coverage [<coverage>]
                      (and (when (..read (/.absolute (<coverage> expected)))
                             {try.#Success actual}
                             (of /.equivalence =
                                 actual
                                 (<coverage> expected))
                             
                             {try.#Failure error}
                             false)
                           (of /.equivalence =
                               [location.dummy {<tag> expected}]
                               (<coverage> expected)))))]

                 [/.bit random.bit .#Bit]
                 [/.nat random.nat .#Nat]
                 [/.int random.int .#Int]
                 [/.rev random.rev .#Rev]
                 [/.dec random.safe_dec .#Dec]
                 [/.text ..random_text .#Text]
                 [/.symbol ..random_symbol .#Symbol]
                 [/.form (..random_sequence ..random) .#Form]
                 [/.variant (..random_sequence ..random) .#Variant]
                 [/.tuple (..random_sequence ..random) .#Tuple]))
           (,, (template.with [<coverage> <random> <tag>]
                 [(do [! random.monad]
                    [expected <random>]
                    (_.coverage [<coverage>]
                      (and (when (..read (/.absolute (<coverage> expected)))
                             {try.#Success actual}
                             (of /.equivalence =
                                 actual
                                 (<coverage> expected))
                             
                             {try.#Failure error}
                             false)
                           (of /.equivalence =
                               [location.dummy {<tag> ["" expected]}]
                               (<coverage> expected)))
                      ))]

                 [/.local ..random_text .#Symbol]
                 )))))

(the .public test
  Test
  (<| (_.covering /._)
      (all _.and
           (_.for [/.equivalence]
                  (equivalenceT.spec /.equivalence ..random))
           
           (_.for [/.absolute]
                  ..for_format)
           
           (do [! random.monad]
             [[original substitute] (random.only (function (_ [original substitute])
                                                   (not (of /.equivalence = original substitute)))
                                                 (random.and ..random ..random))
              [sample expected] (random.only (function (_ [sample expected])
                                               (not (of /.equivalence = sample expected)))
                                             (..replacement_simulation [original substitute]))]
             (_.coverage [/.replaced]
               (of /.equivalence =
                   expected
                   (/.replaced original substitute sample))))

           ..\\projection
           )))
