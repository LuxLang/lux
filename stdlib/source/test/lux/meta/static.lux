... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" mix)]]]
   [logic
    ["[0]" bit]]
   [math
    ["[0]" random (.use "[1]#[0]" functor)]
    [number
     [/64
      ["n" natural]
      ["i" integer]
      ["r" revolution]
      ["d" decimal]]]]
   [macro
    ["[0]" expansion]
    ["[0]" template]]
   ["[0]" meta (.use "[1]#[0]" monad)
    ["[0]" code]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [meta.eval])
      (`` (all _.and
               (,, (template.with [<static> <random> <=> <+> <tag>]
                     [(_.coverage [<static> <random>]
                        (expansion.let [<left> (<random>)
                                        <right> (<random>)
                                        <l+r> (<static> (<+> <left> <right>))]
                          (when (' <l+r>)
                            {<tag> _ l+r}
                            (<=> l+r (<+> <left> <right>))

                            _
                            false)))]

                     [[/.bit /.random_bit bit.= and .#Bit]
                      [/.natural /.random_natural n.= n.+ .#Natural]
                      [/.integer /.random_integer i.= i.+ .#Integer]
                      [/.revolution /.random_revolution r.= r.+ .#Revolution]]))
               (_.coverage [/.decimal /.random_decimal]
                 (expansion.let [<left> (/.random_decimal)
                                 <right> (/.random_decimal)
                                 <l+r> (/.decimal (d.+ <left> <right>))]
                   (when (' <l+r>)
                     {.#Decimal _ l+r}
                     (or (d.= l+r (d.+ <left> <right>))
                         (and (d.not_a_number? l+r)
                              (d.not_a_number? (d.+ <left> <right>))
                              (or (d.not_a_number? <left>)
                                  (d.not_a_number? <right>))))

                     _
                     false)))
               (_.coverage [/.text /.random]
                 (expansion.let [<left> (/.random code.text (random.alpha_numeric 1))
                                 <right> (/.random code.text (random.alpha_numeric 1))
                                 <l+r> (/.text (text <left> <right>))]
                   (when (' <l+r>)
                     {.#Text _ l+r}
                     (text.= l+r (text <left> <right>))

                     _
                     false)))
               (_.coverage [/.randoms]
                 (expansion.let [<amount> (/.random code.natural
                                                    (random#each (|>> (n.% 10) ++) random.natural))
                                 l/* (/.randoms code.natural (random.list <amount> random.natural))]
                   (and (n.= <amount> (list.size (list l/*)))
                        (n.= (list#mix n.+ 0 (list l/*))
                             (all n.+ l/*)))))
               (_.coverage [/.literal]
                 (expansion.let [<left> (/.random code.text (random.alpha_numeric 1))
                                 <right> (/.random code.text (random.alpha_numeric 1))
                                 <l+r> (/.literal code.text (text <left> <right>))]
                   (when (' <l+r>)
                     {.#Text _ l+r}
                     (text.= l+r (text <left> <right>))

                     _
                     false)))
               (_.coverage [/.literals]
                 (expansion.let [l/0 (/.random_natural)
                                 l/1 (/.random_natural)
                                 l/2 (/.random_natural)
                                 l/* (/.literals code.natural (list l/0 l/1 l/2))]
                   (n.= (all n.+ l/0 l/1 l/2)
                        (all n.+ l/*))))
               (_.coverage [/.if]
                 (expansion.let [<?> (/.random_bit)
                                 <then> (/.random_natural)
                                 <else> (/.random_natural)]
                   (n.= (if <?> <then> <else>)
                        (/.if <?> <then> <else>))))
               (_.coverage [/.when]
                 (expansion.let [<0> (/.random_natural)
                                 <1> (/.random_natural)
                                 <2> (/.random_natural)]
                   (and (n.= (all n.+ <0> <1>)
                             (`` (all n.+ <0> <1> (,, (/.when false <2>)))))
                        (n.= (all n.+ <0> <1> <2>)
                             (`` (all n.+ <0> <1> (,, (/.when true <2>))))))))
               (_.coverage [/.seed]
                 (not (n.= (/.seed)
                           (/.seed))))
               (_.coverage [/.expansion]
                 (expansion.let [,expected (/.random_natural)]
                   (n.= ,expected
                        (/.expansion (meta#pure (list (code.natural ,expected)))))))
               ))))
