... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" mix)]]]
   [math
    ["[0]" random (.use "[1]#[0]" functor)]
    [number
     ["n" nat]
     ["i" int]
     ["r" rev]
     ["d" dec]]]
   ["[0]" meta (.only)
    ["[0]" code]
    [macro
     ["[0]" expansion]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(the .public test
  Test
  (<| (_.covering /._)
      (for .old (_.test "PLACEHOLDER" true))
      (_.for [meta.eval])
      (`` (all _.and
               (,, (with_template [<static> <random> <=> <+> <tag>]
                     [(_.coverage [<static> <random>]
                        (expansion.let [<left> (<random>)
                                        <right> (<random>)
                                        <l+r> (<static> (<+> <left> <right>))]
                          (when (' <l+r>)
                            [_ {<tag> l+r}]
                            (<=> l+r (<+> <left> <right>))

                            _
                            false)))]

                     [/.bit /.random_bit bit#= and .#Bit]
                     [/.nat /.random_nat n.= n.+ .#Nat]
                     [/.int /.random_int i.= i.+ .#Int]
                     [/.rev /.random_rev r.= r.+ .#Rev]
                     ))
               (_.coverage [/.dec /.random_dec]
                 (expansion.let [<left> (/.random_dec)
                                 <right> (/.random_dec)
                                 <l+r> (/.dec (d.+ <left> <right>))]
                   (when (' <l+r>)
                     [_ {.#Dec l+r}]
                     (or (d.= l+r (d.+ <left> <right>))
                         (and (d.not_a_number? l+r)
                              (d.not_a_number? (d.+ <left> <right>))
                              (or (d.not_a_number? <left>)
                                  (d.not_a_number? <right>))))

                     _
                     false)))
               (_.coverage [/.text /.random]
                 (expansion.let [<left> (/.random code.text (random.alpha_numeric 1))
                                 <right> (/.random code.text (random.alpha_numeric 1))
                                 <l+r> (/.text (%.message <left> <right>))]
                   (when (' <l+r>)
                     [_ {.#Text l+r}]
                     (text#= l+r (%.message <left> <right>))

                     _
                     false)))
               (_.coverage [/.randoms]
                 (expansion.let [<amount> (/.random code.nat
                                                    (random#each (|>> (n.% 10) ++) random.nat))
                                 l/* (/.randoms code.nat (random.list <amount> random.nat))]
                   (and (n.= <amount> (list.size (list l/*)))
                        (n.= (list#mix n.+ 0 (list l/*))
                             (all n.+ l/*)))))
               (_.coverage [/.literal]
                 (expansion.let [<left> (/.random code.text (random.alpha_numeric 1))
                                 <right> (/.random code.text (random.alpha_numeric 1))
                                 <l+r> (/.literal code.text (%.message <left> <right>))]
                   (when (' <l+r>)
                     [_ {.#Text l+r}]
                     (text#= l+r (%.message <left> <right>))

                     _
                     false)))
               (_.coverage [/.literals]
                 (expansion.let [l/0 (/.random_nat)
                                 l/1 (/.random_nat)
                                 l/2 (/.random_nat)
                                 l/* (/.literals code.nat (list l/0 l/1 l/2))]
                   (n.= (all n.+ l/0 l/1 l/2)
                        (all n.+ l/*))))
               (_.coverage [/.if]
                 (expansion.let [<?> (/.random_bit)
                                 <then> (/.random_nat)
                                 <else> (/.random_nat)]
                   (n.= (if <?> <then> <else>)
                        (/.if <?> <then> <else>))))
               (_.coverage [/.cond]
                 (expansion.let [<?> (/.random_bit)
                                 <then> (/.random_nat)
                                 <else> (/.random_nat)
                                 <never> (/.random_dec)]
                   (n.= (if <?> <then> <else>)
                        (/.cond <?> <then>
                                (not <?>) <else>
                                ... never
                                <never>))))
               (_.coverage [/.when]
                 (expansion.let [<0> (/.random_nat)
                                 <1> (/.random_nat)
                                 <2> (/.random_nat)]
                   (and (n.= (all n.+ <0> <1>)
                             (`` (all n.+ <0> <1> (,, (/.when false <2>)))))
                        (n.= (all n.+ <0> <1> <2>)
                             (`` (all n.+ <0> <1> (,, (/.when true <2>))))))))
               (_.coverage [/.seed]
                 (not (n.= (/.seed)
                           (/.seed))))
               ))))
