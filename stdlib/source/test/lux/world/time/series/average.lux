... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]
     ["d" dec]
     ["[0]" i64]]]
   [world
    [time
     ["[0]" instant (.use "[1]#[0]" order)]
     ["[0]" duration]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" //]]])

(the (series events)
  (-> Nat
      (Random (//.Series Dec)))
  (do [! random.monad]
    [.let [duration (random.only duration.positive? duration.random)]
     offset (of ! each (duration.framed (duration.up 100 duration.normal_year))
                duration)
     .let [start (instant.after offset instant.epoch)]
     interval (of ! each (duration.framed duration.week)
                  duration)
     data (random.sequence events random.safe_dec)]
    (in [//.#start start
         //.#interval interval
         //.#data data])))

(the (well_windowed? input additional output)
  (for_any (_ input output)
    (-> (//.Series input) Nat (//.Series output)
        Bit))
  (let [correct_start!
        (instant#< (//.start output)
                   (//.start input))

        correct_size!
        (n.= (n.- (++ additional) (//.size input))
             (//.size output))]
    (and correct_start!
         correct_size!)))

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [.let [minimum (i64.left_shifted 7 1)
               additional (i64.left_shifted 1 minimum)]
         expected_events (of ! each
                             (|>> (n.% additional) (n.+ minimum))
                             random.nat)
         input (series expected_events)
         additional (of ! each (n.% expected_events) random.nat)])
      (all _.and
           (_.coverage [/.cumulative]
             (let [output (/.cumulative input)]
               (and (instant.= (//.start input)
                               (//.start output))
                    (n.= (//.size input)
                         (//.size output)))))
           (_.coverage [/.windows]
             (<| (try.else false)
                 (do try.monad
                   [output (/.windows additional input)]
                   (in (well_windowed? input additional output)))))
           (_.coverage [/.window_size_is_too_large]
             (when (/.windows (++ expected_events) input)
               {try.#Failure error}
               (exception.is? /.window_size_is_too_large error)

               {try.#Success _}
               false))
           (<| (_.for [/.Average])
               (all _.and
                    (_.coverage [/.exponential /.simple /.weighted

                                 /.Factor /.simple_factor]
                      (let [exponential (/.exponential /.simple_factor input)
                            simple (/.simple input)
                            weighted (/.weighted input)]
                        (and (not (d.= exponential simple))
                             (not (d.= exponential weighted))
                             (not (d.= simple weighted)))))
                    (_.coverage [/.moving]
                      (<| (try.else false)
                          (do try.monad
                            [exponential (/.moving (/.exponential /.simple_factor)
                                                   additional
                                                   input)
                             simple (/.moving /.simple
                                              additional
                                              input)
                             weighted (/.moving /.weighted
                                                additional
                                                input)
                             .let [(open "//#[0]") (//.equivalence d.equivalence)

                                   all_are_well_windowed!
                                   (and (well_windowed? input additional exponential)
                                        (well_windowed? input additional simple)
                                        (well_windowed? input additional weighted))

                                   all_are_different!
                                   (not (or (//#= exponential simple)
                                            (//#= exponential weighted)
                                            (//#= simple weighted)))]]
                            (in (and all_are_well_windowed!
                                     all_are_different!)))))
                    ))
           )))
