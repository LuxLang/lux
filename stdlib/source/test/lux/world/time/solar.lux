... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]
    ["[0]" exception]
    ["[0]" io]]
   [data
    ["[0]" bit (.use "[1]#[0]" equivalence)]
    ["[0]" text (.use "[1]#[0]" equivalence)]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]
     ["i" int]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" duration]
    ["[0]" instant]]]])

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Instant])
      (do random.monad
        [_ (in [])])
      (all _.and
           (_.for [/.Zone]
                  (_.coverage [/.local_zone]
                    (io.value (do io.monad
                                [zone /.local_zone]
                                (in (for .lua
                                         ... Lua doesn't natively support getting the time-zone ID.
                                         (text.empty? zone)
                                         
                                         ... else
                                         (not (text.empty? zone))))))))
           (_.coverage [/.now /.zone]
             (io.value (do io.monad
                         [zone /.local_zone
                          it /.now]
                         (in (text#= zone (/.zone it))))))
           (_.coverage [/.injection /.universal /.offset]
             (io.value (do io.monad
                         [zone /.local_zone
                          it /.now]
                         (in (text.starts_with? (instant.as_text (instant.after (/.offset it) (/.universal it)))
                                                (/.injection it))))))
           )))
