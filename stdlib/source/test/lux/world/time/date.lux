... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" enum
     ["[1]T" \\test]]
    ["[0]" format
     ["[1]T" \\test]]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" order
     ["[1]T" \\test]]]
   [control
    ["[0]" try (.use "[1]#[0]" functor)]
    ["[0]" exception]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]
     ["?[1]" \\projection]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]
     ["i" integer]]]
   [meta
    ["[0]" static]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" instant]]]])

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Date])
      (all _.and
           (_.for [/.equivalence /.=]
                  (static.when (same? /.equivalence /.=) 
                    (equivalenceT.spec /.equivalence /.random)))
           (_.for [/.order /.< /.<= /.> /.>=]
                  (orderT.spec /.order /.random))
           (_.for [/.enum]
                  (enumT.spec /.enum /.random))
           (_.for [/.format]
                  (formatT.spec /.equivalence /.format /.random))

           (do random.monad
             [expected /.random]
             (_.coverage [/.date /.year /.month /.day_of_month]
               (|> (/.date (/.year expected)
                           (/.month expected)
                           (/.day_of_month expected))
                   (try#each (of /.equivalence = expected))
                   (try.else false))))
           (do random.monad
             [expected /.random]
             (_.coverage [/.invalid_day]
               (when (/.date (/.year expected)
                             (/.month expected)
                             (n.+ 31 (/.day_of_month expected)))
                 {try.#Failure error}
                 (exception.is? /.invalid_day error)
                 
                 {try.#Success _}
                 false)))
           (do random.monad
             [expected /.random]
             (_.coverage [/.as_days /.of_days]
               (|> expected
                   /.as_days
                   /.of_days
                   (of /.equivalence = expected))))
           (_.coverage [/.epoch]
             (|> /.epoch
                 /.as_days
                 (i.= +0)))
           (do [! random.monad]
             [year (of ! each (|>> (n.% 10,000) ++)
                       random.natural)
              month (of ! each (|>> (n.% 10) (n.+ 13))
                        random.natural)
              day (of ! each (|>> (n.% 10) (n.+ 10))
                      random.natural)
              .let [input (%.message (%.natural year)
                                     "-" (%.natural month)
                                     "-" (%.natural day))]]
             (_.coverage [/.invalid_month]
               (when (?text.value /.projection input)
                 {try.#Failure error}
                 (exception.is? /.invalid_month error)
                 
                 {try.#Success _}
                 false)))
           (do random.monad
             [expected /.random]
             (_.coverage [/.as_text /.projection]
               (|> expected
                   /.as_text
                   (?text.value /.projection)
                   (try#each (of /.equivalence = expected))
                   (try.else false))))
           (do random.monad
             [expected /.random
              actual (random.only (|>> (of /.equivalence = expected) not)
                                  /.random)]
             (_.coverage [/.random]
               (not (of /.equivalence = expected actual))))
           )))
