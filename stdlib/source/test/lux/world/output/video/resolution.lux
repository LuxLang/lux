... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]
    ["[0]" hash
     ["[1]T" \\test]]
    ["[0]" equality
     ["[1]T" \\test]]]
   [data
    ["[0]" maybe]
    [collection
     ["[0]" list]
     ["[0]" set (.only Set)]]]
   [math
    ["[0]" random (.only Random)]
    [number
     [/64
      ["n" natural]]]]
   [macro
    ["[0]" expansion]
    ["[0]" template]]
   [meta
    ["[0]" static]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" /]])

(expansion.let [<resolutions> (these /.svga
                                     /.wsvga
                                     /.xga
                                     /.xga+
                                     /.wxga_16:9
                                     /.wxga_5:3
                                     /.wxga_16:10
                                     /.sxga
                                     /.wxga+
                                     /.hd+
                                     /.wsxga+
                                     /.fhd
                                     /.wuxga
                                     /.wqhd
                                     /.uhd_4k)]
  (these (the listing
           (List /.Resolution)
           (list <resolutions>))

         (the catalogue
           (Set /.Resolution)
           (set.of_list /.hash ..listing))

         (the .public random
           (Random /.Resolution)
           (let [count (list.size ..listing)]
             (monad.let [! random.monad]
               [choice (by ! each (n.% count) random.natural)]
               (pure (maybe.trusted (list.item choice ..listing))))))

         (the .public test
           Test
           (<| (_.covering /._)
               (_.for [/.Resolution])
               (`` (all _.and
                        (_.for [/.equality /.=]
                               (static.when (identical? /.equality /.=)
                                 (equalityT.spec /.equality ..random)))
                        (_.for [/.hash]
                               (hashT.spec /.hash ..random))

                        (_.coverage [<resolutions>]
                          (n.= (list.size ..listing)
                               (set.size ..catalogue)))
                        (,, (template.with [<slot>]
                              [(_.coverage [<slot>]
                                 (list.every? (|>> (its <slot>) (n.> 0)) ..listing))]

                              [[/.:width]
                               [/.:height]]))
                        ))))))
