(.using
 [library
  [lux "*"
   ["_" test (.only Test)]
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" maybe]]
   [data
    ["[0]" bit ("[1]#[0]" equivalence)]
    [collection
     ["[0]" list]
     ["[0]" set (.only Set)]]]
   [macro
    ["[0]" template]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]]]
 [\\library
  ["[0]" /]])

(with_expansions [<groups> (these [keys/commands
                                   [/.back_space
                                    /.enter
                                    /.shift
                                    /.control
                                    /.alt
                                    /.caps_lock
                                    /.escape
                                    /.space
                                    /.page_up
                                    /.page_down
                                    /.end
                                    /.home
                                    /.delete
                                    /.num_lock
                                    /.scroll_lock
                                    /.print_screen
                                    /.insert
                                    /.windows

                                    /.left
                                    /.up
                                    /.right
                                    /.down]]
                                  
                                  [keys/letters
                                   [/.a
                                    /.b
                                    /.c
                                    /.d
                                    /.e
                                    /.f
                                    /.g
                                    /.h
                                    /.i
                                    /.j
                                    /.k
                                    /.l
                                    /.m
                                    /.n
                                    /.o
                                    /.p
                                    /.q
                                    /.r
                                    /.s
                                    /.t
                                    /.u
                                    /.v
                                    /.w
                                    /.x
                                    /.y
                                    /.z]]
                                  
                                  [keys/num_pad
                                   [/.num_pad_0
                                    /.num_pad_1
                                    /.num_pad_2
                                    /.num_pad_3
                                    /.num_pad_4
                                    /.num_pad_5
                                    /.num_pad_6
                                    /.num_pad_7
                                    /.num_pad_8
                                    /.num_pad_9]]

                                  [keys/functions
                                   [/.f1
                                    /.f2
                                    /.f3
                                    /.f4
                                    /.f5
                                    /.f6
                                    /.f7
                                    /.f8
                                    /.f9
                                    /.f10
                                    /.f11
                                    /.f12
                                    /.f13
                                    /.f14
                                    /.f15
                                    /.f16
                                    /.f17
                                    /.f18
                                    /.f19
                                    /.f20
                                    /.f21
                                    /.f22
                                    /.f23
                                    /.f24]])]
  (def: listing
    (List /.Key)
    (list.together (`` (list (~~ (template [<definition> <keys>]
                                   [((is (-> Any (List /.Key))
                                         (function (_ _)
                                           (`` (list (~~ (template.spliced <keys>))))))
                                     [])]
                                   
                                   <groups>))))))

  (def: catalogue
    (Set /.Key)
    (set.of_list n.hash ..listing))

  (def: verdict
    (n.= (list.size ..listing)
         (set.size ..catalogue)))

  (template [<definition> <keys>]
    [(def: <definition>
       Test
       (_.coverage <keys>
         ..verdict))]
    
    <groups>)

  (def: .public random
    (Random /.Key)
    (let [count (list.size ..listing)]
      (do [! random.monad]
        [choice (# ! each (n.% count) random.nat)]
        (in (maybe.trusted (list.item choice ..listing))))))

  (def: .public test
    Test
    (<| (_.covering /._)
        (_.for [/.Key])
        (`` (all _.and
                 (~~ (template [<definition> <keys>]
                       [<definition>]
                       
                       <groups>))

                 (_.for [/.Press]
                        (`` (all _.and
                                 (~~ (template [<pressed?> <function>]
                                       [(do random.monad
                                          [key ..random
                                           .let [sample (<function> key)]]
                                          (_.coverage [<function>]
                                            (and (bit#= <pressed?> (the /.#pressed? sample))
                                                 (n.= key (the /.#input sample)))))]

                                       [#0 /.release]
                                       [#1 /.press]
                                       ))
                                 )))
                 )))))
