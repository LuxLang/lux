... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["//" projection (.use "[1]#[0]" monad)]
    ["[0]" pipe]
    ["[0]" io]
    ["[0]" maybe (.use "[1]#[0]" functor)]
    ["[0]" try]
    ["[0]" exception]
    [concurrency
     ["[0]" async (.only Async)]]]
   [data
    ["[0]" text]
    [collection
     ["[0]" dictionary]
     ["[0]" list]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]
    ["[0]" unit]]]]
 ["[0]" \\projection (.only Environment)]
 [\\library
  ["[0]" / (.only)
   [//
    [file (.only Path)]]]])

(the .public (spec subject)
  (-> (/.Environment Async)
      Test)
  (do random.monad
    [exit random.int]
    (in (do [! async.monad]
          [environment (/.environment ! subject)]
          (unit.coverage [/.Environment

                          /.available_variables /.variable /.home /.directory /.exit]
            (and (not (dictionary.empty? environment))
                 (list.every? (|>> text.empty? not)
                              (dictionary.keys environment))
                 (not (text.empty? (of subject home)))
                 (not (text.empty? (of subject directory)))))))))

(the \\projection
  Test
  (<| (_.covering \\projection._)
      (_.for [\\projection.Environment \\projection.Projection])
      (all _.and
           (_.coverage [\\projection.empty]
             (dictionary.empty? \\projection.empty))
           (do random.monad
             [expected random.nat]
             (_.coverage [\\projection.value]
               (|> (\\projection.value (//#in expected) \\projection.empty)
                   (of try.functor each (n.= expected))
                   (try.else false))))
           (do random.monad
             [property (random.alphabetic 1)
              expected (random.alphabetic 1)]
             (_.coverage [\\projection.Property \\projection.property]
               (|> \\projection.empty
                   (dictionary.has property expected)
                   (\\projection.value (\\projection.property property))
                   (of try.functor each (text.= expected))
                   (try.else false))))
           (do random.monad
             [property (random.alphabetic 1)]
             (_.coverage [\\projection.unknown_property]
               (when (\\projection.value (\\projection.property property) \\projection.empty)
                 {try.#Success _}
                 false
                 
                 {try.#Failure error}
                 (exception.is? \\projection.unknown_property error))))
           )))

(the (environment env_size)
  (-> Nat (Random Environment))
  (random.dictionary text.hash env_size
                     (random.alphabetic 5)
                     (random.alphabetic 5)))

(the path
  (Random Path)
  (random.alphabetic 5))

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [env_size (of ! each (|>> (n.% 10) ++) random.nat)
         environment (..environment env_size)
         home ..path
         directory ..path

         unknown (random.alphabetic 1)]
        (all _.and
             (_.for [/.mock /.async]
                    (..spec (/.async (/.mock environment home directory))))
             (_.coverage [/.environment]
               (let [it (/.mock environment home directory)]
                 (io.value
                  (do io.monad
                    [actual (/.environment io.monad it)]
                    (in (and (n.= (dictionary.size environment)
                                  (dictionary.size actual))
                             (|> actual
                                 dictionary.entries
                                 (list.every? (function (_ [key value])
                                                (|> environment
                                                    (dictionary.value key)
                                                    (maybe#each (text.= value))
                                                    (maybe.else false)))))))))))
             (_.coverage [/.unknown_environment_variable]
               (let [it (/.mock environment home directory)]
                 (|> unknown
                     (of it variable)
                     io.value
                     (pipe.when {try.#Success _}
                       false
                       
                       {try.#Failure error}
                       (exception.is? /.unknown_environment_variable error)))))

             ..\\projection
             ))))
