... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["|" pipe]
    ["[0]" pure (.only Pure)]]
   [error
    ["[0]" try]]
   [data
    ["[0]" binary]
    [text
     [encoding
      ["[0]" utf8 (.use "[1]#[0]" binary)]]]
    [format
     ["[0]" json (.use "[1]#[0]" format)
      ["[1]T" \\test]]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [macro
    ["[0]" template]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   ["/[1]" // (.only)
    ["[0]" header]
    [//
     ["[0]" mime]
     [uri
      ["[0]" scheme]
      ["[0]" query (.use "[1]#[0]" text)
       ["[1]T" \\test]]]]]]])

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [expected_text (random.upper_cased 3)
         expected_json jsonT.random
         expected_form (queryT.random 2)
         expected_uri (random.upper_cased 4)
         expected_content_length random.natural])
      (_.for [/.Request
              /.#identification /.#message /.#protocol /.#resource])
      (`` (all _.and
               (_.coverage [/.Identification
                            /.#local /.#remote]
                 true)
               (_.coverage [/.Protocol
                            /.#version /.#scheme]
                 true)
               (_.coverage [/.Resource
                            /.#method /.#uri]
                 true)
               (_.coverage [/.utf8 /.text]
                 (and (same? /.utf8 /.text)
                      (let [it (/.utf8 pure.monad expected_text)]
                        (and (|> it
                                 (its [/.#message //.#headers])
                                 (header.one header.content_type)
                                 (|.when
                                   {try.#Success actual}
                                   (mime.= mime.utf_8 actual)
                                   
                                   {try.#Failure error}
                                   false))
                             (when ((its [/.#message //.#body] it) {.#None})
                               {try.#Success [_ actual]}
                               (binary.= (utf8#as expected_text)
                                         actual)
                               
                               {try.#Failure error}
                               false)
                             ))))
               (_.coverage [/.json]
                 (let [it (/.json pure.monad expected_json)]
                   (and (|> it
                            (its [/.#message //.#headers])
                            (header.one header.content_type)
                            (|.when
                              {try.#Success actual}
                              (mime.= mime.json actual)
                              
                              {try.#Failure error}
                              false))
                        (when ((its [/.#message //.#body] it) {.#None})
                          {try.#Success [_ actual]}
                          (binary.= (utf8#as (json#as expected_json))
                                    actual)
                          
                          {try.#Failure error}
                          false)
                        )))
               (_.coverage [/.form]
                 (let [it (/.form pure.monad expected_form)]
                   (and (|> it
                            (its [/.#message //.#headers])
                            (header.one header.content_type)
                            (|.when
                              {try.#Success actual}
                              (mime.= mime.form actual)
                              
                              {try.#Failure error}
                              false))
                        (when ((its [/.#message //.#body] it) {.#None})
                          {try.#Success [_ actual]}
                          (binary.= (utf8#as (query#as expected_form))
                                    actual)
                          
                          {try.#Failure error}
                          false)
                        )))
               (,, (template.with [<request> <scheme>]
                     [(_.coverage [<request>]
                        (let [it (is (/.Request Pure)
                                     (<request> (/.utf8 pure.monad expected_text)))]
                          (and (|> it
                                   (its [/.#protocol /.#scheme])
                                   (scheme.= <scheme>))
                               (|> it
                                   (its [/.#message //.#headers])
                                   (header.one header.content_type)
                                   (|.when
                                     {try.#Success actual}
                                     (mime.= mime.utf_8 actual)
                                     
                                     {try.#Failure error}
                                     false))
                               (when ((its [/.#message //.#body] it) {.#None})
                                 {try.#Success [_ actual]}
                                 (binary.= (utf8#as expected_text)
                                           actual)
                                 
                                 {try.#Failure error}
                                 false)
                               )))]

                     [/.http scheme.http]
                     [/.https scheme.https]
                     ))
               (,, (template.with [<request> <method>]
                     [(_.coverage [<request>]
                        (let [it (is (/.Request Pure)
                                     (<request> (/.utf8 pure.monad expected_text)))]
                          (and (|> it
                                   (its [/.#resource /.#method])
                                   (|.when
                                     {<method>}
                                     true

                                     _
                                     false))
                               (|> it
                                   (its [/.#message //.#headers])
                                   (header.one header.content_type)
                                   (|.when
                                     {try.#Success actual}
                                     (mime.= mime.utf_8 actual)
                                     
                                     {try.#Failure error}
                                     false))
                               (when ((its [/.#message //.#body] it) {.#None})
                                 {try.#Success [_ actual]}
                                 (binary.= (utf8#as expected_text)
                                           actual)
                                 
                                 {try.#Failure error}
                                 false)
                               )))]

                     [/.post //.#Post]
                     [/.get //.#Get]
                     [/.put //.#Put]
                     [/.patch //.#Patch]
                     [/.delete //.#Delete]
                     [/.head //.#Head]
                     [/.connect //.#Connect]
                     [/.options //.#Options]
                     [/.trace //.#Trace]
                     ))
               (_.coverage [/.uri]
                 (let [it (is (/.Request Pure)
                              (/.uri expected_uri (/.utf8 pure.monad expected_text)))]
                   (and (|> it
                            (its [/.#resource /.#uri])
                            (same? expected_uri))
                        (|> it
                            (its [/.#message //.#headers])
                            (header.one header.content_type)
                            (|.when
                              {try.#Success actual}
                              (mime.= mime.utf_8 actual)
                              
                              {try.#Failure error}
                              false))
                        (when ((its [/.#message //.#body] it) {.#None})
                          {try.#Success [_ actual]}
                          (binary.= (utf8#as expected_text)
                                    actual)
                          
                          {try.#Failure error}
                          false)
                        )))
               (_.coverage [/.with_header]
                 (let [it (is (/.Request Pure)
                              (|> (/.utf8 pure.monad expected_text)
                                  (/.with_header header.content_length expected_content_length)))]
                   (and (|> it
                            (its [/.#message //.#headers])
                            (header.one header.content_length)
                            (|.when
                              {try.#Success actual}
                              (n.= expected_content_length actual)
                              
                              {try.#Failure error}
                              false))
                        (|> it
                            (its [/.#message //.#headers])
                            (header.one header.content_type)
                            (|.when
                              {try.#Success actual}
                              (mime.= mime.utf_8 actual)
                              
                              {try.#Failure error}
                              false))
                        (when ((its [/.#message //.#body] it) {.#None})
                          {try.#Success [_ actual]}
                          (binary.= (utf8#as expected_text)
                                    actual)
                          
                          {try.#Failure error}
                          false)
                        )))
               ))))
