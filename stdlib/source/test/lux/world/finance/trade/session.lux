... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [data
    ["[0]" bit]
    ["[0]" text]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" functor)]
    [number
     ["n" natural]
     ["i" integer]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only)
   [//
    ["[0]" volume (.only)
     ["[1]T" \\test]]
    [//
     ["[0]" money (.only)
      ["[0]" currency (.only Currency)]
      ["[1]T" \\test]]]]]])

(the .public (random $ max_sub_units)
  (for_any (_ $)
    (-> (Currency $) Natural
        (Random (/.Session $))))
  (let [random_natural (is (Random Natural)
                           (random#each (n.% max_sub_units) random.natural))]
    (all random.and
         (moneyT.random $ max_sub_units)
         random_natural
         (random#each (i.% (.integer max_sub_units)) random.integer)
         random_natural
         (volumeT.random $ max_sub_units))))

(the .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [before (..random currency.usd 1000,00)
         after (..random currency.usd 1000,00)])
      (_.for [/.Session
              /.#low /.#to_bottom /.#real_body /.#to_high /.#volume])
      (all _.and
           (_.for [/.equivalence]
                  (equivalenceT.spec /.equivalence (..random currency.usd 1000,00)))
           
           (_.coverage [/.composite]
             (let [both (/.composite before after)]
               (and (money.= (/.open before)
                             (/.open both))
                    (and (money.>= (/.high before)
                                   (/.high both))
                         (money.>= (/.high after)
                                   (/.high both)))
                    (and (money.<= (/.low before)
                                   (/.low both))
                         (money.<= (/.low after)
                                   (/.low both)))
                    (money.= (/.close after)
                             (/.close both))
                    (and (n.>= (its [/.#volume volume.#in] before)
                               (its [/.#volume volume.#in] both))
                         (n.>= (its [/.#volume volume.#in] after)
                               (its [/.#volume volume.#in] both))

                         (money.>= (its [/.#volume volume.#out] before)
                                   (its [/.#volume volume.#out] both))
                         (money.>= (its [/.#volume volume.#out] after)
                                   (its [/.#volume volume.#out] both))))))
           (_.coverage [/.injection]
             (bit.= (of /.equivalence =
                        before
                        after)
                    (text.= (/.injection before)
                            (/.injection after))))
           )))
