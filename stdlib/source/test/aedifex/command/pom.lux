... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try (.use "[1]#[0]" functor)]
    [concurrency
     ["[0]" async]]]
   [data
    ["[0]" binary (.use "[1]#[0]" equivalence)]
    ["[0]" text (.use "[1]#[0]" equivalence)
     [encoding
      ["[0]" utf8]]]
    [format
     ["[0]" xml]]]
   [math
    ["[0]" random]]
   [world
    ["[0]" file]]
   [test
    ["[0]" unit]
    ["_" property (.only Test)]]]]
 [//
  ["@[0]" version]
  [//
   ["@[0]" profile]]]
 [\\program
  ["[0]" / (.only)
   ["//[1]" ///
    ["[1]" profile]
    ["[1][0]" action]
    ["[1][0]" pom]]]])

(the .public test
  Test
  (<| (_.covering /._)
      (do random.monad
        [sample @profile.random
         .let [fs (file.mock (of file.default separator))]]
        (in (do [! async.monad]
              [.let [console (@version.echo "")]
               outcome (/.do! console fs sample)]
              (when outcome
                {try.#Success _}
                (do !
                  [verdict (do ///action.monad
                             [expected (|> (///pom.write sample)
                                           (try#each (|>> (of xml.format injection)
                                                          (of utf8.format injection)))
                                           (of ! in))
                              actual (of fs read ///pom.file)

                              logging! (of ///action.monad each
                                           (text#= /.success)
                                           (of console read_line []))

                              .let [expected_content!
                                    (binary#= expected actual)]]
                             (in (and logging!
                                      expected_content!)))]
                  (unit.coverage [/.do! /.success]
                    (try.else false verdict)))
                
                {try.#Failure error}
                (unit.coverage [/.do!]
                  (when (its ///.#identity sample)
                    {.#Some _}
                    false

                    {.#None}
                    true))))))))
