(.using
 [library
  [lux (.except)
   ["_" test (.only Test)]
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" pipe]
    ["[0]" try]
    [parser
     ["[0]" environment]]
    [concurrency
     ["[0]" atom (.only Atom)]
     ["[0]" async (.only Async)]]]
   [data
    ["[0]" binary]
    ["[0]" text (.only)
     ["%" \\format (.only format)]
     [encoding
      ["[0]" utf8]]]
    [collection
     ["[0]" set]]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [time
    ["[0]" instant]]
   [world
    [console (.only Console)]
    ["[0]" shell (.only Exit Shell)]
    ["[0]" program (.only Program)]
    ["[0]" file
     ["[0]" watch]]]]]
 ["[0]" //
  ["$[0]" version]
  ["$[0]" build]]
 [\\program
  ["[0]" / (.only)
   ["//[1]" ///
    [command (.only Command)]
    ["[1]" profile (.only Profile)]
    ["[1][0]" action]
    [dependency
     [resolution (.only Resolution)]]]]])

(def (command expected_runs end_signal fs dummy_file)
  (-> Nat Text (file.System Async) file.Path
      [(Atom Nat)
       (-> (Console Async) (Program Async) (file.System Async) (Shell Async) Resolution (Command [Exit Any]))])
  (let [@runs (is (Atom Nat)
                  (atom.atom 0))]
    [@runs
     (function (_ console program fs shell resolution profile)
       (do [! async.monad]
         [[_ actual_runs] (async.future (atom.update! ++ @runs))]
         (if (n.= expected_runs actual_runs)
           (in {try.#Failure end_signal})
           (do (try.with !)
             [_ (at fs write dummy_file (at utf8.codec encoded (%.nat actual_runs)))
              _ (at fs modify dummy_file (|> actual_runs .int instant.of_millis))]
             (in [shell.normal []])))))]))

(def .public test
  Test
  (<| (_.covering /._)
      (do [! random.monad]
        [end_signal (random.alphabetic 5)
         .let [/ (at file.default separator)
               [fs watcher] (watch.mock /)]
         
         program (random.alphabetic 5)
         target (random.alphabetic 5)
         source (random.alphabetic 5)
         .let [empty_profile (is Profile
                                 (at ///.monoid identity))
               with_target (is (-> Profile Profile)
                               (has ///.#target target))
               with_program (is (-> Profile Profile)
                                (has ///.#program {.#Some program}))
               
               profile (|> empty_profile
                           with_program
                           with_target
                           (has ///.#sources (set.of_list text.hash (list source))))]

         home (random.alphabetic 5)
         working_directory (random.alphabetic 5)
         
         expected_runs (at ! each (|>> (n.% 10) (n.max 2)) random.nat)
         dummy_path (at ! each (|>> (format source /)) (random.alphabetic 5))
         [compiler resolution] $build.resolution]
        (all _.and
             (_.coverage [/.delay]
               (n.> 0 /.delay))
             (in (do async.monad
                   [verdict (do ///action.monad
                              [_ (at fs make_directory source)
                               _ (at fs write dummy_path (binary.empty 0))
                               .let [[@runs command] (..command expected_runs end_signal fs dummy_path)]
                               _ (at watcher poll [])]
                              (do [! async.monad]
                                [no_dangling_process! (|> profile
                                                          (has ///.#lux compiler)
                                                          ((/.do! 1 watcher command)
                                                           ($version.echo "")
                                                           (program.async (program.mock environment.empty home working_directory))
                                                           fs
                                                           (shell.async ($build.good_shell []))
                                                           resolution)
                                                          (at ! each (|>> (pipe.case
                                                                            {try.#Failure error}
                                                                            (same? end_signal error)

                                                                            {try.#Success _}
                                                                            false))))
                                 correct_number_of_runs! (|> @runs
                                                             atom.read!
                                                             async.future
                                                             (at ! each (n.= expected_runs)))]
                                (in {try.#Success (and correct_number_of_runs!
                                                       no_dangling_process!)})))]
                   (_.coverage' [/.do!]
                     (try.else false verdict))))
             ))))
