(.module:
  [lux #*
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do)]]
   [control
    ["." try]
    [parser
     ["." environment (#+ Environment)]]
    [concurrency
     ["." atom (#+ Atom)]
     ["." promise (#+ Promise)]]
    [security
     ["!" capability]]]
   [data
    ["." text
     ["%" format (#+ format)]]
    [number
     ["n" nat]]
    [collection
     ["." dictionary]
     ["." set]
     ["." list ("#\." functor)]]]
   [math
    ["." random]]
   [world
    [console (#+ Console)]
    ["." shell (#+ Shell)]
    ["." program (#+ Program)]
    ["." file (#+ Path)
     ["." watch]]]]
  ["." // #_
   ["@." version]
   ["@." build]
   ["$/#" // #_
    ["#." package]]]
  {#program
   ["." /
    ["/#" // #_
     ["#." build]
     ["/#" // #_
      [command (#+ Command)]
      ["#" profile (#+ Profile)]
      ["#." action]
      ["#." artifact
       ["#/." type]]
      ["#." dependency
       ["#/." resolution (#+ Resolution)]]]]]})

(def: (command end-signal dummy-files)
  (-> Text (List Path)
      [(Atom [Nat (List Path)])
       (-> (Console Promise) (Program Promise) (file.System Promise) (Shell Promise) Resolution (Command Any))])
  (let [@runs (: (Atom [Nat (List Path)])
                 (atom.atom [0 dummy-files]))]
    [@runs
     (function (_ console program fs shell resolution profile)
       (do {! promise.monad}
         [[runs remaining-files] (promise.future
                                  (atom.update (function (_ [runs remaining-files])
                                                 [(inc runs) remaining-files])
                                               @runs))]
         (case remaining-files
           #.Nil
           (wrap (#try.Failure end-signal))
           
           (#.Cons head tail)
           (do (try.with !)
             [_ (!.use (\ fs create-file) [head])]
             (do !
               [_ (promise.future (atom.write [runs tail] @runs))]
               (wrap (#try.Success [])))))))]))

(def: #export test
  Test
  (<| (_.covering /._)
      (do {! random.monad}
        [#let [/ (\ file.default separator)
               [fs watcher] (watch.mock /)]
         end-signal (random.ascii/alpha 5)
         program (random.ascii/alpha 5)
         target (random.ascii/alpha 5)
         home (random.ascii/alpha 5)
         working-directory (random.ascii/alpha 5)
         expected-runs (\ ! map (|>> (n.% 10) (n.max 2)) random.nat)
         source (random.ascii/alpha 5)
         dummy-files (|> (random.ascii/alpha 5)
                         (random.set text.hash (dec expected-runs))
                         (\ ! map (|>> set.to-list (list\map (|>> (format source /))))))
         #let [empty-profile (: Profile
                                (\ ///.monoid identity))
               with-target (: (-> Profile Profile)
                              (set@ #///.target (#.Some target)))
               with-program (: (-> Profile Profile)
                               (set@ #///.program (#.Some program)))
               
               profile (|> empty-profile
                           with-program
                           with-target
                           (set@ #///.sources (set.from-list text.hash (list source))))]
         resolution @build.resolution]
        ($_ _.and
            (wrap (do promise.monad
                    [verdict (do ///action.monad
                               [#let [[@runs command] (..command end-signal dummy-files)]
                                _ (!.use (\ fs create-directory) [source])
                                _ (\ watcher poll [])]
                               (do promise.monad
                                 [outcome ((/.do! watcher command)
                                           (@version.echo "")
                                           (program.async (program.mock environment.empty home working-directory))
                                           fs
                                           (@build.good-shell [])
                                           resolution
                                           profile)
                                  [actual-runs _] (promise.future (atom.read @runs))]
                                 (wrap (#try.Success (and (n.= expected-runs actual-runs)
                                                          (case outcome
                                                            (#try.Failure error)
                                                            (is? end-signal error)

                                                            (#try.Success _)
                                                            false))))))]
                    (_.cover' [/.do!]
                              (try.default false verdict))))
            ))))
