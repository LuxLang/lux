... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" maybe]
    ["[0]" try]
    ["[0]" exception]
    [concurrency
     ["[0]" async (.only Async)]]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [math
    ["[0]" random]]
   [meta
    ["lux_[0]" version]
    [compiler
     ["[0]" version]]]
   [world
    ["[0]" console (.only Console Mock)]]
   [test
    ["[0]" unit]
    ["_" property (.only Test)]]]]
 [///
  ["@[0]" profile]]
 [\\program
  ["[0]" /]])

(exception.the .public console_is_closed!)

(the mock
  (Mock [Bit Text])
  (implementation
   (the (on_read [open? state])
     (if open?
       (<| (try.of_maybe "")
           (do maybe.monad
             [head (text.character 0 state)
              [_ tail] (text.split_at 1 state)]
             (in [[open? tail] head])))
       (exception.except ..console_is_closed! [])))
   (the (on_read_line [open? state])
     (if open?
       (<| (try.of_maybe "")
           (do maybe.monad
             [[output state] (text.split_by text.new_line state)]
             (in [[open? state] output])))
       (exception.except ..console_is_closed! [])))
   (the (on_write input [open? state])
     (if open?
       {try.#Success [open? (%.message state input)]}
       (exception.except ..console_is_closed! [])))
   (the (on_close [open? buffer])
     (if open?
       {try.#Success [false buffer]}
       (exception.except ..console_is_closed! [])))))

(the .public echo
  (-> Text (Console Async))
  (|>> [true]
       (console.mock ..mock)
       console.async))

(the .public test
  Test
  (<| (_.covering /._)
      (do random.monad
        [profile @profile.random]
        (in (do async.monad
              [.let [console (..echo "")]
               verdict (do (try.with async.monad)
                         [_ (/.do! console profile)
                          logging (of console read_line [])]
                         (in (text.= (version.injection lux_version.latest)
                                     logging)))]
              (unit.coverage [/.do!]
                (try.else false verdict)))))))
