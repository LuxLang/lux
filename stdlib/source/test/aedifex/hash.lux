... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]
    ["[0]" format
     ["[1]T" \\test]]]
   [control
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" binary (.only Binary)]
    [text
     ["%" \\injection]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" natural]]]
   [meta
    [macro
     ["[0]" template]]]
   [test
    ["_" property (.only Test)]]]]
 [\\program
  ["[0]" /]]
 [test
  [lux
   [data
    ["_[0]" binary]]]])

(the .public (random hash)
  (for_any (_ h)
    (-> (-> Binary (/.Hash h))
        (Random (/.Hash h))))
  (do [! random.monad]
    [size (of ! each (n.% 100) random.natural)]
    (of ! each hash (_binary.random size))))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Hash /.SHA1 /.MD5])
      (`` (all _.and
               (_.for [/.equivalence]
                      (all _.and
                           (equivalenceT.spec /.equivalence (..random /.sha1))
                           (equivalenceT.spec /.equivalence (..random /.md5))
                           ))
               (_.for [/.data]
                      (all _.and
                           (,, (template.with [<hash> <constructor> <exception>]
                                 [(do random.monad
                                    [expected (..random <hash>)]
                                    (_.coverage [<hash> <constructor> <exception>]
                                      (and (when (<constructor> (/.data expected))
                                             {try.#Success actual}
                                             (of /.equivalence = expected actual)

                                             {try.#Failure error}
                                             false)
                                           (when (<constructor> (of binary.monoid composite
                                                                    (/.data expected)
                                                                    (/.data expected)))
                                             {try.#Success actual}
                                             false

                                             {try.#Failure error}
                                             (exception.is? <exception> error)))))]

                                 [/.sha1 /.as_sha1 /.not_a_sha1]
                                 [/.md5 /.as_md5 /.not_a_md5]
                                 ))))
               (,, (template.with [<format> <hash>]
                     [(_.for [<format>]
                             (formatT.spec /.equivalence <format> (..random <hash>)))]
                     
                     [/.sha1_format /.sha1]
                     [/.md5_format /.md5]
                     ))
               (_.for [/.not_a_hash]
                      (all _.and
                           (,, (template.with [<format> <hash>]
                                 [(do random.monad
                                    [expected (..random <hash>)]
                                    (_.coverage [<format>]
                                      (when (of <format> projection
                                                (%.message (of <format> injection expected)
                                                           "AABBCC"))
                                        {try.#Success actual}
                                        false

                                        {try.#Failure error}
                                        (exception.is? /.not_a_hash error))))]

                                 [/.sha1_format /.sha1]
                                 [/.md5_format /.md5]
                                 ))))
               ))))
