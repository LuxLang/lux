(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    ["." time]
    [abstract
     [monad (#+ do)]]
    [control
     ["." try ("#\." functor)]
     [parser
      ["<.>" text]]]
    [math
     ["." random (#+ Random)]
     [number
      ["n" nat]
      ["i" int]]]]]
  [\\program
   ["." /]])

(def: #export random
  (Random /.Time)
  (random.one (|>> time.clock (set@ #time.milli_second 0) time.time)
              random.time))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [/.Time])
      ($_ _.and
          (do random.monad
            [expected ..random]
            (_.cover [/.format /.parser]
                     (|> expected
                         /.format
                         (<text>.run /.parser)
                         (try\map (\ time.equivalence = expected))
                         (try.default false))))
          )))
