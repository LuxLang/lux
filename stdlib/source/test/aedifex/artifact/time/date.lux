... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [control
    ["[0]" try (.use "[1]#[0]" functor)]
    ["[0]" exception]]
   [data
    ["[0]" text
     ["<[1]>" \\projection]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]
     ["i" int]]]
   [world
    [time
     ["[0]" date (.use "[1]#[0]" equivalence)]
     ["[0]" year]]]
   [test
    ["_" property (.only Test)]]]]
 [\\program
  ["[0]" /]])

(the .public random
  (Random /.Date)
  (random.one (function (_ raw)
                (try.maybe
                 (do try.monad
                   [year (|> raw date.year year.value i.abs (i.% +9,000) (i.+ +1,000) year.year)
                    raw (date.date year
                                   (date.month raw)
                                   (date.day_of_month raw))]
                   (/.date raw))))
              random.date))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Date])
      (do random.monad
        [expected ..random
         candidate random.date]
        (all _.and
             (_.for [/.equivalence]
                    (equivalenceT.spec /.equivalence ..random))

             (_.coverage [/.format /.projection]
               (|> expected
                   /.format
                   (<text>.value /.projection)
                   (try#each (of /.equivalence = expected))
                   (try.else false)))
             (_.coverage [/.value /.date]
               (|> expected
                   /.value
                   /.date
                   (try#each (of /.equivalence = expected))
                   (try.else false)))
             (_.coverage [/.year_is_out_of_range]
               (when (/.date candidate)
                 {try.#Success date}
                 (same? candidate (/.value date))
                 
                 {try.#Failure error}
                 (exception.is? /.year_is_out_of_range error)))
             (_.coverage [/.epoch]
               (date#= date.epoch (/.value /.epoch)))
             ))))
