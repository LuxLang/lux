... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [error
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    ["[0]" text
     ["<[1]>" \\projection]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["i" integer]]]
   [time
    ["[0]" instant]]
   [test
    ["_" property (.only Test)]]]]
 [\\program
  ["[0]" /]]
 ["[0]" /
  ["[1][0]" date]
  ["[1][0]" time]])

(the .public random
  (Random /.Time)
  (do random.monad
    [date /date.random
     time /time.random]
    (pure [date time])))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Time])
      (all _.and
           (_.for [/.equivalence]
                  (equivalenceT.spec /.equivalence ..random))

           (do random.monad
             [expected ..random]
             (_.coverage [/.as_text /.of_text]
               (|> expected
                   /.as_text
                   (<text>.value /.of_text)
                   (try#each (by /.equivalence = expected))
                   (try.else false))))
           (do random.monad
             [expected ..random]
             (_.coverage [/.instant /.of_instant]
               (|> expected
                   /.instant
                   /.of_instant
                   (try#each (by /.equivalence = expected))
                   (try.else false))))
           (_.coverage [/.epoch]
             (instant.= instant.epoch (/.instant /.epoch)))

           /date.test
           /time.test
           )))
