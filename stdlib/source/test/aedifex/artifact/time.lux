(.module:
  [lux #*
   ["_" test (#+ Test)]
   [abstract
    [monad (#+ do)]
    {[0 #spec]
     [/
      ["$." equivalence]]}]
   [control
    ["." try ("#\." functor)]
    [parser
     ["<.>" text]]]
   [math
    ["." random (#+ Random)]
    [number
     ["i" int]]]
   [time
    ["." instant]]]
  {#program
   ["." /]}
  ["." / #_
   ["#." date]
   ["#." time]])

(def: #export random
  (Random /.Time)
  (do random.monad
    [date /date.random
     time /time.random]
    (wrap (instant.from_date_time date time))))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [/.Time])
      ($_ _.and
          (_.for [/.equivalence]
                 ($equivalence.spec /.equivalence ..random))

          (do random.monad
            [expected ..random]
            (_.cover [/.format /.parser]
                     (|> expected
                         /.format
                         (<text>.run /.parser)
                         (try\map (\ instant.equivalence = expected))
                         (try.default false))))

          /date.test
          /time.test
          )))
