(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [abstract
     [monad (#+ do)]
     [\\specification
      ["$." equivalence]]]
    [control
     ["." try ("#\." functor)]
     [parser
      ["<.>" xml]]]
    [math
     ["." random (#+ Random) ("#\." monad)]]]]
  ["$." / #_
   ["#." build]
   ["#." stamp]
   ["#." time]
   ["#." version]]
  [\\program
   ["." /]])

(def: #export random
  (Random /.Snapshot)
  (random.or (random\in [])
             $/stamp.random))

(def: #export test
  Test
  (<| (_.covering /._)
      (_.for [/.Snapshot]
             ($_ _.and
                 (_.for [/.equivalence]
                        ($equivalence.spec /.equivalence ..random))
                 
                 (do random.monad
                   [expected ..random]
                   (_.cover [/.format /.parser]
                            (|> expected
                                /.format
                                list
                                (<xml>.run /.parser)
                                (try\map (\ /.equivalence = expected))
                                (try.default false))))

                 $/build.test
                 $/stamp.test
                 $/time.test
                 $/version.test
                 ))))
