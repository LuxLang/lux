... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [control
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    [format
     ["[0]" xml
      ["<[1]>" \\projection]]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]]
   [test
    ["_" property (.only Test)]]]]
 ["$[0]" /
  ["[1][0]" build]
  ["[1][0]" stamp]
  ["[1][0]" time]
  ["[1][0]" version]]
 [\\program
  ["[0]" /]])

(the .public random
  (Random /.Snapshot)
  (random.or (random#in [])
             $/stamp.random))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Snapshot]
             (all _.and
                  (_.for [/.equivalence]
                         (equivalenceT.spec /.equivalence ..random))
                  
                  (do random.monad
                    [expected ..random]
                    (_.coverage [/.injection /.projection]
                      (|> expected
                          /.injection
                          list
                          (<xml>.value /.projection)
                          (try#each (of /.equivalence = expected))
                          (try.else false))))

                  $/build.test
                  $/stamp.test
                  $/time.test
                  $/version.test
                  ))))
