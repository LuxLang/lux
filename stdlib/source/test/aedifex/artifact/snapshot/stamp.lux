(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [abstract
     [monad (#+ do)]
     [\\specification
      ["$." equivalence]]]
    [control
     ["." try ("#\." functor)]
     [parser
      ["<.>" xml]]]
    [math
     ["." random (#+ Random)]]
    [time
     ["." instant]]]]
  [\\program
   ["." /]]
  ["$." // #_
   ["#." time]
   ["#." build]])

(def: .public random
  (Random /.Stamp)
  ($_ random.and
      $//time.random
      $//build.random
      ))

(def: .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Stamp])
      ($_ _.and
          (_.for [/.equivalence]
                 ($equivalence.spec /.equivalence ..random))

          (do random.monad
            [expected ..random]
            (_.cover [/.format /.parser]
                     (|> expected
                         /.format
                         (<xml>.result /.parser)
                         (try\map (\ /.equivalence = expected))
                         (try.else false))))
          )))
