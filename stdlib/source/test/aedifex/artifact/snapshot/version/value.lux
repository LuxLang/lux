... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" equivalence
     ["[1]T" \\test]]]
   [control
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]
     ["<[1]>" \\projection]]]
   [math
    ["[0]" random (.only Random) (.use "[1]#[0]" monad)]
    [number
     ["n" natural]
     ["i" integer]]]
   [time
    ["[0]" instant]]
   [test
    ["_" property (.only Test)]]]]
 ["$[0]" ///
  ["[1][0]" stamp]]
 [\\program
  ["[0]" / (.only)
   ["//[1]" /// (.only)
    ["[1][0]" stamp]
    ["[1][0]" time]]]])

(the .public random
  (Random /.Value)
  (all random.and
       (random.alphabetic 5)
       (random.or (random#in [])
                  $///stamp.random)
       ))

(the .public test
  Test
  (<| (_.covering /._)
      (_.for [/.Value])
      (all _.and
           (_.for [/.equivalence]
                  (equivalenceT.spec /.equivalence ..random))

           (do random.monad
             [sample random
              stamp $///stamp.random]
             (let [version (its /.#version sample)

                   local!
                   (text.= version
                           (/.injection (has /.#snapshot {///.#Local} sample)))

                   remote_injection (/.injection [/.#version (%.message version /.snapshot)
                                                  /.#snapshot {///.#Remote stamp}])
                   remote!
                   (and (text.starts_with? (%.message version (///time.injection (its ///stamp.#time stamp)))
                                           remote_injection)
                        (text.ends_with? (%.natural (its ///stamp.#build stamp))
                                         remote_injection))]
               (_.coverage [/.snapshot /.injection]
                 (and local!
                      remote!))))
           )))
