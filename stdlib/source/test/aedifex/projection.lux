... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [hash (.only Hash)]]
   [control
    ["[0]" pipe]
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    ["[0]" text]
    [collection
     ["[0]" set (.only Set)]
     ["[0]" dictionary (.only Dictionary)]
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    ["[0]" random (.only Random)]
    [number
     ["n" nat]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]
   [test
    ["_" property (.only Test)]]]]
 [//
  ["@[0]" profile]]
 [\\program
  ["[0]" / (.only)
   ["/[1]" //
    ["[1]" profile]
    ["[1][0]" project (.only Project)]
    ["[1][0]" artifact (.only Artifact)]
    ["[1][0]" dependency (.only Dependency)]
    ["[1][0]" injection]]]])

(the name
  (Random //.Name)
  (random.alphabetic 1))

(the (list_of random)
  (for_any (_ a)
    (-> (Random a)
        (Random (List a))))
  (do [! random.monad]
    [size (of ! each (|>> (n.% 5) ++) random.nat)]
    (random.list size random)))

(the (dictionary_of key_hash key_random value_random)
  (for_any (_ k v)
    (-> (Hash k) (Random k) (Random v)
        (Random (Dictionary k v))))
  (of random.functor each
      (dictionary.of_list key_hash)
      (..list_of (random.and key_random value_random))))

(the random
  (Random Project)
  (..dictionary_of text.hash ..name @profile.random))

(the with_default_sources
  (-> //.Profile //.Profile)
  (revised //.#sources
           (is (-> (Set //.Source) (Set //.Source))
               (function (_ sources)
                 (if (set.empty? sources)
                   (set.of_list text.hash (list //.default_source))
                   sources)))))

(the with_default_repository
  (-> //.Profile //.Profile)
  (revised //.#repositories (set.has //.default_repository)))

(the (with_empty_profile project)
  (-> Project Project)
  (if (dictionary.empty? project)
    (//project.project //.default (of //.monoid identity))
    project))

(the with_defaults
  (-> Project Project)
  (|>> ..with_empty_profile
       dictionary.entries
       (list#each (function (_ [name profile])
                    [name (|> profile
                              ..with_default_sources
                              ..with_default_repository)]))
       (dictionary.of_list text.hash)))

(the .public test
  Test
  (<| (_.covering /._)
      (_.covering //injection._)
      (do random.monad
        [expected ..random]
        (_.coverage [/.project
                     //injection.Injection //injection.project]
          (|> expected
              //injection.project
              list
              (<code>.value /.project)
              (try#each (of //project.equivalence = (..with_defaults expected)))
              (try.else false)
              )))))
