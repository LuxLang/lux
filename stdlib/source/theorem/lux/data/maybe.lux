... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except by)
   ["_" theorem (.only)
    ["?" proposition]
    ["in" rule]
    ["by" proof]]]]
 [\\library
  ["[0]" /]]
 [///
  [logic
   ["[0]" bit]]])

(_.the .public else|none
  (?.let [any]
    [it any]
    (?.= (/.else it {.:None})
         it))
  (by.= (by.when in.as_is
          (function (_ =_none)
            in.as_is))
        in.as_is))

(_.the .public else|some
  (?.let [any]
    [it any
     else any]
    (?.= (/.else else {.:Some it})
         it))
  (by.= (by.when in.as_is
          (function (_ =_some)
            (in.by =_some)))
        in.as_is))

(_.the .public as_list|none
  (?.= (/.as_list {.:None})
       (.list))
  (by.= (all by.and
             (by.of (by.definition) (list in.as_is))
             (by.when in.as_is
               (function (_ =_none)
                 in.as_is)))
        in.as_is))

(_.the .public as_list|some
  (?.let [any]
    [it any]
    (?.= (/.as_list {.:Some it})
         (.list it)))
  (by.= (all by.and
             (by.of by.definition (list in.as_is))
             (by.when in.as_is
               (function (_ =_some)
                 (in.list (list (in.by =_some))))))
        in.as_is))

(_.the .public whether|false
  (?.let [any]
    [it (Maybe any)]
    (?.= (Maybe any)
         (/.whether .false it)
         {.:None}))
  (by.= (by.when by.definition
          (function (_ =_some)
            in.as_is))
        in.as_is))

(_.the .public whether|true
  (?.let [any]
    [it (Maybe any)]
    (?.= (Maybe any)
         (/.whether .true it)
         it))
  (by.= (in.by bit.if_true)
        in.as_is))

(_.the .public or|none
  (?.let [any]
    [it (Maybe any)]
    (?.= (Maybe any)
         (/.or {.:None} it)
         it))
  (by.= (by.when in.as_is
          (function (_ =_none)
            in.as_is))
        in.as_is))

(_.the .public or|some
  (?.let [any]
    [it any
     else (Maybe any)]
    (?.= (Maybe any)
         (/.or {.:Some it} else)
         {.:Some it}))
  (by.= (by.when in.as_is
          (function (_ =_some)
            (in.right_case (in.by =_some))))
        in.as_is))

(_.the .public if|none
  (?.let [any]
    [then any
     else any]
    (?.= (/.if [it {.:None}]
               then
               else)
         else))
  (by.= (by.when in.as_is
          (function (_ =_none)
            in.as_is))
        in.as_is))

(_.the .public if|some
  (?.let [any any_premise]
    [some any_premise
     then (-> any_premise any)
     else any]
    (?.= (/.if [it {.:Some some}]
               (then it)
               else)
         (then some)))
  (by.= (by.when in.as_is
          (function (_ =_some)
            (by.of' in.as_is (list (in.by =_some)))))
        in.as_is))
