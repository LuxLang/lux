... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   ["_" theorem (.only)
    ["?" proposition]
    ["!" proof (.only)
     ["[0]" induction]]
    ["in" substitution]
    ["by" rule]]
   [macro
    ["[0]" template]]]]
 ["/" \\library])

(template.the .public (axiom_of_left_identity /:Type /:identity /:composite)
  [(?.let [it /:Type]
     (?.= (/:composite /:identity it)
          it))])

(template.the .public (axiom_of_right_identity /:Type /:identity /:composite)
  [(?.let [it /:Type]
     (?.= (/:composite it /:identity)
          it))])

(template.the .public (axiom_of_associativity /:Type /:composite)
  [(?.let [this /:Type
           that /:Type
           else /:Type]
     (?.= (/:composite (/:composite this that) else)
          (/:composite this (/:composite that else))))])

... https://en.wikipedia.org/wiki/Monoid
(template.the .public (system /:Type /:identity /:composite)
  [(all ?.and
        (..axiom_of_left_identity /:Type /:identity /:composite)
        (..axiom_of_right_identity /:Type /:identity /:composite)
        (..axiom_of_associativity /:Type /:composite))])
