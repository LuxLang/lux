... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except list nat int rev symbol type)
   [abstract
    [monad (.only do)]
    [functor
     ["[0]" contravariant]]]
   [control
    ["<>" projection]
    ["[0]" maybe]]
   [data
    ["[0]" bit]
    ["[0]" text]
    [format
     ["[0]" xml]
     ["[0]" json]]
    [collection
     ["[0]" list (.use "[1]#[0]" monad)]]]
   [math
    [number
     ["[0]" nat]
     ["[0]" int]
     ["[0]" rev]
     ["[0]" dec]
     ["[0]" frac]
     ["[0]" complex]]
    [arithmetic
     ["[0]" modular]]]
   [meta
    ["[0]" location]
    ["[0]" symbol]
    ["[0]" type]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro
     [syntax (.only syntax)]
     ["[0]" template]]]
   [world
    ["[0]" time (.only)
     ["[0]" instant]
     ["[0]" duration]
     ["[0]" date]
     ["[0]" day]
     ["[0]" month]]]]])

(.every .public (Format of)
  (-> of
      Text))

(the .public functor
  (contravariant.Functor Format)
  (implementation
   (the (each f fb)
     (|>> f fb))))

(the .public format
  (syntax (_ [fragments (<>.many <code>.any)])
    (in (.list (` (.text_composite# (,* fragments)))))))

(with_template [<name> <type> <formatter>]
  [(the .public <name>
     (Format <type>)
     <formatter>)]

  [bit      Bit               (of bit.codec injection)]
  [nat      Nat               (of nat.decimal injection)]
  [int      Int               (of int.decimal injection)]
  [rev      Rev               (of rev.decimal injection)]
  [dec      Dec               (of dec.decimal injection)]
  [text     Text              text.format]
  
  [frac     frac.Frac         (of frac.codec injection)]
  [complex  complex.Complex   complex.format]
  
  [symbol   Symbol            (of symbol.absolute injection)]
  [location Location          location.format]
  [code     Code              code.absolute]
  [type     Type              type.absolute_format]
  
  [instant  instant.Instant   (of instant.codec injection)]
  [duration duration.Duration (of duration.codec injection)]
  [date     date.Date         (of date.codec injection)]
  [time     time.Time         (of time.codec injection)]
  [day      day.Day           (of day.codec injection)]
  [month    month.Month       (of month.codec injection)]
  
  [xml      xml.XML           (of xml.codec injection)]
  [json     json.JSON         (of json.codec injection)]
  )

(with_template [<name>]
  [(the .public <name>
     (Format Dec)
     (let [suffix (|> +0.0
                      (of (template.symbol [dec._] [<name>]) injection)
                      (text.clip_since 4)
                      (maybe.else ""))]
       (|>> ((template.symbol [dec._] [as_ <name>]))
            dec.int
            ..int
            (text.suffix suffix))))]

  [degree]
  [percentage]
  [permille]
  [permyriad]
  )

(with_template [<type> <format>,<codec>]
  [(`` (with_template [<format> <codec>]
         [(the .public <format>
            (Format <type>)
            (of <codec> injection))]

         (,, (template.spliced <format>,<codec>))))]

  [Nat
   [[nat_2 nat.binary]
    [nat_8 nat.octal]
    [nat_10 nat.decimal]
    [nat_16 nat.hex]]]
  [Int
   [[int_2 int.binary]
    [int_8 int.octal]
    [int_10 int.decimal]
    [int_16 int.hex]]]
  [Rev
   [[rev_2 rev.binary]
    [rev_8 rev.octal]
    [rev_10 rev.decimal]
    [rev_16 rev.hex]]]
  [Dec
   [[dec_2 dec.binary]
    [dec_8 dec.octal]
    [dec_10 dec.decimal]
    [dec_16 dec.hex]]]
  )

(the .public (padded padding format)
  (-> Nat (Format Nat)
      (Format Nat))
  (of (nat.padded padding
                  (implementation
                   (the injection format)
                   (the (projection _) (undefined))))
      injection))

(the .public (mod modular)
  (All (_ %)
    (Format (modular.Mod %)))
  (let [codec (modular.codec (modular.modulus modular))]
    (of codec injection modular)))

(the .public (list formatter)
  (All (_ of)
    (-> (Format of)
        (Format (List of))))
  (|>> (list#each (|>> formatter (format " ")))
       text.together
       (text.enclosed ["(list" ")"])))

(the .public (maybe format)
  (All (_ of)
    (-> (Format of)
        (Format (Maybe of))))
  (function (_ value)
    (when value
      {.#None}
      "{.#None}"

      {.#Some value}
      (..format "{.#Some " (format value) "}"))))
