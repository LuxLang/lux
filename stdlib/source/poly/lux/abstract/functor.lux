(.module:
  [lux #*
   [abstract
    [monad (#+ Monad do)]]
   [control
    ["p" parser
     ["<.>" type]
     ["s" code (#+ Parser)]]]
   [data
    ["." product]
    [number
     ["n" nat]]
    ["." text
     ["%" format (#+ format)]]
    [collection
     ["." list ("#\." monad monoid)]]]
   [macro
    ["." code]
    [syntax (#+ syntax:)
     ["." common]]
    ["." poly (#+ poly:)]]
   ["." type]]
  {1
   ["." /]})

(poly: #export functor
  (do {! p.monad}
    [#let [type-funcC (code.local-identifier "____________type-funcC")
           funcC (code.local-identifier "____________funcC")
           inputC (code.local-identifier "____________inputC")]
     *env* <type>.env
     inputT <type>.peek
     [polyC varsC non-functorT] (<type>.local (list inputT)
                                              (<type>.polymorphic <type>.any))
     #let [num-vars (list.size varsC)]
     #let [@Functor (: (-> Type Code)
                       (function (_ unwrappedT)
                         (if (n.= 1 num-vars)
                           (` ((~! /.Functor) (~ (poly.to-code *env* unwrappedT))))
                           (let [paramsC (|> num-vars dec list.indices (list\map (|>> %.nat code.local-identifier)))]
                             (` (All [(~+ paramsC)]
                                  ((~! /.Functor) ((~ (poly.to-code *env* unwrappedT)) (~+ paramsC)))))))))
           Arg<?> (: (-> Code (<type>.Parser Code))
                     (function (Arg<?> valueC)
                       ($_ p.either
                           ## Type-var
                           (do p.monad
                             [#let [varI (|> num-vars (n.* 2) dec)]
                              _ (<type>.parameter! varI)]
                             (wrap (` ((~ funcC) (~ valueC)))))
                           ## Variants
                           (do !
                             [_ (wrap [])
                              membersC (<type>.variant (p.many (Arg<?> valueC)))
                              #let [last (dec (list.size membersC))]]
                             (wrap (` (case (~ valueC)
                                        (~+ (list\join (list\map (function (_ [tag memberC])
                                                                   (if (n.= last tag)
                                                                     (list (` ((~ (code.nat (dec tag))) #1 (~ valueC)))
                                                                           (` ((~ (code.nat (dec tag))) #1 (~ memberC))))
                                                                     (list (` ((~ (code.nat tag)) #0 (~ valueC)))
                                                                           (` ((~ (code.nat tag)) #0 (~ memberC))))))
                                                                 (list.enumeration membersC))))))))
                           ## Tuples
                           (do p.monad
                             [pairsCC (: (<type>.Parser (List [Code Code]))
                                         (<type>.tuple (loop [idx 0
                                                              pairsCC (: (List [Code Code])
                                                                         (list))]
                                                         (p.either (let [slotC (|> idx %.nat (format "____________slot") code.local-identifier)]
                                                                     (do !
                                                                       [_ (wrap [])
                                                                        memberC (Arg<?> slotC)]
                                                                       (recur (inc idx)
                                                                              (list\compose pairsCC (list [slotC memberC])))))
                                                                   (wrap pairsCC)))))]
                             (wrap (` (case (~ valueC)
                                        [(~+ (list\map product.left pairsCC))]
                                        [(~+ (list\map product.right pairsCC))]))))
                           ## Functions
                           (do !
                             [_ (wrap [])
                              #let [g! (code.local-identifier "____________")
                                    outL (code.local-identifier "____________outL")]
                              [inT+ outC] (<type>.function (p.many <type>.any)
                                            (Arg<?> outL))
                              #let [inC+ (|> (list.size inT+)
                                             list.indices
                                             (list\map (|>> %.nat (format "____________inC") code.local-identifier)))]]
                             (wrap (` (function ((~ g!) (~+ inC+))
                                        (let [(~ outL) ((~ valueC) (~+ inC+))]
                                          (~ outC))))))
                           ## Recursion
                           (do p.monad
                             [_ <type>.recursive-call]
                             (wrap (` ((~' map) (~ funcC) (~ valueC)))))
                           ## Parameters
                           (do p.monad
                             [_ <type>.any]
                             (wrap valueC))
                           )))]
     [_ _ outputC] (: (<type>.Parser [Code (List Code) Code])
                      (p.either (<type>.polymorphic
                                 (Arg<?> inputC))
                                (p.fail (format "Cannot create Functor for: " (%.type inputT)))))]
    (wrap (` (: (~ (@Functor inputT))
                (structure (def: ((~' map) (~ funcC) (~ inputC))
                             (~ outputC))))))))
