... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [control
    ["//" projection]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" dictionary (.only Dictionary)]]]]])

(every .public Property
  Text)

(every .public Environment
  (Dictionary Property Text))

(exception.the .public (unknown_property property)
  (Exception Property)
  (exception.report
   (list ["Property" (%.text property)])))

(every .public (Projection of)
  (//.Projection Environment of))

(the .public empty
  Environment
  (dictionary.empty text.hash))

(the .public (property name)
  (-> Property
      (Projection Text))
  (function (_ environment)
    (when (dictionary.value name environment)
      {try.#Success value}
      {try.#Success [environment value]}
      
      failure
      (exception.except ..unknown_property [name]))))

(the .public (value projection environment)
  (for_any (_ of)
    (-> (Projection of) Environment
        (Try of)))
  (|> (projection environment)
      (of try.monad each product.right)))
