... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except function local parameter variant tuple)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["//" projection]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]
    ["[0]" function]]
   [data
    ["[0]" text (.use "[1]#[0]" monoid)
     ["%" \\injection]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]
     ["[0]" dictionary (.only Dictionary)]]]
   [math
    [number
     ["n" nat (.use "[1]#[0]" decimal)]]]
   [meta
    ["[0]" code]
    [macro
     ["^" pattern]
     ["[0]" template]]]]]
 [\\library
  ["[0]" / (.use "[1]#[0]" equivalence)
   ["[0]" check]]])

(the .public recursion_parameter
  (template.macro (_)
    [{.#Nominal "" {.#End}}]))

(the .public recursive_definition
  (template.macro (_ ,type)
    [{.#Apply (..recursion_parameter)
              {.#Universal (list) ,type}}]))

(the .public recursive_iteration
  (template.macro (_)
    [{.#Apply (..recursion_parameter)
              {.#Parameter 0}}]))

(template.with [<name>]
  [(exception.the .public (<name> type)
     (Exception Type)
     (exception.report
      (list ["Type" (%.type type)])))]

  [not_existential]
  [not_recursive]
  [not_named]
  [not_parameter]
  [unknown_parameter]
  [not_function]
  [not_application]
  [not_polymorphic]
  [not_variant]
  [not_tuple]
  )

(template.with [<name>]
  [(exception.the .public (<name> [expected actual])
     (Exception [Type Type])
     (exception.report
      (list ["Expected" (%.type expected)]
            ["Actual" (%.type actual)])))]

  [types_do_not_match]
  [wrong_parameter]
  )

(exception.the .public empty_input)

(exception.the .public (unconsumed_input remaining)
  (Exception (List Type))
  (exception.report
   (list ["Types" (|> remaining
                      (list#each (|>> %.type (%.message text.new_line "* ")))
                      (text.interposed ""))])))

(every .public Env
  (Dictionary Nat [Type Code]))

(every .public (Projection a)
  (//.Projection [Env (List Type)] a))

(the .public fresh
  Env
  (dictionary.empty n.hash))

(the (value' env poly types)
  (for_any (_ a) (-> Env (Projection a) (List Type) (Try a)))
  (when (//.value poly [env types])
    {try.#Failure error}
    {try.#Failure error}

    {try.#Success [[env' remaining] output]}
    (when remaining
      {.#End}
      {try.#Success output}

      _
      (exception.except ..unconsumed_input remaining))))

(the .public (value poly type)
  (for_any (_ a) (-> (Projection a) Type (Try a)))
  (value' ..fresh poly (list type)))

(the .public env
  (Projection Env)
  (.function (_ [env inputs])
    {try.#Success [[env inputs] env]}))

(the .public arity
  (Projection Nat)
  (.function (_ [env inputs])
    {try.#Success [[env inputs] (dictionary.size env)]}))

(the (with_env temp poly)
  (for_any (_ a) (-> Env (Projection a) (Projection a)))
  (.function (_ [env inputs])
    (when (//.value poly [temp inputs])
      {try.#Failure error}
      {try.#Failure error}

      {try.#Success [[_ remaining] output]}
      {try.#Success [[env remaining] output]})))

(the .public next
  (Projection Type)
  (.function (_ [env inputs])
    (when inputs
      {.#End}
      (exception.except ..empty_input [])

      {.#Item headT tail}
      {try.#Success [[env inputs] headT]})))

(the .public any
  (Projection Type)
  (.function (_ [env inputs])
    (when inputs
      {.#End}
      (exception.except ..empty_input [])

      {.#Item headT tail}
      {try.#Success [[env tail] headT]})))

(the .public (local types poly)
  (for_any (_ a) (-> (List Type) (Projection a) (Projection a)))
  (.function (_ [env pass_through])
    (when (value' env poly types)
      {try.#Failure error}
      {try.#Failure error}

      {try.#Success output}
      {try.#Success [[env pass_through] output]})))

(the (label idx)
  (-> Nat Code)
  (code.local (all text#composite "label" text.tab (n#injection idx))))

(the .public (with_extension type poly)
  (for_any (_ a) (-> Type (Projection a) (Projection [Code a])))
  (.function (_ [env inputs])
    (let [current_id (dictionary.size env)
          g!var (label current_id)]
      (when (//.value poly
                      [(dictionary.has current_id [type g!var] env)
                       inputs])
        {try.#Failure error}
        {try.#Failure error}

        {try.#Success [[_ inputs'] output]}
        {try.#Success [[env inputs'] [g!var output]]}))))

(template.with [<name> <flattener> <exception>]
  [(`` (the .public (<name> poly)
         (for_any (_ a) (-> (Projection a) (Projection a)))
         (do //.monad
           [headT ..any]
           (let [members (<flattener> (/.anonymous headT))]
             (if (n.> 1 (list.size members))
               (local members poly)
               (//.failure (exception.error <exception> headT)))))))]

  [variant /.flat_variant ..not_variant]
  [tuple   /.flat_tuple   ..not_tuple]
  )

(the polymorphic'
  (Projection [Nat Type])
  (do //.monad
    [headT any
     .let [[num_arg bodyT] (/.flat_univ_q (/.anonymous headT))]]
    (if (n.= 0 num_arg)
      (//.failure (exception.error ..not_polymorphic headT))
      (in [num_arg bodyT]))))

(the .public (polymorphic poly)
  (for_any (_ a) (-> (Projection a) (Projection [Code (List Code) a])))
  (do [! //.monad]
    [headT any
     funcI (of ! each dictionary.size ..env)
     [num_args non_poly] (local (list headT) ..polymorphic')
     env ..env
     .let [funcL (label funcI)
           [all_varsL env'] (loop (again [current_arg 0
                                          env' env
                                          all_varsL (is (List Code) (list))])
                              (if (n.< num_args current_arg)
                                (if (n.= 0 current_arg)
                                  (let [varL (label (++ funcI))]
                                    (again (++ current_arg)
                                           (|> env'
                                               (dictionary.has funcI [headT funcL])
                                               (dictionary.has (++ funcI) [{.#Parameter (++ funcI)} varL]))
                                           {.#Item varL all_varsL}))
                                  (let [partialI (|> current_arg (n.* 2) (n.+ funcI))
                                        partial_varI (++ partialI)
                                        partial_varL (label partial_varI)
                                        partialC (` ((, funcL) (,* (|> (list.indices num_args)
                                                                       (list#each (|>> (n.* 2) ++ (n.+ funcI) label))
                                                                       list.reversed))))]
                                    (again (++ current_arg)
                                           (|> env'
                                               (dictionary.has partialI [(recursion_parameter) partialC])
                                               (dictionary.has partial_varI [{.#Parameter partial_varI} partial_varL]))
                                           {.#Item partial_varL all_varsL})))
                                [all_varsL env']))]]
    (<| (with_env env')
        (local (list non_poly))
        (do !
          [output poly]
          (in [funcL all_varsL output])))))

(the .public (function in_poly out_poly)
  (for_any (_ i o) (-> (Projection i) (Projection o) (Projection [i o])))
  (do //.monad
    [headT any
     .let [[inputsT outputT] (/.flat_function (/.anonymous headT))]]
    (if (n.> 0 (list.size inputsT))
      (//.and (local inputsT in_poly)
              (local (list outputT) out_poly))
      (//.failure (exception.error ..not_function headT)))))

(the .public (applied poly)
  (for_any (_ a) (-> (Projection a) (Projection a)))
  (do //.monad
    [headT any
     .let [[funcT paramsT] (/.flat_application (/.anonymous headT))]]
    (if (n.= 0 (list.size paramsT))
      (//.failure (exception.error ..not_application headT))
      (..local {.#Item funcT paramsT} poly))))

(template.with [<name> <test>]
  [(the .public (<name> expected)
     (-> Type (Projection Any))
     (do //.monad
       [actual any]
       (if (<test> expected actual)
         (in [])
         (//.failure (exception.error ..types_do_not_match [expected actual])))))]

  [exactly /#=]
  [sub     check.subsumed?]
  [super   (function.flipped check.subsumed?)]
  )

(the .public (argument env idx)
  (-> Env Nat Nat)
  (let [env_level (n./ 2 (dictionary.size env))
        parameter_level (n./ 2 idx)
        parameter_idx (n.% 2 idx)]
    (|> env_level -- (n.- parameter_level) (n.* 2) (n.+ parameter_idx))))

(the .public parameter
  (Projection Code)
  (do //.monad
    [env ..env
     headT any]
    (when headT
      {.#Parameter idx}
      (when (dictionary.value (..argument env idx) env)
        {try.#Success [poly_type poly_code]}
        (in poly_code)

        failure
        (//.failure (exception.error ..unknown_parameter headT)))

      _
      (//.failure (exception.error ..not_parameter headT)))))

(the .public (this_parameter id)
  (-> Nat (Projection Any))
  (do //.monad
    [env ..env
     headT any]
    (when headT
      {.#Parameter idx}
      (if (n.= id (..argument env idx))
        (in [])
        (//.failure (exception.error ..wrong_parameter [{.#Parameter id} headT])))

      _
      (//.failure (exception.error ..not_parameter headT)))))

(the .public existential
  (Projection Nat)
  (do //.monad
    [headT any]
    (when headT
      {.#Opaque ex_id}
      (in ex_id)

      _
      (//.failure (exception.error ..not_existential headT)))))

(the .public named
  (Projection [Symbol Type])
  (do //.monad
    [inputT any]
    (when inputT
      {.#Named name anonymousT}
      (in [name anonymousT])

      _
      (//.failure (exception.error ..not_named inputT)))))

(the .public (recursive poly)
  (for_any (_ a)
    (-> (Projection a)
        (Projection [Code a])))
  (do [! //.monad]
    [headT any]
    (when (/.anonymous headT)
      (..recursive_definition headT')
      (do !
        [[recT _ output] (|> poly
                             (with_extension (recursion_parameter))
                             (with_extension headT)
                             (local (list headT')))]
        (in [recT output]))

      _
      (//.failure (exception.error ..not_recursive headT)))))

(the .public recursive_self
  (Projection Code)
  (do //.monad
    [env ..env
     headT any]
    (when (/.anonymous headT)
      (^.multi (..recursive_iteration)
               [(dictionary.value 0 env) {try.#Success [self_type self_call]}])
      (in self_call)

      _
      (//.failure (exception.error ..not_recursive headT)))))

(the .public recursive_call
  (Projection Code)
  (do [! //.monad]
    [env ..env
     [funcT argsT] (..applied (//.and any (//.many any)))
     _ (local (list funcT) (..this_parameter 0))
     allC (let [allT (list.partial funcT argsT)]
            (|> allT
                (monad.each ! (function.constant ..parameter))
                (local allT)))]
    (in (` ((,* allC))))))
