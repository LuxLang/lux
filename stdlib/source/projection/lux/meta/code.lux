... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except natural integer revolution local not symbol local global variant tuple)
   [abstract
    ["[0]" monad (.only do)]]
   [control
    ["//" projection]
    ["[0]" try (.only Try)]]
   [data
    ["[0]" bit]
    ["[0]" text (.use "[1]#[0]" monoid)]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    [number
     ["[0]" natural]
     ["[0]" integer]
     ["[0]" revolution]
     ["[0]" decimal]]]
   [meta
    ["[0]" location]
    ["[0]" symbol]
    ["[0]" code]
    [macro
     ["[0]" expansion]]]]])

(the with_template' (.in_module# .prelude .with_template))

(the (un_paired pairs)
  (for_any (_ a) (-> (List [a a]) (List a)))
  (when pairs
    {.#End}
    {.#End}
    
    {.#Item [[x y] pairs']}
    (list.partial x y (un_paired pairs'))))

(every .public Projection
  (//.Projection (List Code)))

(the remaining_inputs
  (-> (List Code) Text)
  (|>> (list#each code.absolute)
       (text.interposed " ")
       (all text#composite text.new_line "Remaining input: ")))

(the .public empty
  Error
  (location.with (location.here)
    "There are no tokens to parse!"))

(the .public any
  (Projection Code)
  (function (_ tokens)
    (when tokens
      {.#End}
      {try.#Failure ..empty}
      
      {.#Item [t tokens']}
      {try.#Success [tokens' t]})))

(the .public next
  (Projection Code)
  (function (_ tokens)
    (when tokens
      {.#End}
      {try.#Failure ..empty}
      
      {.#Item next _}
      {try.#Success [tokens next]})))

(with_template' [<query> <check> <type> <tag> <eq> <desc>]
  [(expansion.let [<failure> (these {try.#Failure (all text#composite "Cannot parse " <desc> (remaining_inputs tokens))})]
     (these (the .public <query>
              (Projection <type>)
              (function (_ tokens)
                (when tokens
                  {.#Item [[_ {<tag> x}] tokens']}
                  {try.#Success [tokens' x]}

                  _
                  <failure>)))

            (the .public (<check> expected)
              (-> <type> (Projection Any))
              (function (_ tokens)
                (when tokens
                  {.#Item [[_ {<tag> actual}] tokens']}
                  (if (of <eq> = expected actual)
                    {try.#Success [tokens' []]}
                    <failure>)

                  _
                  <failure>)))))]

  [bit this_bit Bit .#Bit bit.equivalence "bit"]
  [natural this_natural Natural .#Natural natural.equivalence "natural"]
  [integer this_integer Integer .#Integer integer.equivalence "integer"]
  [revolution this_revolution Revolution .#Revolution revolution.equivalence "revolution"]
  [decimal this_decimal Decimal .#Decimal decimal.equivalence "decimal"]
  [text this_text Text .#Text text.equivalence "text"]
  [symbol this_symbol Symbol .#Symbol symbol.equivalence "symbol"]
  )

(the .public (this code)
  (-> Code (Projection Any))
  (function (_ tokens)
    (when tokens
      {.#Item [token tokens']}
      (if (code.= code token)
        {try.#Success [tokens' []]}
        {try.#Failure (all text#composite "Expected a " (code.absolute code) " but instead got " (code.absolute token)
                           (remaining_inputs tokens))})

      _
      {try.#Failure ..empty})))

(expansion.let [<failure> (these {try.#Failure (all text#composite "Cannot parse local symbol" (remaining_inputs tokens))})]
  (these (the .public local
           (Projection Text)
           (function (_ tokens)
             (when tokens
               {.#Item [[_ {.#Symbol ["" x]}] tokens']}
               {try.#Success [tokens' x]}

               _
               <failure>)))

         (the .public (this_local expected)
           (-> Text (Projection Any))
           (function (_ tokens)
             (when tokens
               {.#Item [[_ {.#Symbol ["" actual]}] tokens']}
               (if (of text.equivalence = expected actual)
                 {try.#Success [tokens' []]}
                 <failure>)

               _
               <failure>)))))

(expansion.let [<failure> (these {try.#Failure (all text#composite "Cannot parse local symbol" (remaining_inputs tokens))})]
  (these (the .public global
           (Projection Symbol)
           (function (_ tokens)
             (when tokens
               {.#Item [[_ {.#Symbol ["" short]}] tokens']}
               <failure>
               
               {.#Item [[_ {.#Symbol it}] tokens']}
               {try.#Success [tokens' it]}

               _
               <failure>)))

         (the .public (this_global expected)
           (-> Symbol (Projection Any))
           (function (_ tokens)
             (when tokens
               {.#Item [[_ {.#Symbol ["" actual]}] tokens']}
               <failure>
               
               {.#Item [[_ {.#Symbol it}] tokens']}
               (if (of symbol.equivalence = expected it)
                 {try.#Success [tokens' []]}
                 <failure>)

               _
               <failure>)))))

(with_template' [<name> <tag> <desc>]
  [(the .public (<name> p)
     (for_any (_ a)
       (-> (Projection a) (Projection a)))
     (function (_ tokens)
       (when tokens
         {.#Item [[_ {<tag> members}] tokens']}
         (when (p members)
           {try.#Success [{.#End} x]} {try.#Success [tokens' x]}
           _                          {try.#Failure (all text#composite "Projection was expected to fully consume " <desc> (remaining_inputs tokens))})

         _
         {try.#Failure (all text#composite "Cannot parse " <desc> (remaining_inputs tokens))})))]

  [form .#Form "form"]
  [variant .#Variant "variant"]
  [tuple .#Tuple "tuple"]
  )

(the .public end
  (Projection Any)
  (function (_ tokens)
    (when tokens
      {.#End} {try.#Success [tokens []]}
      _       {try.#Failure (all text#composite "Expected list of tokens to be empty!" (remaining_inputs tokens))})))

(the .public end?
  (Projection Bit)
  (function (_ tokens)
    {try.#Success [tokens (when tokens
                            {.#End} true
                            _       false)]}))

(the .public (value projection inputs)
  (for_any (_ a) (-> (Projection a) (List Code) (Try a)))
  (when (projection inputs)
    {try.#Failure error}
    {try.#Failure error}

    {try.#Success [unconsumed value]}
    (when unconsumed
      {.#End}
      {try.#Success value}

      _
      {try.#Failure (|> unconsumed
                        (list#each code.absolute)
                        (text.interposed ", ")
                        (text#composite "Unconsumed inputs: "))})))

(the .public (locally inputs projection)
  (for_any (_ a) (-> (List Code) (Projection a) (Projection a)))
  (function (_ real)
    (do try.monad
      [value (..value projection inputs)]
      (in [real value]))))

(the .public (not projection)
  (for_any (_ a)
    (-> (Projection a)
        (Projection Code)))
  (do //.monad
    [sample ..next
     it (//.or projection
               ..any)]
    (when it
      {.#Left _} (//.failure (text#composite "Did NOT expect to parse code: " (code.absolute sample)))
      {.#Right output} (in output))))
