... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except function local variant tuple)
   [abstract
    ["//" projection]
    ["[0]" monad (.only do)]]
   [control
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   ["[0]" function]
   [data
    ["[0]" text (.use "[1]#[0]" monoid)
     ["%" \\injection]]
    [collection
     ["[0]" stack (.use "[1]#[0]" functor)]
     ["[0]" dictionary (.only Dictionary)]]]
   [math
    [number
     ["n" natural (.use "[1]#[0]" base_10)]]]
   [macro
    ["^" pattern]
    ["[0]" template]]
   [meta
    ["[0]" code]]]]
 [\\library
  ["[0]" / (.only)
   ["[0]" check]]])

(the .public recursion_parameter
  (template.macro (_)
    [{.#Nominal "" {.#Empty}}]))

(the .public recursive_definition
  (template.macro (_ ,type)
    [{.#Reification (..recursion_parameter)
                    {.#Universal (stack) ,type}}]))

(the .public recursive_iteration
  (template.macro (_)
    [{.#Reification (..recursion_parameter)
                    {.#Parameter 0}}]))

(template.with [<name>]
  [(exception.the .public (<name> type)
     (Exception Type)
     (exception.report
      (stack ["Type" (/.as_text type)])))]

  [not_existential]
  [not_recursive]
  [not_named]
  [not_parameter]
  [unknown_parameter]
  [not_function]
  [not_application]
  [not_polymorphic]
  [not_variant]
  [not_tuple]
  )

(template.with [<name>]
  [(exception.the .public (<name> [expected actual])
     (Exception [Type Type])
     (exception.report
      (stack ["Expected" (/.as_text expected)]
             ["Actual" (/.as_text actual)])))]

  [types_do_not_match]
  [wrong_parameter]
  )

(exception.the .public empty_input)

(exception.the .public (unconsumed_input remaining)
  (Exception (Stack Type))
  (exception.report
   (stack ["Types" (|> remaining
                       (stack#each (|>> /.as_text (text text.new_line "* ")))
                       (text.interposed ""))])))

(every .public Env
  (Dictionary Natural [Type Code]))

(every .public (Projection a)
  (//.Projection [Env (Stack Type)] a))

(the .public fresh
  Env
  (dictionary.empty n.hash))

(the (value' env poly types)
  (for_any (_ a) (-> Env (Projection a) (Stack Type) (Try a)))
  (when (//.value poly [env types])
    {try.#Failure error}
    {try.#Failure error}

    {try.#Success [[env' remaining] output]}
    (when remaining
      {.#Empty}
      {try.#Success output}

      _
      (exception.except ..unconsumed_input remaining))))

(the .public (value poly type)
  (for_any (_ a) (-> (Projection a) Type (Try a)))
  (value' ..fresh poly (stack type)))

(the .public env
  (Projection Env)
  (.function (_ [env inputs])
    {try.#Success [[env inputs] env]}))

(the .public arity
  (Projection Natural)
  (.function (_ [env inputs])
    {try.#Success [[env inputs] (dictionary.size env)]}))

(the (with_env temp poly)
  (for_any (_ a) (-> Env (Projection a) (Projection a)))
  (.function (_ [env inputs])
    (when (//.value poly [temp inputs])
      {try.#Failure error}
      {try.#Failure error}

      {try.#Success [[_ remaining] output]}
      {try.#Success [[env remaining] output]})))

(the .public next
  (Projection Type)
  (.function (_ [env inputs])
    (when inputs
      {.#Empty}
      (exception.except ..empty_input [])

      {.#Top headT tail}
      {try.#Success [[env inputs] headT]})))

(the .public any
  (Projection Type)
  (.function (_ [env inputs])
    (when inputs
      {.#Empty}
      (exception.except ..empty_input [])

      {.#Top headT tail}
      {try.#Success [[env tail] headT]})))

(the .public (local types poly)
  (for_any (_ a) (-> (Stack Type) (Projection a) (Projection a)))
  (.function (_ [env pass_through])
    (when (value' env poly types)
      {try.#Failure error}
      {try.#Failure error}

      {try.#Success output}
      {try.#Success [[env pass_through] output]})))

(the (label idx)
  (-> Natural Code)
  (code.local (all text#composite "label" text.tab (n#as idx))))

(the .public (with_extension type poly)
  (for_any (_ a) (-> Type (Projection a) (Projection [Code a])))
  (.function (_ [env inputs])
    (let [current_id (dictionary.size env)
          'var (label current_id)]
      (when (//.value poly
                      [(dictionary.has current_id [type 'var] env)
                       inputs])
        {try.#Failure error}
        {try.#Failure error}

        {try.#Success [[_ inputs'] output]}
        {try.#Success [[env inputs'] ['var output]]}))))

(template.with [<name> <flattener> <exception>]
  [(`` (the .public (<name> poly)
         (for_any (_ a) (-> (Projection a) (Projection a)))
         (do //.monad
           [headT ..any]
           (let [members (<flattener> (/.anonymous headT))]
             (if (n.> 1 (stack.size members))
               (local members poly)
               (//.failure (exception.error <exception> headT)))))))]

  [variant /.flat_variant ..not_variant]
  [tuple   /.flat_tuple   ..not_tuple]
  )

(the polymorphic'
  (Projection [Natural Type])
  (do //.monad
    [headT any
     .let [[num_arg bodyT] (/.flat_univ_q (/.anonymous headT))]]
    (if (n.= 0 num_arg)
      (//.failure (exception.error ..not_polymorphic headT))
      (in [num_arg bodyT]))))

(the .public (polymorphic poly)
  (for_any (_ a) (-> (Projection a) (Projection [Code (Stack Code) a])))
  (do [! //.monad]
    [headT any
     funcI (by ! each dictionary.size ..env)
     [num_args non_poly] (local (stack headT) ..polymorphic')
     env ..env
     .let [funcL (label funcI)
           [all_varsL env'] (loop (again [current_arg 0
                                          env' env
                                          all_varsL (is (Stack Code) (stack))])
                              (if (n.< num_args current_arg)
                                (if (n.= 0 current_arg)
                                  (let [varL (label (++ funcI))]
                                    (again (++ current_arg)
                                           (|> env'
                                               (dictionary.has funcI [headT funcL])
                                               (dictionary.has (++ funcI) [{.#Parameter (++ funcI)} varL]))
                                           {.#Top varL all_varsL}))
                                  (let [partialI (|> current_arg (n.* 2) (n.+ funcI))
                                        partial_varI (++ partialI)
                                        partial_varL (label partial_varI)
                                        partialC (` ((, funcL) (,* (|> (stack.indices num_args)
                                                                       (stack#each (|>> (n.* 2) ++ (n.+ funcI) label))
                                                                       stack.reversed))))]
                                    (again (++ current_arg)
                                           (|> env'
                                               (dictionary.has partialI [(recursion_parameter) partialC])
                                               (dictionary.has partial_varI [{.#Parameter partial_varI} partial_varL]))
                                           {.#Top partial_varL all_varsL})))
                                [all_varsL env']))]]
    (<| (with_env env')
        (local (stack non_poly))
        (do !
          [output poly]
          (in [funcL all_varsL output])))))

(the .public (function in_poly out_poly)
  (for_any (_ i o) (-> (Projection i) (Projection o) (Projection [i o])))
  (do //.monad
    [headT any
     .let [[inputsT outputT] (/.flat_function (/.anonymous headT))]]
    (if (n.> 0 (stack.size inputsT))
      (//.and (local inputsT in_poly)
              (local (stack outputT) out_poly))
      (//.failure (exception.error ..not_function headT)))))

(the .public (applied poly)
  (for_any (_ a) (-> (Projection a) (Projection a)))
  (do //.monad
    [headT any
     .let [[funcT paramsT] (/.flat_application (/.anonymous headT))]]
    (if (n.= 0 (stack.size paramsT))
      (//.failure (exception.error ..not_application headT))
      (..local {.#Top funcT paramsT} poly))))

(template.with [<name> <test>]
  [(the .public (<name> expected)
     (-> Type (Projection Any))
     (do //.monad
       [actual any]
       (if (<test> expected actual)
         (in [])
         (//.failure (exception.error ..types_do_not_match [expected actual])))))]

  [exactly /.=]
  [sub     check.<]
  [super   check.>]
  )

(the .public (argument env idx)
  (-> Env Natural Natural)
  (let [env_level (n./ 2 (dictionary.size env))
        parameter_level (n./ 2 idx)
        parameter_idx (n.% 2 idx)]
    (|> env_level -- (n.- parameter_level) (n.* 2) (n.+ parameter_idx))))

(the .public parameter
  (Projection Code)
  (do //.monad
    [env ..env
     headT any]
    (when headT
      {.#Parameter idx}
      (when (dictionary.value (..argument env idx) env)
        {try.#Success [poly_type poly_code]}
        (in poly_code)

        failure
        (//.failure (exception.error ..unknown_parameter headT)))

      _
      (//.failure (exception.error ..not_parameter headT)))))

(the .public (this_parameter id)
  (-> Natural (Projection Any))
  (do //.monad
    [env ..env
     headT any]
    (when headT
      {.#Parameter idx}
      (if (n.= id (..argument env idx))
        (in [])
        (//.failure (exception.error ..wrong_parameter [{.#Parameter id} headT])))

      _
      (//.failure (exception.error ..not_parameter headT)))))

(the .public existential
  (Projection Natural)
  (do //.monad
    [headT any]
    (when headT
      {.#Opaque ex_id}
      (in ex_id)

      _
      (//.failure (exception.error ..not_existential headT)))))

(the .public named
  (Projection [Name Type])
  (do //.monad
    [inputT any]
    (when inputT
      {.#Named name anonymousT}
      (in [name anonymousT])

      _
      (//.failure (exception.error ..not_named inputT)))))

(the .public (recursive poly)
  (for_any (_ a)
    (-> (Projection a)
        (Projection [Code a])))
  (do [! //.monad]
    [headT any]
    (when (/.anonymous headT)
      (..recursive_definition headT')
      (do !
        [[recT _ output] (|> poly
                             (with_extension (recursion_parameter))
                             (with_extension headT)
                             (local (stack headT')))]
        (in [recT output]))

      _
      (//.failure (exception.error ..not_recursive headT)))))

(the .public recursive_self
  (Projection Code)
  (do //.monad
    [env ..env
     headT any]
    (when (/.anonymous headT)
      (^.multi (..recursive_iteration)
               [(dictionary.value 0 env) {try.#Success [self_type self_call]}])
      (in self_call)

      _
      (//.failure (exception.error ..not_recursive headT)))))

(the .public recursive_call
  (Projection Code)
  (do [! //.monad]
    [env ..env
     [funcT argsT] (..applied (//.and any (//.many any)))
     _ (local (stack funcT) (..this_parameter 0))
     allC (let [allT (stack.partial funcT argsT)]
            (|> allT
                (monad.each ! (function.constant ..parameter))
                (local allT)))]
    (in (` ((,* allC))))))
