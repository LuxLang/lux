... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["//" projection]
    ["[0]" monad]]
   ["[0]" error (.only)
    ["[0]" try (.only Try)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" list]]]
   [aspect
    ["[0]" view]]]])

(every .public (Projection it)
  (//.Projection (Stack Text)
                 it))

(the .public (value inputs projection)
  (for_any (_ it)
    (-> (Stack Text) (Projection it)
        (Try it)))
  (when (//.value inputs projection)
    {try.:Success [remaining output]}
    (when remaining 
      {.:Empty}
      {try.:Success output}

      _
      {try.:Failure (text "Remaining CLI inputs: " (text.interposed " " (view.of list.stack remaining)))})

    {try.:Failure try}
    {try.:Failure try}))

(the .public any
  (Projection Text)
  (function (_ inputs)
    (when inputs
      {.:Top arg inputs'}
      {try.:Success [inputs' arg]}
      
      _
      {try.:Failure error.empty})))

(the .public (parse projection)
  (for_any (_ it)
    (-> (-> Text (Try it))
        (Projection it)))
  (function (_ inputs)
    (monad.let try.monad
      [[remaining raw] (any inputs)
       output (projection raw)]
      (pure [remaining output]))))

(the .public (this reference)
  (-> Text
      (Projection Any))
  (function (_ inputs)
    (monad.let try.monad
      [[remaining raw] (any inputs)]
      (if (text.= reference raw)
          (pure [remaining []])
          {try.:Failure (text "Missing token: '" reference "'")}))))

(the .public (somewhere cli)
  (for_any (_ it)
    (-> (Projection it)
        (Projection it)))
  (function (_ inputs)
    (loop (again [immediate inputs])
      (when (//.value immediate cli)
        {try.:Failure try}
        (when immediate
          {.:Empty}
          {try.:Failure try}
          
          {.:Top to_omit immediate'}
          (monad.let try.monad
            [[remaining output] (again immediate')]
            (pure [{.:Top to_omit remaining}
                   output])))

        success
        success))))

(the .public (named name value)
  (for_any (_ it)
    (-> Text (Projection it)
        (Projection it)))
  (|> value
      (//.after (..this name))
      ..somewhere))

(the .public (parameter [short long] value)
  (for_any (_ it)
    (-> [Text Text] (Projection it)
        (Projection it)))
  (|> value
      (//.after (//.either (..this short) (..this long)))
      ..somewhere))
