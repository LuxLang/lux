... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["//" projection]]
   [error (.only error)
    ["[0]" try (.only Try)]]
   [function
    [predicate (.only Predicate)]]
   [math
    [number
     ["[0]" natural]]]
   [meta
    ["[0]" name]]]]
 ["/" \\library])

(the Tape
  (type (for_any (_ it)
          [Natural (List it)])))

(the (fully_consumed? [so_far input])
  (for_any (_ it)
    (Predicate (Tape it)))
  (natural.= so_far (/.size input)))

(every .public (Projection list it)
  (//.Projection (Tape it) it))

(the with_template' (.in_module# .prelude .with_template))

(with_template' [,name]
  [(the .public ,name
     (error (name.proper (.name ,name))))]

  [not_consumed]
  [not_available]
  )

(the .public (value input it)
  (for_any (_ list it)
    (-> (List list) (Projection list it)
        (Try it)))
  (when (it [0 input])
    {try.#Success [after output]}
    (if (fully_consumed? after)
        {try.#Success output}
        {try.#Failure ..not_consumed})
    
    {try.#Failure error}
    {try.#Failure error}))

(the .public any
  (for_any (_ it)
    (Projection it it))
  (function (_ [next tape])
    (when (/.item next tape)
      {try.#Success it}
      {try.#Success [[(++ next) tape] it]}

      else
      {try.#Failure ..not_available})))
