... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except macro)
   [abstract
    ["//" projection]]
   ["[0]" error (.only)
    ["[0]" try (.only Try)]]
   [function
    [predicate (.only Predicate)]]
   [math
    [number
     [/64
      ["[0]" natural]]]]
   [meta
    ["[0]" name]]]]
 ["/" \\library])

(the macro (.in_module# .prelude template#macro))

(the Tape
  (macro (_ ,it)
    [[Natural (List ,it)]]))

(the (fully_consumed? [so_far input])
  (for_any (_ it)
    (Predicate (Tape it)))
  (natural.= so_far (/.size input)))

(every .public (Projection list it)
  (//.Projection (Tape list) it))

(error.the .public not_consumed)
(error.the .public not_available)

(the .public (remainder [so_far input])
  (for_any (_ it)
    (-> (Tape it)
        (List it)))
  (/.after so_far input))

(the .public (value input it)
  (for_any (_ list it)
    (-> (List list) (Projection list it)
        (Try it)))
  (when (it [0 input])
    {try.#Success [after output]}
    (if (fully_consumed? after)
        {try.#Success output}
        {try.#Failure ..not_consumed})
    
    {try.#Failure error}
    {try.#Failure error}))

(the .public any
  (for_any (_ it)
    (Projection it it))
  (function (_ [next tape])
    (when (/.item next tape)
      {try.#Success it}
      {try.#Success [[(++ next) tape] it]}

      else
      {try.#Failure ..not_available})))
