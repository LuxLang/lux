... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except left right)
   [abstract
    [monad (.only do)]]
   [control
    ["//" projection]
    ["[0]" try (.only Try)]
    ["[0]" exception]]]]
 [\\library
  [/ (.only Tree)
   ["[0]" zipper (.only Zipper)]]])

(every .public (Projection tree of)
  (//.Projection (Zipper tree) of))

(the .public (value' projection zipper)
  (All (_ tree of)
    (-> (Projection tree of) (Zipper tree)
        (Try of)))
  (do try.monad
    [[zipper output] (//.value projection zipper)]
    (in output)))

(the .public (value projection tree)
  (All (_ tree of)
    (-> (Projection tree of) (Tree tree)
        (Try of)))
  (value' projection (zipper.zipper tree)))

(the .public here
  (All (_ tree)
    (Projection tree tree))
  (function (_ zipper)
    {try.#Success [zipper (zipper.value zipper)]}))

(exception.the .public cannot_move_further)

(with_template [<name> <direction>]
  [(the .public <name>
     (All (_ tree)
       (Projection tree Any))
     (function (_ zipper)
       (when (<direction> zipper)
         {try.#Success next}
         {try.#Success [next []]}

         failure
         (exception.except ..cannot_move_further []))))]

  [down      zipper.down]
  [up        zipper.up]

  [right     zipper.right]
  [rightmost zipper.rightmost]

  [left      zipper.left]
  [leftmost  zipper.leftmost]
  
  [next      zipper.next]
  [end       zipper.end]
  
  [previous  zipper.previous]
  [start     zipper.start]
  )
