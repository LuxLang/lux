... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["//" projection]]
   [error
    ["[0]" try (.only Try) (.use "[1]#[0]" functor)]
    ["[0]" exception]]
   [data
    ["[0]" product]]
   [macro
    ["[0]" template]]]]
 [\\library
  [/ (.only Tree)
   ["[0]" zipper (.only Zipper)]]])

(every .public (Projection tree it)
  (//.Projection (Zipper tree) it))

(the .public (value' projection zipper)
  (for_any (_ tree it)
    (-> (Projection tree it) (Zipper tree)
        (Try it)))
  (|> projection
      (//.value zipper)
      (try#each product.right)))

(the .public (value projection tree)
  (for_any (_ tree it)
    (-> (Projection tree it) (Tree tree)
        (Try it)))
  (value' projection (zipper.zipper tree)))

(the .public here
  (for_any (_ tree)
    (Projection tree tree))
  (function (_ zipper)
    {try.#Success [zipper (zipper.value zipper)]}))

(exception.the .public cannot_move_further)

(template.with [<name> <direction>]
  [(the .public <name>
     (for_any (_ tree)
       (Projection tree Any))
     (function (_ zipper)
       (when (<direction> zipper)
         {try.#Success next}
         {try.#Success [next []]}

         failure
         (exception.except ..cannot_move_further []))))]

  [down      zipper.down]
  [up        zipper.up]

  [right     zipper.right]
  [rightmost zipper.rightmost]

  [left      zipper.left]
  [leftmost  zipper.leftmost]
  
  [next      zipper.next]
  [end       zipper.end]
  
  [previous  zipper.previous]
  [start     zipper.start]
  )
