... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["//" projection]]
   [error
    ["[0]" try (.only Try)]
    ["[0]" exception]]
   [function
    [predicate (.only Predicate)]]]]
 ["/" \\library])

(every .public (Projection stack it)
  (//.Projection (Stack stack) it))

(exception.the .public invalid)

(exception.the .public empty)

(the .public any
  (for_any (_ it)
    (Projection it it))
  (function (_ state)
    (when state
      (/.partial head tail)
      {try.#Success [tail head]}

      (stack)
      (exception.except ..empty []))))

(the .public (one it)
  (for_any (_ it)
    (-> (Predicate it)
        (Projection it it)))
  (do //.monad
    [head ..any]
    (if (it head)
      (in head)
      (//.of_try (exception.except ..invalid [])))))

(exception.the .public left_over)

(the .public (value it input)
  (for_any (_ stack it)
    (-> (Projection stack it) (Stack stack)
        (Try it)))
  (when (it input)
    {try.#Success [remainder output]}
    (when remainder
      (stack)
      {try.#Success output}

      _
      (exception.except ..left_over []))
    
    {try.#Failure error}
    {try.#Failure error}))
