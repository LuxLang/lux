... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except and or text natural integer revolution list name type)
   [ffi (.only)]
   [abstract
    [hash (.only Hash)]
    [monad (.only do)]
    ["//" projection (.use "[1]#[0]" monad)]]
   [control
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["/" binary
     ["[1]" \\unsafe (.only Binary)]]
    [text
     ["%" \\injection]
     [encoding
      ["[0]" utf8]]]
    [collection
     ["[0]" list]
     ["[0]" sequence (.only Sequence)]
     ["[0]" set (.only Set)]
     [array
      [\\unsafe (.only)]]]]
   [math
    [number
     ["n" natural]
     ["[0]" decimal]]]
   [macro
    ["^" pattern]
    ["[0]" template]
    ["[0]" expansion]]
   [type (.only sharing)]
   [time
    ["[0]" instant (.only Instant)]
    ["[0]" duration (.only Duration)]]]])

(.every .public Offset
  Natural)

(.every .public Projection
  (//.Projection [Offset Binary]))

(exception.the .public (binary_was_not_fully_read [binary_length bytes_read])
  (Exception [Natural Natural])
  (exception.report
   (.list ["Binary length" (%.natural binary_length)]
          ["Bytes read" (%.natural bytes_read)])))

(template.with [<name> <extension>]
  [(the <name>
     (template.macro (<name> <parameter> <subject>)
       [(<extension> <parameter> <subject>)]))]

  [n#= .i64_=#]
  [n#+ .i64_+#]
  [n#- .i64_-#]
  )

(the .public (value projection input)
  (for_any (_ of)
    (-> (Projection of) Binary
        (Try of)))
  (when (projection [0 input])
    {try.#Success [[end _] output]}
    (let [length (/.size input)]
      (if (n#= end length)
        {try.#Success output}
        (exception.except ..binary_was_not_fully_read [length end])))

    failure
    (as_expected failure)))

(the .public end?
  (Projection Bit)
  (function (_ (^.let input [offset data]))
    {try.#Success [input (n#= offset (/.size data))]}))

(the .public offset
  (Projection Offset)
  (function (_ (^.let input [offset data]))
    {try.#Success [input offset]}))

(the .public remaining
  (Projection Natural)
  (function (_ (^.let input [offset data]))
    {try.#Success [input (n#- offset (/.size data))]}))

(.every .public Size
  Natural)

(the .public size_08 Size 1)
(the .public size_16 Size (n.* 2 size_08))
(the .public size_32 Size (n.* 2 size_16))
(the .public size_64 Size (n.* 2 size_32))

(exception.the .public (range_out_of_bounds [length start end])
  (Exception [Natural Natural Natural])
  (exception.report
   (.list ["Length" (%.natural length)]
          ["Range start" (%.natural start)]
          ["Range end" (%.natural end)])))

(template.with [<name> <size> <read>]
  [(the .public <name>
     (Projection I64)
     (function (_ [start binary])
       (let [end (n#+ <size> start)]
         (if (n.< end (/.size binary))
           (exception.except ..range_out_of_bounds [(/.size binary) start end])
           (|> (<read> start binary)
               [[end binary]]
               {try.#Success})))))]

  [bits_08 ..size_08 /.bits_08]
  [bits_16 ..size_16 /.bits_16]
  [bits_32 ..size_32 /.bits_32]
  [bits_64 ..size_64 /.bits_64]
  )

(template.with [<name> <type>]
  [(the .public <name>
     (Projection <type>)
     ..bits_64)]

  [natural Natural]
  [integer Integer]
  [revolution Revolution]
  )

(the .public decimal
  (Projection Decimal)
  (//#each decimal.of_bits ..bits_64))

(exception.the .public (invalid_tag [range byte])
  (Exception [Natural Natural])
  (exception.report
   (.list ["Tag range" (%.natural range)]
          ["Tag value" (%.natural byte)])))

(the !variant
  (template.macro (!variant <case>+)
    [(do [! //.monad]
       [flag (is (Projection Natural)
                 ..bits_08)]
       (expansion.let [<case>+' (template.spliced <case>+)]
         (`` (when flag
               (,, (template.with [<number> <tag> <projection>]
                     [<number> (`` (by ! each (|>> {(,, (template.spliced <tag>))}) <projection>))]

                     <case>+'))
               
               _ (//.of_try (exception.except ..invalid_tag [(template.amount [<case>+]) flag]))))))]))

(the .public (or left right)
  (for_any (_ left right)
    (-> (Projection left) (Projection right)
        (Projection (Or left right))))
  (!variant [[0 [.#Left] left]
             [1 [.#Right] right]]))

(the .public (rec body)
  (for_any (_ of)
    (-> (-> (Projection of) (Projection of))
        (Projection of)))
  (function (_ input)
    (let [projection (body (rec body))]
      (projection input))))

(the .public any
  (Projection Any)
  (//#in []))

(exception.the .public (not_a_bit value)
  (Exception Natural)
  (exception.report
   (.list ["Expected values" "either 0 or 1"]
          ["Actual value" (%.natural value)])))

(the .public bit
  (Projection Bit)
  (do //.monad
    [value (is (Projection Natural)
               ..bits_08)]
    (when value
      0 (in #0)
      1 (in #1)
      _ (//.of_try (exception.except ..not_a_bit [value])))))

(the .public (segment size)
  (-> Natural
      (Projection Binary))
  (when size
    0 (//#in (/.empty 0))
    _ (function (_ [start binary])
        (let [end (n#+ size start)]
          (if (n.< end (/.size binary))
            (exception.except ..range_out_of_bounds [(/.size binary) start end])
            (|> binary
                (/.slice start size)
                [[end binary]]
                {try.#Success}))))))

(template.with [<size> <name> <bits>]
  [(`` (the .public <name>
         (Projection Binary)
         (do //.monad
           [size (//#each (|>> .natural) <bits>)]
           (..segment size))))]

  [08 binary_08 ..bits_08]
  [16 binary_16 ..bits_16]
  [32 binary_32 ..bits_32]
  [64 binary_64 ..bits_64]
  )

(template.with [<size> <name> <binary>]
  [(`` (the .public <name>
         (Projection Text)
         (do //.monad
           [utf8 <binary>]
           (//.of_try (by utf8.binary of utf8)))))]

  [08 utf8_08 ..binary_08]
  [16 utf8_16 ..binary_16]
  [32 utf8_32 ..binary_32]
  [64 utf8_64 ..binary_64]
  )

(the .public text ..utf8_64)

(template.with [<size> <name> <bits>]
  [(the .public (<name> valueP)
     (for_any (_ of)
       (-> (Projection of)
           (Projection (Sequence of))))
     (do //.monad
       [amount (is (Projection Natural)
                   <bits>)]
       (function (_ state)
         (loop (again [state state
                       index 0
                       output (sharing [of]
                                (is (Projection of)
                                    valueP)
                                (is (Sequence of)
                                    sequence.empty))])
           (if (n.< amount index)
             (when (valueP state)
               {try.#Success [state value]}
               (again state
                      (.++ index)
                      (sequence.suffix value output))

               {try.#Failure error}
               {try.#Failure error})
             {try.#Success [state output]})))))]

  [08 sequence_08 ..bits_08]
  [16 sequence_16 ..bits_16]
  [32 sequence_32 ..bits_32]
  [64 sequence_64 ..bits_64]
  )

(the .public maybe
  (for_any (_ of)
    (-> (Projection of)
        (Projection (Maybe of))))
  (..or ..any))

(the .public (list value)
  (for_any (_ of)
    (-> (Projection of)
        (Projection (List of))))
  (..rec
   (|>> (//.and value)
        (..or ..any))))

(exception.the .public set_elements_are_not_unique)

(the .public (set hash value)
  (for_any (_ of)
    (-> (Hash of) (Projection of)
        (Projection (Set of))))
  (do //.monad
    [raw (..list value)
     .let [output (set.of_list hash raw)]
     _ (//.assertion (exception.error ..set_elements_are_not_unique [])
                     (n#= (list.size raw)
                          (set.size output)))]
    (in output)))

(the .public name
  (Projection Name)
  (//.and ..text ..text))

(the .public type
  (Projection Type)
  (..rec
   (function (_ type)
     (let [pair (//.and type type)
           indexed ..natural
           quantified (//.and (..list type) type)]
       (!variant [[00 [.#Nominal] (//.and ..text (..list type))]
                  [01 [.#Sum] pair]
                  [02 [.#Product] pair]
                  [03 [.#Function] pair]
                  [04 [.#Parameter] indexed]
                  [05 [.#Variable] indexed]
                  [06 [.#Opaque] indexed]
                  [07 [.#Universal] quantified]
                  [08 [.#Existential] quantified]
                  [09 [.#Reification] pair]
                  [10 [.#Named] (//.and ..name type)]])))))

(the .public provenance
  (Projection Provenance)
  (all //.and ..text ..natural ..natural))

(the .public code
  (Projection Code)
  (..rec
   (function (_ again)
     (let [sequence (..list again)]
       (//.and ..provenance
               (!variant [[0 [.#Bit] ..bit]
                          [1 [.#Natural] ..natural]
                          [2 [.#Integer] ..integer]
                          [3 [.#Revolution] ..revolution]
                          [4 [.#Decimal] ..decimal]
                          [5 [.#Text] ..text]
                          [6 [.#Name] ..name]
                          [7 [.#Form] sequence]
                          [8 [.#Variant] sequence]
                          [9 [.#Tuple] sequence]]))))))

(the .public instant
  (Projection Instant)
  (by //.monad each instant.of_millis ..integer))

(the .public duration
  (Projection Duration)
  (by //.monad each duration.of_millis ..integer))
