... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Tag)
   [abstract
    [monad (.only do)]]
   [control
    ["//" projection]
    ["[0]" try (.only Try) (.use "[1]#[0]" functor)]
    ["[0]" exception (.only Exception)]]
   [data
    [text
     ["%" \\injection]]
    [collection
     ["[0]" list]
     ["[0]" dictionary]]]
   [meta
    ["[0]" symbol (.use "[1]#[0]" absolute)]]]]
 [\\library
  ["[0]" / (.only Attribute Attrs Tag XML)]])

(every .public (Projection of)
  (//.Projection [Attrs (List XML)] of))

(exception.the .public empty_input)
(exception.the .public unexpected_input)

(exception.the .public (wrong_tag [expected actual])
  (Exception [Tag Tag])
  (exception.report
   (list ["Expected" (%.text (/.tag expected))]
         ["Actual" (%.text (/.tag actual))])))

(exception.the .public (unknown_attribute [expected available])
  (Exception [Attribute (List Attribute)])
  (exception.report
   (list ["Expected" (%.text (/.attribute expected))]
         ["Available" (exception.listing (|>> /.attribute %.text) available)])))

(exception.the .public (unconsumed_inputs inputs)
  (Exception (List XML))
  (exception.report
   (list ["Inputs" (exception.listing (by /.format injection) inputs)])))

(the (value' projection attrs documents)
  (for_any (_ of)
    (-> (Projection of) Attrs (List XML)
        (Try of)))
  (when (//.value projection [attrs documents])
    {try.#Success [[attrs' remaining] output]}
    (if (list.empty? remaining)
      {try.#Success output}
      (exception.except ..unconsumed_inputs remaining))
    
    {try.#Failure error}
    {try.#Failure error}))

(the .public (value projection documents)
  (for_any (_ of)
    (-> (Projection of) (List XML)
        (Try of)))
  (..value' projection /.attributes documents))

(the .public text
  (Projection Text)
  (function (_ [attrs documents])
    (when documents
      {.#End}
      (exception.except ..empty_input [])
      
      {.#Item head tail}
      (when head
        {/.#Text value}
        {try.#Success [[attrs tail] value]}
        
        {/.#Node _}
        (exception.except ..unexpected_input [])))))

(the .public tag
  (Projection Tag)
  (function (_ [attrs documents])
    (when documents
      {.#End}
      (exception.except ..empty_input [])
      
      {.#Item head _}
      (when head
        {/.#Text _}
        (exception.except ..unexpected_input [])
        
        {/.#Node tag _ _}
        {try.#Success [[attrs documents] tag]}))))

(the .public (attribute name)
  (-> Attribute
      (Projection Text))
  (function (_ [attrs documents])
    (when (dictionary.value name attrs)
      {try.#Success value}
      {try.#Success [[attrs documents] value]}

      failure
      (exception.except ..unknown_attribute [name (dictionary.keys attrs)]))))

(the .public (node expected projection)
  (for_any (_ of)
    (-> Tag (Projection of)
        (Projection of)))
  (function (_ [attrs documents])
    (when documents
      {.#End}
      (exception.except ..empty_input [])
      
      {.#Item head tail}
      (when head
        {/.#Text _}
        (exception.except ..unexpected_input [])
        
        {/.#Node actual attrs' children}
        (if (symbol.= expected actual)
          (|> children
              (..value' projection attrs')
              (try#each (|>> [[attrs tail]])))
          (exception.except ..wrong_tag [expected actual]))))))

(the .public any
  (Projection XML)
  (function (_ [attrs documents])
    (when documents
      {.#End}
      (exception.except ..empty_input [])
      
      {.#Item head tail}
      {try.#Success [[attrs tail] head]})))

(exception.the .public nowhere)

(the .public (somewhere projection)
  (for_any (_ of)
    (-> (Projection of)
        (Projection of)))
  (function (again [attrs input])
    (when (//.value projection [attrs input])
      {try.#Success [[attrs remaining] output]}
      {try.#Success [[attrs remaining] output]}
      
      {try.#Failure error}
      (when input
        {.#End}
        (exception.except ..nowhere [])
        
        {.#Item head tail}
        (do try.monad
          [[[attrs tail'] output] (again [attrs tail])]
          (in [[attrs {.#Item head tail'}]
               output]))))))
