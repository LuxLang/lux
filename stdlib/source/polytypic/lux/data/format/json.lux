... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    ["[0]" codec]]
   [control
    ["[0]" try]]
   [data
    [collection
     [dictionary (.only Dictionary)]]]
   [meta
    [type
     ["[0]" poly (.only polytypic)]]]
   [world
    [time
     ["[0]" instant]
     ["[0]" duration]
     ["[0]" date]
     ["[0]" day]
     ["[0]" month]]]]]
 [\\library
  ["[0]" /]])

(the .public codec
  (polytypic
    (` (codec.Codec /.JSON))
    [poly.#sum (poly.composite (` /.sum))
     poly.#product (poly.composite (` /.product))
     poly.#function poly.not_applicable
     poly.#apply {.#None}
     poly.#recursive (function (_ code ['*self body])
                       (do try.monad
                         ['body (code body)]
                         (in (` (/.recursive
                                 (.function ((,* '*self) (,* '*self))
                                   (, 'body)))))))
     poly.#recursion {.#None}
     poly.#any {.#Some (` /.any)}
     poly.#polymorphic {.#None}
     poly.#parameter {.#None}]
    (list [Bit (` /.bit)]
          [Nat (` /.nat)]
          [Int (` /.int)]
          [Rev (` /.rev)]
          [Frac (` /.frac)]
          [Text (` /.text)]
          [duration.Duration (` /.duration)]
          [instant.Instant (` /.instant)]
          [date.Date (` /.date)]
          [day.Day (` /.day)]
          [month.Month (` /.month)]

          [(type (Dictionary Text)) (` /.dictionary)]
          [List (` /.list)]
          )))
