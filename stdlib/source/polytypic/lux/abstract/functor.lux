... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [error
    ["[0]" try]]
   [data
    [collection
     ["[0]" list]]]
   [math
    [number
     ["[0]" natural]]]
   [type
    ["[0]" poly]]]]
 [\\library
  ["[0]" /]])

(the .public functor
  (poly.macro
    (` /.Functor)
    [poly.#sum (poly.nested (` /.sum))
     poly.#product (poly.nested (` /.product))
     poly.#function (function (_ code [left right])
                      (do try.monad
                        [right (code right)]
                        (in (` (/.function (, right))))))
     poly.#reification {.#None}
     poly.#recursive poly.not_applicable
     poly.#recursion {.#Some (function (_ code ['*recursive])
                               {try.#Success
                                (` ((,* (list.as_stack '*recursive))))})}
     poly.#any {.#Some (` /.constant)}
     poly.#polymorphic {.#Some (function (_ code ['*self arity it])
                                 (do try.monad
                                   ['it (code it)]
                                   (in (` (/.recursive
                                           (function ((,* (list.as_stack '*self))
                                                      (,* (list.as_stack '*self)))
                                             (, 'it)))))))}
     poly.#parameter {.#Some (function (_ code [arity it])
                               {try.#Success
                                (if (natural.= 1 it)
                                  (` /.variable)
                                  (` /.constant))})}
     poly.#specializations (list)]))
