... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try]]
   [math
    [number
     ["[0]" nat]]]
   [meta
    [type
     ["[0]" poly (.only polytypic)]]]]]
 [\\library
  ["[0]" /]])

(the .public functor
  (polytypic
    (` /.Functor)
    [poly.#sum (poly.composite (` /.sum))
     poly.#product (poly.composite (` /.product))
     poly.#function (function (_ code [left right])
                      (do try.monad
                        [right (code right)]
                        (in (` (/.function (, right))))))
     poly.#apply {.#None}
     poly.#recursive poly.not_applicable
     poly.#recursion {.#Some (function (_ code ['*recursive])
                               {try.#Success
                                (` ((,* '*recursive)))})}
     poly.#any {.#Some (` /.constant)}
     poly.#polymorphic {.#Some (function (_ code ['*self arity it])
                                 (do try.monad
                                   ['it (code it)]
                                   (in (` (/.recursive
                                           (function ((,* '*self) (,* '*self))
                                             (, 'it)))))))}
     poly.#parameter {.#Some (function (_ code [arity it])
                               {try.#Success
                                (if (nat.= 1 it)
                                  (` /.variable)
                                  (` /.constant))})}]
    (list)))
