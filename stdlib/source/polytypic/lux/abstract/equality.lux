... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad]]
   [error
    ["[0]" try]]
   [data
    ["[0]" maybe]
    ["[0]" any]
    ["[0]" text]
    [collection
     ["[0]" list]
     ["[0]" sequence]
     ["[0]" array]
     ["[0]" queue]
     ["[0]" set]
     ["[0]" dictionary (.only Dictionary)]
     ["[0]" tree]]]
   [logic
    ["[0]" bit]]
   [math
    [number
     [/64
      ["[0]" natural]
      ["[0]" integer]
      ["[0]" revolution]
      ["[0]" decimal]]]]
   [type
    ["[0]" poly]
    ["[0]" unit]]
   [time
    ["[0]" duration]
    ["[0]" date]
    ["[0]" instant]
    ["[0]" day]
    ["[0]" month]]]]
 [\\library
  ["[0]" /]])

(the .public equality
  (poly.macro
    (` /.Equality)
    [poly.:sum (poly.nested (` /.sum))
     poly.:product (poly.nested (` /.product))
     poly.:function poly.not_applicable
     poly.:reification {.:None}
     poly.:recursive (function (_ code ['*self body])
                       (monad.let try.monad
                         ['body (code body)]
                         (pure (` (/.recursive
                                   (.function ((,* '*self) (,* '*self))
                                     (, 'body)))))))
     poly.:recursion {.:None}
     poly.:any {.:Some (` any.equality)}
     poly.:polymorphic {.:None}
     poly.:parameter {.:None}
     poly.:specializations (list [Bit (` bit.equality)]
                                 [Natural (` natural.equality)]
                                 [Integer (` integer.equality)]
                                 [Revolution (` revolution.equality)]
                                 [Decimal (` decimal.equality)]
                                 [Text (` text.equality)]
                                 [duration.Duration (` duration.equality)]
                                 [instant.Instant (` instant.equality)]
                                 [date.Date (` date.equality)]
                                 [day.Day (` day.equality)]
                                 [month.Month (` month.equality)]

                                 [.Maybe (` maybe.equality)]
                                 [.List (` list.equality)]
                                 [sequence.Sequence (` sequence.equality)]
                                 [.Array (` array.equality)]
                                 [queue.Queue (` queue.equality)]
                                 [set.Set (` set.equality)]
                                 [tree.Tree (` tree.equality)]
                                 [(type (dictionary.Dictionary Any)) (` dictionary.equality)]
                                 [(type (unit.Measure Any)) (` unit.equality)])]))
