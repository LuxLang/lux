(.module:
  [lux (#- static)
   [abstract
    [monoid (#+)]
    ["." equivalence (#+ Equivalence)]
    ["." monad (#+ do)]]
   [control
    ["." try]
    ["." state (#+ State)]
    ["<>" parser
     ["<2>" binary (#+ Parser)]]]
   [data
    [number (#+)
     [i64 (#+)]]
    [format
     [".F" binary (#+ Writer) ("#@." monoid)]]
    [collection
     ["." row (#+ Row)]]]
   [type
    [abstract (#+)]]]
  ["." // #_
   ["#." modifier (#+ Modifier modifiers:)]
   ["#." index (#+ Index)]
   ["#." descriptor (#+ Descriptor)]
   ["#." attribute (#+ Attribute)
    ["#/." code]]
   ["#." constant (#+ UTF8)
    ["#/." pool (#+ Pool)]]
   ["#." program (#+ Program)
    ["#/." condition]
    ["#/." instruction]]])

(type: #export #rec Method
  {#modifier (Modifier Method)
   #name (Index UTF8)
   #descriptor (Index (Descriptor //descriptor.Method))
   #attributes (Row Attribute)})

(modifiers: Method
  ["0001" public]
  ["0002" private]
  ["0004" protected]
  ["0008" static]
  ["0010" final]
  ["0020" synchronized]
  ["0040" bridge]
  ["0080" var-args]
  ["0100" native]
  ["0400" abstract]
  ["0800" strict]
  ["1000" synthetic]
  )

(def: #export (method modifier name descriptor attributes code)
  (-> (Modifier Method) UTF8 (Descriptor //descriptor.Method) (List (State Pool Attribute)) (Program Any)
      (State Pool Method))
  (do state.monad
    [@name (//constant/pool.utf8 name)
     @descriptor (//constant/pool.descriptor descriptor)
     attributes (monad.seq @ attributes)
     ?code (//program.resolve code)
     [environment bytecode] (case (do try.monad
                                    [[instruction output] ?code
                                     [environment specification] (//program/instruction.run instruction)]
                                    (wrap [environment (binaryF.instance specification)]))
                              (#try.Success [environment bytecode])
                              (wrap [environment bytecode])
                              
                              (#try.Failure error)
                              ## TODO: Allow error-management within
                              ## the monad.
                              (undefined))
     @code (//attribute.code {#//attribute/code.resources (get@ #//program/condition.resources environment)
                              #//attribute/code.code bytecode
                              #//attribute/code.exception-table (row.row)
                              #//attribute/code.attributes (row.row)})]
    (wrap {#modifier modifier
           #name @name
           #descriptor @descriptor
           #attributes (|> attributes row.from-list (row.add @code))})))

(def: #export equivalence
  (Equivalence Method)
  ($_ equivalence.product
      //modifier.equivalence
      //index.equivalence
      //index.equivalence
      (row.equivalence //attribute.equivalence)))

(def: #export (parser pool)
  (-> Pool (Parser Method))
  ($_ <>.and
      //modifier.parser
      //index.parser
      //index.parser
      (<2>.row/16 (//attribute.parser pool))))

(def: #export (writer field)
  (Writer Method)
  (`` ($_ binaryF@compose
          (~~ (template [<writer> <slot>]
                [(<writer> (get@ <slot> field))]

                [//modifier.writer #modifier]
                [//index.writer #name]
                [//index.writer #descriptor]
                [(binaryF.row/16 //attribute.writer) #attributes]))
          )))
