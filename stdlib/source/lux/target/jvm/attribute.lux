(.module:
  [lux (#- Info Code)
   [abstract
    [monad (#+ do)]
    ["." equivalence (#+ Equivalence)]]
   [control
    ["." state (#+ State)]
    ["." exception (#+ exception:)]
    ["<>" parser]]
   [data
    ["." product]
    ["." error]
    [format
     [".F" binary (#+ Reader Writer Format) ("#@." monoid)]]]]
  ["." // #_
   ["#." index (#+ Index)]
   [encoding
    ["#." unsigned (#+ U2 U4)]]
   ["#." constant (#+ UTF8 Class Value)
    ["#/." pool (#+ Pool)]]]
  ["." / #_
   ["#." constant (#+ Constant)]
   ["#." code]])

(type: #export (Info about)
  {#name (Index UTF8)
   #length U4
   #info about})

(def: #export (info-equivalence Equivalence<about>)
  (All [about]
    (-> (Equivalence about)
        (Equivalence (Info about))))
  ($_ equivalence.product
      //index.equivalence
      //unsigned.equivalence
      Equivalence<about>))

(def: (info-writer writer)
  (All [about]
    (-> (Writer about)
        (Writer (Info about))))
  (function (_ [name length info])
    (let [[nameS nameT] ((get@ #binaryF.writer //index.format) name)
          [lengthS lengthT] ((get@ #binaryF.writer //unsigned.u4-format) length)
          [infoS infoT] (writer info)]
      [($_ n/+ nameS lengthS infoS)
       (|>> nameT lengthT infoT)])))

(with-expansions [<Code> (as-is (/code.Code Attribute))]
  (type: #export #rec Attribute
    (#Constant (Info Constant))
    (#Code (Info <Code>)))

  (type: #export Code
    <Code>)
  )

(def: #export equivalence
  (Equivalence Attribute)
  (equivalence.rec
   (function (_ equivalence)
     ($_ equivalence.sum
         (info-equivalence /constant.equivalence)
         (info-equivalence (/code.equivalence equivalence))))))

(def: fixed-attribute-length
  ($_ n/+
      ## u2 attribute_name_index;
      //unsigned.u2-bytes
      ## u4 attribute_length;
      //unsigned.u4-bytes
      ))

(def: (length attribute)
  (-> Attribute Nat)
  (case attribute
    (^template [<tag>]
      (<tag> [name length info])
      (|> length //unsigned.nat .nat (n/+ fixed-attribute-length)))
    ([#Constant] [#Code])))

(def: constant-name "ConstantValue")

(def: (constant' @name index)
  (-> (Index UTF8) Constant Attribute)
  (#Constant {#name @name
              #length (//unsigned.u4 /constant.length)
              #info index}))

(def: #export (constant index)
  (-> Constant (State Pool Attribute))
  (do state.monad
    [@name (//constant/pool.utf8 ..constant-name)]
    (wrap (constant' @name index))))

(def: code-name "Code")

(def: (code' @name specification)
  (-> (Index UTF8) Code Attribute)
  (#Code {#name @name
          ## https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-4.html#jvms-4.7.3
          #length (//unsigned.u4
                   (/code.length ..length specification))
          #info specification}))

(def: #export (code specification)
  (-> Code (State Pool Attribute))
  (do state.monad
    [@name (//constant/pool.utf8 ..code-name)]
    (wrap (code' @name specification))))

(exception: #export invalid-attribute)

(def: #export (reader pool)
  (-> Pool (Reader Attribute))
  (let [?@constant (|> ..constant-name
                       //constant/pool.find-utf8
                       (state.run pool)
                       product.right)
        ?@code (|> ..code-name
                   //constant/pool.find-utf8
                   (state.run pool)
                   product.right)
        (^open "_@.") (error.equivalence //index.equivalence)]
    (<>.rec
     (function (_ reader)
       (do <>.monad
         [@name (get@ #binaryF.reader //index.format)
          length (get@ #binaryF.reader //unsigned.u4-format)]
         (cond (_@= ?@constant (#error.Success @name))
               (:: @ map (..constant' @name) (get@ #binaryF.reader /constant.format))
               
               (_@= ?@code (#error.Success @name))
               (:: @ map (..code' @name) (/code.reader reader))

               ## else
               (<>.fail (exception.construct ..invalid-attribute []))))))))

(def: #export (writer value)
  (Writer Attribute)
  (case value
    (#Constant attribute)
    ((info-writer (get@ #binaryF.writer /constant.format))
     attribute)
    
    (#Code attribute)
    ((info-writer (/code.writer writer))
     attribute)))
