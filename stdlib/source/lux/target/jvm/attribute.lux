(.module:
  [lux (#- Info Code)
   [abstract
    [monad (#+ do)]
    ["." equivalence (#+ Equivalence)]]
   [control
    ["." try]
    ["." exception (#+ exception:)]]
   [data
    ["." product]
    [number
     ["n" nat]]
    [format
     [".F" binary (#+ Writer) ("#@." monoid)]]]]
  ["." // #_
   ["#." index (#+ Index)]
   [encoding
    ["#." unsigned (#+ U2 U4)]]
   ["#." constant (#+ UTF8 Class Value)
    ["#/." pool (#+ Pool Resource)]]]
  ["." / #_
   ["#." constant (#+ Constant)]
   ["#." code]])

(type: #export (Info about)
  {#name (Index UTF8)
   #length U4
   #info about})

(def: #export (info-equivalence Equivalence<about>)
  (All [about]
    (-> (Equivalence about)
        (Equivalence (Info about))))
  ($_ equivalence.product
      //index.equivalence
      //unsigned.equivalence
      Equivalence<about>))

(def: (info-writer writer)
  (All [about]
    (-> (Writer about)
        (Writer (Info about))))
  (function (_ [name length info])
    (let [[nameS nameT] (//index.writer name)
          [lengthS lengthT] (//unsigned.writer/4 length)
          [infoS infoT] (writer info)]
      [($_ n.+ nameS lengthS infoS)
       (|>> nameT lengthT infoT)])))

(with-expansions [<Code> (as-is (/code.Code Attribute))]
  (type: #export #rec Attribute
    (#Constant (Info Constant))
    (#Code (Info <Code>)))

  (type: #export Code
    <Code>)
  )

(def: #export equivalence
  (Equivalence Attribute)
  (equivalence.rec
   (function (_ equivalence)
     ($_ equivalence.sum
         (info-equivalence /constant.equivalence)
         (info-equivalence (/code.equivalence equivalence))))))

(def: fixed-attribute-length
  ($_ n.+
      ## u2 attribute_name_index;
      //unsigned.bytes/2
      ## u4 attribute_length;
      //unsigned.bytes/4
      ))

(def: (length attribute)
  (-> Attribute Nat)
  (case attribute
    (^template [<tag>]
      (<tag> [name length info])
      (|> length //unsigned.value (n.+ fixed-attribute-length)))
    ([#Constant] [#Code])))

(def: constant-name "ConstantValue")

(def: (constant' @name index)
  (-> (Index UTF8) Constant Attribute)
  (#Constant {#name @name
              #length (|> /constant.length //unsigned.u4 try.assume)
              #info index}))

(def: #export (constant index)
  (-> Constant (Resource Attribute))
  (do //constant/pool.monad
    [@name (//constant/pool.utf8 ..constant-name)]
    (wrap (constant' @name index))))

(def: code-name "Code")

(def: (code' @name specification)
  (-> (Index UTF8) Code Attribute)
  (#Code {#name @name
          ## https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-4.html#jvms-4.7.3
          #length (|> specification
                      (/code.length ..length)
                      //unsigned.u4
                      try.assume)
          #info specification}))

(def: #export (code specification)
  (-> Code (Resource Attribute))
  (do //constant/pool.monad
    [@name (//constant/pool.utf8 ..code-name)]
    (wrap (code' @name specification))))

(def: #export (writer value)
  (Writer Attribute)
  (case value
    (#Constant attribute)
    ((info-writer /constant.writer) attribute)
    
    (#Code attribute)
    ((info-writer (/code.writer writer)) attribute)))
