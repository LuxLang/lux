(.module:
  [lux #*
   [abstract
    ["." equivalence (#+ Equivalence)]
    [monad (#+ do)]]
   [control
    ["." state (#+ State)]
    ["." try (#+ Try)]
    ["." exception (#+ exception:)]
    ["<>" parser ("#@." functor)
     ["<2>" binary (#+ Parser)]]]
   [data
    [number
     ["." int]
     ["." frac]]
    ["." text
     ["%" format]]
    [format
     [".F" binary (#+ Writer)]]
    [collection
     ["." list ("#;." fold)]
     ["." row (#+ Row)]]]
   [type
    abstract]
   [macro
    ["." template]]]
  ["." // (#+ UTF8 Class Long Double Constant)
   [//
    [encoding
     ["." unsigned]]
    ["." index (#+ Index)]
    ["." descriptor (#+ Descriptor)]]])

(def: offset 1)

(type: #export Pool (Row Constant))

(def: #export equivalence
  (Equivalence Pool)
  (row.equivalence //.equivalence))

(template: (!add <tag> <=> <value>)
  (function (_ pool)
    (with-expansions [<index> (as-is (index.index (unsigned.u2 (n/+ offset idx))))
                      <try-again> (as-is (recur (.inc idx)))]
      (loop [idx 0]
        (case (row.nth idx pool)
          (#.Some entry)
          (case entry
            (<tag> reference)
            (if (:: <=> = reference <value>)
              [pool
               <index>]
              <try-again>)
            
            _
            <try-again>)
          
          #.None
          [(row.add (<tag> <value>) pool)
           <index>])))))

(template: (!raw-index <index>)
  (|> <index> index.number unsigned.nat .nat))

(exception: #export (invalid-index {index (Index Any)}
                                   {maximum Nat})
  (exception.report
   ["Index" (|> index !raw-index %.nat)]
   ["Maximum" (%.nat maximum)]))

(exception: #export (invalid-constant {index (Index Any)}
                                      {tag Name})
  (exception.report
   ["Index" (|> index !raw-index %.nat)]
   ["Expected tag" (%.name tag)]))

(template: (!fetch <tag> <index>)
  (function (_ pool)
    (case (row.nth (|> <index> !raw-index (n/- offset))
                   pool)
      (#.Some entry)
      (case entry
        (<tag> value)
        [pool (#try.Success value)]

        _
        [pool (exception.throw ..invalid-constant [<index> (name-of <tag>)])])

      #.None
      [pool (exception.throw ..invalid-index [<index> (row.size pool)])])))

(exception: #export (cannot-find {tag Name} {value Text})
  (exception.report
   ["Expected tag" (%.name tag)]
   ["Value" value]))

(template: (!find <tag> <=> <%> <expected>)
  (function (_ pool)
    (with-expansions [<index> (as-is (index.index (unsigned.u2 (n/+ offset idx))))
                      <try-again> (as-is (recur (.inc idx)))]
      (loop [idx 0]
        (case (row.nth idx pool)
          (#.Some entry)
          (case entry
            (<tag> actual)
            (if (:: <=> = actual <expected>)
              [pool
               (#try.Success <index>)]
              <try-again>)
            
            _
            <try-again>)
          
          #.None
          [pool
           (exception.throw ..cannot-find [(name-of <tag>) (<%> <expected>)])])))))

(type: (Adder of)
  (-> of (State Pool (Index of))))

(type: (Fetcher of)
  (-> (Index of) (State Pool (Try of))))

(type: (Finder of)
  (-> of (State Pool (Try (Index of)))))

(template [<name> <type> <tag> <equivalence> <format>]
  [(def: #export (<name> value)
     (Adder <type>)
     (!add <tag> <equivalence> value))

   (`` (def: #export ((~~ (template.identifier ["fetch-" <name>])) index)
         (Fetcher <type>)
         (!fetch <tag> index)))

   (`` (def: #export ((~~ (template.identifier ["find-" <name>])) reference)
         (Finder <type>)
         (!find <tag> <equivalence> <format> reference)))]

  [long Long #//.Long (//.value-equivalence int.equivalence) (|>> //.value %.int)]
  [double Double #//.Double (//.value-equivalence frac.equivalence) (|>> //.value %.frac)]
  [utf8 UTF8 #//.UTF8 text.equivalence %.text]
  )

(def: (class' value)
  (Adder Class)
  (!add #//.Class //.class-equivalence value))

(def: #export (class name)
  (-> UTF8 (State Pool (Index Class)))
  (do state.monad
    [@name (utf8 name)]
    (class' (//.class @name))))

(def: #export (descriptor value)
  (All [kind]
    (-> (Descriptor kind)
        (State Pool (Index (Descriptor kind)))))
  (let [value (descriptor.descriptor value)]
    (!add #//.UTF8 text.equivalence value)))

(def: #export parser
  (Parser Pool)
  (<2>.row/16' ..offset //.parser))

(def: #export writer
  (Writer Pool)
  (binaryF.row/16' ..offset //.writer))

(def: #export empty
  Pool
  row.empty)
