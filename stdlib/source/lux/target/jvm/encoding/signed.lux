(.module:
  [lux (#- int)
   [abstract
    [equivalence (#+ Equivalence)]
    [order (#+ Order)]]
   [data
    [number
     ["." i64]
     ["n" nat]
     ["i" int]]
    [format
     [".F" binary (#+ Writer)]]]
   [macro
    ["." template]]
   [type
    abstract]])

(abstract: #export (Signed brand)
  {}
  Int

  (def: #export int
    (-> (Signed Any) Int)
    (|>> :representation))

  (structure: #export equivalence
    (All [brand] (Equivalence (Signed brand)))
    (def: (= reference sample)
      (i.= (:representation reference) (:representation sample))))

  (structure: #export order
    (All [brand] (Order (Signed brand)))
    
    (def: &equivalence ..equivalence)
    (def: (< reference sample)
      (i.< (:representation reference) (:representation sample))))

  (template [<bytes> <name> <size> <constructor> <max> <+>]
    [(with-expansions [<raw> (template.identifier [<name> "'"])]
       (abstract: #export <raw> {} Any)
       (type: #export <name> (Signed <raw>)))

     (def: #export <size> Nat <bytes>)
     
     (def: #export <max>
       <name>
       (|> <bytes> (n.* i64.bits-per-byte) dec i64.mask :abstraction))
     
     (def: #export <constructor>
       (-> Int <name>)
       (let [limit (|> <bytes> (n.* i64.bits-per-byte) i64.mask .nat)]
         (|>> (i64.and limit) :abstraction)))

     (def: #export (<+> parameter subject)
       (-> <name> <name> <name>)
       (let [limit (|> <bytes> (n.* i64.bits-per-byte) i64.mask .nat)]
         (:abstraction
          (i64.and limit
                   (i.+ (:representation parameter)
                        (:representation subject))))))]

    [1 S1 s1-bytes s1 max-s1 s1/+]
    [2 S2 s2-bytes s2 max-s2 s2/+]
    [4 S4 s4-bytes s4 max-s4 s4/+]
    )
  )

(template [<writer-name> <type> <writer>]
  [(def: #export <writer-name>
     (Writer <type>)
     (|>> ..int <writer>))]

  [s1-writer S1 binaryF.bits/8]
  [s2-writer S2 binaryF.bits/16]
  [s4-writer S4 binaryF.bits/32]
  )
