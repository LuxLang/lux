(.module:
  [lux #*
   [data
    ["." text
     ["%" format (#+ format)]]]
   [type
    abstract]])

(def: #export internal-separator "/")
(def: #export external-separator ".")

(type: #export External Text)

(abstract: #export Internal
  {}

  Text

  (def: #export internal
    (-> External Internal)
    (|>> (text.replace-all ..external-separator
                           ..internal-separator)
         :abstraction))

  (def: #export read
    (-> Internal Text)
    (|>> :representation))

  (def: #export external
    (-> Internal External)
    (|>> :representation
         (text.replace-all ..internal-separator
                           ..external-separator))))

(def: #export sanitize
  (-> Text External)
  (|>> ..internal ..external))

(def: #export (qualify package class)
  (-> Text External External)
  (format (..sanitize package) ..external-separator class))
