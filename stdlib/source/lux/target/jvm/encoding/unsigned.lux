(.module:
  [lux (#- nat)
   [abstract
    [equivalence (#+ Equivalence)]
    [order (#+ Order)]]
   [control
    ["<>" parser ("#@." functor)
     ["<2>" binary (#+ Parser)]]]
   [data
    [number
     ["." i64]]
    [format
     [".F" binary (#+ Writer)]]]
   [macro
    ["." template]]
   [type
    abstract]])

(abstract: #export (Unsigned brand)
  {}
  Nat

  (def: #export nat
    (-> (Unsigned Any) Nat)
    (|>> :representation))

  (structure: #export equivalence
    (All [brand] (Equivalence (Unsigned brand)))
    (def: (= reference sample)
      (n/= (:representation reference) (:representation sample))))

  (structure: #export order
    (All [brand] (Order (Unsigned brand)))
    
    (def: &equivalence ..equivalence)
    (def: (< reference sample)
      (n/< (:representation reference) (:representation sample))))

  (template [<bytes> <name> <size> <constructor> <max> <+>]
    [(with-expansions [<raw> (template.identifier [<name> "'"])]
       (abstract: #export <raw> {} Any)
       (type: #export <name> (Unsigned <raw>)))

     (def: #export <size> Nat <bytes>)
     
     (def: #export <max>
       <name>
       (|> <bytes> (n/* i64.bits-per-byte) i64.mask :abstraction))
     
     (def: #export <constructor>
       (-> Nat <name>)
       (|>> (i64.and (:representation <max>)) :abstraction))

     (def: #export (<+> parameter subject)
       (-> <name> <name> <name>)
       (:abstraction
        (i64.and (:representation <max>)
                 (n/+ (:representation parameter)
                      (:representation subject)))))]

    [1 U1 u1-bytes u1 max-u1 u1/+]
    [2 U2 u2-bytes u2 max-u2 u2/+]
    [4 U4 u4-bytes u4 max-u4 u4/+]
    )
  )

(template [<parser-name> <writer-name> <type> <parser> <writer> <post-read>]
  [(def: #export <parser-name>
     (Parser <type>)
     (<>@map (|>> .nat <post-read>) <parser>))

   (def: #export <writer-name>
     (Writer <type>)
     (|>> ..nat <writer>))]

  [u1-parser u1-writer U1 <2>.bits/8 binaryF.bits/8  ..u1]
  [u2-parser u2-writer U2 <2>.bits/16 binaryF.bits/16 ..u2]
  [u4-parser u4-writer U4 <2>.bits/32 binaryF.bits/32 ..u4]
  )
