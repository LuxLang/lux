(.module:
  [lux (#- static)
   [abstract
    [monoid (#+)]
    ["." equivalence (#+ Equivalence)]
    ["." monad (#+ do)]]
   [control
    ["." state (#+ State)]
    ["<>" parser]]
   [data
    [number (#+)
     [i64 (#+)]]
    [format
     [".F" binary (#+ Reader Writer Format) ("#@." monoid)]]
    [collection
     ["." row (#+ Row)]]]
   [type
    [abstract (#+)]]]
  ["." // #_
   ["." modifier (#+ Modifier modifiers:)]
   ["#." constant (#+ UTF8)
    ["#/." pool (#+ Pool)]]
   ["#." index (#+ Index)]
   ["#." attribute (#+ Attribute)]
   ["#." descriptor (#+ Descriptor Value)]])

(type: #export #rec Field
  {#modifier (Modifier Field)
   #name (Index UTF8)
   #descriptor (Index (Descriptor (Value Any)))
   #attributes (Row Attribute)})

(modifiers: Field
  ["0001" public]
  ["0002" private]
  ["0004" protected]
  ["0008" static]
  ["0010" final]
  ["0040" volatile]
  ["0080" transient]
  ["1000" synthetic]
  ["4000" enum]
  )

(def: #export equivalence
  (Equivalence Field)
  ($_ equivalence.product
      modifier.equivalence
      //index.equivalence
      //index.equivalence
      (row.equivalence //attribute.equivalence)))

(def: #export (reader pool)
  (-> Pool (Reader Field))
  ($_ <>.and
      (get@ #binaryF.reader modifier.format)
      (get@ #binaryF.reader //index.format)
      (get@ #binaryF.reader //index.format)
      (get@ #binaryF.reader
            (binaryF.row/16 {#binaryF.reader (//attribute.reader pool)
                             ## TODO: Get rid of this dirty hack ASAP
                             #binaryF.writer (function (_ _) binaryF.no-op)}))))

(def: #export (writer field)
  (Writer Field)
  (let [attribute-format (: (Format Attribute)
                            {## TODO: Get rid of this dirty hack ASAP
                             #binaryF.reader (<>.fail "")
                             #binaryF.writer //attribute.writer})]
    (`` ($_ binaryF@compose
            (~~ (template [<format> <slot>]
                  [((get@ #binaryF.writer <format>) (get@ <slot> field))]

                  [modifier.format #modifier]
                  [//index.format #name]
                  [//index.format #descriptor]
                  [(binaryF.row/16 attribute-format) #attributes]))
            ))))

(def: #export (field modifier name descriptor attributes)
  (-> (Modifier Field) UTF8 (Descriptor (Value Any)) (Row Attribute)
      (State Pool Field))
  (do state.monad
    [@name (//constant/pool.utf8 name)
     @descriptor (//constant/pool.descriptor descriptor)]
    (wrap {#modifier modifier
           #name @name
           #descriptor @descriptor
           #attributes attributes})))
