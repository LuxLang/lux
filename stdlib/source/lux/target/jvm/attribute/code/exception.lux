(.module:
  [lux #*
   [abstract
    ["." equivalence (#+ Equivalence)]]
   [data
    [number
     ["n" nat]]
    [format
     [".F" binary (#+ Writer)]]]]
  ["." // #_
   ["//#" /// #_
    [constant (#+ Class)]
    ["#." index (#+ Index)]
    [instruction
     ["#." address (#+ Address)]]
    [encoding
     ["#." unsigned (#+ U2)]]]])

(type: #export Exception
  {#start Address
   #end Address
   #handler Address
   #catch (Index Class)})

(def: #export equivalence
  (Equivalence Exception)
  ($_ equivalence.product
      ////address.equivalence
      ////address.equivalence
      ////address.equivalence
      ////index.equivalence
      ))

## https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-4.html#jvms-4.7.3
(def: #export length
  Nat
  ($_ n.+
      ## u2 start_pc;
      ////unsigned.u2-bytes
      ## u2 end_pc;
      ////unsigned.u2-bytes
      ## u2 handler_pc;
      ////unsigned.u2-bytes
      ## u2 catch_type;
      ////unsigned.u2-bytes
      ))

(def: #export writer
  (Writer Exception)
  ($_ binaryF.and
      ////address.writer
      ////address.writer
      ////address.writer
      ////index.writer
      ))
