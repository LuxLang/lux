(.module:
  [lux (#- int char)
   [abstract
    [equivalence (#+ Equivalence)]]
   [data
    ["." text ("#@." equivalence)
     ["%" format (#+ format)]]
    [collection
     ["." list ("#@." functor)]]]
   [macro
    ["." template]]
   [type
    abstract]]
  ["." // #_
   ["#." descriptor]
   ["/#" // #_
    [encoding
     ["#." name (#+ External)]]]])

(abstract: #export Void' {} Any)
(abstract: #export (Value' kind) {} Any)
(abstract: #export (Return' kind) {} Any)
(abstract: #export Method {} Any)

(abstract: #export (Signature brand)
  {}

  Text

  (type: #export Return (<| Return' Any))
  (type: #export Value (<| Return' Value' Any))
  (type: #export Void (<| Return' Void'))

  (abstract: #export (Object' brand) {} Any)
  (type: #export Object (<| Return' Value' Object' Any))

  (abstract: #export (Parameter' brand) {} Any)
  (type: #export Parameter (<| Return' Value' Object' Parameter' Any))

  (template [<parents> <child>]
    [(with-expansions [<raw> (template.identifier [<child> "'"])]
       (abstract: #export <raw> {} Any)
       (type: #export <child>
         (`` (<| Return' Value' (~~ (template.splice <parents>)) <raw>))))]

    [[] Primitive]
    [[Object' Parameter'] Class]
    [[Object'] Array]
    )

  (def: #export signature
    (-> (Signature Any) Text)
    (|>> :representation))

  (template [<brand> <name> <descriptor>]
    [(def: #export <name>
       (Signature <brand>)
       (:abstraction (//descriptor.descriptor <descriptor>)))]

    [Void void //descriptor.void]
    [Primitive boolean //descriptor.boolean]
    [Primitive byte //descriptor.byte]
    [Primitive short //descriptor.short]
    [Primitive int //descriptor.int]
    [Primitive long //descriptor.long]
    [Primitive float //descriptor.float]
    [Primitive double //descriptor.double]
    [Primitive char //descriptor.char]
    )

  (def: #export array
    (-> (Signature Value) (Signature Array))
    (|>> :representation
         (format //descriptor.array-prefix)
         :abstraction))

  (def: #export wildcard
    (Signature Parameter)
    (:abstraction "*"))

  (def: var-prefix "T")
  
  (def: #export var
    (-> Text (Signature Parameter))
    (|>> (text.enclose [..var-prefix //descriptor.class-suffix])
         :abstraction))

  (def: lower-prefix "-")
  (def: upper-prefix "+")
  
  (template [<name> <prefix>]
    [(def: #export <name>
       (-> (Signature Class) (Signature Parameter))
       (|>> :representation (format <prefix>) :abstraction))]

    [lower ..lower-prefix]
    [upper ..upper-prefix]
    )

  (def: #export (class name parameters)
    (-> External (List (Signature Parameter)) (Signature Class))
    (:abstraction
     (format //descriptor.class-prefix
             (|> name ///name.internal ///name.read)
             (case parameters
               #.Nil
               ""

               _
               (format "<"
                       (|> parameters
                           (list@map ..signature)
                           (text.join-with ""))
                       ">"))
             //descriptor.class-suffix)))

  (def: #export (method [inputs output exceptions])
    (-> [(List (Signature Value))
         (Signature Return)
         (List (Signature Class))]
        (Signature Method))
    (:abstraction
     (format (|> inputs
                 (list@map ..signature)
                 (text.join-with "")
                 (text.enclose ["(" ")"]))
             (:representation output)
             (|> exceptions
                 (list@map (|>> :representation (format "^")))
                 (text.join-with "")))))

  (structure: #export equivalence
    (All [brand] (Equivalence (Signature brand)))
    
    (def: (= parameter subject)
      (text@= (:representation parameter)
              (:representation subject))))
  )
