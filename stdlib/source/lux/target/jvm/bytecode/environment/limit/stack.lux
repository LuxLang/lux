(.module:
  [lux #*
   [abstract
    ["." equivalence (#+ Equivalence)]]
   [control
    ["." try (#+ Try)]]
   [data
    ["." maybe]
    [format
     [binary (#+ Writer)]]]
   [type
    abstract]]
  ["." ///// #_
   [encoding
    ["#." unsigned (#+ U2)]]])

(abstract: #export Stack
  {}
  
  U2

  (def: #export empty
    Stack
    (|> 0 /////unsigned.u2 maybe.assume :abstraction))

  (def: #export equivalence
    (Equivalence Stack)
    (:: equivalence.contravariant map-1
        (|>> :representation)
        /////unsigned.equivalence))

  (def: #export writer
    (Writer Stack)
    (|>> :representation /////unsigned.writer/2))

  (def: stack
    (-> U2 Stack)
    (|>> :abstraction))

  (template [<op> <name>]
    [(def: #export (<name> amount)
       (-> U2 (-> Stack (Try Stack)))
       (|>> :representation
            (<op> amount)
            (:: try.functor map ..stack)))]

    [/////unsigned.+/2 push]
    [/////unsigned.-/2 pop]
    )

  (def: #export (max left right)
    (-> Stack Stack Stack)
    (:abstraction
     (/////unsigned.max/2 (:representation left)
                          (:representation right))))
  )

(def: #export length
  /////unsigned.bytes/2)
