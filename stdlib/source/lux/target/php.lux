(.module:
  [lux (#- Location Code Global static int if cond or and not comment for)
   ["@" target]
   ["." host]
   [control
    [pipe (#+ case> cond> new>)]]
   [data
    ["." text
     ["%" format (#+ format)]]
    [collection
     ["." list ("#\." functor fold)]]]
   [macro
    ["." template]]
   [math
    [number
     ["f" frac]]]
   [type
    abstract]])

(def: input_separator ", ")
(def: statement_suffix ";")

(.for {@.old (as_is (host.import: java/lang/CharSequence)
                    (host.import: java/lang/String
                      ["#::."
                       (replace [java/lang/CharSequence java/lang/CharSequence] java/lang/String)]))}
      (as_is))

(def: nest
  (-> Text Text)
  (.let [nested_new_line (format text.new_line text.tab)]
    (.for {@.old (|>> (format text.new_line)
                      (:coerce java/lang/String)
                      (java/lang/String::replace (:coerce java/lang/CharSequence text.new_line)
                                                 (:coerce java/lang/CharSequence nested_new_line)))}
          (|>> (format text.new_line)
               (text.replace_all text.new_line nested_new_line)))))

(def: block
  (-> Text Text)
  (|>> ..nest (text.enclose ["{" (format text.new_line "}")])))

(def: group
  (-> Text Text)
  (text.enclose ["(" ")"]))

(abstract: #export (Code brand)
  Text

  (def: #export manual
    (-> Text Code)
    (|>> :abstraction))

  (def: #export code
    (-> (Code Any) Text)
    (|>> :representation))

  (template [<type> <super>+]
    [(with_expansions [<brand> (template.identifier [<type> "'"])]
       (abstract: (<brand> brand) Any)
       (`` (type: #export <type> (|> Any <brand> (~~ (template.splice <super>+))))))]
    
    [Expression [Code]]
    [Computation [Expression' Code]]
    [Location [Computation' Expression' Code]]
    [Statement [Code]]
    )

  (template [<type> <super>+]
    [(with_expansions [<brand> (template.identifier [<type> "'"])]
       (abstract: #export <brand> Any)
       (`` (type: #export <type> (|> <brand> (~~ (template.splice <super>+))))))]

    [Literal [Computation' Expression' Code]]
    [Var [Location' Computation' Expression' Code]]
    [Access [Location' Computation' Expression' Code]]
    [Constant [Location' Computation' Expression' Code]]
    [Global [Location' Computation' Expression' Code]]
    )

  (type: #export Argument
    {#reference? Bit
     #var Var})

  (def: #export ;
    (-> Expression Statement)
    (|>> :representation
         (text.suffix ..statement_suffix)
         :abstraction))

  (def: #export var
    (-> Text Var)
    (|>> (format "$") :abstraction))

  (def: #export constant
    (-> Text Constant)
    (|>> :abstraction))

  (def: #export null
    Literal
    (:abstraction "NULL"))

  (def: #export bool
    (-> Bit Literal)
    (|>> (case> #0 "false"
                #1 "true")
         :abstraction))

  (def: #export int
    (-> Int Literal)
    (|>> %.int :abstraction))

  (def: #export float
    (-> Frac Literal)
    (|>> (cond> [(f.= f.positive_infinity)]
                [(new> "+INF" [])]
                
                [(f.= f.negative_infinity)]
                [(new> "-INF" [])]
                
                [(f.= f.not_a_number)]
                [(new> "NAN" [])]
                
                ## else
                [%.frac])
         :abstraction))

  (def: sanitize
    (-> Text Text)
    (`` (|>> (~~ (template [<find> <replace>]
                   [(text.replace_all <find> <replace>)]

                   ["\" "\\"]
                   [text.tab "\t"]
                   [text.vertical_tab "\v"]
                   [text.null "\0"]
                   [text.back_space "\b"]
                   [text.form_feed "\f"]
                   [text.new_line "\n"]
                   [text.carriage_return "\r"]
                   [text.double_quote (format "\" text.double_quote)]
                   ))
             )))

  (def: #export string
    (-> Text Literal)
    (|>> ..sanitize
         (text.enclose [text.double_quote text.double_quote])
         :abstraction))

  (def: arguments
    (-> (List Expression) Text)
    (|>> (list\map ..code) (text.join_with ..input_separator) ..group))

  (def: #export (apply/* args func)
    (-> (List Expression) Expression Computation)
    (:abstraction
     (format (:representation func) (..arguments args))))

  (def: parameters
    (-> (List Argument) Text)
    (|>> (list\map (function (_ [reference? var])
                     (.if reference?
                       (format "&" (:representation var))
                       (:representation var))))
         (text.join_with ..input_separator)
         ..group))

  (template [<name> <reference?>]
    [(def: #export <name>
       (-> Var Argument)
       (|>> [<reference?>]))]

    [parameter #0]
    [reference #1]
    )

  (def: #export (closure uses arguments body!)
    (-> (List Argument) (List Argument) Statement Literal)
    (let [uses (case uses
                 #.Nil
                 ""

                 _
                 (format "use " (..parameters uses)))]
      (|> (format "function " (..parameters arguments)
                  " " uses " "
                  (..block (:representation body!)))
          ..group
          :abstraction)))

  (template [<apply> <input_var>+ <input_type>+ <function>+]
    [(`` (def: #export (<apply> [(~~ (template.splice <input_var>+))] function)
           (-> [(~~ (template.splice <input_type>+))] Expression Computation)
           (..apply/* (list (~~ (template.splice <input_var>+))) function)))

     (`` (template [<lux_name> <php_name>]
           [(def: #export (<lux_name> args)
              (-> [(~~ (template.splice <input_type>+))] Computation)
              (<apply> args (..constant <php_name>)))]
           
           (~~ (template.splice <function>+))))]

    [apply/0 [] []
     [[func_num_args/0 "func_num_args"]
      [func_get_args/0 "func_get_args"]
      [time/0          "time"]]]
    [apply/1 [in0] [Expression]
     [[is_null/1   "is_null"]
      [empty/1     "empty"]
      [count/1     "count"]
      [strlen/1    "strlen"]
      [array_pop/1 "array_pop"]
      [array_reverse/1 "array_reverse"]
      [intval/1    "intval"]
      [floatval/1  "floatval"]
      [strval/1    "strval"]
      [ord/1       "ord"]
      [chr/1       "chr"]
      [print/1     "print"]
      [exit/1      "exit"]]]
    [apply/2 [in0 in1] [Expression Expression]
     [[call_user_func_array/2 "call_user_func_array"]
      [array_slice/2          "array_slice"]
      [array_push/2           "array_push"]]]
    [apply/3 [in0 in1 in2] [Expression Expression Expression]
     [[array_slice/3 "array_slice"]
      [array_splice/3 "array_splice"]
      [strpos/3 "strpos"]
      [substr/3 "substr"]]]
    )

  (def: #export (array/* values)
    (-> (List Expression) Literal)
    (|> values
        (list\map ..code)
        (text.join_with ..input_separator)
        ..group
        (format "array")
        :abstraction))

  (def: #export (array_merge/+ required optionals)
    (-> Expression (List Expression) Computation)
    (..apply/* (list& required optionals) (..constant "array_merge")))

  (def: #export (array/** kvs)
    (-> (List [Expression Expression]) Literal)
    (|> kvs
        (list\map (function (_ [key value])
                    (format (:representation key) " => " (:representation value))))
        (text.join_with ..input_separator)
        ..group
        (format "array")
        :abstraction))

  (def: #export (new constructor inputs)
    (-> Constant (List Expression) Computation)
    (|> (format "new " (:representation constructor) (arguments inputs))
        :abstraction))

  (def: #export (do method inputs object)
    (-> Text (List Expression) Expression Computation)
    (|> (format (:representation object) "->" method (arguments inputs))
        :abstraction))

  (def: #export (nth idx array)
    (-> Expression Expression Access)
    (|> (format (:representation array) "[" (:representation idx) "]")
        :abstraction))

  (def: #export (global name)
    (-> Text Global)
    (|> (..var "GLOBALS") (..nth (..string name)) :transmutation))

  (def: #export (? test then else)
    (-> Expression Expression Expression Computation)
    (|> (format (:representation test) " ? "
                (:representation then) " : "
                (:representation else))
        ..group
        :abstraction))

  (template [<name> <op>]
    [(def: #export (<name> parameter subject)
       (-> Expression Expression Computation)
       (|> (format (:representation subject) " " <op> " " (:representation parameter))
           ..group
           :abstraction))]

    [or      "||"]
    [and     "&&"]
    [=       "==="]
    [<       "<"]
    [<=      "<="]
    [>       ">"]
    [>=      ">="]
    [+       "+"]
    [-       "-"]
    [*       "*"]
    [/       "/"]
    [%       "%"]
    [bit_or  "|"]
    [bit_and "&"]
    [bit_xor "^"]
    [bit_shl "<<"]
    [bit_shr ">>"]
    [concat  "."]
    )

  (def: #export not
    (-> Computation Computation)
    (|>> :representation (format "!") :abstraction))

  (def: #export (set var value)
    (-> Location Expression Computation)
    (|> (format (:representation var) " = " (:representation value))
        ..group
        :abstraction))

  (def: #export (set? var)
    (-> Var Computation)
    (..apply/1 [var] (..constant "isset")))

  (template [<name> <modifier>]
    [(def: #export <name>
       (-> Var Statement)
       (|>> :representation (format <modifier> " ") (text.suffix ..statement_suffix) :abstraction))]

    [define_global "global"]
    )

  (template [<name> <modifier> <location>]
    [(def: #export (<name> location value)
       (-> <location> Expression Statement)
       (:abstraction (format <modifier> " " (:representation location)
                             " = " (:representation value)
                             ..statement_suffix)))]

    [define_static   "static" Var]
    [define_constant "const"  Constant]
    )

  (def: #export (if test then! else!)
    (-> Expression Statement Statement Statement)
    (:abstraction
     (format "if " (..group (:representation test)) " "
             (..block (:representation then!))
             " else "
             (..block (:representation else!)))))

  (def: #export (when test then!)
    (-> Expression Statement Statement)
    (:abstraction
     (format "if " (..group (:representation test)) " "
             (..block (:representation then!)))))

  (def: #export (then pre! post!)
    (-> Statement Statement Statement)
    (:abstraction
     (format (:representation pre!)
             text.new_line
             (:representation post!))))

  (def: #export (while test body!)
    (-> Expression Statement Statement)
    (:abstraction
     (format "while " (..group (:representation test)) " "
             (..block (:representation body!)))))

  (def: #export (do_while test body!)
    (-> Expression Statement Statement)
    (:abstraction
     (format "do " (..block (:representation body!))
             " while " (..group (:representation test))
             ..statement_suffix)))

  (def: #export (for_each array value body!)
    (-> Expression Var Statement Statement)
    (:abstraction
     (format "foreach(" (:representation array)
             " as " (:representation value)
             ") " (..block (:representation body!)))))

  (type: #export Except
    {#class Constant
     #exception Var
     #handler Statement})

  (def: (catch except)
    (-> Except Text)
    (let [declaration (format (:representation (get@ #class except))
                              " " (:representation (get@ #exception except)))]
      (format "catch" (..group declaration) " "
              (..block (:representation (get@ #handler except))))))
  
  (def: #export (try body! excepts)
    (-> Statement (List Except) Statement)
    (:abstraction
     (format "try " (..block (:representation body!))
             text.new_line
             (|> excepts
                 (list\map catch)
                 (text.join_with text.new_line)))))

  (template [<name> <keyword>]
    [(def: #export <name>
       (-> Expression Statement)
       (|>> :representation (format <keyword> " ") (text.suffix ..statement_suffix) :abstraction))]

    [throw  "throw"]
    [return "return"]
    [echo   "echo"]
    )

  (def: #export (define name value)
    (-> Constant Expression Expression)
    (..apply/2 [(|> name :representation ..string)
                value]
               (..constant "define")))

  (def: #export (define_function name arguments body!)
    (-> Constant (List Argument) Statement Statement)
    (:abstraction
     (format "function " (:representation name)
             " " (..parameters arguments)
             " "
             (..block (:representation body!)))))

  (template [<name> <keyword>]
    [(def: #export <name>
       Statement
       (|> <keyword>
           (text.suffix ..statement_suffix)
           :abstraction))]

    [break "break"]
    [continue "continue"]
    )
  )

(def: #export (cond clauses else!)
  (-> (List [Expression Statement]) Statement Statement)
  (list\fold (function (_ [test then!] next!)
               (..if test then! next!))
             else!
             (list.reverse clauses)))

(def: #export command_line_arguments
  Var
  (..var "argv"))
