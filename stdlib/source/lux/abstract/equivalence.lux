(.module:
  [lux #*]
  [//
   [functor
    ["." contravariant]]])

(signature: #export (Equivalence a)
  {#.doc "Equivalence for a type's instances."}
  (: (-> a a Bit)
     =))

(def: #export (sum left right)
  (All [l r] (-> (Equivalence l) (Equivalence r) (Equivalence (| l r))))
  (structure
   (def: (= reference sample)
     (case [reference sample]
       [(#.Left reference) (#.Left sample)]
       (:: left = reference sample)

       [(#.Right reference) (#.Right sample)]
       (:: right = reference sample)

       _
       false))))

(def: #export (product left right)
  (All [l r] (-> (Equivalence l) (Equivalence r) (Equivalence [l r])))
  (structure
   (def: (= [a b] [x y])
     (and (:: left = a x)
          (:: right = b y)))))

(def: #export (rec sub)
  (All [a] (-> (-> (Equivalence a) (Equivalence a)) (Equivalence a)))
  (structure
   (def: (= left right)
     (sub = left right))))

(structure: #export functor
  (contravariant.Functor Equivalence)
  
  (def: (map f equivalence)
    (structure
     (def: (= reference sample)
       (:: equivalence = (f reference) (f sample))))))
