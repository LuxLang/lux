(.module:
  lux
  (lux (control [functor #+ Functor]
                [applicative #+ Applicative]
                [monad #+ do Monad])
       [io #+ IO io]
       (data (coll [list "list/" Monoid<List>]))
       (type abstract))
  (// [atom #+ Atom atom]
      [promise #+ Promise]))

## [Types]
(abstract: #export (Channel a)
  {#.doc "An asynchronous channel to distribute values."}
  (Atom (List (-> a (IO Top))))

  (def: #export (channel _)
    (All [a] (-> Top (Channel a)))
    (@abstraction (atom (list))))

  (def: #export (listen listener (^@representation channel))
    (All [a] (-> (-> a (IO Top)) (Channel a) (IO Top)))
    ## TODO: Simplify when possible.
    (do io.Monad<IO>
      [_ (atom.update (|>> (#.Cons listener)) channel)]
      (wrap [])))

  (def: #export (publish (^@representation channel) value)
    {#.doc "Publish to a channel."}
    (All [a] (-> (Channel a) a (IO Top)))
    (do io.Monad<IO>
      [listeners (atom.read channel)]
      (monad.map @ (function (_ listener) (listener value)) listeners)))
  )

## [Values]
(def: #export (filter predicate input)
  (All [a] (-> (-> a Bool) (Channel a) (Channel a)))
  (let [output (channel [])]
    (exec (io.run (listen (function (_ value)
                            (if (predicate value)
                              (publish output value)
                              (io [])))
                          input))
      output)))

(def: #export (pipe output input)
  {#.doc "Copy/pipe the contents of a channel on to another."}
  (All [a] (-> (Channel a) (Channel a) (IO Top)))
  (listen (publish output)
          input))

(def: #export (merge inputs)
  {#.doc "Fuse all the elements in a list of channels by piping them onto a new output channel."}
  (All [a] (-> (List (Channel a)) (IO (Channel a))))
  (let [output (channel [])]
    (do io.Monad<IO>
      [_ (monad.map @ (pipe output) inputs)]
      (wrap output))))

(def: #export (from-promise promise)
  (All [a] (-> (Promise a) (Channel a)))
  (let [output (channel [])]
    (exec (promise.await (publish output) promise)
      output)))

(def: #export (poll time action)
  (All [a] (-> Nat (IO a) (Channel a)))
  (let [output (channel [])]
    (exec (io.run
           (loop [_ []]
             (do io.Monad<IO>
               [value action
                _ (publish output value)]
               (wrap (promise.await recur (promise.wait time))))))
      output)))

(def: #export (periodic time)
  (-> Nat (Channel Unit))
  (let [output (channel [])]
    (exec (io.run
           (loop [_ []]
             (do io.Monad<IO>
               [_ (publish output [])]
               (wrap (promise.await recur (promise.wait time))))))
      output)))

(def: #export (iterate f init)
  (All [a] (-> (-> a (Promise a)) a (Channel a)))
  (let [output (channel [])]
    (exec (io.run
           (loop [zero init]
             (do io.Monad<IO>
               [_ (publish output zero)]
               (wrap (promise.await recur (f zero))))))
      output)))

## [Structures]
(struct: #export _ (Functor Channel)
  (def: (map f input)
    (let [output (channel [])]
      (exec (io.run (listen (|>> f (publish output))
                            input))
        output))))

(struct: #export _ (Applicative Channel)
  (def: functor Functor<Channel>)

  (def: (apply ff fa)
    (let [output (channel [])]
      (exec (io.run (listen (function (_ f)
                              (listen (|>> f (publish output))
                                      fa))
                            ff))
        output))))

(struct: #export _ (Monad Channel)
  (def: functor Functor<Channel>)

  (def: (wrap a)
    (let [output (channel [])]
      (exec (io.run (publish output a))
        output)))

  (def: (join mma)
    (let [output (channel [])]
      (exec (io.run (listen (listen (publish output))
                            mma))
        output))))
