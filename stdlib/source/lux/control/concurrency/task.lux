(.module:
  [lux #*
   [control
    [functor (#+ Functor)]
    [apply (#+ Apply)]
    [monad (#+ Monad do)]
    ["ex" exception (#+ Exception)]]
   [data
    ["." error (#+ Error)]]
   ["." macro
    ["s" syntax (#+ syntax: Syntax)]]]
  [//
   ["." promise (#+ Promise)]])

(type: #export (Task a)
  (Promise (Error a)))

(type: #export (Resolver a)
  (promise.Resolver (Error a)))

(do-template [<name> <input> <tag>]
  [(def: #export <name>
     (All [a] (-> <input> (Task a)))
     (|>> <tag> promise.resolved))]

  [return a    #error.Success]
  [fail   Text #error.Failure]
  )

(def: #export (throw exception message)
  (All [e a] (-> (Exception e) e (Task a)))
  (:: promise.Monad<Promise> wrap
      (ex.throw exception message)))

(def: #export (try computation)
  (All [a] (-> (Task a) (Task (Error a))))
  (:: promise.Functor<Promise> map (|>> #error.Success) computation))

(structure: #export _ (Functor Task)
  (def: (map f fa)
    (:: promise.Functor<Promise> map
        (function (_ fa')
          (case fa'
            (#error.Failure error)
            (#error.Failure error)

            (#error.Success a)
            (#error.Success (f a))))
        fa)))

(structure: #export _ (Apply Task)
  (def: functor Functor<Task>)

  (def: (apply ff fa)
    (do promise.Monad<Promise>
      [ff' ff
       fa' fa]
      (wrap (do error.Monad<Error>
              [f ff'
               a fa']
              (wrap (f a)))))))

(structure: #export _ (Monad Task)
  (def: functor Functor<Task>)

  (def: wrap return)

  (def: (join mma)
    (do promise.Monad<Promise>
      [mma' mma]
      (case mma'
        (#error.Failure error)
        (wrap (#error.Failure error))

        (#error.Success ma)
        ma))))

(def: #export task
  (All [a] (-> Any [(Task a) (Resolver a)]))
  promise.promise)

(def: #export (from-promise promise)
  (All [a] (-> (Promise a) (Task a)))
  (:: promise.Functor<Promise> map (|>> #error.Success) promise))
