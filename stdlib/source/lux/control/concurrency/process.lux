(.module:
  [lux #*
   [abstract
    ["." monad (#+ do)]]
   [control
    ["ex" exception (#+ exception:)]
    ["." io (#+ IO io)]]
   [data
    [collection
     ["." list]]]
   [tool
    [compiler
     ["." host]]]
   [host (#+ import: object)]]
  [//
   ["." atom (#+ Atom)]])

(`` (for {(~~ (static host.old))
          (as-is (import: #long java/lang/Object)

                 (import: #long java/lang/Runtime
                   (#static getRuntime [] java/lang/Runtime)
                   (availableProcessors [] int))

                 (import: #long java/lang/Runnable)

                 (import: #long java/util/concurrent/TimeUnit
                   (#enum MILLISECONDS))
                 
                 (import: #long java/util/concurrent/Executor
                   (execute [java/lang/Runnable] #io void))

                 (import: #long (java/util/concurrent/ScheduledFuture a))

                 (import: #long java/util/concurrent/ScheduledThreadPoolExecutor
                   (new [int])
                   (schedule [java/lang/Runnable long java/util/concurrent/TimeUnit] #io (java/util/concurrent/ScheduledFuture java/lang/Object))))}
         
         ## Default
         (type: Process
           {#creation Nat
            #delay Nat
            #action (IO Any)})
         ))

(def: #export parallelism
  Nat
  (`` (for {(~~ (static host.old))
            (|> (java/lang/Runtime::getRuntime)
                (java/lang/Runtime::availableProcessors)
                .nat)}
           
           ## Default
           1)))

(def: runner
  (`` (for {(~~ (static host.old))
            (java/util/concurrent/ScheduledThreadPoolExecutor::new (.int ..parallelism))}
           
           ## Default
           (: (Atom (List Process))
              (atom.atom (list))))))

(def: #export (schedule milli-seconds action)
  (-> Nat (IO Any) (IO Any))
  (`` (for {(~~ (static host.old))
            (let [runnable (object [] [java/lang/Runnable]
                             []
                             (java/lang/Runnable [] (run) void
                                                 (io.run action)))]
              (case milli-seconds
                0 (java/util/concurrent/Executor::execute runnable runner)
                _ (java/util/concurrent/ScheduledThreadPoolExecutor::schedule runnable (.int milli-seconds) java/util/concurrent/TimeUnit::MILLISECONDS
                                                                              runner)))}
           
           ## Default
           (atom.update (|>> (#.Cons {#creation ("lux io current-time")
                                      #delay milli-seconds
                                      #action action}))
                        runner))))

(`` (for {(~~ (static host.old))
          (as-is)}
         
         ## Default
         (as-is (exception: #export (cannot-continue-running-processes) "")
                
                (def: #export run!
                  (IO Any)
                  (loop [_ []]
                    (do io.monad
                      [processes (atom.read runner)]
                      (case processes
                        ## And... we're done!
                        #.Nil
                        (wrap [])

                        _
                        (do @
                          [#let [now ("lux io current-time")
                                 [ready pending] (list.partition (function (_ process)
                                                                   (|> (get@ #creation process)
                                                                       (n/+ (get@ #delay process))
                                                                       (n/<= now)))
                                                                 processes)]
                           swapped? (atom.compare-and-swap! processes pending runner)]
                          (if swapped?
                            (monad.seq @ ready)
                            (error! (ex.construct cannot-continue-running-processes []))))
                        ))))
                )))
