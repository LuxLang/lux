(.module:
  [lux #*
   [host (#+ import:)]
   ["@" target]
   [abstract
    [monad (#+ do)]]
   [control
    ["." function]
    ["." io (#- run)]]
   [type
    abstract]])

(`` (for {(~~ (static @.old))
          (import: #long (java/util/concurrent/atomic/AtomicReference a)
            (new [a])
            (get [] a)
            (compareAndSet [a a] boolean))

          (~~ (static @.jvm))
          (import: #long (java/util/concurrent/atomic/AtomicReference a)
            (new [a])
            (get [] a)
            (compareAndSet [a a] boolean))}))

(`` (abstract: #export (Atom a)
      {#.doc "Atomic references that are safe to mutate concurrently."}

      (for {(~~ (static @.old))
            (java/util/concurrent/atomic/AtomicReference a)

            (~~ (static @.jvm))
            (java/util/concurrent/atomic/AtomicReference a)})

      (def: #export (atom value)
        (All [a] (-> a (Atom a)))
        (:abstraction (for {(~~ (static @.old))
                            (java/util/concurrent/atomic/AtomicReference::new value)

                            (~~ (static @.jvm))
                            (java/util/concurrent/atomic/AtomicReference::new value)})))

      (def: #export (read atom)
        (All [a] (-> (Atom a) (IO a)))
        (io (for {(~~ (static @.old))
                  (java/util/concurrent/atomic/AtomicReference::get (:representation atom))

                  (~~ (static @.jvm))
                  (java/util/concurrent/atomic/AtomicReference::get (:representation atom))})))

      (def: #export (compare-and-swap current new atom)
        {#.doc (doc "Only mutates an atom if you can present its current value."
                    "That guarantees that atom was not updated since you last read from it.")}
        (All [a] (-> a a (Atom a) (IO Bit)))
        (io (for {(~~ (static @.old))
                  (java/util/concurrent/atomic/AtomicReference::compareAndSet current new (:representation atom))

                  (~~ (static @.jvm))
                  (|> (:representation atom)
                      (java/util/concurrent/atomic/AtomicReference::compareAndSet current new)
                      (: (primitive "java.lang.Boolean"))
                      (:coerce Bit))})))
      ))

(def: #export (update f atom)
  {#.doc (doc "Updates an atom by applying a function to its current value."
              "If it fails to update it (because some other process wrote to it first), it will retry until it succeeds."
              "The retries will be done with the new values of the atom, as they show up.")}
  (All [a] (-> (-> a a) (Atom a) (IO a)))
  (loop [_ []]
    (do io.monad
      [old (read atom)
       #let [new (f old)]
       swapped? (compare-and-swap old new atom)]
      (if swapped?
        (wrap new)
        (recur [])))))

(def: #export (write value atom)
  (All [a] (-> a (Atom a) (IO Any)))
  (update (function.constant value) atom))
