##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;module:
  [lux #- min max]
  (.. eq)
  lux/codata/function)

## [Signatures]
(sig: #export (Ord a)
  (: (Eq a)
     eq)
  
  (do-template [<name>]
    [(: (-> a a Bool) <name>)]

    [<] [<=] [>] [>=]))

## [Values]
(def: #export (ord eq <)
  (All [a]
    (-> (Eq a) (-> a a Bool) (Ord a)))
  (let [> (flip <)]
    (struct
     (def: eq eq)
     (def: < <)
     (def: (<= test subject)
       (or (< test subject)
           (:: eq = test subject)))
     (def: > >)
     (def: (>= test subject)
       (or (> test subject)
           (:: eq = test subject))))))

(do-template [<name> <op>]
  [(def: #export (<name> ord x y)
     (All [a]
       (-> (Ord a) a a a))
     (if (:: ord <op> y x) x y))]

  [max >]
  [min <])
