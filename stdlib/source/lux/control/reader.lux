(.module:
  lux
  (lux (control ["F" functor]
                ["A" applicative]
                [monad #+ do Monad])))

## [Types]
(type: #export (Reader r a)
  {#.doc "Computations that have access to some environmental value."}
  (-> r a))

## [Structures]
(struct: #export Functor<Reader> (All [r] (F.Functor (Reader r)))
  (def: (map f fa)
    (function (_ env)
      (f (fa env)))))

(struct: #export Applicative<Reader> (All [r] (A.Applicative (Reader r)))
  (def: functor Functor<Reader>)

  (def: (wrap x)
    (function (_ env) x))

  (def: (apply ff fa)
    (function (_ env)
      ((ff env) (fa env)))))

(struct: #export Monad<Reader> (All [r] (Monad (Reader r)))
  (def: applicative Applicative<Reader>)

  (def: (join mma)
    (function (_ env)
      (mma env env))))

## [Values]
(def: #export ask
  {#.doc "Get the environment."}
  (All [r] (Reader r r))
  (function (_ env) env))

(def: #export (local change proc)
  {#.doc "Run computation with a locally-modified environment."}
  (All [r a] (-> (-> r r) (Reader r a) (Reader r a)))
  (|>> change proc))

(def: #export (run env proc)
  (All [r a] (-> r (Reader r a) a))
  (proc env))

(struct: #export (ReaderT Monad<M>)
  {#.doc "Monad transformer for Reader."}
  (All [M] (-> (Monad M) (All [e] (Monad (All [a] (Reader e (M a)))))))
  (def: applicative (A.compose Applicative<Reader> (get@ #monad.applicative Monad<M>)))
  (def: (join eMeMa)
    (function (_ env)
      (do Monad<M>
        [eMa (run env eMeMa)]
        (run env eMa)))))

(def: #export lift
  {#.doc "Lift monadic values to the Reader wrapper."}
  (All [M e a] (-> (M a) (Reader e (M a))))
  (:: Monad<Reader> wrap))
