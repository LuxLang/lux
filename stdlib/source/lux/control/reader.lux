(.module:
  [lux #*
   [control
    ["F" functor]
    ["A" apply]
    [monad (#+ do Monad)]]])

## [Types]
(type: #export (Reader r a)
  {#.doc "Computations that have access to some environmental value."}
  (-> r a))

## [Structures]
(structure: #export Functor<Reader>
  (All [r] (F.Functor (Reader r)))
  
  (def: (map f fa)
    (function (_ env)
      (f (fa env)))))

(structure: #export Apply<Reader>
  (All [r] (A.Apply (Reader r)))
  
  (def: functor Functor<Reader>)

  (def: (apply ff fa)
    (function (_ env)
      ((ff env) (fa env)))))

(structure: #export Monad<Reader>
  (All [r] (Monad (Reader r)))
  
  (def: functor Functor<Reader>)

  (def: (wrap x)
    (function (_ env) x))

  (def: (join mma)
    (function (_ env)
      (mma env env))))

## [Values]
(def: #export ask
  {#.doc "Get the environment."}
  (All [r] (Reader r r))
  (function (_ env) env))

(def: #export (local change proc)
  {#.doc "Run computation with a locally-modified environment."}
  (All [r a] (-> (-> r r) (Reader r a) (Reader r a)))
  (|>> change proc))

(def: #export (run env proc)
  (All [r a] (-> r (Reader r a) a))
  (proc env))

(structure: #export (ReaderT Monad<M>)
  {#.doc "Monad transformer for Reader."}
  (All [M] (-> (Monad M) (All [e] (Monad (All [a] (Reader e (M a)))))))

  (def: functor (F.compose Functor<Reader> (get@ #monad.functor Monad<M>)))

  (def: wrap (|>> (:: Monad<M> wrap) (:: Monad<Reader> wrap)))
  
  (def: (join eMeMa)
    (function (_ env)
      (do Monad<M>
        [eMa (run env eMeMa)]
        (run env eMa)))))

(def: #export lift
  {#.doc "Lift monadic values to the Reader wrapper."}
  (All [M e a] (-> (M a) (Reader e (M a))))
  (:: Monad<Reader> wrap))
