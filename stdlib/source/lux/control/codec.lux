##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;module:
  lux
  (lux control/monad
       data/error))

## [Signatures]
(sig: #export (Codec m a)
  {#;doc "A way to move back-and-forth between a type and an alternative representation for it."}
  (: (-> a m)
     encode)
  (: (-> m (Error a))
     decode))

## [Values]
(struct: #export (compC Codec<c,b> Codec<b,a>)
  {#;doc "Codec composition."}
  (All [a b c] (-> (Codec c b) (Codec b a) (Codec c a)))
  (def: encode (|>. (:: Codec<b,a> encode) (:: Codec<c,b> encode)))

  (def: (decode cy)
    (do Monad<Error>
      [by (:: Codec<c,b> decode cy)]
      (:: Codec<b,a> decode by)))
  )
