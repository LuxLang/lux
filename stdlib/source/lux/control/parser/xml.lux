(.module:
  [lux #*
   [abstract
    [monad (#+ do)]]
   [control
    ["." try (#+ Try)]
    ["." exception (#+ exception:)]]
   [data
    ["." name ("#@." equivalence codec)]
    ["." text ("#@." monoid)]
    [collection
     ["." list ("#@." functor)]
     ["." dictionary]]
    [format
     ["/" xml (#+ XML)]]]]
  ["." //])

(type: #export (Parser a)
  (//.Parser (List XML) a))

(exception: #export empty-input)
(exception: #export unexpected-input)
(exception: #export unknown-attribute)

(exception: #export (wrong-tag {tag Name})
  (exception.report
   ["Tag" (name@encode tag)]))

(def: blank-line ($_ text@compose text.new-line text.new-line))

(exception: #export (unconsumed-inputs {inputs (List XML)})
  (|> inputs
      (list@map (:: /.codec encode))
      (text.join-with blank-line)))

(def: #export text
  (Parser Text)
  (function (_ docs)
    (case docs
      #.Nil
      (exception.throw empty-input [])
      
      (#.Cons head tail)
      (case head
        (#/.Text value)
        (#try.Success [tail value])
        
        (#/.Node _)
        (exception.throw unexpected-input [])))))

(def: #export (attr name)
  (-> Name (Parser Text))
  (function (_ docs)
    (case docs
      #.Nil
      (exception.throw empty-input [])
      
      (#.Cons head _)
      (case head
        (#/.Text _)
        (exception.throw unexpected-input [])
        
        (#/.Node tag attrs children)
        (case (dictionary.get name attrs)
          #.None
          (exception.throw unknown-attribute [])
          
          (#.Some value)
          (#try.Success [docs value]))))))

(def: (run' reader docs)
  (All [a] (-> (Parser a) (List XML) (Try a)))
  (case (//.run reader docs)
    (#try.Success [remaining output])
    (if (list.empty? remaining)
      (#try.Success output)
      (exception.throw unconsumed-inputs remaining))
    
    (#try.Failure error)
    (#try.Failure error)))

(def: #export (node tag)
  (-> Name (Parser Any))
  (function (_ docs)
    (case docs
      #.Nil
      (exception.throw empty-input [])
      
      (#.Cons head _)
      (case head
        (#/.Text _)
        (exception.throw unexpected-input [])
        
        (#/.Node _tag _attrs _children)
        (if (name@= tag _tag)
          (#try.Success [docs []])
          (exception.throw wrong-tag tag))))))

(def: #export (children reader)
  (All [a] (-> (Parser a) (Parser a)))
  (function (_ docs)
    (case docs
      #.Nil
      (exception.throw empty-input [])
      
      (#.Cons head tail)
      (case head
        (#/.Text _)
        (exception.throw unexpected-input [])
        
        (#/.Node _tag _attrs _children)
        (do try.monad
          [output (run' reader _children)]
          (wrap [tail output]))))))

(def: #export ignore
  (Parser Any)
  (function (_ docs)
    (case docs
      #.Nil
      (exception.throw empty-input [])
      
      (#.Cons head tail)
      (#try.Success [tail []]))))

(def: #export (run reader document)
  (All [a] (-> (Parser a) XML (Try a)))
  (run' reader (list document)))
