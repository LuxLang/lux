(.module:
  [lux #*
   [control
    ["." try (#+ Try)]
    ["." exception (#+ exception:)]]
   [data
    [collection
     [tree (#+ Tree)
      ["." zipper (#+ Zipper)]]]]]
  ["." //])

(type: #export (Parser t a)
  (//.Parser (Zipper t) a))

(def: #export (run' parser zipper)
  (All [t a] (-> (Parser t a) (Zipper t) (Try a)))
  (case (//.run parser zipper)
    (#try.Success [zipper output])
    (#try.Success output)

    (#try.Failure error)
    (#try.Failure error)))

(def: #export (run parser tree)
  (All [t a] (-> (Parser t a) (Tree t) (Try a)))
  (run' parser (zipper.zip tree)))

(def: #export value
  (All [t] (Parser t t))
  (function (_ zipper)
    (#try.Success [zipper (zipper.value zipper)])))

(exception: #export cannot-move-further)

(template [<name> <direction>]
  [(def: #export <name>
     (All [t] (Parser t []))
     (function (_ zipper)
       (let [next (<direction> zipper)]
         (if (is? zipper next)
           (exception.throw cannot-move-further [])
           (#try.Success [next []])))))]

  [down      zipper.down]
  [up        zipper.up]
  [right     zipper.right]
  [left      zipper.left]
  [rightmost zipper.rightmost]
  [leftmost  zipper.leftmost]
  [next      zipper.next]
  [prev      zipper.prev]
  [end       zipper.end]
  [start     zipper.start]
  )
