(.module:
  [lux #*
   [control
    ["." try (#+ Try)]
    ["." exception (#+ exception:)]]
   [data
    [tree (#+ Tree)
     ["." zipper (#+ Zipper)]]]]
  ["." //])

(type: #export (Parser t a)
  (//.Parser (Zipper t) a))

(def: #export (run-zipper zipper parser)
  (All [t a] (-> (Zipper t) (Parser t a) (Try a)))
  (case (//.run zipper parser)
    (#try.Success [zipper output])
    (#try.Success output)

    (#try.Failure error)
    (#try.Failure error)))

(def: #export (run tree parser)
  (All [t a] (-> (Tree t) (Parser t a) (Try a)))
  (run-zipper (zipper.zip tree) parser))

(def: #export value
  (All [t] (Parser t t))
  (function (_ zipper)
    (#try.Success [zipper (zipper.value zipper)])))

(exception: #export cannot-move-further)

(template [<name> <direction>]
  [(def: #export <name>
     (All [t] (Parser t []))
     (function (_ zipper)
       (let [next (<direction> zipper)]
         (if (is? zipper next)
           (exception.throw cannot-move-further [])
           (#try.Success [next []])))))]

  [up        zipper.up]
  [down      zipper.down]
  [left      zipper.left]
  [right     zipper.right]
  [root      zipper.root]
  [rightmost zipper.rightmost]
  [leftmost  zipper.leftmost]
  [next      zipper.next]
  [prev      zipper.prev]
  )
