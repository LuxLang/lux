(.module:
  [lux #*
   [control
    [functor (#+ Functor)]
    [apply (#+ Apply)]
    ["." monad (#+ Monad do)]
    ["ex" exception (#+ Exception exception:)]]
   [data
    ["." error (#+ Error)]
    ["." text
     format]
    [collection
     ["." list ("#/." fold)]]]])

(type: (Cleaner r m)
  (-> r (m (Error Any))))

(type: #export (Region r m a)
  (-> [r (List (Cleaner r m))]
      (m [(List (Cleaner r m))
          (Error a)])))

(def: separator
  Text
  (format text.new-line
          "-----------------------------------------" text.new-line
          "-----------------------------------------" text.new-line
          "-----------------------------------------" text.new-line
          text.new-line))

(exception: #export [a] (clean-up-error {error Text}
                                        {output (Error a)})
  (format error
          (case output
            (#error.Success _)
            ""

            (#error.Failure error|output)
            (format separator
                    error|output))))

(def: (combine-outcomes clean-up output)
  (All [a] (-> (Error Any) (Error a) (Error a)))
  (case clean-up
    (#error.Success _)
    output

    (#error.Failure error|clean-up)
    (ex.throw clean-up-error [error|clean-up output])))

(def: #export (run Monad<m> computation)
  (All [m a]
    (-> (Monad m) (All [r] (Region r m a))
        (m (Error a))))
  (do Monad<m>
    [[cleaners output] (computation [[] (list)])
     results (monad.map @ (function (_ cleaner) (cleaner []))
                        cleaners)]
    (wrap (list/fold combine-outcomes output results))))

(def: #export (acquire Monad<m> cleaner value)
  (All [m a] (-> (Monad m) (-> a (m (Error Any))) a
                 (All [r] (Region r m a))))
  (function (_ [region cleaners])
    (:: Monad<m> wrap [(#.Cons (function (_ region) (cleaner value))
                               cleaners)
                       (#error.Success value)])))

(structure: #export (functor Functor<m>)
  (All [m]
    (-> (Functor m)
        (All [r] (Functor (Region r m)))))

  (def: (map f)
    (function (_ fa)
      (function (_ region+cleaners)
        (:: Functor<m> map
            (function (_ [cleaners' temp])
              [cleaners' (case temp
                           (#error.Success value)
                           (#error.Success (f value))

                           (#error.Failure error)
                           (#error.Failure error))])
            (fa region+cleaners))))))

(structure: #export (apply Monad<m>)
  (All [m]
    (-> (Monad m)
        (All [r] (Apply (Region r m)))))

  (def: &functor
    (..functor (get@ #monad.&functor Monad<m>)))

  (def: (apply ff fa)
    (function (_ [region cleaners])
      (do Monad<m>
        [[cleaners ef] (ff [region cleaners])
         [cleaners ea] (fa [region cleaners])]
        (case [ef ea]
          [(#error.Success f) (#error.Success a)]
          (wrap [cleaners (#error.Success (f a))])

          (^or [(#error.Failure error) _]
               [_ (#error.Failure error)])
          (wrap [cleaners (#error.Failure error)]))))))

(structure: #export (monad Monad<m>)
  (All [m]
    (-> (Monad m)
        (All [r] (Monad (Region r m)))))

  (def: &functor
    (..functor (get@ #monad.&functor Monad<m>)))

  (def: (wrap value)
    (function (_ [region cleaners])
      (:: Monad<m> wrap [cleaners (#error.Success value)])))

  (def: (join ffa)
    (function (_ [region cleaners])
      (do Monad<m>
        [[cleaners efa] (ffa [region cleaners])]
        (case efa
          (#error.Success fa)
          (fa [region cleaners])

          (#error.Failure error)
          (wrap [cleaners (#error.Failure error)]))))))

(def: #export (fail Monad<m> error)
  (All [m a]
    (-> (Monad m) Text
        (All [r] (Region r m a))))
  (function (_ [region cleaners])
    (:: Monad<m> wrap [cleaners (#error.Failure error)])))

(def: #export (throw Monad<m> exception message)
  (All [m e a]
    (-> (Monad m) (Exception e) e
        (All [r] (Region r m a))))
  (function (_ [region cleaners])
    (:: Monad<m> wrap [cleaners (ex.throw exception message)])))

(def: #export (lift Monad<m> operation)
  (All [m a]
    (-> (Monad m) (m a)
        (All [r] (Region r m a))))
  (function (_ [region cleaners])
    (do Monad<m>
      [output operation]
      (wrap [cleaners (#error.Success output)]))))
