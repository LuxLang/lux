##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;module:
  lux
  ["F" ../functor]
  [lux/data/struct/list #* "" Fold<List>])

## [Signatures]
(sig: #export (CoMonad w)
  (: (F;Functor w)
     functor)
  (: (All [a]
       (-> (w a) a))
     unwrap)
  (: (All [a]
       (-> (w a) (w (w a))))
     split))

## [Syntax]
(macro: #export (be tokens state)
  {#;doc (doc "A co-monadic parallel to the \"do\" macro."
              (let [square (lambda [n] (i.* n n))]
                (be CoMonad<Stream>
                  [inputs (iterate i.inc 2)]
                  (square (head inputs)))))}
  (case tokens
    (#;Cons comonad (#;Cons [_ (#;TupleS bindings)] (#;Cons body #;Nil)))
    (let [g!@ (: AST [["" -1 -1] (#;SymbolS ["" "@"])])
          g!map (: AST [["" -1 -1] (#;SymbolS ["" " map "])])
          g!split (: AST [["" -1 -1] (#;SymbolS ["" " split "])])
          body' (fold (: (-> [AST AST] AST AST)
                         (lambda [binding body']
                           (let [[var value] binding]
                             (case var
                               [_ (#;TagS ["" "let"])]
                               (` (let (~ value) (~ body')))

                               _
                               (` (|> (~ value) (~ g!split) ((~ g!map) (lambda [(~ var)] (~ body')))))
                               ))))
                      body
                      (reverse (as-pairs bindings)))]
      (#;Right [state (#;Cons (` (;_lux_case (~ comonad)
                                             (~ g!@)
                                             (;_lux_case (~ g!@)
                                                         {#functor {#F;map (~ g!map)} #unwrap (~' unwrap) #split (~ g!split)}
                                                         (~ body'))))
                              #;Nil)]))

    _
    (#;Left "Wrong syntax for be")))
