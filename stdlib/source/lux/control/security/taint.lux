(.module:
  [lux #*
   [control
    [predicate (#+ Predicate)]
    [functor (#+ Functor)]
    [apply (#+ Apply)]
    [monad (#+ Monad)]]
   [type
    abstract]])

(abstract: #export (Dirty a)
  a

  (def: #export taint
    (All [a] (-> a (Dirty a)))
    (|>> :abstraction))

  (def: #export (validate valid? dirty)
    (All [a] (-> (Predicate a) (Dirty a) (Maybe a)))
    (let [value (:representation dirty)]
      (if (valid? value)
        (#.Some value)
        #.None)))

  (def: #export trust
    (All [a] (-> (Dirty a) a))
    (|>> :representation))

  (structure: #export _ (Functor Dirty)
    (def: (map f fa)
      (|> fa :representation f :abstraction)))

  (structure: #export _ (Apply Dirty)
    (def: functor Functor<Dirty>)

    (def: (apply ff fa)
      (:abstraction ((:representation ff) (:representation fa)))))

  (structure: #export _ (Monad Dirty)
    (def: functor Functor<Dirty>)

    (def: wrap (|>> :abstraction))

    (def: join (|>> :representation)))
  )
