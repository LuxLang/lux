##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;module:
  lux
  (lux (control monad)
       (data text/format)
       [compiler]
       (macro [ast]
              ["s" syntax #+ syntax: Syntax])
       [type]
       (type [check])))

(do-template [<name> <rec> <nat-op> <int-op> <real-op> <frac-op>]
  [(syntax: #export (<name> {args ($_ s;alt
                                      (s;seq s;symbol s;symbol)
                                      (s;seq s;any s;any)
                                      s;symbol
                                      s;any
                                      s;end)})
     (case args
       (+0 [x y])
       (do @
         [=x (compiler;find-type x)
          =y (compiler;find-type y)
          op (cond (and (check;checks? Nat =x)
                        (check;checks? Nat =y))
                   (wrap (` <nat-op>))

                   (and (check;checks? Int =x)
                        (check;checks? Int =y))
                   (wrap (` <int-op>))

                   (and (check;checks? Real =x)
                        (check;checks? Real =y))
                   (wrap (` <real-op>))

                   (and (check;checks? Frac =x)
                        (check;checks? Frac =y))
                   (wrap (` <frac-op>))

                   (compiler;fail (format "No operation for types: " (%type =x) " and " (%type =y))))]
         (wrap (list (` ((~ op) (~ (ast;symbol x)) (~ (ast;symbol y)))))))
       
       (+1 [x y])
       (do @
         [g!x (compiler;gensym "g!x")
          g!y (compiler;gensym "g!y")]
         (wrap (list (` (let [(~ g!x) (~ x)
                              (~ g!y) (~ y)]
                          (<rec> (~ g!x) (~ g!y)))))))

       (+2 x)
       (do @
         [=x (compiler;find-type x)
          op (cond (check;checks? Nat =x)
                   (wrap (` <nat-op>))

                   (check;checks? Int =x)
                   (wrap (` <int-op>))

                   (check;checks? Real =x)
                   (wrap (` <real-op>))

                   (check;checks? Frac =x)
                   (wrap (` <frac-op>))

                   (compiler;fail (format "No operation for type: " (%type =x))))]
         (wrap (list (` ((~ op) (~ (ast;symbol x)))))))
       
       (+3 x)
       (do @
         [g!x (compiler;gensym "g!x")]
         (wrap (list (` (let [(~ g!x) (~ x)]
                          (<rec> (~ g!x)))))))

       (+4 [])
       (do @
         [=e compiler;expected-type
          op (cond (check;checks? (-> Nat Nat Nat) =e)
                   (wrap (` <nat-op>))

                   (check;checks? (-> Int Int Int) =e)
                   (wrap (` <int-op>))

                   (check;checks? (-> Real Real Real) =e)
                   (wrap (` <real-op>))

                   (check;checks? (-> Frac Frac Frac) =e)
                   (wrap (` <frac-op>))

                   (compiler;fail (format "No operation for type: " (%type =e))))]
         (wrap (list op)))
       ))]

  [+ ;;+ n.+ i.+ r.+ f.+]
  [- ;;- n.- i.- r.- f.-]
  [* ;;* n.* i.* r.* f.*]
  [/ ;;/ n./ i./ r./ f./]
  [% ;;% n.% i.% r.% f.%]
  )

(do-template [<name> <rec> <nat-op> <int-op> <real-op>]
  [(syntax: #export (<name> {args ($_ s;alt
                                      (s;seq s;symbol s;symbol)
                                      (s;seq s;any s;any)
                                      s;symbol
                                      s;any
                                      s;end)})
     (case args
       (+0 [x y])
       (do @
         [=x (compiler;find-type x)
          =y (compiler;find-type y)
          op (cond (and (check;checks? Nat =x)
                        (check;checks? Nat =y))
                   (wrap (` <nat-op>))

                   (and (check;checks? Int =x)
                        (check;checks? Int =y))
                   (wrap (` <int-op>))

                   (and (check;checks? Real =x)
                        (check;checks? Real =y))
                   (wrap (` <real-op>))

                   (compiler;fail (format "No operation for types: " (%type =x) " and " (%type =y))))]
         (wrap (list (` ((~ op) (~ (ast;symbol x)) (~ (ast;symbol y)))))))
       
       (+1 [x y])
       (do @
         [g!x (compiler;gensym "g!x")
          g!y (compiler;gensym "g!y")]
         (wrap (list (` (let [(~ g!x) (~ x)
                              (~ g!y) (~ y)]
                          (<rec> (~ g!x) (~ g!y)))))))

       (+2 x)
       (do @
         [=x (compiler;find-type x)
          op (cond (check;checks? Nat =x)
                   (wrap (` <nat-op>))

                   (check;checks? Int =x)
                   (wrap (` <int-op>))

                   (check;checks? Real =x)
                   (wrap (` <real-op>))

                   (compiler;fail (format "No operation for type: " (%type =x))))]
         (wrap (list (` ((~ op) (~ (ast;symbol x)))))))
       
       (+3 x)
       (do @
         [g!x (compiler;gensym "g!x")]
         (wrap (list (` (let [(~ g!x) (~ x)]
                          (<rec> (~ g!x)))))))

       (+4 [])
       (do @
         [=e compiler;expected-type
          op (cond (check;checks? (-> Nat Nat Nat) =e)
                   (wrap (` <nat-op>))

                   (check;checks? (-> Int Int Int) =e)
                   (wrap (` <int-op>))

                   (check;checks? (-> Real Real Real) =e)
                   (wrap (` <real-op>))

                   (compiler;fail (format "No operation for type: " (%type =e))))]
         (wrap (list op)))
       ))]

  [=  ;;=  n.=  i.=  r.=]
  [<  ;;<  n.<  i.<  r.<]
  [<= ;;<= n.<= i.<= r.<=]
  [>  ;;>  n.>  i.>  r.>]
  [>= ;;>= n.>= i.>= r.>=]
  )

(do-template [<name> <rec> <nat-op> <int-op>]
  [(syntax: #export (<name> {args ($_ s;alt
                                      (s;seq s;symbol s;symbol)
                                      (s;seq s;any s;any)
                                      s;symbol
                                      s;any
                                      s;end)})
     (case args
       (+0 [x y])
       (do @
         [=x (compiler;find-type x)
          =y (compiler;find-type y)
          op (cond (and (check;checks? Nat =x)
                        (check;checks? Nat =y))
                   (wrap (` <nat-op>))

                   (and (check;checks? Int =x)
                        (check;checks? Int =y))
                   (wrap (` <int-op>))

                   (compiler;fail (format "No operation for types: " (%type =x) " and " (%type =y))))]
         (wrap (list (` ((~ op) (~ (ast;symbol x)) (~ (ast;symbol y)))))))
       
       (+1 [x y])
       (do @
         [g!x (compiler;gensym "g!x")
          g!y (compiler;gensym "g!y")]
         (wrap (list (` (let [(~ g!x) (~ x)
                              (~ g!y) (~ y)]
                          (<rec> (~ g!x) (~ g!y)))))))

       (+2 x)
       (do @
         [=x (compiler;find-type x)
          op (cond (check;checks? Nat =x)
                   (wrap (` <nat-op>))

                   (check;checks? Int =x)
                   (wrap (` <int-op>))

                   (compiler;fail (format "No operation for type: " (%type =x))))]
         (wrap (list (` ((~ op) (~ (ast;symbol x)))))))
       
       (+3 x)
       (do @
         [g!x (compiler;gensym "g!x")]
         (wrap (list (` (let [(~ g!x) (~ x)]
                          (<rec> (~ g!x)))))))

       (+4 [])
       (do @
         [=e compiler;expected-type
          op (cond (check;checks? (-> Nat Nat Nat) =e)
                   (wrap (` <nat-op>))

                   (check;checks? (-> Int Int Int) =e)
                   (wrap (` <int-op>))

                   (compiler;fail (format "No operation for type: " (%type =e))))]
         (wrap (list op)))
       ))]

  [min ;;min n.min i.min]
  [max ;;max n.max i.max]
  )

(do-template [<name> <rec> <nat-op> <int-op>]
  [(syntax: #export (<name> {args ($_ s;alt
                                      s;symbol
                                      s;any
                                      s;end)})
     (case args
       (+0 x)
       (do @
         [=x (compiler;find-type x)
          op (cond (check;checks? Nat =x)
                   (wrap (` <nat-op>))

                   (check;checks? Int =x)
                   (wrap (` <int-op>))

                   (compiler;fail (format "No operation for type: " (%type =x))))]
         (wrap (list (` ((~ op) (~ (ast;symbol x)))))))
       
       (+1 x)
       (do @
         [g!x (compiler;gensym "g!x")]
         (wrap (list (` (let [(~ g!x) (~ x)]
                          (<rec> (~ g!x)))))))

       (+2 [])
       (do @
         [=e compiler;expected-type
          op (cond (check;checks? (-> Nat Nat) =e)
                   (wrap (` <nat-op>))

                   (check;checks? (-> Int Int) =e)
                   (wrap (` <int-op>))

                   (compiler;fail (format "No operation for type: " (%type =e))))]
         (wrap (list op)))
       ))]

  [inc   ;;inc   n.inc   i.inc]
  [dec   ;;dec   n.dec   i.dec]
  )

(do-template [<name> <rec> <nat-op> <int-op>]
  [(syntax: #export (<name> {args ($_ s;alt
                                      s;symbol
                                      s;any
                                      s;end)})
     (case args
       (+0 x)
       (do @
         [=x (compiler;find-type x)
          op (cond (check;checks? Nat =x)
                   (wrap (` <nat-op>))

                   (check;checks? Int =x)
                   (wrap (` <int-op>))

                   (compiler;fail (format "No operation for type: " (%type =x))))]
         (wrap (list (` ((~ op) (~ (ast;symbol x)))))))
       
       (+1 x)
       (do @
         [g!x (compiler;gensym "g!x")]
         (wrap (list (` (let [(~ g!x) (~ x)]
                          (<rec> (~ g!x)))))))

       (+2 [])
       (do @
         [=e compiler;expected-type
          op (cond (check;checks? (-> Nat Bool) =e)
                   (wrap (` <nat-op>))

                   (check;checks? (-> Int Bool) =e)
                   (wrap (` <int-op>))

                   (compiler;fail (format "No operation for type: " (%type =e))))]
         (wrap (list op)))
       ))]

  [even? ;;even? n.even? i.even?]
  [odd?  ;;odd?  n.odd?  i.odd?]
  )
