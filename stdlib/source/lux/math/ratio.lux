##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;module:
  lux
  (lux [math]
       (control eq
                [ord]
                number
                codec
                monad)
       (data [number "i:" Number<Int> Codec<Text,Int>]
             [text "Text/" Monoid<Text>]
             error)
       [compiler]
       (macro [ast]
              ["s" syntax #+ syntax: Syntax])))

(type: #export Ratio
  {#numerator Int
   #denominator Int})

(def: #hidden (normalize (^slots [#numerator #denominator]))
  (-> Ratio Ratio)
  (let [common (math;gcd numerator denominator)
        numerator (i./ common numerator)
        denominator (i./ common denominator)]
    {#numerator (if (and (i.< 0 numerator)
                         (i.< 0 denominator))
                  (i:abs numerator)
                  numerator)
     #denominator (i:abs denominator)}))

(def: #export (q.* param input)
  (-> Ratio Ratio Ratio)
  (normalize [(i.* (get@ #numerator param)
                   (get@ #numerator input))
              (i.* (get@ #denominator param)
                   (get@ #denominator input))]))

(def: #export (q./ param input)
  (-> Ratio Ratio Ratio)
  (normalize [(i.* (get@ #denominator param)
                   (get@ #numerator input))
              (i.* (get@ #numerator param)
                   (get@ #denominator input))]))

(def: #export (q.+ param input)
  (-> Ratio Ratio Ratio)
  (normalize [(i.+ (i.* (get@ #denominator input)
                        (get@ #numerator param))
                   (i.* (get@ #denominator param)
                        (get@ #numerator input)))
              (i.* (get@ #denominator param)
                   (get@ #denominator input))]))

(def: #export (q.- param input)
  (-> Ratio Ratio Ratio)
  (normalize [(i.- (i.* (get@ #denominator input)
                        (get@ #numerator param))
                   (i.* (get@ #denominator param)
                        (get@ #numerator input)))
              (i.* (get@ #denominator param)
                   (get@ #denominator input))]))

(def: #export (q.% param input)
  (-> Ratio Ratio Ratio)
  (let [quot (i./ (i.* (get@ #denominator input)
                       (get@ #numerator param))
                  (i.* (get@ #denominator param)
                       (get@ #numerator input)))]
    (q.- (update@ #numerator (i.* quot) param)
         input)))

(def: #export (q.= param input)
  (-> Ratio Ratio Bool)
  (and (i.= (get@ #numerator param)
            (get@ #numerator input))
       (i.= (get@ #denominator param)
            (get@ #denominator input))))

(do-template [<name> <op>]
  [(def: #export (<name> param input)
     (-> Ratio Ratio Bool)
     (and (<op> (i.* (get@ #denominator input)
                     (get@ #numerator param))
                (i.* (get@ #denominator param)
                     (get@ #numerator input)))))]

  [q.<  i.<]
  [q.<= i.<=]
  [q.>  i.>]
  [q.>= i.>=]
  )

(struct: #export _ (Eq Ratio)
  (def: = q.=))

(struct: #export _ (ord;Ord Ratio)
  (def: eq Eq<Ratio>)
  (def: < q.<)
  (def: <= q.<=)
  (def: > q.>)
  (def: >= q.>=))

(struct: #export _ (Number Ratio)
  (def: ord Ord<Ratio>)
  (def: + q.+)
  (def: - q.-)
  (def: * q.*)
  (def: / q./)
  (def: % q.%)
  (def: negate (|>. (update@ #numerator i:negate) normalize))
  (def: abs (|>. (update@ #numerator i:abs) (update@ #denominator i:abs)))
  (def: (signum x)
    {#numerator (i:signum (get@ #numerator x))
     #denominator 1}))

(def: separator Text ":")

(struct: #export _ (Codec Text Ratio)
  (def: (encode (^slots [#numerator #denominator]))
    ($_ Text/append (i:encode numerator) separator (i:encode denominator)))

  (def: (decode input)
    (case (text;split-with separator input)
      (#;Some [num denom])
      (do Monad<Error>
        [numerator (i:decode num)
         denominator (i:decode denom)]
        (wrap (normalize {#numerator numerator
                          #denominator denominator})))
      
      #;None
      (#;Left (Text/append "Invalid syntax for ratio: " input)))))

(syntax: #export (ratio numerator denominator)
  (wrap (list (` (normalize {#;;numerator (~ numerator)
                             #;;denominator (~ denominator)})))))
