(.module:
  lux
  (lux (control [monad #+ do]
                ["ex" exception #+ exception:])
       (data ["e" error]
             [text]
             (coll (dictionary ["dict" unordered #+ Dict]))))
  [// #+ Eval]
  [//compiler #+ Operation Compiler]
  [//analysis #+ Analyser]
  [//synthesis #+ Synthesizer]
  [//translation #+ Translator])

(do-template [<name>]
  [(exception: #export (<name> {extension Text})
     extension)]

  [unknown-analysis]
  [unknown-synthesis]
  [unknown-translation]
  [unknown-statement]

  [cannot-define-analysis-more-than-once]
  [cannot-define-synthesis-more-than-once]
  [cannot-define-translation-more-than-once]
  [cannot-define-statement-more-than-once]
  )

(type: #export Analysis
  (-> Analyser Eval
      (Compiler .Lux
                (List Code)
                //analysis.Analysis)))

(type: #export Synthesis
  (-> Synthesizer
      (Compiler //synthesis.State
                (List //analysis.Analysis)
                //synthesis.Synthesis)))

(type: #export (Translation anchor code)
  (-> (Translator anchor code)
      (Compiler (//translation.State anchor code)
                (List //synthesis.Synthesis)
                code)))

(type: #export Statement
  (-> (List Code) (Meta Any)))

(type: #export (Extension e)
  (Dict Text e))

(type: #export Extensions
  {#analysis (Extension Analysis)
   #synthesis (Extension Synthesis)
   #translation (Extension Translation)
   #statement (Extension Statement)})

(def: #export fresh
  Extensions
  {#analysis (dict.new text.Hash<Text>)
   #synthesis (dict.new text.Hash<Text>)
   #translation (dict.new text.Hash<Text>)
   #statement (dict.new text.Hash<Text>)})

(def: get
  (Meta Extensions)
  (function (_ compiler)
    (#e.Success [compiler
                 (|> compiler (get@ #.extensions) (:! Extensions))])))

(def: (set extensions)
  (-> Extensions (Meta Any))
  (function (_ compiler)
    (#e.Success [(set@ #.extensions (:! Nothing extensions) compiler)
                 []])))

(do-template [<name> <type> <category> <exception>]
  [(def: #export (<name> name)
     (-> Text (Meta <type>))
     (do //compiler.Monad<Operation>
       [extensions ..get]
       (case (dict.get name (get@ <category> extensions))
         (#.Some extension)
         (wrap extension)

         #.None
         (//compiler.throw <exception> name))))]

  [find-analysis    Analysis    #analysis    unknown-analysis]
  [find-synthesis   Synthesis   #synthesis   unknown-synthesis]
  [find-translation Translation #translation unknown-translation]
  [find-statement   Statement   #statement   unknown-statement]
  )

(def: #export empty
  (All [e] (Extension e))
  (dict.new text.Hash<Text>))

(do-template [<params> <all> <state> <type> <category>]
  [(def: #export <all>
     (All <params> (Operation <state> (Extension <type>)))
     (|> ..get
         (:: //compiler.Monad<Operation> map (get@ <category>))))]

  [[]            all-analyses     .Lux
   Analysis    #analysis]
  [[]            all-syntheses    //synthesis.State
   Synthesis   #synthesis]
  [[anchor code] all-translations (//translation.State anchor code)
   Translation #translation]
  [[]            all-statements   Any
   Statement   #statement]
  )

(do-template [<name> <type> <category> <exception>]
  [(def: #export (<name> name extension)
     (-> Text <type> (Meta Any))
     (do //compiler.Monad<Operation>
       [extensions ..get
        _ (if (not (dict.contains? name (get@ <category> extensions)))
            (wrap [])
            (//compiler.throw <exception> name))
        _ (..set (update@ <category> (dict.put name extension) extensions))]
       (wrap [])))]

  [install-analysis    Analysis    #analysis    cannot-define-analysis-more-than-once]
  [install-synthesis   Synthesis   #synthesis   cannot-define-synthesis-more-than-once]
  [install-translation Translation #translation cannot-define-translation-more-than-once]
  [install-statement   Statement   #statement   cannot-define-statement-more-than-once]
  )
