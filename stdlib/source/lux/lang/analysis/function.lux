(.module:
  [lux #- function]
  (lux (control monad
                ["ex" exception #+ exception:])
       (data [maybe]
             [text]
             text/format
             (coll [list "list/" Fold<List> Monoid<List> Monad<List>]))
       [macro]
       (macro [code])
       [lang]
       (lang [type]
             (type ["tc" check])
             [".L" scope]
             [".L" analysis #+ Analysis Analyser]
             (analysis [".A" type]
                       [".A" inference]))))

(exception: #export (cannot-analyse {expected Type} {function Text} {argument Text} {body Code})
  (ex.report ["Type" (%type expected)]
             ["Function" function]
             ["Argument" argument]
             ["Body" (%code body)]))

(exception: #export (cannot-apply {function Type} {arguments (List Code)})
  (ex.report [" Function" (%type function)]
             ["Arguments" (|> arguments
                              list.enumerate
                              (list/map (.function (_ [idx argC])
                                          (format "\n  " (%n idx) " " (%code argC))))
                              (text.join-with ""))]))

## [Analysers]
(def: #export (function analyse function-name arg-name body)
  (-> Analyser Text Text Code (Meta Analysis))
  (do macro.Monad<Meta>
    [functionT macro.expected-type]
    (loop [expectedT functionT]
      (lang.with-stacked-errors
        (.function (_ _)
          (ex.construct cannot-analyse [expectedT function-name arg-name body]))
        (case expectedT
          (#.Named name unnamedT)
          (recur unnamedT)

          (#.Apply argT funT)
          (case (type.apply (list argT) funT)
            (#.Some value)
            (recur value)

            #.None
            (lang.fail (ex.construct cannot-analyse [expectedT function-name arg-name body])))

          (^template [<tag> <instancer>]
            (<tag> _)
            (do @
              [[_ instanceT] (typeA.with-env <instancer>)]
              (recur (maybe.assume (type.apply (list instanceT) expectedT)))))
          ([#.UnivQ tc.existential]
           [#.ExQ tc.var])
          
          (#.Var id)
          (do @
            [?expectedT' (typeA.with-env
                           (tc.read id))]
            (case ?expectedT'
              (#.Some expectedT')
              (recur expectedT')

              _
              ## Inference
              (do @
                [[input-id inputT] (typeA.with-env tc.var)
                 [output-id outputT] (typeA.with-env tc.var)
                 #let [functionT (#.Function inputT outputT)]
                 functionA (recur functionT)
                 _ (typeA.with-env
                     (tc.check expectedT functionT))]
                (wrap functionA))
              ))

          (#.Function inputT outputT)
          (<| (:: @ map (.function (_ [scope bodyA])
                          (#analysisL.Function (scopeL.environment scope) bodyA)))
              lang.with-scope
              ## Functions have access not only to their argument, but
              ## also to themselves, through a local variable.
              (scopeL.with-local [function-name expectedT])
              (scopeL.with-local [arg-name inputT])
              (typeA.with-type outputT)
              (analyse body))
          
          _
          (lang.fail "")
          )))))

(def: #export (apply analyse functionT functionA args)
  (-> Analyser Type Analysis (List Code) (Meta Analysis))
  (lang.with-stacked-errors
    (.function (_ _)
      (ex.construct cannot-apply [functionT args]))
    (do macro.Monad<Meta>
      [[applyT argsA] (inferenceA.general analyse functionT args)]
      (wrap (analysisL.apply [functionA argsA])))))
