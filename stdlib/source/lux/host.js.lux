(.module:
  [lux #*
   ["." meta (#+ with-gensyms)]
   [abstract
    [monad (#+ do)]]
   [control
    ["." io]
    ["<>" parser
     ["<c>" code (#+ Parser)]]]
   [data
    ["." product]
    ["." maybe]
    ["." text
     ["%" format (#+ format)]]
    [collection
     ["." list ("#\." functor fold)]]]
   [type
    abstract]
   [macro
    [syntax (#+ syntax:)]
    ["." code]
    ["." template]]])

(abstract: #export (Object brand) Any)

(template [<name>]
  [(with-expansions [<brand> (template.identifier [<name> "'"])]
     (abstract: #export <brand> Any)
     (type: #export <name> (Object <brand>)))]

  [Function]
  [Symbol]
  [Null]
  [Undefined]
  )

(template [<name> <type>]
  [(type: #export <name> <type>)]

  [Boolean Bit]
  [Number  Frac]
  [String  Text]
  )

(type: Nullable [Bit Code])

(def: nullable
  (Parser Nullable)
  (let [token (' #?)]
    (<| (<>.and (<>.parses? (<c>.this! token)))
        (<>.after (<>.not (<c>.this! token)))
        <c>.any)))

(type: Constructor (List Nullable))

(def: constructor
  (Parser Constructor)
  (<c>.form (<>.after (<c>.this! (' new))
                      (<c>.tuple (<>.some ..nullable)))))

(type: Field [Bit Text Nullable])

(def: static!
  (Parser Any)
  (<c>.this! (' #static)))

(def: field
  (Parser Field)
  (<c>.form ($_ <>.and
                (<>.parses? ..static!)
                <c>.local-identifier
                ..nullable)))

(type: Common-Method
  {#name Text
   #alias (Maybe Text)
   #inputs (List Nullable)
   #io? Bit
   #try? Bit
   #output Nullable})

(type: Static-Method Common-Method)
(type: Virtual-Method Common-Method)

(type: Method
  (#Static Static-Method)
  (#Virtual Virtual-Method))

(def: common-method
  (Parser Common-Method)
  ($_ <>.and
      <c>.local-identifier
      (<>.maybe (<>.after (<c>.this! (' #as)) <c>.local-identifier))
      (<c>.tuple (<>.some ..nullable))
      (<>.parses? (<c>.this! (' #io)))
      (<>.parses? (<c>.this! (' #try)))
      ..nullable))

(def: static-method
  (<>.after ..static! ..common-method))

(def: method
  (Parser Method)
  (<c>.form (<>.or ..static-method
                   ..common-method)))

(type: Member
  (#Constructor Constructor)
  (#Field Field)
  (#Method Method))

(def: member
  (Parser Member)
  ($_ <>.or
      ..constructor
      ..field
      ..method
      ))

(def: input-variables
  (-> (List Nullable) (List [Bit Code]))
  (|>> list.enumeration
       (list\map (function (_ [idx [nullable? type]])
                   [nullable? (|> idx %.nat code.local-identifier)]))))

(def: (nullable-type [nullable? type])
  (-> Nullable Code)
  (if nullable?
    (` (.Maybe (~ type)))
    type))

(def: (with-null g!temp [nullable? input])
  (-> Code [Bit Code] Code)
  (if nullable?
    (` (case (~ input)
         (#.Some (~ g!temp))
         (~ g!temp)

         #.None
         ("js object null")))
    input))

(def: (without-null g!temp [nullable? outputT] output)
  (-> Code Nullable Code Code)
  (if nullable?
    (` (let [(~ g!temp) (~ output)]
         (if ("js object null?" (~ g!temp))
           #.None
           (#.Some (~ g!temp)))))
    output))

(type: Import
  (#Class [Text (List Member)])
  (#Function Static-Method))

(def: import
  ($_ <>.or
      ($_ <>.and
          <c>.local-identifier
          (<>.some member))
      (<c>.form ..common-method)
      ))

(syntax: #export (try expression)
  {#.doc (doc (case (try (risky-computation input))
                (#.Right success)
                (do-something success)

                (#.Left error)
                (recover-from-failure error)))}
  (wrap (list (` ("lux try" ((~! io.io) (~ expression)))))))

(def: (with-io with? without)
  (-> Bit Code Code)
  (if with?
    (` (io.io (~ without)))
    without))

(def: (io-type io? rawT)
  (-> Bit Code Code)
  (if io?
    (` (io.IO (~ rawT)))
    rawT))

(def: (with-try with? without-try)
  (-> Bit Code Code)
  (if with?
    (` (..try (~ without-try)))
    without-try))

(def: (try-type try? rawT)
  (-> Bit Code Code)
  (if try?
    (` (.Either .Text (~ rawT)))
    rawT))

(def: (make-function g!method g!temp source inputsT io? try? outputT)
  (-> Code Code Text (List Nullable) Bit Bit Nullable Code)
  (let [g!inputs (input-variables inputsT)]
    (` (def: ((~ g!method)
              [(~+ (list\map product.right g!inputs))])
         (-> [(~+ (list\map nullable-type inputsT))]
             (~ (|> (nullable-type outputT)
                    (try-type try?)
                    (io-type io?))))
         (:assume
          (~ (<| (with-io io?)
                 (with-try try?)
                 (without-null g!temp outputT)
                 (` ("js apply"
                     ("js constant" (~ (code.text source)))
                     (~+ (list\map (with-null g!temp) g!inputs)))))))))))

(syntax: #export (import: {import ..import})
  (with-gensyms [g!temp]
    (case import
      (#Class [class members])
      (with-gensyms [g!object]
        (let [qualify (: (-> Text Code)
                         (|>> (format class "::") code.local-identifier))
              g!type (code.local-identifier class)
              real-class (text.replace-all "/" "." class)]
          (wrap (list& (` (type: (~ g!type)
                            (..Object (primitive (~ (code.text real-class))))))
                       (list\map (function (_ member)
                                   (case member
                                     (#Constructor inputsT)
                                     (let [g!inputs (input-variables inputsT)]
                                       (` (def: ((~ (qualify "new"))
                                                 [(~+ (list\map product.right g!inputs))])
                                            (-> [(~+ (list\map nullable-type inputsT))]
                                                (~ g!type))
                                            (:assume
                                             ("js object new"
                                              ("js constant" (~ (code.text real-class)))
                                              [(~+ (list\map (with-null g!temp) g!inputs))])))))
                                     
                                     (#Field [static? field fieldT])
                                     (if static?
                                       (` ((~! syntax:) ((~ (qualify field)))
                                           (\ (~! meta.monad) (~' wrap)
                                              (list (` (.:coerce (~ (nullable-type fieldT))
                                                                 ("js constant" (~ (code.text (format real-class "." field))))))))))
                                       (` (def: ((~ (qualify field))
                                                 (~ g!object))
                                            (-> (~ g!type)
                                                (~ (nullable-type fieldT)))
                                            (:assume
                                             (~ (without-null g!temp fieldT (` ("js object get" (~ (code.text field)) (~ g!object)))))))))
                                     
                                     (#Method method)
                                     (case method
                                       (#Static [method alias inputsT io? try? outputT])
                                       (..make-function (qualify (maybe.default method alias))
                                                        g!temp
                                                        (format real-class "." method)
                                                        inputsT
                                                        io?
                                                        try?
                                                        outputT)
                                       
                                       (#Virtual [method alias inputsT io? try? outputT])
                                       (let [g!inputs (input-variables inputsT)]
                                         (` (def: ((~ (qualify (maybe.default method alias)))
                                                   [(~+ (list\map product.right g!inputs))]
                                                   (~ g!object))
                                              (-> [(~+ (list\map nullable-type inputsT))]
                                                  (~ g!type)
                                                  (~ (|> (nullable-type outputT)
                                                         (try-type try?)
                                                         (io-type io?))))
                                              (:assume
                                               (~ (<| (with-io io?)
                                                      (with-try try?)
                                                      (without-null g!temp outputT)
                                                      (` ("js object do"
                                                          (~ (code.text method))
                                                          (~ g!object)
                                                          [(~+ (list\map (with-null g!temp) g!inputs))])))))))))))
                                 members)))))
      
      (#Function [name alias inputsT io? try? outputT])
      (wrap (list (..make-function (code.local-identifier (maybe.default name alias))
                                   g!temp
                                   name
                                   inputsT
                                   io?
                                   try?
                                   outputT)))
      )))

(template: #export (type-of object)
  ("js type-of" object))

(syntax: #export (constant type
                           {[head tail] (<c>.tuple (<>.and <c>.local-identifier (<>.some <c>.local-identifier)))})
  (with-gensyms [g!_]
    (let [constant (` ("js constant" (~ (code.text head))))]
      (case tail
        #.Nil
        (wrap (list (` (: (.Maybe (~ type))
                          (case (..type-of (~ constant))
                            "undefined"
                            #.None

                            (~ g!_)
                            (#.Some (:coerce (~ type) (~ constant))))))))
        
        (#.Cons [next tail])
        (let [separator "."]
          (wrap (list (` (: (.Maybe (~ type))
                            (case (..type-of (~ constant))
                              "undefined"
                              #.None

                              (~ g!_)
                              (..constant (~ type) [(~ (code.local-identifier (format head "." next)))
                                                    (~+ (list\map code.local-identifier tail))])))))))))))

(template: (!defined? <constant>)
  (.case (..constant Any <constant>)
    #.None
    .false

    (#.Some _)
    .true))

(template [<name> <constant>]
  [(def: #export <name>
     Bit
     (!defined? <constant>))]

  [on-browser? [window]]
  [on-nashorn? [java lang Object]]
  )

(def: #export on-node-js?
  Bit
  (case (..constant (Object Any) [process])
    (#.Some process)
    (case (:coerce Text
                   ("js apply" ("js constant" "Object.prototype.toString.call") process))
      "[object process]"
      true

      _
      false)

    #.None
    false))

(template: #export (closure <inputs> <output>)
  (.:coerce ..Function
            (`` ("js function"
                 (~~ (template.count <inputs>))
                 (.function (_ [<inputs>])
                   <output>)))))
