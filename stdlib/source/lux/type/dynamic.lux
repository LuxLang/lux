(.module:
  [lux #*
   [control
    ["ex" exception (#+ exception:)]]
   [data
    ["." error]
    [text
     format]]
   [macro (#+ with-gensyms)
    ["." syntax (#+ syntax:)]]
   ["." type
    abstract]])

(exception: #export (wrong-type {expected Type} {actual Type})
  (ex.report ["Expected" (%type expected)]
             ["Actual" (%type actual)]))

(abstract: #export Dynamic
  {#.doc "A value coupled with its type, so it can be checked later."}

  [Type Any]

  (def: dynamic-abstraction (-> [Type Any] Dynamic) (|>> :abstraction))
  (def: dynamic-representation (-> Dynamic [Type Any]) (|>> :representation))

  (syntax: #export (:dynamic value)
    {#.doc (doc (: Dynamic
                   (:dynamic 123)))}
    (with-gensyms [g!value]
      (wrap (list (` (let [(~ g!value) (~ value)]
                       ((~! ..dynamic-abstraction) [(:of (~ g!value)) (~ g!value)])))))))

  (syntax: #export (:check type value)
    {#.doc (doc (: (error.Error Nat)
                   (:check Nat (:dynamic 123))))}
    (with-gensyms [g!type g!value]
      (wrap (list (` (let [[(~ g!type) (~ g!value)] ((~! ..dynamic-representation) (~ value))]
                       (: ((~! error.Error) (~ type))
                          (if (:: (~! type.equivalence) (~' =)
                                  (.type (~ type)) (~ g!type))
                            (#error.Success (:coerce (~ type) (~ g!value)))
                            ((~! ex.throw) ..wrong-type [(.type (~ type)) (~ g!type)])))))))))
  )
