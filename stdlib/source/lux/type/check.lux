(.module: {#.doc "Type-checking functionality."}
  [lux #*
   [abstract
    [functor (#+ Functor)]
    [apply (#+ Apply)]
    ["." monad (#+ Monad do)]]
   [control
    ["." exception (#+ Exception exception:)]]
   [data
    ["." maybe]
    ["." product]
    ["." error (#+ Error)]
    [number
     ["." nat ("#@." decimal)]]
    ["." text ("#@." monoid equivalence)]
    [collection
     ["." list]
     ["." set (#+ Set)]]]]
  ["." // ("#@." equivalence)])

(template: (!n/= reference subject)
  ("lux i64 =" reference subject))

(template: (!text@= reference subject)
  ("lux text =" reference subject))

(exception: #export (unknown-type-var {id Nat})
  (exception.report
   ["ID" (nat@encode id)]))

(exception: #export (unbound-type-var {id Nat})
  (exception.report
   ["ID" (nat@encode id)]))

(exception: #export (invalid-type-application {funcT Type} {argT Type})
  (exception.report
   ["Type function" (//.to-text funcT)]
   ["Type argument" (//.to-text argT)]))

(exception: #export (cannot-rebind-var {id Nat} {type Type} {bound Type})
  (exception.report
   ["Var" (nat@encode id)]
   ["Wanted Type" (//.to-text type)]
   ["Current Type" (//.to-text bound)]))

(exception: #export (type-check-failed {expected Type} {actual Type})
  (exception.report
   ["Expected" (//.to-text expected)]
   ["Actual" (//.to-text actual)]))

(type: #export Var Nat)

(type: #export Assumption [Type Type])

(type: #export (Check a)
  (-> Type-Context (Error [Type-Context a])))

(type: #export (Checker a)
  (-> (List Assumption) a a (Check (List Assumption))))

(type: #export Type-Vars
  (List [Var (Maybe Type)]))

(structure: #export functor (Functor Check)
  (def: (map f fa)
    (function (_ context)
      (case (fa context)
        (#error.Success [context' output])
        (#error.Success [context' (f output)])

        (#error.Failure error)
        (#error.Failure error)))))

(structure: #export apply (Apply Check)
  (def: &functor ..functor)

  (def: (apply ff fa)
    (function (_ context)
      (case (ff context)
        (#error.Success [context' f])
        (case (fa context')
          (#error.Success [context'' a])
          (#error.Success [context'' (f a)])

          (#error.Failure error)
          (#error.Failure error))

        (#error.Failure error)
        (#error.Failure error)
        )))
  )

(structure: #export monad (Monad Check)
  (def: &functor ..functor)

  (def: (wrap x)
    (function (_ context)
      (#error.Success [context x])))

  (def: (join ffa)
    (function (_ context)
      (case (ffa context)
        (#error.Success [context' fa])
        (case (fa context')
          (#error.Success [context'' a])
          (#error.Success [context'' a])

          (#error.Failure error)
          (#error.Failure error))

        (#error.Failure error)
        (#error.Failure error)
        )))
  )

(open: "check@." ..monad)

(def: (var::new id plist)
  (-> Var Type-Vars Type-Vars)
  (#.Cons [id #.None] plist))

(def: (var::get id plist)
  (-> Var Type-Vars (Maybe (Maybe Type)))
  (case plist
    (#.Cons [var-id var-type]
            plist')
    (if (!n/= id var-id)
      (#.Some var-type)
      (var::get id plist'))

    #.Nil
    #.None))

(def: (var::put id value plist)
  (-> Var (Maybe Type) Type-Vars Type-Vars)
  (case plist
    #.Nil
    (list [id value])

    (#.Cons [var-id var-type]
            plist')
    (if (!n/= id var-id)
      (#.Cons [var-id value]
              plist')
      (#.Cons [var-id var-type]
              (var::put id value plist')))))

(def: #export (run context proc)
  (All [a] (-> Type-Context (Check a) (Error a)))
  (case (proc context)
    (#error.Success [context' output])
    (#error.Success output)

    (#error.Failure error)
    (#error.Failure error)))

(def: #export (fail message)
  (All [a] (-> Text (Check a)))
  (function (_ context)
    (#error.Failure message)))

(def: #export (assert message test)
  (-> Text Bit (Check Any))
  (function (_ context)
    (if test
      (#error.Success [context []])
      (#error.Failure message))))

(def: #export (throw exception message)
  (All [e a] (-> (Exception e) e (Check a)))
  (..fail (exception.construct exception message)))

(def: #export existential
  {#.doc "A producer of existential types."}
  (Check [Nat Type])
  (function (_ context)
    (let [id (get@ #.ex-counter context)]
      (#error.Success [(update@ #.ex-counter inc context)
                       [id (#.Ex id)]]))))

(template [<name> <outputT> <fail> <succeed>]
  [(def: #export (<name> id)
     (-> Var (Check <outputT>))
     (function (_ context)
       (case (|> context (get@ #.var-bindings) (var::get id))
         (^or (#.Some (#.Some (#.Var _)))
              (#.Some #.None))
         (#error.Success [context <fail>])
         
         (#.Some (#.Some bound))
         (#error.Success [context <succeed>])

         #.None
         (exception.throw unknown-type-var id))))]

  [bound? Bit          false  true]
  [read   (Maybe Type) #.None (#.Some bound)]
  )

(def: #export (read! id)
  (-> Var (Check Type))
  (do ..monad
    [?type (read id)]
    (case ?type
      (#.Some type)
      (wrap type)

      #.None
      (..throw unbound-type-var id))))

(def: (peek id)
  (-> Var (Check Type))
  (function (_ context)
    (case (|> context (get@ #.var-bindings) (var::get id))
      (#.Some (#.Some bound))
      (#error.Success [context bound])

      (#.Some _)
      (exception.throw unbound-type-var id)

      _
      (exception.throw unknown-type-var id))))

(def: #export (bind type id)
  (-> Type Var (Check Any))
  (function (_ context)
    (case (|> context (get@ #.var-bindings) (var::get id))
      (#.Some #.None)
      (#error.Success [(update@ #.var-bindings (var::put id (#.Some type)) context)
                       []])

      (#.Some (#.Some bound))
      (exception.throw cannot-rebind-var [id type bound])
      
      _
      (exception.throw unknown-type-var id))))

(def: (update type id)
  (-> Type Var (Check Any))
  (function (_ context)
    (case (|> context (get@ #.var-bindings) (var::get id))
      (#.Some _)
      (#error.Success [(update@ #.var-bindings (var::put id (#.Some type)) context)
                       []])
      
      _
      (exception.throw unknown-type-var id))))

(def: #export var
  (Check [Var Type])
  (function (_ context)
    (let [id (get@ #.var-counter context)]
      (#error.Success [(|> context
                           (update@ #.var-counter inc)
                           (update@ #.var-bindings (var::new id)))
                       [id (#.Var id)]]))))

(def: (apply-type! funcT argT)
  (-> Type Type (Check Type))
  (case funcT
    (#.Var func-id)
    (do ..monad
      [?funcT' (read func-id)]
      (case ?funcT'
        (#.Some funcT')
        (apply-type! funcT' argT)

        _
        (throw invalid-type-application [funcT argT])))

    _
    (case (//.apply (list argT) funcT)
      (#.Some output)
      (check@wrap output)

      _
      (throw invalid-type-application [funcT argT]))))

(type: #export Ring (Set Var))

(def: empty-ring Ring (set.new nat.hash))

## TODO: Optimize this by not using sets anymore.
(def: #export (ring start)
  (-> Var (Check Ring))
  (function (_ context)
    (loop [current start
           output (set.add start empty-ring)]
      (case (|> context (get@ #.var-bindings) (var::get current))
        (#.Some (#.Some type))
        (case type
          (#.Var post)
          (if (!n/= start post)
            (#error.Success [context output])
            (recur post (set.add post output)))
          
          _
          (#error.Success [context empty-ring]))

        (#.Some #.None)
        (#error.Success [context output])
        
        #.None
        (exception.throw unknown-type-var current)))))

(def: #export fresh-context
  Type-Context
  {#.var-counter 0
   #.ex-counter 0
   #.var-bindings (list)})

(def: (attempt op)
  (All [a] (-> (Check a) (Check (Maybe a))))
  (function (_ context)
    (case (op context)
      (#error.Success [context' output])
      (#error.Success [context' (#.Some output)])

      (#error.Failure _)
      (#error.Success [context #.None]))))

(def: (either left right)
  (All [a] (-> (Check a) (Check a) (Check a)))
  (function (_ context)
    (case (left context)
      (#error.Failure _)
      (right context)

      output
      output)))

(def: (assumed? [e a] assumptions)
  (-> Assumption (List Assumption) Bit)
  (list.any? (function (_ [e' a'])
               (and (//@= e e')
                    (//@= a a')))
             assumptions))

(def: (assume! assumption assumptions)
  (-> Assumption (List Assumption) (List Assumption))
  (#.Cons assumption assumptions))

## TODO: "if-bind" can be optimized...
(def: (if-bind id type then else)
  (All [a]
    (-> Var Type (Check a) (-> Type (Check a))
        (Check a)))
  ($_ either
      (do ..monad
        [_ (..bind type id)]
        then)
      (do ..monad
        [ring (..ring id)
         _ (assert "" (n/> 1 (set.size ring)))
         _ (monad.map @ (update type) (set.to-list ring))]
        then)
      (do ..monad
        [?bound (read id)]
        (else (maybe.default (#.Var id) ?bound)))))

## TODO: "link-2" can be optimized...
(def: (link-2 left right)
  (-> Var Var (Check Any))
  (do ..monad
    [_ (..bind (#.Var right) left)]
    (..bind (#.Var left) right)))

## TODO: "link-3" can be optimized...
(def: (link-3 interpose to from)
  (-> Var Var Var (Check Any))
  (do ..monad
    [_ (update (#.Var interpose) from)]
    (update (#.Var to) interpose)))

## TODO: "check-vars" can be optimized...
(def: (check-vars check' assumptions idE idA)
  (-> (Checker Type) (Checker Var))
  (if (!n/= idE idA)
    (check@wrap assumptions)
    (do ..monad
      [ebound (attempt (peek idE))
       abound (attempt (peek idA))]
      (case [ebound abound]
        ## Link the 2 variables circularly
        [#.None #.None]
        (do @
          [_ (link-2 idE idA)]
          (wrap assumptions))

        ## Interpose new variable between 2 existing links
        [(#.Some etype) #.None]
        (case etype
          (#.Var targetE)
          (do @
            [_ (link-3 idA targetE idE)]
            (wrap assumptions))

          _
          (check' assumptions etype (#.Var idA)))

        ## Interpose new variable between 2 existing links
        [#.None (#.Some atype)]
        (case atype
          (#.Var targetA)
          (do @
            [_ (link-3 idE targetA idA)]
            (wrap assumptions))

          _
          (check' assumptions (#.Var idE) atype))

        [(#.Some etype) (#.Some atype)]
        (case [etype atype]
          [(#.Var targetE) (#.Var targetA)]
          (do @
            [ringE (..ring idE)
             ringA (..ring idA)]
            (if (:: set.equivalence = ringE ringA)
              (wrap assumptions)
              ## Fuse 2 rings
              (do @
                [_ (monad.fold @ (function (_ interpose to)
                                   (do @
                                     [_ (link-3 interpose to idE)]
                                     (wrap interpose)))
                               targetE
                               (set.to-list ringA))]
                (wrap assumptions))))

          (^template [<pattern> <id> <type>]
            <pattern>
            (do @
              [ring (..ring <id>)
               _ (monad.map @ (update <type>) (set.to-list ring))]
              (wrap assumptions)))
          ([[(#.Var _) _] idE atype]
           [[_ (#.Var _)] idA etype])
          
          _
          (check' assumptions etype atype))))))

## TODO: "check-apply" can be optimized...
(def: (check-apply check' assumptions expected actual)
  (-> (Checker Type) (Checker [Type Type]))
  (let [[expected-input expected-function] expected
        [actual-input actual-function] actual]
    (case [expected-function actual-function]
      [(#.Ex exE) (#.Ex exA)]
      (if (n/= exE exA)
        (check' assumptions expected-input actual-input)
        (fail ""))

      [(#.UnivQ _ _) (#.Ex _)]
      (do ..monad
        [expected' (apply-type! expected-function expected-input)]
        (check' assumptions expected' (#.Apply actual)))

      [(#.Ex _) (#.UnivQ _ _)]
      (do ..monad
        [actual' (apply-type! actual-function actual-input)]
        (check' assumptions (#.Apply expected) actual'))

      [(#.Apply [expected-input' expected-function']) (#.Ex _)]
      (do ..monad
        [expected-function'' (apply-type! expected-function' expected-input')]
        (check' assumptions (#.Apply [expected-input expected-function'']) (#.Apply actual)))

      [(#.Ex _) (#.Apply [actual-input' actual-function'])]
      (do ..monad
        [actual-function'' (apply-type! actual-function' actual-input')]
        (check' assumptions (#.Apply expected) (#.Apply [actual-input actual-function''])))

      (^or [(#.Ex _) _] [_ (#.Ex _)])
      (do ..monad
        [assumptions (check' assumptions expected-function actual-function)]
        (check' assumptions expected-input actual-input))

      [(#.Var id) _]
      (function (_ context)
        (case ((do ..monad
                 [expected-function' (read! id)]
                 (check' assumptions (#.Apply expected-input expected-function') (#.Apply actual)))
               context)
          (#error.Success output)
          (#error.Success output)

          (#error.Failure error)
          (case actual-function
            (#.UnivQ _ _)
            ((do ..monad
               [actual' (apply-type! actual-function actual-input)]
               (check' assumptions (#.Apply expected) actual'))
             context)
            
            (#.Ex exA)
            ((do ..monad
               [assumptions (check' assumptions expected-function actual-function)]
               (check' assumptions expected-input actual-input))
             context)

            _
            ((do ..monad
               [assumptions (check' assumptions expected-function actual-function)
                expected' (apply-type! actual-function expected-input)
                actual' (apply-type! actual-function actual-input)]
               (check' assumptions expected' actual'))
             context))))

      [_ (#.Var id)]
      (function (_ context)
        (case ((do ..monad
                 [actual-function' (read! id)]
                 (check' assumptions (#.Apply expected) (#.Apply actual-input actual-function')))
               context)
          (#error.Success output)
          (#error.Success output)

          _
          ((do ..monad
             [assumptions (check' assumptions expected-function actual-function)
              expected' (apply-type! expected-function expected-input)
              actual' (apply-type! expected-function actual-input)]
             (check' assumptions expected' actual'))
           context)))

      _
      (fail ""))))

(def: (with-stack exception parameter check)
  (All [e a] (-> (Exception e) e (Check a) (Check a)))
  (|>> check (exception.with-stack exception parameter)))

## TODO: "check'" can be optimized...
(def: #export (check' assumptions expected actual)
  {#.doc "Type-check to ensure that the 'expected' type subsumes the 'actual' type."}
  (Checker Type)
  (if (is? expected actual)
    (check@wrap assumptions)
    (with-stack type-check-failed [expected actual]
      (case [expected actual]
        [(#.Var idE) (#.Var idA)]
        (check-vars check' assumptions idE idA)
        
        [(#.Var id) _]
        (if-bind id actual
                 (check@wrap assumptions)
                 (function (_ bound)
                   (check' assumptions bound actual)))
        
        [_ (#.Var id)]
        (if-bind id expected
                 (check@wrap assumptions)
                 (function (_ bound)
                   (check' assumptions expected bound)))

        (^template [<fE> <fA>]
          [(#.Apply aE <fE>) (#.Apply aA <fA>)]
          (check-apply check' assumptions [aE <fE>] [aA <fA>]))
        ([F1 (#.Ex ex)]
         [(#.Ex exE) fA]
         [fE (#.Var idA)]
         [(#.Var idE) fA])
        
        [(#.Apply A F) _]
        (let [new-assumption [expected actual]]
          (if (assumed? new-assumption assumptions)
            (check@wrap assumptions)
            (do ..monad
              [expected' (apply-type! F A)]
              (check' (assume! new-assumption assumptions) expected' actual))))

        [_ (#.Apply A F)]
        (do ..monad
          [actual' (apply-type! F A)]
          (check' assumptions expected actual'))

        ## TODO: Refactor-away as cold-code
        (^template [<tag> <instancer>]
          [(<tag> _) _]
          (do ..monad
            [[_ paramT] <instancer>
             expected' (apply-type! expected paramT)]
            (check' assumptions expected' actual)))
        ([#.UnivQ ..existential]
         [#.ExQ ..var])

        ## TODO: Refactor-away as cold-code
        (^template [<tag> <instancer>]
          [_ (<tag> _)]
          (do ..monad
            [[_ paramT] <instancer>
             actual' (apply-type! actual paramT)]
            (check' assumptions expected actual')))
        ([#.UnivQ ..var]
         [#.ExQ ..existential])

        [(#.Primitive e-name e-params) (#.Primitive a-name a-params)]
        (if (!text@= e-name a-name)
          (loop [assumptions assumptions
                 e-params e-params
                 a-params a-params]
            (case [e-params a-params]
              [#.Nil #.Nil]
              (check@wrap assumptions)
              
              [(#.Cons e-head e-tail) (#.Cons a-head a-tail)]
              (do ..monad
                [assumptions' (check' assumptions e-head a-head)]
                (recur assumptions' e-tail a-tail))

              _
              (fail "")))
          (fail ""))

        (^template [<compose>]
          [(<compose> eL eR) (<compose> aL aR)]
          (do ..monad
            [assumptions (check' assumptions eL aL)]
            (check' assumptions eR aR)))
        ([#.Sum]
         [#.Product])
        
        [(#.Function eI eO) (#.Function aI aO)]
        (do ..monad
          [assumptions (check' assumptions aI eI)]
          (check' assumptions eO aO))

        [(#.Ex e!id) (#.Ex a!id)]
        (if (!n/= e!id a!id)
          (check@wrap assumptions)
          (fail ""))

        [(#.Named _ ?etype) _]
        (check' assumptions ?etype actual)

        [_ (#.Named _ ?atype)]
        (check' assumptions expected ?atype)

        _
        (fail "")))))

(def: #export (check expected actual)
  {#.doc "Type-check to ensure that the 'expected' type subsumes the 'actual' type."}
  (-> Type Type (Check Any))
  (check' (list) expected actual))

(def: #export (checks? expected actual)
  {#.doc "A simple type-checking function that just returns a yes/no answer."}
  (-> Type Type Bit)
  (case (run fresh-context (check' (list) expected actual))
    (#error.Failure _)
    false

    (#error.Success _)
    true))

(def: #export context
  (Check Type-Context)
  (function (_ context)
    (#error.Success [context context])))

(def: #export (clean inputT)
  (-> Type (Check Type))
  (case inputT
    (#.Primitive name paramsT+)
    (|> paramsT+
        (monad.map ..monad clean)
        (check@map (|>> (#.Primitive name))))

    (^or (#.Parameter _) (#.Ex _) (#.Named _))
    (check@wrap inputT)

    (^template [<tag>]
      (<tag> leftT rightT)
      (do ..monad
        [leftT' (clean leftT)]
        (|> (clean rightT)
            (check@map (|>> (<tag> leftT'))))))
    ([#.Sum] [#.Product] [#.Function] [#.Apply])

    (#.Var id)
    (do ..monad
      [?actualT (read id)]
      (case ?actualT
        (#.Some actualT)
        (clean actualT)

        _
        (wrap inputT)))

    (^template [<tag>]
      (<tag> envT+ unquantifiedT)
      (do ..monad
        [envT+' (monad.map @ clean envT+)]
        (wrap (<tag> envT+' unquantifiedT))))
    ([#.UnivQ] [#.ExQ])
    ))
