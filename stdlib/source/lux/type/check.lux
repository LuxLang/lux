(;module: {#;doc "Type-checking functionality.

                  Very useful for writing advanced macros."}
  lux
  (lux (control ["F" functor]
                ["A" applicative]
                ["M" monad #+ do Monad])
       (data [text "text/" Monoid<Text> Eq<Text>]
             [number "nat/" Codec<Text,Nat>]
             maybe
             [product]
             (coll [list])
             ["R" result])
       [type "Type/" Eq<Type>]
       ))

(type: #export Assumptions (List [[Type Type] Bool]))

(type: #export (Check a)
  (-> Type-Context (R;Result [Type-Context a])))

(type: #export Type-Vars
  (List [Nat (Maybe Type)]))

(struct: #export _ (F;Functor Check)
  (def: (map f fa)
    (function [context]
      (case (fa context)
        (#R;Error error)
        (#R;Error error)

        (#R;Success [context' output])
        (#R;Success [context' (f output)])
        ))))

(struct: #export _ (A;Applicative Check)
  (def: functor Functor<Check>)

  (def: (wrap x)
    (function [context]
      (#R;Success [context x])))

  (def: (apply ff fa)
    (function [context]
      (case (ff context)
        (#R;Success [context' f])
        (case (fa context')
          (#R;Success [context'' a])
          (#R;Success [context'' (f a)])

          (#R;Error error)
          (#R;Error error))

        (#R;Error error)
        (#R;Error error)
        )))
  )

(struct: #export _ (Monad Check)
  (def: applicative Applicative<Check>)

  (def: (join ffa)
    (function [context]
      (case (ffa context)
        (#R;Success [context' fa])
        (case (fa context')
          (#R;Success [context'' a])
          (#R;Success [context'' a])

          (#R;Error error)
          (#R;Error error))

        (#R;Error error)
        (#R;Error error)
        )))
  )

(open Monad<Check> "Check/")

(def: (var::get id plist)
  (-> Nat Type-Vars (Maybe (Maybe Type)))
  (case plist
    #;Nil
    #;None

    (#;Cons [var-id var-type]
            plist')
    (if (n.= id var-id)
      (#;Some var-type)
      (var::get id plist'))
    ))

(def: (var::put id value plist)
  (-> Nat (Maybe Type) Type-Vars Type-Vars)
  (case plist
    #;Nil
    (list [id value])

    (#;Cons [var-id var-type]
            plist')
    (if (n.= id var-id)
      (#;Cons [var-id value]
              plist')
      (#;Cons [var-id var-type]
              (var::put id value plist')))
    ))

(def: (var::remove id plist)
  (-> Nat Type-Vars Type-Vars)
  (case plist
    #;Nil
    #;Nil

    (#;Cons [var-id var-type]
            plist')
    (if (n.= id var-id)
      plist'
      (#;Cons [var-id var-type]
              (var::remove id plist')))
    ))

## [[Logic]]
(def: #export (run context proc)
  (All [a] (-> Type-Context (Check a) (R;Result a)))
  (case (proc context)
    (#R;Error error)
    (#R;Error error)

    (#R;Success [context' output])
    (#R;Success output)))

(def: (apply-type! t-func t-arg)
  (-> Type Type (Check Type))
  (function [context]
    (case (type;apply (list t-arg) t-func)
      #;None
      (#R;Error ($_ text/compose "Invalid type application: " (type;to-text t-func) " on " (type;to-text t-arg)))

      (#;Some output)
      (#R;Success [context output]))))

(def: #export existential
  {#;doc "A producer of existential types."}
  (Check [Nat Type])
  (function [context]
    (let [id (get@ #;ex-counter context)]
      (#R;Success [(update@ #;ex-counter n.inc context)
                   [id (#;Ex id)]]))))

(def: #export (bound? id)
  (-> Nat (Check Bool))
  (function [context]
    (case (|> context (get@ #;var-bindings) (var::get id))
      (#;Some (#;Some _))
      (#R;Success [context true])

      (#;Some #;None)
      (#R;Success [context false])
      
      #;None
      (#R;Error ($_ text/compose "Unknown type-var: " (nat/encode id))))))

(def: #export (read id)
  (-> Nat (Check Type))
  (function [context]
    (case (|> context (get@ #;var-bindings) (var::get id))
      (#;Some (#;Some type))
      (#R;Success [context type])

      (#;Some #;None)
      (#R;Error ($_ text/compose "Unbound type-var: " (nat/encode id)))
      
      #;None
      (#R;Error ($_ text/compose "Unknown type-var: " (nat/encode id))))))

(def: #export (write id type)
  (-> Nat Type (Check Unit))
  (function [context]
    (case (|> context (get@ #;var-bindings) (var::get id))
      (#;Some (#;Some bound))
      (#R;Error ($_ text/compose "Cannot rebind type-var: " (nat/encode id) " | Current type: " (type;to-text bound)))
      
      (#;Some #;None)
      (#R;Success [(update@ #;var-bindings (var::put id (#;Some type)) context)
                   []])

      #;None
      (#R;Error ($_ text/compose "Unknown type-var: " (nat/encode id))))))

(def: (update id type)
  (-> Nat Type (Check Unit))
  (function [context]
    (case (|> context (get@ #;var-bindings) (var::get id))
      (#;Some _)
      (#R;Success [(update@ #;var-bindings (var::put id (#;Some type)) context)
                   []])
      
      #;None
      (#R;Error ($_ text/compose "Unknown type-var: " (nat/encode id))))))

(def: #export (clear id)
  (-> Nat (Check Unit))
  (function [context]
    (case (|> context (get@ #;var-bindings) (var::get id))
      (#;Some _)
      (#R;Success [(update@ #;var-bindings (var::put id #;None) context)
                   []])
      
      #;None
      (#R;Error ($_ text/compose "Unknown type-var: " (nat/encode id))))))

(def: #export (clean t-id type)
  (-> Nat Type (Check Type))
  (case type
    (#;Var id)
    (if (n.= t-id id)
      (do Monad<Check>
        [? (bound? id)]
        (if ?
          (read id)
          (wrap type)))
      (do Monad<Check>
        [? (bound? id)]
        (if ?
          (do Monad<Check>
            [=type (read id)
             ==type (clean t-id =type)]
            (case ==type
              (#;Var =id)
              (if (n.= t-id =id)
                (do Monad<Check>
                  [_ (clear id)]
                  (wrap type))
                (do Monad<Check>
                  [_ (update id ==type)]
                  (wrap type)))

              _
              (do Monad<Check>
                [_ (update id ==type)]
                (wrap type))))
          (wrap type))))

    (#;Host name params)
    (do Monad<Check>
      [=params (M;map @ (clean t-id) params)]
      (wrap (#;Host name =params)))
    
    (^template [<tag>]
      (<tag> left right)
      (do Monad<Check>
        [=left (clean t-id left)
         =right (clean t-id right)]
        (wrap (<tag> =left =right))))
    ([#;Function]
     [#;Apply]
     [#;Product]
     [#;Sum])

    (^template [<tag>]
      (<tag> env body)
      (do Monad<Check>
        [=env (M;map @ (clean t-id) env)
         =body (clean t-id body)] ## TODO: DO NOT CLEAN THE BODY
        (wrap (<tag> =env =body))))
    ([#;UnivQ]
     [#;ExQ])
    
    _
    (:: Monad<Check> wrap type)
    ))

(def: #export create
  (Check [Nat Type])
  (function [context]
    (let [id (get@ #;var-counter context)]
      (#R;Success [(|> context
                       (update@ #;var-counter n.inc)
                       (update@ #;var-bindings (var::put id #;None)))
                   [id (#;Var id)]]))))

(def: get-bindings
  (Check (List [Nat (Maybe Type)]))
  (function [context]
    (#R;Success [context
                 (get@ #;var-bindings context)])))

(def: (set-bindings value)
  (-> (List [Nat (Maybe Type)]) (Check Unit))
  (function [context]
    (#R;Success [(set@ #;var-bindings value context)
                 []])))

(def: #export (delete id)
  (-> Nat (Check Unit))
  (function [context]
    (#R;Success [(update@ #;var-bindings (var::remove id) context)
                 []])))

(def: #export (with k)
  (All [a] (-> (-> [Nat Type] (Check a)) (Check a)))
  (do Monad<Check>
    [[id var] create
     output (k [id var])
     _ (delete id)]
    (wrap output)))

(def: #export fresh-context
  Type-Context
  {#;var-counter +0
   #;ex-counter +0
   #;var-bindings (list)
   })

(def: (attempt op)
  (All [a] (-> (Check a) (Check (Maybe a))))
  (function [context]
    (case (op context)
      (#R;Success [context' output])
      (#R;Success [context' (#;Some output)])

      (#R;Error _)
      (#R;Success [context #;None]))))

(def: #export (fail message)
  (All [a] (-> Text (Check a)))
  (function [context]
    (#R;Error message)))

(def: (fail-check expected actual)
  (All [a] (-> Type Type (Check a)))
  (fail ($_ text/compose
            "Expected: " (type;to-text expected) "\n\n"
            "Actual:   " (type;to-text actual))))

(def: (either left right)
  (All [a] (-> (Check a) (Check a) (Check a)))
  (function [context]
    (case (left context)
      (#R;Success [context' output])
      (#R;Success [context' output])

      (#R;Error _)
      (right context))))

(def: (assumed? [e a] assumptions)
  (-> [Type Type] Assumptions (Maybe Bool))
  (:: Monad<Maybe> map product;right
      (list;find (function [[[fe fa] status]]
                   (and (Type/= e fe)
                        (Type/= a fa)))
                 assumptions)))

(def: (assume! ea status assumptions)
  (-> [Type Type] Bool Assumptions Assumptions)
  (#;Cons [ea status] assumptions))

(def: (on id type then else)
  (All [a]
    (-> Nat Type (Check a) (-> Type (Check a))
        (Check a)))
  (either (do Monad<Check>
            [_ (write id type)]
            then)
          (do Monad<Check>
            [bound (read id)]
            (else bound))))

(def: #export (check' expected actual assumptions)
  {#;doc "Type-check to ensure that the 'expected' type subsumes the 'actual' type."}
  (-> Type Type Assumptions (Check Assumptions))
  (if (is expected actual)
    (Check/wrap assumptions)
    (case [expected actual]
      [(#;Var e-id) (#;Var a-id)]
      (if (n.= e-id a-id)
        (Check/wrap assumptions)
        (do Monad<Check>
          [ebound (attempt (read e-id))
           abound (attempt (read a-id))]
          (case [ebound abound]
            [#;None #;None]
            (do @
              [_ (write e-id actual)]
              (wrap assumptions))
            
            [(#;Some etype) #;None]
            (check' etype actual assumptions)

            [#;None (#;Some atype)]
            (check' expected atype assumptions)

            [(#;Some etype) (#;Some atype)]
            (check' etype atype assumptions))))
      
      [(#;Var id) _]
      (on id actual (Check/wrap assumptions)
          (function [bound]
            (check' bound actual assumptions)))
      
      [_ (#;Var id)]
      (on id expected (Check/wrap assumptions)
          (function [bound]
            (check' expected bound assumptions)))

      [(#;Apply eA (#;Ex eid)) (#;Apply aA (#;Ex aid))]
      (if (n.= eid aid)
        (check' eA aA assumptions)
        (fail-check expected actual))

      [(#;Apply A1 (#;Var id)) (#;Apply A2 F2)]
      (either (do Monad<Check>
                [F1 (read id)]
                (check' (#;Apply A1 F1) actual assumptions))
              (do Monad<Check>
                [assumptions (check' (#;Var id) F2 assumptions)
                 e' (apply-type! F2 A1)
                 a' (apply-type! F2 A2)]
                (check' e' a' assumptions)))
      
      [(#;Apply A1 F1) (#;Apply A2 (#;Var id))]
      (either (do Monad<Check>
                [F2 (read id)]
                (check' expected (#;Apply A2 F2) assumptions))
              (do Monad<Check>
                [assumptions (check' F1 (#;Var id) assumptions)
                 e' (apply-type! F1 A1)
                 a' (apply-type! F1 A2)]
                (check' e' a' assumptions)))

      [(#;Apply A F) _]
      (let [fx-pair [expected actual]]
        (case (assumed? fx-pair assumptions)
          (#;Some ?)
          (if ?
            (Check/wrap assumptions)
            (fail-check expected actual))

          #;None
          (do Monad<Check>
            [expected' (apply-type! F A)]
            (check' expected' actual (assume! fx-pair true assumptions)))))

      [_ (#;Apply A F)]
      (do Monad<Check>
        [actual' (apply-type! F A)]
        (check' expected actual' assumptions))

      [(#;UnivQ _) _]
      (do Monad<Check>
        [[ex-id ex] existential
         expected' (apply-type! expected ex)]
        (check' expected' actual assumptions))

      [_ (#;UnivQ _)]
      (with
       (function [[var-id var]]
         (do Monad<Check>
           [actual' (apply-type! actual var)
            assumptions (check' expected actual' assumptions)
            _ (clean var-id expected)]
           (Check/wrap assumptions))))

      [(#;ExQ e!env e!def) _]
      (with
       (function [[var-id var]]
         (do Monad<Check>
           [expected' (apply-type! expected var)
            assumptions (check' expected' actual assumptions)
            _ (clean var-id actual)]
           (Check/wrap assumptions))))

      [_ (#;ExQ a!env a!def)]
      (do Monad<Check>
        [[ex-id ex] existential
         actual' (apply-type! actual ex)]
        (check' expected actual' assumptions))

      [(#;Host e-name e-params) (#;Host a-name a-params)]
      (if (and (text/= e-name a-name)
               (n.= (list;size e-params)
                    (list;size a-params)))
        (do Monad<Check>
          [assumptions (M;fold Monad<Check>
                               (function [[e a] assumptions] (check' e a assumptions))
                               assumptions
                               (list;zip2 e-params a-params))]
          (Check/wrap assumptions))
        (fail-check expected actual))

      (^template [<identity> <compose>]
        [<identity> <identity>]
        (Check/wrap assumptions)
        
        [(<compose> eL eR) (<compose> aL aR)]
        (do Monad<Check>
          [assumptions (check' eL aL assumptions)]
          (check' eR aR assumptions)))
      ([#;Void #;Sum]
       [#;Unit #;Product])
      
      [(#;Function eI eO) (#;Function aI aO)]
      (do Monad<Check>
        [assumptions (check' aI eI assumptions)]
        (check' eO aO assumptions))

      [(#;Ex e!id) (#;Ex a!id)]
      (if (n.= e!id a!id)
        (Check/wrap assumptions)
        (fail-check expected actual))

      [(#;Named _ ?etype) _]
      (check' ?etype actual assumptions)

      [_ (#;Named _ ?atype)]
      (check' expected ?atype assumptions)

      _
      (fail-check expected actual))))

(def: #export (check expected actual)
  {#;doc "Type-check to ensure that the 'expected' type subsumes the 'actual' type."}
  (-> Type Type (Check Unit))
  (do Monad<Check>
    [assumptions (check' expected actual (list))]
    (wrap [])))

(def: #export (checks? expected actual)
  {#;doc "A simple type-checking function that just returns a yes/no answer."}
  (-> Type Type Bool)
  (case (run fresh-context (check expected actual))
    (#R;Error error)
    false

    (#R;Success _)
    true))

(def: #export get-context
  (Check Type-Context)
  (function [context]
    (#R;Success [context context])))
