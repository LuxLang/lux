(.module:
  [lux (#- Module)
   [control
    ["ex" exception (#+ exception:)]]
   [data
    ["." error (#+ Error)]
    [collection
     ["." dictionary (#+ Dictionary)]]]
   [type (#+ :share)
    abstract]]
  [//
   ["." signature (#+ Signature)]
   ["." key (#+ Key)]
   ["." descriptor (#+ Module Descriptor)]])

## Document
(exception: #export (invalid-signature {module Module} {expected Signature} {actual Signature})
  (ex.report ["Module" module]
             ["Expected" (signature.description expected)]
             ["Actual" (signature.description actual)]))

(abstract: #export (Document d)
  {}
  
  {#signature Signature
   #descriptor Descriptor
   #content d}

  (def: #export (read key document)
    (All [d] (-> (Key d) (Document Any) (Error d)))
    (let [[document//signature document//descriptor document//content] (:representation document)]
      (if (:: signature.Equivalence<Signature> =
              (key.signature key)
              document//signature)
        (#error.Success (:share [e]
                                {(Key e)
                                 key}
                                {e
                                 document//content}))
        (ex.throw invalid-signature [(get@ #descriptor.name document//descriptor)
                                     (key.signature key)
                                     document//signature]))))

  (def: #export (write key descriptor content)
    (All [d] (-> (Key d) Descriptor d (Document d)))
    (:abstraction {#signature (key.signature key)
                   #descriptor descriptor
                   #content content}))

  (def: #export signature
    (-> (Document Any) Signature)
    (|>> :representation (get@ #signature)))
  )
