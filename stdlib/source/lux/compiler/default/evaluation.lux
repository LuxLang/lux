(.module:
  [lux #*
   [control
    [monad (#+ do)]]
   [data
    ["." error]]]
  [//
   ["." phase
    [analysis (#+ Operation)
     [".A" expression]
     ["." type]]
    ["." synthesis
     [".S" expression]]
    ["." translation]]])

(type: #export Eval
  (-> Type Code (Operation Any)))

(def: #export (evaluator synthesis-state translation-state translate)
  (All [anchor expression statement]
    (-> synthesis.State+
        (translation.State+ anchor expression statement)
        (translation.Phase anchor expression statement)
        Eval))
  (function (eval type exprC)
    (do phase.Monad<Operation>
      [exprA (type.with-type type
               (expressionA.compile exprC))]
      (phase.lift (do error.Monad<Error>
                    [exprS (|> exprA expressionS.synthesize (phase.run synthesis-state))]
                    (phase.run translation-state
                               (do phase.Monad<Operation>
                                 [exprO (translate exprS)]
                                 (translation.evaluate! exprO))))))))
