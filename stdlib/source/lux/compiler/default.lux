(.module:
  [lux (#- Source)
   [control
    [monad (#+ do)]
    ["ex" exception (#+ exception:)]]
   [data
    ["." product]
    ["." error]
    [text ("text/." Hash<Text>)
     format
     ["." encoding]]
    [collection
     ["." dictionary]]]
   [type (#+ :share)]
   ["." macro]
   [world
    ["." file (#+ File)]]]
  [//
   ["." cli (#+ Configuration)]
   [meta
    [io
     ["." context]]]]
  [/
   ["." init]
   ["." syntax (#+ Aliases)]
   ["." phase
    ["." analysis
     ["." module]
     [".A" expression]]
    ["." translation (#+ Host Bundle)]
    ["." statement
     [".S" total]]
    ["." extension]]]
  ## (luxc [cache]
  ##       [cache/description]
  ##       [cache/io])
  )

(def: (read current-module aliases)
  (-> Text Aliases (analysis.Operation Code))
  (function (_ [bundle compiler])
    (case (syntax.read current-module aliases (get@ #.source compiler))
      (#error.Error error)
      (#error.Error error)

      (#error.Success [source' output])
      (#error.Success [[bundle (set@ #.source source' compiler)]
                       output]))))

## ## (def: (write-module target-dir file-name module-name module artifacts)
## ##   (-> File Text Text Module Artifacts (Process Any))
## ##   (do io.Monad<Process>
## ##     [_ (monad.map @ (product.uncurry (&io.write target-dir))
## ##                   (dictionary.entries artifacts))]
## ##     (&io.write target-dir
## ##                (format module-name "/" cache.descriptor-name)
## ##                (encoding.to-utf8 (%code (cache/description.write file-name module))))))

(type: #export (Platform ! anchor expression statement)
  {#host (Host expression statement)
   #phase (translation.Phase anchor expression statement)
   #runtime (translation.Operation anchor expression statement Any)
   #file-system (file.System !)})

(type: #export Source
  {#name Text
   #code Text})

(with-expansions [<Platform> (as-is (Platform ! anchor expression statement))
                  <Operation> (as-is (statement.Operation anchor expression statement Any))
                  <Compiler> (as-is (statement.State+ anchor expression statement))
                  <Bundle> (as-is (Bundle anchor expression statement))]
  
  (def: (begin-module-compilation module-name source)
    (All [anchor expression statement]
      (-> Text Source <Operation>))
    (statement.lift-analysis
     (do phase.Monad<Operation>
       [_ (module.create (text/hash (get@ #code source)) module-name)
        _ (analysis.set-current-module module-name)]
       (analysis.set-source-code (init.source (get@ #name source) (get@ #code source))))))

  (def: end-module-compilation
    (All [anchor expression statement]
      (-> Text <Operation>))
    (|>> module.set-compiled
         statement.lift-analysis))

  (def: (loop-module-compilation module-name)
    (All [anchor expression statement]
      (-> Text <Operation>))
    (let [iteration (: (All [anchor expression statement]
                         <Operation>)
                       (<| (phase.timed (name-of ..loop-module-compilation) "ITERATION")
                           (do phase.Monad<Operation>
                             [code (statement.lift-analysis
                                    (do @
                                      [code (<| (phase.timed (name-of ..loop-module-compilation) "syntax")
                                                (..read module-name syntax.no-aliases))
                                       #let [[cursor _] code]
                                       _ (analysis.set-cursor cursor)]
                                      (wrap code)))
                              _ (<| (phase.timed (name-of ..loop-module-compilation) "PHASE")
                                    (totalS.phase code))]
                             init.refresh)))]
      (function (_ state)
        (loop [state state]
          (case (iteration state)
            (#error.Success [state' output])
            (recur state')
            
            (#error.Error error)
            (if (ex.match? syntax.end-of-file error)
              (#error.Success [state []])
              (#error.Error error)))))))

  (def: (perform-module-compilation module-name source)
    (All [anchor expression statement]
      (-> Text Source <Operation>))
    (do phase.Monad<Operation>
      [_ (begin-module-compilation module-name source)
       _ (loop-module-compilation module-name)]
      (end-module-compilation module-name)))

  (def: #export (compile-module platform configuration compiler)
    (All [! anchor expression statement]
      (-> <Platform> Configuration <Compiler> (! <Compiler>)))
    (do (:: (get@ #file-system platform) &monad)
      [source (context.read (get@ #file-system platform)
                            (get@ #cli.sources configuration)
                            (get@ #cli.module configuration))
       ## _ (&io.prepare-module target-dir (get@ #cli.module configuration))
       ## _ (write-module target-dir file-name (get@ #cli.module configuration) module artifacts)
       ]
      (<| (:: @ map product.left)
          (:: (get@ #file-system platform) lift)
          (phase.run' compiler)
          (:share [! anchor expression statement]
                  {<Platform>
                   platform}
                  {<Operation>
                   (perform-module-compilation (get@ #cli.module configuration) source)}))))

  (def: #export (initialize platform configuration translation-bundle)
    (All [! anchor expression statement]
      (-> <Platform> Configuration <Bundle> (! <Compiler>)))
    (|> platform
        (get@ #runtime)
        statement.lift-translation
        (phase.run' (init.state (get@ #host platform)
                                (get@ #phase platform)
                                translation-bundle))
        (:: error.Functor<Error> map product.left)
        (:: (get@ #file-system platform) lift))
    
    ## (case (runtimeT.translate ## (initL.compiler (io.run js.init))
    ##        (initL.compiler (io.run hostL.init-host))
    ##        )
    ##   ## (#error.Success [compiler disk-write])
    ##   ## (do @
    ##   ##   [_ (&io.prepare-target target)
    ##   ##    _ disk-write
    ##   ##    ## _ (cache/io.pre-load sources target (commonT.load-definition compiler))
    ##   ##    ]
    ##   ##   (wrap (|> compiler
    ##   ##             (set@ [#.info #.mode] #.Build))))

    ##   (#error.Success [compiler [runtime-bc function-bc]])
    ##   (do @
    ##     [_ (&io.prepare-target target)
    ##      ## _ (&io.write target (format hostL.runtime-class ".class") runtime-bc)
    ##      ## _ (&io.write target (format hostL.function-class ".class") function-bc)
    ##      ## _ (cache/io.pre-load sources target (commonT.load-definition compiler))
    ##      ]
    ##     (wrap (|> compiler
    ##               (set@ [#.info #.mode] #.Build))))

    ##   (#error.Error error)
    ##   (io.fail error))
    )

  (def: #export (compile platform configuration translation-bundle)
    (All [! anchor expression statement]
      (-> <Platform> Configuration <Bundle> (! Any)))
    (do (:: (get@ #file-system platform) &monad)
      [compiler (initialize platform configuration translation-bundle)
       _ (compile-module platform (set@ #cli.module syntax.prelude configuration) compiler)
       _ (compile-module platform configuration compiler)
       ## _ (cache/io.clean target ...)
       ]
      (wrap (log! "Compilation complete!"))))
  )
