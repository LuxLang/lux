(.module:
  [lux #*
   [control
    [equivalence (#+ Equivalence)]
    pipe
    ["." monad (#+ do)]]
   [data
    ["." product]
    [bit ("bit/." equivalence)]
    [text ("text/." equivalence)
     format]
    [number ("frac/." equivalence)]
    [collection
     ["." list ("list/." fold monoid)]]]]
  ["." // (#+ Path Synthesis Operation Phase)
   ["." function]
   ["/." // ("operation/." monad)
    ["." analysis (#+ Pattern Match Analysis)]
    [//
     ["." reference]]]])

(def: clean-up
  (-> Path Path)
  (|>> (#//.Seq #//.Pop)))

(def: (path' pattern end? thenC)
  (-> Pattern Bit (Operation Path) (Operation Path))
  (case pattern
    (#analysis.Simple simple)
    (case simple
      #analysis.Unit
      thenC
      
      (^template [<from> <to>]
        (<from> value)
        (operation/map (|>> (#//.Seq (#//.Test (|> value <to>))))
                       thenC))
      ([#analysis.Bit  #//.Bit]
       [#analysis.Nat  (<| #//.I64 .i64)]
       [#analysis.Int  (<| #//.I64 .i64)]
       [#analysis.Rev  (<| #//.I64 .i64)]
       [#analysis.Frac #//.F64]
       [#analysis.Text #//.Text]))
    
    (#analysis.Bind register)
    (<| (:: ///.monad map (|>> (#//.Seq (#//.Bind register))))
        //.with-new-local
        thenC)

    (#analysis.Complex (#analysis.Variant [lefts right? value-pattern]))
    (<| (operation/map (|>> (#//.Seq (#//.Access (#//.Side (if right?
                                                             (#.Right lefts)
                                                             (#.Left lefts)))))))
        (path' value-pattern end?)
        (when (not end?) (operation/map ..clean-up))
        thenC)

    (#analysis.Complex (#analysis.Tuple tuple))
    (let [tuple::last (dec (list.size tuple))]
      (list/fold (function (_ [tuple::lefts tuple::member] nextC)
                   (let [right? (n/= tuple::last tuple::lefts)
                         end?' (and end? right?)]
                     (<| (operation/map (|>> (#//.Seq (#//.Access (#//.Member (if right?
                                                                                (#.Right (dec tuple::lefts))
                                                                                (#.Left tuple::lefts)))))))
                         (path' tuple::member end?')
                         (when (not end?') (operation/map ..clean-up))
                         nextC)))
                 thenC
                 (list.reverse (list.enumerate tuple))))))

(def: #export (path synthesize pattern bodyA)
  (-> Phase Pattern Analysis (Operation Path))
  (path' pattern true (operation/map (|>> #//.Then) (synthesize bodyA))))

(def: #export (weave leftP rightP)
  (-> Path Path Path)
  (with-expansions [<default> (as-is (#//.Alt leftP rightP))]
    (case [leftP rightP]
      [(#//.Seq preL postL)
       (#//.Seq preR postR)]
      (case (weave preL preR)
        (#//.Alt _)
        <default>

        weavedP
        (#//.Seq weavedP (weave postL postR)))

      [#//.Pop #//.Pop]
      rightP

      (^template [<tag> <eq>]
        [(#//.Test (<tag> leftV))
         (#//.Test (<tag> rightV))]
        (if (<eq> leftV rightV)
          rightP
          <default>))
      ([#//.Bit bit/=]
       [#//.I64 "lux i64 ="]
       [#//.F64 frac/=]
       [#//.Text text/=])

      (^template [<access> <side>]
        [(#//.Access (<access> (<side> leftL)))
         (#//.Access (<access> (<side> rightL)))]
        (if (n/= leftL rightL)
          rightP
          <default>))
      ([#//.Side #.Left]
       [#//.Side #.Right]
       [#//.Member #.Left]
       [#//.Member #.Right])

      [(#//.Bind leftR) (#//.Bind rightR)]
      (if (n/= leftR rightR)
        rightP
        <default>)

      _
      <default>)))

(def: #export (synthesize synthesize^ inputA [headB tailB+])
  (-> Phase Analysis Match (Operation Synthesis))
  (do ///.monad
    [inputS (synthesize^ inputA)]
    (with-expansions [<unnecesary-let>
                      (as-is (^multi (^ (#analysis.Reference (reference.local outputR)))
                                     (n/= inputR outputR))
                             (wrap inputS))

                      <let>
                      (as-is [[(#analysis.Bind inputR) headB/bodyA]
                              #.Nil]
                             (case headB/bodyA
                               <unnecesary-let>

                               _
                               (do @
                                 [headB/bodyS (//.with-new-local
                                                (synthesize^ headB/bodyA))]
                                 (wrap (//.branch/let [inputS inputR headB/bodyS])))))

                      <if>
                      (as-is (^or (^ [[(analysis.pattern/bit #1) thenA]
                                      (list [(analysis.pattern/bit #0) elseA])])
                                  (^ [[(analysis.pattern/bit #0) elseA]
                                      (list [(analysis.pattern/bit #1) thenA])]))
                             (do @
                               [thenS (synthesize^ thenA)
                                elseS (synthesize^ elseA)]
                               (wrap (//.branch/if [inputS thenS elseS]))))

                      <case>
                      (as-is _
                             (let [[[lastP lastA] prevsPA] (|> (#.Cons headB tailB+)
                                                               list.reverse
                                                               (case> (#.Cons [lastP lastA] prevsPA)
                                                                      [[lastP lastA] prevsPA]

                                                                      _
                                                                      (undefined)))]
                               (do @
                                 [lastSP (path synthesize^ lastP lastA)
                                  prevsSP+ (monad.map @ (product.uncurry (path synthesize^)) prevsPA)]
                                 (wrap (//.branch/case [inputS (list/fold weave lastSP prevsSP+)])))))]
      (case [headB tailB+]
        <let>
        <if>
        <case>))))
