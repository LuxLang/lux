(.module:
  {#.doc "Commons syntax writers."}
  [lux #*
   [control
    ["." function]]
   [data
    [collection
     ["." list ("#\." functor)]]
    ["." product]]
   [macro
    ["." code]]]
  ["." //])

(def: #export (declaration declaration)
  (-> //.Declaration Code)
  (` ((~ (code.local-identifier (get@ #//.declaration-name declaration)))
      (~+ (list\map code.local-identifier
                    (get@ #//.declaration-args declaration))))))

(def: #export annotations
  (-> //.Annotations Code)
  (|>> (list\map (product.both code.tag function.identity))
       code.record))

(def: #export (typed-input value)
  (-> //.Typed-Input Code)
  (code.record (list [(get@ #//.input-binding value)
                      (get@ #//.input-type value)])))

(def: #export type-variables
  (-> (List //.Type-Var) (List Code))
  (list\map code.local-identifier))
