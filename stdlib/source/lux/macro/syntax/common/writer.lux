(.module:
  {#.doc "Commons syntax writers."}
  [lux #*
   [control
    ["." function]]
   [data
    [collection
     ["." list ("#@." functor)]]
    ["." product]]
   [macro
    ["." code]]]
  ["." //])

(def: #export (export exported?)
  (-> Bit (List Code))
  (if exported?
    (list (' #export))
    (list)))

(def: #export (declaration declaration)
  (-> //.Declaration Code)
  (` ((~ (code.local-identifier (get@ #//.declaration-name declaration)))
      (~+ (list@map code.local-identifier
                    (get@ #//.declaration-args declaration))))))

(def: #export annotations
  (-> //.Annotations Code)
  (|>> (list@map (product.both code.tag function.identity))
       code.record))

(def: #export type-variables
  (-> (List Text) (List Code))
  (list@map code.local-identifier))
