(.module:
  {#.doc "Commons syntax writers."}
  [lux #*
   [control
    ["." function]]
   [data
    [collection
     ["." list ("#\." functor)]]
    ["." product]]
   [macro
    ["." code]]]
  ["." //])

(def: #export (declaration declaration)
  (-> //.Declaration Code)
  (` ((~ (code.local_identifier (get@ #//.declaration_name declaration)))
      (~+ (list\map code.local_identifier
                    (get@ #//.declaration_args declaration))))))

(def: #export annotations
  (-> //.Annotations Code)
  (|>> (list\map (product.both code.tag function.identity))
       code.record))

(def: #export (typed_input value)
  (-> //.Typed_Input Code)
  (code.record (list [(get@ #//.input_binding value)
                      (get@ #//.input_type value)])))

(def: #export type_variables
  (-> (List //.Type_Var) (List Code))
  (list\map code.local_identifier))
