(.module: {#.doc "Codecs for values in the JSON format."}
  lux
  (lux (control [monad #+ do Monad]
                [equivalence #+ Equivalence]
                codec
                ["p" parser "p/" Monad<Parser>])
       (data [bool]
             [bit]
             [text "text/" Equivalence<Text>]
             (text ["l" lexer]
                   format)
             [number "frac/" Codec<Text,Frac> "nat/" Codec<Text,Nat>]
             maybe
             ["e" error]
             [sum]
             [product]
             (coll [list "list/" Fold<List> Monad<List>]
                   [row #+ Row row "row/" Monad<Row>]
                   ["d" dictionary])
             (format ["//" json #+ JSON]))
       (time ## ["i" instant]
             ["du" duration]
             ["da" date])
       [macro #+ with-gensyms]
       (macro ["s" syntax #+ syntax:]
              [code]
              [poly #+ poly:])
       (type [unit])
       (lang [type])
       ))

(def: tag
  (-> Nat Frac)
  (|>> .int int-to-frac))

(def: (rec-encode non-rec)
  (All [a] (-> (-> (-> a JSON)
                   (-> a JSON))
               (-> a JSON)))
  (function (_ input)
    (non-rec (rec-encode non-rec) input)))

(def: low-mask Nat (|> +1 (bit.left-shift +32) dec))
(def: high-mask Nat (|> low-mask (bit.left-shift +32)))

(structure: _ (Codec JSON Nat)
  (def: (encode input)
    (let [high (|> input (bit.and high-mask) (bit.logical-right-shift +32))
          low (bit.and low-mask input)]
      (#//.Array (row (|> high .int int-to-frac #//.Number)
                      (|> low .int int-to-frac #//.Number)))))
  (def: (decode input)
    (<| (//.run input)
        //.array
        (do p.Monad<Parser>
          [high //.number
           low //.number])
        (wrap (n/+ (|> high frac-to-int .nat (bit.left-shift +32))
                   (|> low frac-to-int .nat))))))

(structure: _ (Codec JSON Int)
  (def: encode (|>> .nat (:: Codec<JSON,Nat> encode)))
  (def: decode
    (|>> (:: Codec<JSON,Nat> decode) (:: e.Functor<Error> map .int))))

(def: (nullable writer)
  {#.doc "Builds a JSON generator for potentially inexistent values."}
  (All [a] (-> (-> a JSON) (-> (Maybe a) JSON)))
  (function (_ elem)
    (case elem
      #.None         #//.Null
      (#.Some value) (writer value))))

(structure: Codec<JSON,Qty>
  (All [unit] (Codec JSON (unit.Qty unit)))
  (def: encode
    (|>> unit.out (:: Codec<JSON,Int> encode)))
  (def: decode
    (|>> (:: Codec<JSON,Int> decode) (:: e.Functor<Error> map unit.in))))

(poly: Codec<JSON,?>//encode
  (with-expansions
    [<basic> (do-template [<matcher> <encoder>]
               [(do @
                  [#let [g!_ (code.local-symbol "_______")]
                   _ <matcher>]
                  (wrap (` (: (~ (@JSON//encode inputT))
                              <encoder>))))]

               [(poly.this Any) (function ((~ g!_) (~ (code.symbol ["" "0"]))) #//.Null)]
               [(poly.like Bool) (|>> #//.Boolean)]
               [(poly.like Nat)  (:: (~! ..Codec<JSON,Nat>) (~' encode))]
               [(poly.like Int)  (:: (~! ..Codec<JSON,Int>) (~' encode))]
               [(poly.like Frac) (|>> #//.Number)]
               [(poly.like Text) (|>> #//.String)])
     <time> (do-template [<type> <codec>]
              [(do @
                 [_ (poly.this <type>)]
                 (wrap (` (: (~ (@JSON//encode inputT))
                             (|>> (:: <codec> (~' encode)) #//.String)))))]

              [du.Duration du.Codec<Text,Duration>]
              ## [i.Instant   i.Codec<Text,Instant>]
              [da.Date     da.Codec<Text,Date>]
              [da.Day      da.Codec<Text,Day>]
              [da.Month    da.Codec<Text,Month>])]
    (do @
      [*env* poly.env
       #let [@JSON//encode (: (-> Type Code)
                              (function (_ type)
                                (` (-> (~ (poly.to-code *env* type)) //.JSON))))]
       inputT poly.peek]
      ($_ p.either
          <basic>
          <time>
          (do @
            [unitT (poly.apply (p.after (poly.this unit.Qty)
                                        poly.any))]
            (wrap (` (: (~ (@JSON//encode inputT))
                        (:: (~! Codec<JSON,Qty>) (~' encode))))))
          (do @
            [#let [g!_ (code.local-symbol "_______")
                   g!key (code.local-symbol "_______key")
                   g!val (code.local-symbol "_______val")]
             [_ _ =val=] (poly.apply ($_ p.seq
                                         (poly.this d.Dictionary)
                                         (poly.this .Text)
                                         Codec<JSON,?>//encode))]
            (wrap (` (: (~ (@JSON//encode inputT))
                        (|>> d.entries
                             ((~! list/map) (function ((~ g!_) [(~ g!key) (~ g!val)])
                                              [(~ g!key) ((~ =val=) (~ g!val))]))
                             (d.from-list text.Hash<Text>)
                             #//.Object)))))
          (do @
            [[_ =sub=] (poly.apply ($_ p.seq
                                       (poly.this .Maybe)
                                       Codec<JSON,?>//encode))]
            (wrap (` (: (~ (@JSON//encode inputT))
                        ((~! ..nullable) (~ =sub=))))))
          (do @
            [[_ =sub=] (poly.apply ($_ p.seq
                                       (poly.this .List)
                                       Codec<JSON,?>//encode))]
            (wrap (` (: (~ (@JSON//encode inputT))
                        (|>> ((~! list/map) (~ =sub=)) row.from-list #//.Array)))))
          (do @
            [#let [g!_ (code.local-symbol "_______")
                   g!input (code.local-symbol "_______input")]
             members (poly.variant (p.many Codec<JSON,?>//encode))]
            (wrap (` (: (~ (@JSON//encode inputT))
                        (function ((~ g!_) (~ g!input))
                          (case (~ g!input)
                            (~+ (list/join (list/map (function (_ [tag g!encode])
                                                       (list (` ((~ (code.nat tag)) (~ g!input)))
                                                             (` (//.json [(~ (code.frac (..tag tag)))
                                                                          ((~ g!encode) (~ g!input))]))))
                                                     (list.enumerate members))))))))))
          (do @
            [g!encoders (poly.tuple (p.many Codec<JSON,?>//encode))
             #let [g!_ (code.local-symbol "_______")
                   g!members (|> (list.size g!encoders) dec
                                 (list.n/range +0)
                                 (list/map (|>> nat/encode code.local-symbol)))]]
            (wrap (` (: (~ (@JSON//encode inputT))
                        (function ((~ g!_) [(~+ g!members)])
                          (//.json [(~+ (list/map (function (_ [g!member g!encode])
                                                    (` ((~ g!encode) (~ g!member))))
                                                  (list.zip2 g!members g!encoders)))]))))))
          ## Type recursion
          (do @
            [[selfC non-recC] (poly.recursive Codec<JSON,?>//encode)
             #let [g! (code.local-symbol "____________")]]
            (wrap (` (: (~ (@JSON//encode inputT))
                        ((~! ..rec-encode) (.function ((~ g!) (~ selfC))
                                             (~ non-recC)))))))
          poly.recursive-self
          ## Type applications
          (do @
            [partsC (poly.apply (p.many Codec<JSON,?>//encode))]
            (wrap (` ((~+ partsC)))))
          ## Polymorphism
          (do @
            [[funcC varsC bodyC] (poly.polymorphic Codec<JSON,?>//encode)]
            (wrap (` (: (All [(~+ varsC)]
                          (-> (~+ (list/map (function (_ varC) (` (->  (~ varC) //.JSON)))
                                            varsC))
                              (-> ((~ (poly.to-code *env* inputT)) (~+ varsC))
                                  //.JSON)))
                        (function ((~ funcC) (~+ varsC))
                          (~ bodyC))))))
          poly.parameter
          poly.recursive-call
          ## If all else fails...
          (p.fail (format "Cannot create JSON encoder for: " (type.to-text inputT)))
          ))))

(poly: Codec<JSON,?>//decode
  (with-expansions
    [<basic> (do-template [<matcher> <decoder>]
               [(do @
                  [_ <matcher>]
                  (wrap (` (: (~ (@JSON//decode inputT))
                              <decoder>))))]

               [(poly.this Any) //.null]
               [(poly.like Bool) //.boolean]
               [(poly.like Nat)  (p.codec (~! ..Codec<JSON,Nat>) //.any)]
               [(poly.like Int)  (p.codec (~! ..Codec<JSON,Int>) //.any)]
               [(poly.like Frac) //.number]
               [(poly.like Text) //.string])
     <time> (do-template [<type> <codec>]
              [(do @
                 [_ (poly.this <type>)]
                 (wrap (` (: (~ (@JSON//decode inputT))
                             (p.codec <codec> //.string)))))]

              [du.Duration du.Codec<Text,Duration>]
              ## [i.Instant   i.Codec<Text,Instant>]
              [da.Date     da.Codec<Text,Date>]
              [da.Day      da.Codec<Text,Day>]
              [da.Month    da.Codec<Text,Month>])]
    (do @
      [*env* poly.env
       #let [@JSON//decode (: (-> Type Code)
                              (function (_ type)
                                (` (//.Reader (~ (poly.to-code *env* type))))))]
       inputT poly.peek]
      ($_ p.either
          <basic>
          <time>
          (do @
            [unitT (poly.apply (p.after (poly.this unit.Qty)
                                        poly.any))]
            (wrap (` (: (~ (@JSON//decode inputT))
                        (p.codec (~! Codec<JSON,Qty>) //.any)))))
          (do @
            [[_ _ valC] (poly.apply ($_ p.seq
                                        (poly.this d.Dictionary)
                                        (poly.this .Text)
                                        Codec<JSON,?>//decode))]
            (wrap (` (: (~ (@JSON//decode inputT))
                        (//.object (~ valC))))))
          (do @
            [[_ subC] (poly.apply (p.seq (poly.this .Maybe)
                                         Codec<JSON,?>//decode))]
            (wrap (` (: (~ (@JSON//decode inputT))
                        (//.nullable (~ subC))))))
          (do @
            [[_ subC] (poly.apply (p.seq (poly.this .List)
                                         Codec<JSON,?>//decode))]
            (wrap (` (: (~ (@JSON//decode inputT))
                        (//.array (p.some (~ subC)))))))
          (do @
            [members (poly.variant (p.many Codec<JSON,?>//decode))]
            (wrap (` (: (~ (@JSON//decode inputT))
                        ($_ p.alt
                            (~+ (list/map (function (_ [tag memberC])
                                            (` (|> (~ memberC)
                                                   (p.after (//.number! (~ (code.frac (..tag tag)))))
                                                   //.array)))
                                          (list.enumerate members))))))))
          (do @
            [g!decoders (poly.tuple (p.many Codec<JSON,?>//decode))]
            (wrap (` (: (~ (@JSON//decode inputT))
                        (//.array ($_ p.seq (~+ g!decoders)))))))
          ## Type recursion
          (do @
            [[selfC bodyC] (poly.recursive Codec<JSON,?>//decode)
             #let [g! (code.local-symbol "____________")]]
            (wrap (` (: (~ (@JSON//decode inputT))
                        (p.rec (.function ((~ g!) (~ selfC))
                                 (~ bodyC)))))))
          poly.recursive-self
          ## Type applications
          (do @
            [[funcC argsC] (poly.apply (p.seq Codec<JSON,?>//decode (p.many Codec<JSON,?>//decode)))]
            (wrap (` ((~ funcC) (~+ argsC)))))
          ## Polymorphism
          (do @
            [[funcC varsC bodyC] (poly.polymorphic Codec<JSON,?>//decode)]
            (wrap (` (: (All [(~+ varsC)]
                          (-> (~+ (list/map (|>> (~) //.Reader (`)) varsC))
                              (//.Reader ((~ (poly.to-code *env* inputT)) (~+ varsC)))))
                        (function ((~ funcC) (~+ varsC))
                          (~ bodyC))))))
          poly.parameter
          poly.recursive-call
          ## If all else fails...
          (p.fail (format "Cannot create JSON decoder for: " (type.to-text inputT)))
          ))))

(syntax: #export (Codec<JSON,?> inputT)
  {#.doc (doc "A macro for automatically producing JSON codecs."
              (type: Variant
                (#Case0 Bool)
                (#Case1 Text)
                (#Case2 Frac))

              (type: Record
                {#bool Bool
                 #frac Frac
                 #text Text
                 #maybe (Maybe Frac)
                 #list (List Frac)
                 #variant Variant
                 #tuple [Bool Frac Text]
                 #dict (Dictionary Text Frac)})

              (derived: (Codec<JSON,?> Record)))}
  (with-gensyms [g!inputs]
    (wrap (list (` (: (Codec //.JSON (~ inputT))
                      (structure (def: (~' encode) ((~! Codec<JSON,?>//encode) (~ inputT)))
                                 (def: ((~' decode) (~ g!inputs)) (//.run (~ g!inputs) ((~! Codec<JSON,?>//decode) (~ inputT))))
                                 )))))))
