(.module: {#.doc "Codecs for values in the JSON format."}
  [lux #*
   [abstract
    [monad (#+ Monad do)]
    [equivalence (#+ Equivalence)]
    ["." codec]]
   [control
    ["p" parser
     ["<.>" type]]]
   [data
    ["." bit]
    maybe
    ["e" error]
    ["." sum]
    ["." product]
    [number
     ["." i64]
     ["." nat ("#@." decimal)]
     ["." frac ("#@." decimal)]]
    ["." text ("#@." equivalence)
     ["l" lexer]
     format]
    [format
     ["/" json (#+ JSON)]]
    [collection
     ["." list ("#@." fold monad)]
     ["." row (#+ Row row) ("#@." monad)]
     ["d" dictionary]]]
   [time
    ## ["." instant]
    ## ["." duration]
    ["." date]
    ["." day]
    ["." month]]
   [macro (#+ with-gensyms)
    ["s" syntax (#+ syntax:)]
    ["." code]
    ["." poly (#+ poly:)]]
   ["." type
    ["." unit]]])

(def: tag
  (-> Nat Frac)
  (|>> .int int-to-frac))

(def: (rec-encode non-rec)
  (All [a] (-> (-> (-> a JSON)
                   (-> a JSON))
               (-> a JSON)))
  (function (_ input)
    (non-rec (rec-encode non-rec) input)))

(def: low-mask Nat (|> 1 (i64.left-shift 32) dec))
(def: high-mask Nat (|> low-mask (i64.left-shift 32)))

(structure: nat-codec (codec.Codec JSON Nat)
  (def: (encode input)
    (let [high (|> input (i64.and high-mask) (i64.logic-right-shift 32))
          low (i64.and low-mask input)]
      (#/.Array (row (|> high .int int-to-frac #/.Number)
                     (|> low .int int-to-frac #/.Number)))))
  (def: (decode input)
    (<| (/.run input)
        /.array
        (do p.monad
          [high /.number
           low /.number])
        (wrap (n/+ (|> high frac-to-int .nat (i64.left-shift 32))
                   (|> low frac-to-int .nat))))))

(structure: int-codec (codec.Codec JSON Int)
  (def: encode (|>> .nat (:: nat-codec encode)))
  (def: decode
    (|>> (:: nat-codec decode) (:: e.functor map .int))))

(def: (nullable writer)
  {#.doc "Builds a JSON generator for potentially inexistent values."}
  (All [a] (-> (-> a JSON) (-> (Maybe a) JSON)))
  (function (_ elem)
    (case elem
      #.None         #/.Null
      (#.Some value) (writer value))))

(structure: qty-codec
  (All [unit] (codec.Codec JSON (unit.Qty unit)))
  
  (def: encode
    (|>> unit.out (:: ..int-codec encode)))
  (def: decode
    (|>> (:: ..int-codec decode) (:: e.functor map unit.in))))

(poly: #export codec//encode
  (with-expansions
    [<basic> (template [<matcher> <encoder>]
               [(do @
                  [#let [g!_ (code.local-identifier "_______")]
                   _ <matcher>]
                  (wrap (` (: (~ (@JSON//encode inputT))
                              <encoder>))))]

               [(<type>.exactly Any) (function ((~ g!_) (~ (code.identifier ["" "0"]))) #/.Null)]
               [(<type>.sub Bit)     (|>> #/.Boolean)]
               [(<type>.sub Nat)     (:: (~! ..nat-codec) (~' encode))]
               [(<type>.sub Int)     (:: (~! ..int-codec) (~' encode))]
               [(<type>.sub Frac)    (|>> #/.Number)]
               [(<type>.sub Text)    (|>> #/.String)])
     <time> (template [<type> <codec>]
              [(do @
                 [_ (<type>.exactly <type>)]
                 (wrap (` (: (~ (@JSON//encode inputT))
                             (|>> (:: (~! <codec>) (~' encode)) #/.String)))))]

              ## [duration.Duration duration.codec]
              ## [instant.Instant instant.codec]
              [date.Date date.codec]
              [day.Day day.codec]
              [month.Month month.codec])]
    (do @
      [*env* <type>.env
       #let [@JSON//encode (: (-> Type Code)
                              (function (_ type)
                                (` (-> (~ (poly.to-code *env* type)) /.JSON))))]
       inputT <type>.peek]
      ($_ p.either
          <basic>
          <time>
          (do @
            [unitT (<type>.apply (p.after (<type>.exactly unit.Qty)
                                          <type>.any))]
            (wrap (` (: (~ (@JSON//encode inputT))
                        (:: (~! qty-codec) (~' encode))))))
          (do @
            [#let [g!_ (code.local-identifier "_______")
                   g!key (code.local-identifier "_______key")
                   g!val (code.local-identifier "_______val")]
             [_ _ =val=] (<type>.apply ($_ p.and
                                           (<type>.exactly d.Dictionary)
                                           (<type>.exactly .Text)
                                           codec//encode))]
            (wrap (` (: (~ (@JSON//encode inputT))
                        (|>> ((~! d.entries))
                             ((~! list@map) (function ((~ g!_) [(~ g!key) (~ g!val)])
                                              [(~ g!key) ((~ =val=) (~ g!val))]))
                             ((~! d.from-list) (~! text.hash))
                             #/.Object)))))
          (do @
            [[_ =sub=] (<type>.apply ($_ p.and
                                         (<type>.exactly .Maybe)
                                         codec//encode))]
            (wrap (` (: (~ (@JSON//encode inputT))
                        ((~! ..nullable) (~ =sub=))))))
          (do @
            [[_ =sub=] (<type>.apply ($_ p.and
                                         (<type>.exactly .List)
                                         codec//encode))]
            (wrap (` (: (~ (@JSON//encode inputT))
                        (|>> ((~! list@map) (~ =sub=)) ((~! row.from-list)) #/.Array)))))
          (do @
            [#let [g!_ (code.local-identifier "_______")
                   g!input (code.local-identifier "_______input")]
             members (<type>.variant (p.many codec//encode))]
            (wrap (` (: (~ (@JSON//encode inputT))
                        (function ((~ g!_) (~ g!input))
                          (case (~ g!input)
                            (~+ (list@join (list@map (function (_ [tag g!encode])
                                                       (list (` ((~ (code.nat tag)) (~ g!input)))
                                                             (` ((~! /.json) [(~ (code.frac (..tag tag)))
                                                                              ((~ g!encode) (~ g!input))]))))
                                                     (list.enumerate members))))))))))
          (do @
            [g!encoders (<type>.tuple (p.many codec//encode))
             #let [g!_ (code.local-identifier "_______")
                   g!members (|> (list.size g!encoders)
                                 list.indices
                                 (list@map (|>> nat@encode code.local-identifier)))]]
            (wrap (` (: (~ (@JSON//encode inputT))
                        (function ((~ g!_) [(~+ g!members)])
                          ((~! /.json) [(~+ (list@map (function (_ [g!member g!encode])
                                                        (` ((~ g!encode) (~ g!member))))
                                                      (list.zip2 g!members g!encoders)))]))))))
          ## Type recursion
          (do @
            [[selfC non-recC] (<type>.recursive codec//encode)
             #let [g! (code.local-identifier "____________")]]
            (wrap (` (: (~ (@JSON//encode inputT))
                        ((~! ..rec-encode) (.function ((~ g!) (~ selfC))
                                             (~ non-recC)))))))
          <type>.recursive-self
          ## Type applications
          (do @
            [partsC (<type>.apply (p.many codec//encode))]
            (wrap (` ((~+ partsC)))))
          ## Polymorphism
          (do @
            [[funcC varsC bodyC] (<type>.polymorphic codec//encode)]
            (wrap (` (: (All [(~+ varsC)]
                          (-> (~+ (list@map (function (_ varC) (` (-> (~ varC) /.JSON)))
                                            varsC))
                              (-> ((~ (poly.to-code *env* inputT)) (~+ varsC))
                                  /.JSON)))
                        (function ((~ funcC) (~+ varsC))
                          (~ bodyC))))))
          <type>.parameter
          <type>.recursive-call
          ## If all else fails...
          (p.fail (format "Cannot create JSON encoder for: " (type.to-text inputT)))
          ))))

(poly: #export codec//decode
  (with-expansions
    [<basic> (template [<matcher> <decoder>]
               [(do @
                  [_ <matcher>]
                  (wrap (` (: (~ (@JSON//decode inputT))
                              (~! <decoder>)))))]

               [(<type>.exactly Any)  /.null]
               [(<type>.sub Bit)      /.boolean]
               [(<type>.sub Nat)      (p.codec ..nat-codec /.any)]
               [(<type>.sub Int)      (p.codec ..int-codec /.any)]
               [(<type>.sub Frac)     /.number]
               [(<type>.sub Text)     /.string])
     <time> (template [<type> <codec>]
              [(do @
                 [_ (<type>.exactly <type>)]
                 (wrap (` (: (~ (@JSON//decode inputT))
                             ((~! p.codec) (~! <codec>) (~! /.string))))))]

              ## [duration.Duration duration.codec]
              ## [instant.Instant instant.codec]
              [date.Date date.codec]
              [day.Day day.codec]
              [month.Month month.codec])
     ]
    (do @
      [*env* <type>.env
       #let [@JSON//decode (: (-> Type Code)
                              (function (_ type)
                                (` (/.Reader (~ (poly.to-code *env* type))))))]
       inputT <type>.peek]
      ($_ p.either
          <basic>
          <time>
          (do @
            [unitT (<type>.apply (p.after (<type>.exactly unit.Qty)
                                          <type>.any))]
            (wrap (` (: (~ (@JSON//decode inputT))
                        ((~! p.codec) (~! qty-codec) (~! /.any))))))
          (do @
            [[_ _ valC] (<type>.apply ($_ p.and
                                          (<type>.exactly d.Dictionary)
                                          (<type>.exactly .Text)
                                          codec//decode))]
            (wrap (` (: (~ (@JSON//decode inputT))
                        ((~! /.dictionary) (~ valC))))))
          (do @
            [[_ subC] (<type>.apply (p.and (<type>.exactly .Maybe)
                                           codec//decode))]
            (wrap (` (: (~ (@JSON//decode inputT))
                        ((~! /.nullable) (~ subC))))))
          (do @
            [[_ subC] (<type>.apply (p.and (<type>.exactly .List)
                                           codec//decode))]
            (wrap (` (: (~ (@JSON//decode inputT))
                        ((~! /.array) ((~! p.some) (~ subC)))))))
          (do @
            [members (<type>.variant (p.many codec//decode))]
            (wrap (` (: (~ (@JSON//decode inputT))
                        ($_ ((~! p.or))
                            (~+ (list@map (function (_ [tag memberC])
                                            (` (|> (~ memberC)
                                                   ((~! p.after) ((~! /.number!) (~ (code.frac (..tag tag)))))
                                                   ((~! /.array)))))
                                          (list.enumerate members))))))))
          (do @
            [g!decoders (<type>.tuple (p.many codec//decode))]
            (wrap (` (: (~ (@JSON//decode inputT))
                        ((~! /.array) ($_ ((~! p.and)) (~+ g!decoders)))))))
          ## Type recursion
          (do @
            [[selfC bodyC] (<type>.recursive codec//decode)
             #let [g! (code.local-identifier "____________")]]
            (wrap (` (: (~ (@JSON//decode inputT))
                        ((~! p.rec) (.function ((~ g!) (~ selfC))
                                      (~ bodyC)))))))
          <type>.recursive-self
          ## Type applications
          (do @
            [[funcC argsC] (<type>.apply (p.and codec//decode (p.many codec//decode)))]
            (wrap (` ((~ funcC) (~+ argsC)))))
          ## Polymorphism
          (do @
            [[funcC varsC bodyC] (<type>.polymorphic codec//decode)]
            (wrap (` (: (All [(~+ varsC)]
                          (-> (~+ (list@map (|>> (~) /.Reader (`)) varsC))
                              (/.Reader ((~ (poly.to-code *env* inputT)) (~+ varsC)))))
                        (function ((~ funcC) (~+ varsC))
                          (~ bodyC))))))
          <type>.parameter
          <type>.recursive-call
          ## If all else fails...
          (p.fail (format "Cannot create JSON decoder for: " (type.to-text inputT)))
          ))))

(syntax: #export (codec inputT)
  {#.doc (doc "A macro for automatically producing JSON codecs."
              (type: Variant
                (#Bit Bit)
                (#Text Text)
                (#Frac Frac))

              (type: Record
                {#bit Bit
                 #frac Frac
                 #text Text
                 #maybe (Maybe Frac)
                 #list (List Frac)
                 #variant Variant
                 #tuple [Bit Frac Text]
                 #dictionary (Dictionary Text Frac)})

              (derived: (..codec Record)))}
  (with-gensyms [g!inputs]
    (wrap (list (` (: (codec.Codec /.JSON (~ inputT))
                      (structure (def: (~' encode)
                                   (..codec//encode (~ inputT)))
                                 (def: ((~' decode) (~ g!inputs))
                                   ((~! /.run) (~ g!inputs)
                                    (..codec//decode (~ inputT))))
                                 )))))))
