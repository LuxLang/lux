(;module:
  lux
  (lux (control monad
                [eq])
       (data [text]
             text/format
             (coll [list "List/" Monad<List>]
                   [vector]
                   [array]
                   [queue]
                   [set]
                   [seq]
                   [dict #+ Dict]
                   (tree [rose]))
             [number]
             [product]
             [bool]
             [maybe])
       [macro #+ Monad<Lux> with-gensyms]
       (macro [code]
              [syntax #+ syntax: Syntax]
              (syntax [common])
              [poly #+ poly:])
       [type]
       ))

## [Utils]
(def: (function$ func inputs output)
  (-> Code (List Code) Code Code)
  (case inputs
    #;Nil
    output

    _
    (` (function (~@ (if (list;empty? inputs) (list) (list func)))
         [(~@ inputs)]
         (~ output)))))

## [Derivers]
(poly: #export (Eq<?> env :x:)
  (let [->Eq (: (-> Code Code)
                (function [.type.] (` (eq;Eq (~ .type.)))))]
    (with-expansions
      [<basic> (do-template [<type> <matcher> <eq>]
                 [(do @
                    [_ (<matcher> :x:)]
                    (wrap (` (: (~ (->Eq (` <type>)))
                                <eq>))))]

                 [Unit poly;unit (function [(~' test) (~' input)] true)]
                 [Bool poly;bool bool;Eq<Bool>]
                 [Nat  poly;nat  number;Eq<Nat>]
                 [Int  poly;int  number;Eq<Int>]
                 [Deg  poly;deg  number;Eq<Deg>]
                 [Real poly;real number;Eq<Real>]
                 [Text poly;text text;Eq<Text>])
       <composites> (do-template [<name> <eq>]
                      [(do @
                         [:arg: (poly;apply-1 (ident-for <name>) :x:)
                          g!arg (Eq<?> env :arg:)]
                         (wrap (` (: (~ (->Eq (type;to-ast :x:)))
                                     (<eq> (~ g!arg))))))]

                      [list;List     list;Eq<List>]
                      [vector;Vector vector;Eq<Vector>]
                      [array;Array   array;Eq<Array>]
                      [queue;Queue   queue;Eq<Queue>]
                      [set;Set       set;Eq<Set>]
                      [seq;Seq       seq;Eq<Seq>]
                      [rose;Tree     rose;Eq<Tree>]
                      )]
      ($_ macro;either
          ## Primitive types
          <basic>
          ## Composite types
          <composites>
          (do @
            [[:key: :val:] (poly;apply-2 (ident-for dict;Dict) :x:)
             g!val (Eq<?> env :val:)]
            (wrap (` (: (~ (->Eq (type;to-ast :x:)))
                        (dict;Eq<Dict> (~ g!val))))))
          ## Variants
          (with-gensyms [g!type-fun g!left g!right]
            (do @
              [members (poly;sum+ :x:)
               pattern-matching (mapM @
                                      (function [[tag :case:]]
                                        (do @
                                          [g!eq (Eq<?> env :case:)]
                                          (wrap (list (` [((~ (code;nat tag)) (~ g!left))
                                                          ((~ (code;nat tag)) (~ g!right))])
                                                      (` ((~ g!eq) (~ g!left) (~ g!right)))))))
                                      (list;enumerate members))
               #let [base (` (function [(~ g!left) (~ g!right)]
                               (case [(~ g!left) (~ g!right)]
                                 (~@ (List/join pattern-matching)))))]]
              (wrap (` (: (~ (poly;gen-type env ->Eq g!type-fun (list) :x:))
                          (~ base))))))
          (with-gensyms [g!type-fun g!left g!right]
            (do @
              [[g!vars members] (poly;variant :x:)
               #let [new-env (poly;extend-env [:x: g!type-fun]
                                              (list;zip2 (|> g!vars list;size poly;type-var-indices) g!vars)
                                              env)]
               pattern-matching (mapM @
                                      (function [[name :case:]]
                                        (do @
                                          [g!eq (Eq<?> new-env :case:)]
                                          (wrap (list (` [((~ (code;tag name)) (~ g!left))
                                                          ((~ (code;tag name)) (~ g!right))])
                                                      (` ((~ g!eq) (~ g!left) (~ g!right)))))))
                                      members)
               #let [base (function$ g!type-fun g!vars
                                     (` (function [(~ g!left) (~ g!right)]
                                          (case [(~ g!left) (~ g!right)]
                                            (~@ (List/join pattern-matching))))))]]
              (wrap (` (: (~ (poly;gen-type new-env ->Eq g!type-fun g!vars :x:))
                          (~ base))))))
          ## Tuples
          (with-gensyms [g!type-fun]
            (do @
              [[g!vars members] (poly;tuple :x:)
               #let [new-env (poly;extend-env [:x: g!type-fun]
                                              (list;zip2 (|> g!vars list;size poly;type-var-indices) g!vars)
                                              env)]
               pattern-matching (mapM @
                                      (function [:member:]
                                        (do @
                                          [g!left (macro;gensym "g!left")
                                           g!right (macro;gensym "g!right")
                                           g!eq (Eq<?> new-env :member:)]
                                          (wrap [g!left g!right g!eq])))
                                      members)
               #let [.left. (` [(~@ (List/map product;left pattern-matching))])
                     .right. (` [(~@ (List/map (|>. product;right product;left) pattern-matching))])
                     base (function$ g!type-fun g!vars
                                     (` (function [(~ .left.) (~ .right.)]
                                          (and (~@ (List/map (function [[g!left g!right g!eq]]
                                                               (` ((~ g!eq) (~ g!left) (~ g!right))))
                                                             pattern-matching))))))]]
              (wrap (` (: (~ (poly;gen-type new-env ->Eq g!type-fun g!vars :x:))
                          (~ base))))))
          ## Type recursion
          (with-gensyms [g!rec]
            (do @
              [:non-rec: (poly;recursive :x:)
               #let [new-env (poly;extend-env [:x: g!rec] (list [:x: (` (;undefined))]) env)]
               .non-rec. (Eq<?> new-env :non-rec:)]
              (wrap (` (: (~ (poly;gen-type new-env ->Eq g!rec (list) :x:))
                          (eq;rec (;function [(~ g!rec)]
                                    (~ .non-rec.))))))))
          (poly;self env :x:)
          (poly;recursion env :x:)
          ## Type applications
          (do @
            [[:func: :args:] (poly;apply :x:)
             .func. (Eq<?> env :func:)
             .args. (mapM @ (Eq<?> env) :args:)]
            (wrap (` (: (~ (->Eq (type;to-ast :x:)))
                        ((~ .func.) (~@ .args.))))))
          ## Bound type-vars
          (poly;bound env :x:)
          ## If all else fails...
          (macro;fail (format "Cannot create Eq for: " (%type :x:)))
          ))))
