(.module:
  [lux #*
   [host (#+ import:)]
   ["@" target]
   [abstract
    [monad (#+ do)]]
   [control
    ["." try (#+ Try)]
    ["." exception (#+ exception:)]
    ["." io (#+ IO io)]
    [concurrency
     ["." promise (#+ Promise)]
     ["." stm]]
    [security
     ["!" capability (#+ capability:)]]]
   [data
    ["." text (#+ Char)
     ["%" format (#+ format)]]]])

(template [<name>]
  [(exception: #export (<name>)
     "")]

  [cannot_open]
  [cannot_close]
  )

(capability: #export (Can_Read ! o)
  (can_read [] (! (Try o))))

(capability: #export (Can_Write ! i)
  (can_write i (! (Try Any))))

(capability: #export (Can_Close !)
  (can_close [] (! (Try Any))))

(signature: #export (Console !)
  (: (Can_Read ! Char)
     read)
  (: (Can_Read ! Text)
     read_line)
  (: (Can_Write ! Text)
     write)
  (: (Can_Close !)
     close))

(def: #export (async console)
  (-> (Console IO) (Console Promise))
  (`` (structure (~~ (template [<capability> <forge>]
                       [(def: <capability>
                          (<forge>
                           (|>> (!.use (\ console <capability>)) promise.future)))]

                       [read ..can_read]
                       [read_line ..can_read]
                       [write ..can_write]
                       [close ..can_close])))))

(with_expansions [<jvm> (as_is (import: java/lang/String)

                               (import: java/io/Console
                                 ["#::."
                                  (readLine [] #io #try java/lang/String)])

                               (import: java/io/InputStream
                                 ["#::."
                                  (read [] #io #try int)])

                               (import: java/io/PrintStream
                                 ["#::."
                                  (print [java/lang/String] #io #try void)])

                               (import: java/lang/System
                                 ["#::."
                                  (#static console [] #io #? java/io/Console)
                                  (#static in java/io/InputStream)
                                  (#static out java/io/PrintStream)])

                               (def: #export default
                                 (IO (Try (Console IO)))
                                 (do io.monad
                                   [?jvm_console (java/lang/System::console)]
                                   (case ?jvm_console
                                     #.None
                                     (wrap (exception.throw ..cannot_open []))

                                     (#.Some jvm_console)
                                     (let [jvm_input (java/lang/System::in)
                                           jvm_output (java/lang/System::out)]
                                       (<| wrap
                                           exception.return
                                           (: (Console IO)) ## TODO: Remove ASAP
                                           (structure
                                            (def: read
                                              (..can_read
                                               (function (_ _)
                                                 (|> jvm_input
                                                     java/io/InputStream::read
                                                     (\ (try.with io.monad) map .nat)))))
                                            
                                            (def: read_line
                                              (..can_read
                                               (function (_ _)
                                                 (java/io/Console::readLine jvm_console))))
                                            
                                            (def: write
                                              (..can_write
                                               (function (_ message)
                                                 (java/io/PrintStream::print message jvm_output))))
                                            
                                            (def: close
                                              (..can_close
                                               (|>> (exception.throw ..cannot_close) wrap))))))))))]
  (for {@.old (as_is <jvm>)
        @.jvm (as_is <jvm>)}
       (as_is)))

(def: #export (write_line message console)
  (All [!] (-> Text (Console !) (! (Try Any))))
  (!.use (\ console write) [(format message text.new_line)]))

(signature: #export (Simulation s)
  (: (-> s (Try [s Char]))
     on_read)
  (: (-> s (Try [s Text]))
     on_read_line)
  (: (-> Text s (Try s))
     on_write)
  (: (-> s (Try s))
     on_close))

(def: #export (mock simulation init)
  (All [s] (-> (Simulation s) s (Console Promise)))
  (let [state (stm.var init)]
    (`` (structure
         (~~ (template [<method> <simulation>]
               [(def: <method>
                  (..can_read
                   (function (_ _)
                     (stm.commit
                      (do {! stm.monad}
                        [|state| (stm.read state)]
                        (case (\ simulation <simulation> |state|)
                          (#try.Success [|state| output])
                          (do !
                            [_ (stm.write |state| state)]
                            (wrap (#try.Success output)))
                          
                          (#try.Failure error)
                          (wrap (#try.Failure error))))))))]

               [read on_read]
               [read_line on_read_line]
               ))

         (def: write
           (..can_write
            (function (_ input)
              (stm.commit
               (do {! stm.monad}
                 [|state| (stm.read state)]
                 (case (\ simulation on_write input |state|)
                   (#try.Success |state|)
                   (do !
                     [_ (stm.write |state| state)]
                     (wrap (#try.Success [])))
                   
                   (#try.Failure error)
                   (wrap (#try.Failure error))))))))

         (def: close
           (..can_close
            (function (_ _)
              (stm.commit
               (do {! stm.monad}
                 [|state| (stm.read state)]
                 (case (\ simulation on_close |state|)
                   (#try.Success |state|)
                   (do !
                     [_ (stm.write |state| state)]
                     (wrap (#try.Success [])))
                   
                   (#try.Failure error)
                   (wrap (#try.Failure error))))))))
         ))))
