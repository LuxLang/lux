(.module:
  [lux #*
   ["@" target]
   [host (#+ import:)]
   [control
    ["." io (#+ IO)]]
   [data
    ["." text]
    [collection
     ["." dictionary (#+ Dictionary)]]]])

(type: #export Property
  Text)

(type: #export Environment
  (Dictionary Property Text))

## Do not trust the values of environment variables
## https://wiki.sei.cmu.edu/confluence/display/java/ENV02-J.+Do+not+trust+the+values+of+environment+variables

(with-expansions [<jvm> (as-is (import: java/lang/String)

                               (import: (java/util/Map$Entry k v)
                                 ["#::."
                                  (getKey [] k)
                                  (getValue [] v)])

                               (import: (java/util/Iterator a)
                                 ["#::."
                                  (hasNext [] boolean)
                                  (next [] a)])

                               (import: (java/util/Set a)
                                 ["#::."
                                  (iterator [] (java/util/Iterator a))])

                               (import: (java/util/Map k v)
                                 ["#::."
                                  (entrySet [] (java/util/Set (java/util/Map$Entry k v)))])

                               (import: java/lang/System
                                 ["#::."
                                  (#static getenv [] (java/util/Map java/lang/String java/lang/String))])

                               (def: (consume f iterator)
                                 (All [a b] (-> (-> a b) (java/util/Iterator a) (List b)))
                                 (if (java/util/Iterator::hasNext iterator)
                                   (#.Cons (f (java/util/Iterator::next iterator))
                                           (consume f iterator))
                                   #.Nil))

                               (def: (to-kv entry)
                                 (All [k v] (-> (java/util/Map$Entry k v) [k v]))
                                 [(java/util/Map$Entry::getKey entry)
                                  (java/util/Map$Entry::getValue entry)]))]
  (for {@.old (as-is <jvm>)
        @.jvm (as-is <jvm>)}))

(def: #export read
  (IO Environment)
  (with-expansions [<jvm> (as-is (io.io (|> (java/lang/System::getenv)
                                            java/util/Map::entrySet
                                            java/util/Set::iterator
                                            (..consume ..to-kv)
                                            (dictionary.from-list text.hash))))]
    (for {@.old <jvm>
          @.jvm <jvm>})))
