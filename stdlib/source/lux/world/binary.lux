(.module:
  [lux (#- i64)
   [control
    [monad (#+ do)]
    ["ex" exception (#+ exception:)]
    [equivalence (#+ Equivalence)]]
   [data
    ["." maybe]
    ["." error (#+ Error)]
    [number
     ["." i64]]
    [text
     format]
    [collection
     [array (#+ Array)]]]
   ["." host (#+ import:)]])

(exception: #export (index-out-of-bounds {size Nat} {index Nat})
  (ex.report ["Size" (%n size)]
             ["Index" (%n index)]))

(do-template [<name>]
  [(exception: #export (<name> {size Nat} {from Nat} {to Nat})
     (ex.report ["Size" (%n size)]
                ["From" (%n from)]
                ["To" (%n to)]))]

  [slice-out-of-bounds]
  [inverted-slice]
  )

(type: #export Binary (host.type (Array byte)))

(import: java/lang/System
  (#static arraycopy [Object int Object int int] #try void))

(import: java/util/Arrays
  (#static copyOfRange [(Array byte) int int] (Array byte))
  (#static equals [(Array byte) (Array byte)] boolean))

(def: byte-mask
  I64
  (|> i64.bits-per-byte i64.mask .i64))

(def: i64
  (-> (primitive "java.lang.Byte") I64)
  (|>> host.byte-to-long (:coerce I64) (i64.and ..byte-mask)))

(def: byte
  (-> (I64 Any) (primitive "java.lang.Byte"))
  (|>> .int host.long-to-byte))

(template: (!size binary)
  (host.array-length binary))

(def: #export size
  (-> Binary Nat)
  (|>> !size))

(def: #export (create size)
  (-> Nat Binary)
  (host.array byte size))

(def: #export (read/8 idx binary)
  (-> Nat Binary (Error I64))
  (if (n/< (..!size binary) idx)
    (#error.Success (..i64 (host.array-read idx binary)))
    (ex.throw index-out-of-bounds [(..!size binary) idx])))

(def: #export (read/16 idx binary)
  (-> Nat Binary (Error I64))
  (if (n/< (..!size binary) (n/+ 1 idx))
    (#error.Success ($_ i64.or
                        (i64.left-shift 8 (..i64 (host.array-read idx binary)))
                        (..i64 (host.array-read (n/+ 1 idx) binary))))
    (ex.throw index-out-of-bounds [(..!size binary) idx])))

(def: #export (read/32 idx binary)
  (-> Nat Binary (Error I64))
  (if (n/< (..!size binary) (n/+ 3 idx))
    (#error.Success ($_ i64.or
                        (i64.left-shift 24 (..i64 (host.array-read idx binary)))
                        (i64.left-shift 16 (..i64 (host.array-read (n/+ 1 idx) binary)))
                        (i64.left-shift 8 (..i64 (host.array-read (n/+ 2 idx) binary)))
                        (..i64 (host.array-read (n/+ 3 idx) binary))))
    (ex.throw index-out-of-bounds [(..!size binary) idx])))

(def: #export (read/64 idx binary)
  (-> Nat Binary (Error I64))
  (if (n/< (..!size binary) (n/+ 7 idx))
    (#error.Success ($_ i64.or
                        (i64.left-shift 56 (..i64 (host.array-read idx binary)))
                        (i64.left-shift 48 (..i64 (host.array-read (n/+ 1 idx) binary)))
                        (i64.left-shift 40 (..i64 (host.array-read (n/+ 2 idx) binary)))
                        (i64.left-shift 32 (..i64 (host.array-read (n/+ 3 idx) binary)))
                        (i64.left-shift 24 (..i64 (host.array-read (n/+ 4 idx) binary)))
                        (i64.left-shift 16 (..i64 (host.array-read (n/+ 5 idx) binary)))
                        (i64.left-shift 8 (..i64 (host.array-read (n/+ 6 idx) binary)))
                        (..i64 (host.array-read (n/+ 7 idx) binary))))
    (ex.throw index-out-of-bounds [(..!size binary) idx])))

(def: #export (write/8 idx value binary)
  (-> Nat (I64 Any) Binary (Error Binary))
  (if (n/< (..!size binary) idx)
    (exec (|> binary
              (host.array-write idx (..byte value)))
      (#error.Success binary))
    (ex.throw index-out-of-bounds [(..!size binary) idx])))

(def: #export (write/16 idx value binary)
  (-> Nat (I64 Any) Binary (Error Binary))
  (if (n/< (..!size binary) (n/+ 1 idx))
    (exec (|> binary
              (host.array-write idx (..byte (i64.logical-right-shift 8 value)))
              (host.array-write (n/+ 1 idx) (..byte value)))
      (#error.Success binary))
    (ex.throw index-out-of-bounds [(..!size binary) idx])))

(def: #export (write/32 idx value binary)
  (-> Nat (I64 Any) Binary (Error Binary))
  (if (n/< (..!size binary) (n/+ 3 idx))
    (exec (|> binary
              (host.array-write idx (..byte (i64.logical-right-shift 24 value)))
              (host.array-write (n/+ 1 idx) (..byte (i64.logical-right-shift 16 value)))
              (host.array-write (n/+ 2 idx) (..byte (i64.logical-right-shift 8 value)))
              (host.array-write (n/+ 3 idx) (..byte value)))
      (#error.Success binary))
    (ex.throw index-out-of-bounds [(..!size binary) idx])))

(def: #export (write/64 idx value binary)
  (-> Nat (I64 Any) Binary (Error Binary))
  (if (n/< (..!size binary) (n/+ 7 idx))
    (exec (|> binary
              (host.array-write idx (..byte (i64.logical-right-shift 56 value)))
              (host.array-write (n/+ 1 idx) (..byte (i64.logical-right-shift 48 value)))
              (host.array-write (n/+ 2 idx) (..byte (i64.logical-right-shift 40 value)))
              (host.array-write (n/+ 3 idx) (..byte (i64.logical-right-shift 32 value)))
              (host.array-write (n/+ 4 idx) (..byte (i64.logical-right-shift 24 value)))
              (host.array-write (n/+ 5 idx) (..byte (i64.logical-right-shift 16 value)))
              (host.array-write (n/+ 6 idx) (..byte (i64.logical-right-shift 8 value)))
              (host.array-write (n/+ 7 idx) (..byte value)))
      (#error.Success binary))
    (ex.throw index-out-of-bounds [(..!size binary) idx])))

(def: #export (slice from to binary)
  (-> Nat Nat Binary (Error Binary))
  (let [size (..!size binary)]
    (cond (not (n/<= to from))
          (ex.throw inverted-slice [size from to])

          (not (and (n/< size from)
                    (n/< size to)))
          (ex.throw slice-out-of-bounds [size from to])

          ## else
          (#error.Success (Arrays::copyOfRange binary (:coerce Int from) (:coerce Int (inc to)))))))

(def: #export (slice' from binary)
  (-> Nat Binary (Error Binary))
  (slice from (dec (..!size binary)) binary))

(structure: #export _ (Equivalence Binary)
  (def: (= reference sample)
    (Arrays::equals reference sample)))

(def: #export (copy bytes source-offset source target-offset target)
  (-> Nat Nat Binary Nat Binary (Error Binary))
  (do error.Monad<Error>
    [_ (System::arraycopy source (.int source-offset) target (.int target-offset) (.int bytes))]
    (wrap target)))
