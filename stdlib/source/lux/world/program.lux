(.module:
  [lux #*
   ["@" target]
   ["." host (#+ import:)]
   [abstract
    [monad (#+ do)]]
   [control
    ["." function]
    ["." io (#+ IO)]
    [concurrency
     ["." atom]
     ["." promise (#+ Promise)]]
    [parser
     ["." environment (#+ Environment)]]]
   [data
    ["." maybe]
    ["." text
     ["%" format (#+ format)]]
    [collection
     ["." array (#+ Array) ("#\." fold)]
     ["." dictionary (#+ Dictionary)]]]
   [math
    [number
     ["i" int]]]]
  [//
   [file (#+ Path)]
   [shell (#+ Exit)]])

(signature: #export (Program !)
  (: (-> Any (! Environment))
     environment)
  (: (-> Any (! Path))
     home)
  (: (-> Any (! Path))
     directory)
  (: (-> Exit (! Nothing))
     exit))

(def: #export (async program)
  (-> (Program IO) (Program Promise))
  (structure
   (def: environment
     (|>> (\ program environment) promise.future))
   (def: home
     (|>> (\ program home) promise.future))
   (def: directory
     (|>> (\ program directory) promise.future))
   (def: exit
     (|>> (\ program exit) promise.future))))

(def: #export (mock environment home directory)
  (-> Environment Path Path (Program IO))
  (let [@dead? (atom.atom false)]
    (structure
     (def: environment
       (function.constant (io.io environment)))
     (def: home
       (function.constant (io.io home)))
     (def: directory
       (function.constant (io.io directory)))
     (def: (exit code)
       (io.io (error! (%.int code)))))))

## Do not trust the values of environment variables
## https://wiki.sei.cmu.edu/confluence/display/java/ENV02-J.+Do+not+trust+the+values+of+environment+variables

(with_expansions [<jvm> (as_is (import: java/lang/String)

                               (import: (java/util/Map$Entry k v)
                                 ["#::."
                                  (getKey [] k)
                                  (getValue [] v)])

                               (import: (java/util/Iterator a)
                                 ["#::."
                                  (hasNext [] boolean)
                                  (next [] a)])

                               (import: (java/util/Set a)
                                 ["#::."
                                  (iterator [] (java/util/Iterator a))])

                               (import: (java/util/Map k v)
                                 ["#::."
                                  (entrySet [] (java/util/Set (java/util/Map$Entry k v)))])

                               (import: java/lang/System
                                 ["#::."
                                  (#static getenv [] (java/util/Map java/lang/String java/lang/String))
                                  (#static getProperty [java/lang/String] #? java/lang/String)
                                  (#static exit [int] #io void)])

                               (def: (jvm\\consume f iterator)
                                 (All [a b] (-> (-> a b) (java/util/Iterator a) (List b)))
                                 (if (java/util/Iterator::hasNext iterator)
                                   (#.Cons (f (java/util/Iterator::next iterator))
                                           (jvm\\consume f iterator))
                                   #.Nil))

                               (def: (jvm\\to_kv entry)
                                 (All [k v] (-> (java/util/Map$Entry k v) [k v]))
                                 [(java/util/Map$Entry::getKey entry)
                                  (java/util/Map$Entry::getValue entry)])

                               (def: jvm\\environment
                                 (IO Environment)
                                 (with_expansions [<jvm> (as_is (io.io (|> (java/lang/System::getenv)
                                                                           java/util/Map::entrySet
                                                                           java/util/Set::iterator
                                                                           (..jvm\\consume ..jvm\\to_kv)
                                                                           (dictionary.from_list text.hash))))]
                                   (for {@.old <jvm>
                                         @.jvm <jvm>})))
                               )]
  (for {@.old (as_is <jvm>)
        @.jvm (as_is <jvm>)
        @.js (as_is (def: default_exit!
                      (-> Exit (IO Nothing))
                      (|>> %.int error! io.io))

                    (import: NodeJs_Process
                      (exit [host.Number] #io Nothing)
                      (cwd [] #io Path))

                    (def: (exit_node_js! code)
                      (-> Exit (IO Nothing))
                      (case (host.constant ..NodeJs_Process [process])
                        (#.Some process)
                        (NodeJs_Process::exit (i.frac code) process)
                        
                        #.None
                        (..default_exit! code)))

                    (import: Browser_Window
                      (close [] Nothing))

                    (import: Browser_Location
                      (reload [] Nothing))

                    (def: (exit_browser! code)
                      (-> Exit (IO Nothing))
                      (case [(host.constant ..Browser_Window [window])
                             (host.constant ..Browser_Location [location])]
                        [(#.Some window) (#.Some location)]
                        (exec
                          (Browser_Window::close [] window)
                          (Browser_Location::reload [] location)
                          (..default_exit! code))

                        [(#.Some window) #.None]
                        (exec
                          (Browser_Window::close [] window)
                          (..default_exit! code))

                        [#.None (#.Some location)]
                        (exec
                          (Browser_Location::reload [] location)
                          (..default_exit! code))
                        
                        [#.None #.None]
                        (..default_exit! code)))

                    (import: Object
                      (#static entries [Object] (Array (Array host.String))))

                    (import: NodeJs_OS
                      (homedir [] #io Path))

                    (import: (require [host.String] Any)))}
       (as_is)))

(structure: #export default
  (Program IO)

  (def: (environment _)
    (with_expansions [<jvm> ..jvm\\environment]
      (for {@.old <jvm>
            @.jvm <jvm>
            @.js (io.io (if host.on_node_js?
                          (case (host.constant Object [process env])
                            (#.Some process/env)
                            (array\fold (function (_ entry environment)
                                          (<| (maybe.default environment)
                                              (do maybe.monad
                                                [variable (array.read 0 entry)
                                                 value (array.read 1 entry)]
                                                (wrap (dictionary.put variable value environment)))))
                                        environment.empty
                                        (Object::entries [process/env]))

                            #.None
                            (undefined))
                          environment.empty))}
           ## TODO: Replace dummy implementation.
           (io.io environment.empty))))
  
  (def: (home _)
    (with_expansions [<default> (io.io "~")
                      <jvm> (io.io (maybe.default "" (java/lang/System::getProperty "user.home")))]
      (for {@.old <jvm>
            @.jvm <jvm>
            @.js (if host.on_node_js?
                   (|> (..require "os")
                       (:coerce NodeJs_OS)
                       (NodeJs_OS::homedir []))
                   <default>)}
           ## TODO: Replace dummy implementation.
           <default>)))

  (def: (directory _)
    (with_expansions [<default> (io.io ".")
                      <jvm> (io.io (maybe.default "" (java/lang/System::getProperty "user.dir")))]
      (for {@.old <jvm>
            @.jvm <jvm>
            @.js (if host.on_node_js?
                   (case (host.constant ..NodeJs_Process [process])
                     (#.Some process)
                     (NodeJs_Process::cwd [] process)
                     
                     #.None
                     <default>)
                   <default>)}
           ## TODO: Replace dummy implementation.
           <default>)))
  
  (def: (exit code)
    (with_expansions [<jvm> (do io.monad
                              [_ (java/lang/System::exit code)]
                              (wrap (undefined)))]
      (for {@.old <jvm>
            @.jvm <jvm>
            @.js (cond host.on_node_js?
                       (..exit_node_js! code)

                       host.on_browser?
                       (..exit_browser! code)

                       ## else
                       (..default_exit! code))}))))
