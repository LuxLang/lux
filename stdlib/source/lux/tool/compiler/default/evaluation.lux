(.module:
  [lux #*
   [abstract
    [monad (#+ do)]]
   [control
    ["." try]]
   [data
    [text
     ["%" format (#+ format)]]]]
  [///
   ["." phase
    [macro (#+ Expander)]
    [".P" analysis
     ["." type]]
    [".P" synthesis]
    ["." generation]
    [//
     [analysis (#+ Operation)]
     ["." synthesis]]]])

(type: #export Eval
  (-> Nat Type Code (Operation Any)))

(def: #export (evaluator expander synthesis-state generation-state generate)
  (All [anchor expression directive]
    (-> Expander
        synthesis.State+
        (generation.State+ anchor expression directive)
        (generation.Phase anchor expression directive)
        Eval))
  (let [analyze (analysisP.phase expander)]
    (function (eval count type exprC)
      (do phase.monad
        [exprA (type.with-type type
                 (analyze exprC))]
        (phase.lift (do try.monad
                      [exprS (|> exprA synthesisP.phase (phase.run synthesis-state))]
                      (phase.run generation-state
                                 (do phase.monad
                                   [exprO (generate exprS)]
                                   (generation.evaluate! (format "eval" (%.nat count)) exprO)))))))))
