(.module:
  [lux #*
   [control
    [monad (#+ do)]]
   [data
    ["." error]
    [text
     format]]]
  [///
   ["." phase
    [macro (#+ Expander)]
    [".P" analysis
     ["." type]]
    [".P" synthesis]
    ["." translation]
    [//
     [analysis (#+ Operation)]
     ["." synthesis]]]])

(type: #export Eval
  (-> Nat Type Code (Operation Any)))

(def: #export (evaluator expander synthesis-state translation-state translate)
  (All [anchor expression statement]
    (-> Expander
        synthesis.State+
        (translation.State+ anchor expression statement)
        (translation.Phase anchor expression statement)
        Eval))
  (let [analyze (analysisP.phase expander)]
    (function (eval count type exprC)
      (do phase.monad
        [exprA (type.with-type type
                 (analyze exprC))]
        (phase.lift (do error.monad
                      [exprS (|> exprA synthesisP.phase (phase.run synthesis-state))]
                      (phase.run translation-state
                                 (do phase.monad
                                   [exprO (translate exprS)]
                                   (translation.evaluate! (format "eval" (%n count)) exprO)))))))))
