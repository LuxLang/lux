(.module:
  [lux (#- Module Code)
   ["@" target]
   [abstract
    [monad (#+ Monad do)]]
   [control
    ["." try (#+ Try)]
    ["ex" exception (#+ Exception exception:)]
    [security
     ["!" capability]]]
   [data
    ["." text ("#;." hash)
     ["%" format (#+ format)]
     ["." encoding]]]
   [world
    ["." file (#+ Path File)]
    [binary (#+ Binary)]]
   [type (#+ :share)]]
  ["." // (#+ Context Code)
   ["#/" // #_
    [archive
     [descriptor (#+ Module)]]
    ["#/" // (#+ Input)]]])

(template [<name>]
  [(exception: #export (<name> {module Module})
     (ex.report ["Module" module]))]

  [cannot-find-module]
  [cannot-read-module]
  )

(type: #export Extension Text)

(def: lux-extension
  Extension
  ".lux")

(def: #export (path system context module)
  (All [m] (-> (file.System m) Context Module Path))
  (|> module
      (//.sanitize system)
      (format context (:: system separator))))

(def: (find-source-file monad system contexts module extension)
  (All [!]
    (-> (Monad !) (file.System !) (List Context) Module Extension
        (! (Try [Path (File !)]))))
  (case contexts
    #.Nil
    (:: monad wrap (ex.throw ..cannot-find-module [module]))

    (#.Cons context contexts')
    (do monad
      [#let [path (format (..path system context module) extension)]
       file (!.use (:: system file) path)]
      (case file
        (#try.Success file)
        (wrap (#try.Success [path file]))

        (#try.Failure _)
        (find-source-file monad system contexts' module extension)))))

(def: #export (find-any-source-file monad system contexts partial-host-extension module)
  (All [!]
    (-> (Monad !) (file.System !) (List Context) Text Module
        (! (Try [Path (File !)]))))
  (let [full-host-extension (format partial-host-extension lux-extension)]
    (do monad
      [outcome (find-source-file monad system contexts module full-host-extension)]
      (case outcome
        (#try.Success output)
        (wrap outcome)

        (#try.Failure _)
        (find-source-file monad system contexts module ..lux-extension)))))

(def: #export (read monad system contexts partial-host-extension module)
  (All [!]
    (-> (Monad !) (file.System !) (List Context) Text Module
        (! (Try Input))))
  (do (try.with monad)
    [## TODO: Get rid of both ":share"s ASAP
     path,file (:share [!]
                       {(Monad !)
                        monad}
                       {(! (Try [Path (File !)]))
                        (find-any-source-file monad system contexts partial-host-extension module)})
     #let [[path file] (:share [!]
                               {(Monad !)
                                monad}
                               {[Path (File !)]
                                path,file})]
     binary (!.use (:: file content) [])]
    (case (encoding.from-utf8 binary)
      (#try.Success code)
      (wrap {#////.module module
             #////.file path
             #////.hash (text;hash code)
             #////.code code})
      
      (#try.Failure _)
      (:: monad wrap (ex.throw ..cannot-read-module [module])))))
