(.module:
  [lux (#- Module Code)
   [control
    [monad (#+ Monad do)]
    ["ex" exception (#+ Exception exception:)]
    [security
     ["!" capability]]]
   [data
    ["." error (#+ Error)]
    ["." text ("#/." hash)
     format
     ["." encoding]]]
   [world
    ["." file (#+ Path File)]
    [binary (#+ Binary)]]
   [type (#+ :share)]]
  ["." // (#+ Context Code)
   [//
    [archive
     [descriptor (#+ Module)]]
    ["//." // (#+ Input)
     ["." host]]]])

(do-template [<name>]
  [(exception: #export (<name> {module Module})
     (ex.report ["Module" module]))]

  [cannot-find-module]
  [cannot-read-module]
  )

(type: #export Extension Text)

(def: lux-extension
  Extension
  ".lux")

(def: partial-host-extension
  Extension
  (`` (for {(~~ (static host.common-lisp)) ".cl"
            (~~ (static host.js))          ".js"
            (~~ (static host.jvm))         ".jvm"
            (~~ (static host.lua))         ".lua"
            (~~ (static host.php))         ".php"
            (~~ (static host.python))      ".py"
            (~~ (static host.r))           ".r"
            (~~ (static host.ruby))        ".rb"
            (~~ (static host.scheme))      ".scm"})))

(def: full-host-extension
  Extension
  (format partial-host-extension lux-extension))

(def: #export (path system context module)
  (All [m] (-> (file.System m) Context Module Path))
  (|> module
      (//.sanitize system)
      (format context (:: system separator))))

(def: (find-source-file monad system contexts module extension)
  (All [!]
    (-> (Monad !) (file.System !) (List Context) Module Extension
        (! (Error [Path (File !)]))))
  (case contexts
    #.Nil
    (:: monad wrap (ex.throw ..cannot-find-module [module]))

    (#.Cons context contexts')
    (do monad
      [#let [path (format (..path system context module) extension)]
       file (!.use (:: system file) path)]
      (case file
        (#error.Success file)
        (wrap (#error.Success [path file]))

        (#error.Failure error)
        (find-source-file monad system contexts' module extension)))))

(def: #export (find-any-source-file monad system contexts module)
  (All [!]
    (-> (Monad !) (file.System !) (List Context) Module
        (! (Error [Path (File !)]))))
  (do monad
    [outcome (find-source-file monad system contexts module ..full-host-extension)]
    (case outcome
      (#error.Success output)
      (wrap outcome)

      (#error.Failure error)
      (find-source-file monad system contexts module ..lux-extension))))

(def: #export (read monad system contexts module)
  (All [!]
    (-> (Monad !) (file.System !) (List Context) Module
        (! (Error Input))))
  (do (error.with-error monad)
    [## TODO: Get rid of both ":share"s ASAP
     path,file (:share [!]
                       {(Monad !)
                        monad}
                       {(! (Error [Path (File !)]))
                        (find-any-source-file monad system contexts module)})
     #let [[path file] (:share [!]
                               {(Monad !)
                                monad}
                               {[Path (File !)]
                                path,file})]
     binary (!.use (:: file content) [])]
    (case (encoding.from-utf8 binary)
      (#error.Success code)
      (wrap {#////.module module
             #////.file path
             #////.hash (text/hash code)
             #////.code code})
      
      (#error.Failure _)
      (:: monad wrap (ex.throw ..cannot-read-module [module])))))
