(.module:
  [lux (#- Module)
   ["@" target (#+ Host)]
   [abstract
    [predicate (#+ Predicate)]
    ["." monad (#+ do)]]
   [control
    ["." try (#+ Try)]
    ["." exception (#+ exception:)]
    [concurrency
     ["." promise (#+ Promise) ("#\." monad)]]
    [security
     ["!" capability (#+ capability:)]]
    ["<>" parser
     ["<b>" binary (#+ Parser)]]]
   [data
    [binary (#+ Binary)]
    ["." product]
    ["." text ("#\." equivalence)
     ["%" format (#+ format)]]
    [collection
     ["." list ("#\." functor fold)]
     ["." dictionary (#+ Dictionary)]
     ["." row (#+ Row)]
     ["." set]]
    [number
     ["n" nat]]]
   [world
    ["." file (#+ Path File Directory)]]]
  [program
   [compositor
    [import (#+ Import)]
    ["." static (#+ Static)]]]
  ["." // (#+ Context)
   ["#." context]
   ["/#" //
    ["." archive (#+ Archive)
     ["." artifact (#+ Artifact)]
     ["." descriptor (#+ Module Descriptor)]
     ["." document (#+ Document)]]
    [cache
     ["." dependency]]
    ["/#" // (#+ Input)
     [language
      ["$" lux
       ["." version]
       ["." analysis]
       ["." synthesis]
       ["." generation]
       ["." directive]
       ["#/." program]]]]]])

(exception: #export (cannot-prepare {archive Path}
                                    {module-id archive.ID}
                                    {error Text})
  (exception.report
   ["Archive" archive]
   ["Module ID" (%.nat module-id)]
   ["Error" error]))

(def: (archive system static)
  (All [!] (-> (file.System !) Static Path))
  (format (get@ #static.target static)
          (\ system separator)
          (get@ #static.host static)))

(def: (unversioned-lux-archive system static)
  (All [!] (-> (file.System !) Static Path))
  (format (..archive system static)
          (\ system separator)
          //.lux-context))

(def: (versioned-lux-archive system static)
  (All [!] (-> (file.System !) Static Path))
  (format (..unversioned-lux-archive system static)
          (\ system separator)
          (%.nat version.version)))

(def: (module system static module-id)
  (All [!] (-> (file.System !) Static archive.ID Path))
  (format (..versioned-lux-archive system static)
          (\ system separator)
          (%.nat module-id)))

(def: #export (artifact system static module-id name)
  (All [!] (-> (file.System !) Static archive.ID Text Path))
  (format (..module system static module-id)
          (\ system separator)
          name
          (get@ #static.artifact-extension static)))

(def: #export (prepare system static module-id)
  (-> (file.System Promise) Static archive.ID (Promise (Try Any)))
  (do {! promise.monad}
    [#let [module (..module system static module-id)]
     module-exists? (file.exists? promise.monad system module)]
    (if module-exists?
      (wrap (#try.Success []))
      (do !
        [_ (file.get-directory ! system (..unversioned-lux-archive system static))
         _ (file.get-directory ! system (..versioned-lux-archive system static))
         outcome (!.use (\ system create-directory) module)]
        (case outcome
          (#try.Success output)
          (wrap (#try.Success []))

          (#try.Failure error)
          (wrap (exception.throw ..cannot-prepare [(..archive system static)
                                                   module-id
                                                   error])))))))

(def: #export (write system static module-id name content)
  (-> (file.System Promise) Static archive.ID Text Binary (Promise (Try Any)))
  (do (try.with promise.monad)
    [artifact (: (Promise (Try (File Promise)))
                 (file.get-file promise.monad system
                                (..artifact system static module-id name)))]
    (!.use (\ artifact over-write) content)))

(def: #export (enable system static)
  (-> (file.System Promise) Static (Promise (Try Any)))
  (do (try.with promise.monad)
    [_ (: (Promise (Try (Directory Promise)))
          (file.get-directory promise.monad system (get@ #static.target static)))
     _ (: (Promise (Try (Directory Promise)))
          (file.get-directory promise.monad system (..archive system static)))]
    (wrap [])))

(def: (general-descriptor system static)
  (-> (file.System Promise) Static Path)
  (format (..archive system static)
          (\ system separator)
          "general-descriptor"))

(def: #export (freeze system static archive)
  (-> (file.System Promise) Static Archive (Promise (Try Any)))
  (do (try.with promise.monad)
    [file (: (Promise (Try (File Promise)))
             (file.get-file promise.monad system (..general-descriptor system static)))]
    (!.use (\ file over-write) (archive.export ///.version archive))))

(def: module-descriptor-file
  "module-descriptor")

(def: (module-descriptor system static module-id)
  (-> (file.System Promise) Static archive.ID Path)
  (format (..module system static module-id)
          (\ system separator)
          ..module-descriptor-file))

(def: #export (cache system static module-id content)
  (-> (file.System Promise) Static archive.ID Binary (Promise (Try Any)))
  (do (try.with promise.monad)
    [file (: (Promise (Try (File Promise)))
             (file.get-file promise.monad system
                            (..module-descriptor system static module-id)))]
    (!.use (\ file over-write) content)))

(def: (read-module-descriptor system static module-id)
  (-> (file.System Promise) Static archive.ID (Promise (Try Binary)))
  (do (try.with promise.monad)
    [file (: (Promise (Try (File Promise)))
             (file.get-file promise.monad system
                            (..module-descriptor system static module-id)))]
    (!.use (\ file content) [])))

(def: parser
  (Parser [Descriptor (Document .Module)])
  (<>.and descriptor.parser
          (document.parser $.parser)))

(def: (fresh-analysis-state host)
  (-> Host .Lux)
  (analysis.state (analysis.info version.version host)))

(def: (analysis-state host archive)
  (-> Host Archive (Try .Lux))
  (do {! try.monad}
    [modules (: (Try (List [Module .Module]))
                (monad.map ! (function (_ module)
                               (do !
                                 [[descriptor document] (archive.find module archive)
                                  content (document.read $.key document)]
                                 (wrap [module content])))
                           (archive.archived archive)))]
    (wrap (set@ #.modules modules (fresh-analysis-state host)))))

(def: (cached-artifacts system static module-id)
  (-> (file.System Promise) Static archive.ID (Promise (Try (Dictionary Text Binary))))
  (do {! (try.with promise.monad)}
    [module-dir (!.use (\ system directory) (..module system static module-id))
     cached-files (!.use (\ module-dir files) [])]
    (|> cached-files
        (list\map (function (_ file)
                    [(!.use (\ file name) [])
                     (!.use (\ file path) [])]))
        (list.filter (|>> product.left (text\= ..module-descriptor-file) not))
        (monad.map ! (function (_ [name path])
                       (do !
                         [file (: (Promise (Try (File Promise)))
                                  (!.use (\ system file) path))
                          data (: (Promise (Try Binary))
                                  (!.use (\ file content) []))]
                         (wrap [name data]))))
        (\ ! map (dictionary.from-list text.hash)))))

(type: Definitions (Dictionary Text Any))
(type: Analysers (Dictionary Text analysis.Handler))
(type: Synthesizers (Dictionary Text synthesis.Handler))
(type: Generators (Dictionary Text generation.Handler))
(type: Directives (Dictionary Text directive.Handler))

(type: Bundles
  [Analysers
   Synthesizers
   Generators
   Directives])

(def: empty-bundles
  Bundles
  [(dictionary.new text.hash)
   (dictionary.new text.hash)
   (dictionary.new text.hash)
   (dictionary.new text.hash)])

(def: (loaded-document extension host module-id expected actual document)
  (All [expression directive]
    (-> Text (generation.Host expression directive) archive.ID (Row Artifact) (Dictionary Text Binary) (Document .Module)
        (Try [(Document .Module) Bundles])))
  (do {! try.monad}
    [[definitions bundles] (: (Try [Definitions Bundles])
                              (loop [input (row.to-list expected)
                                     definitions (: Definitions
                                                    (dictionary.new text.hash))
                                     bundles ..empty-bundles]
                                (let [[analysers synthesizers generators directives] bundles]
                                  (case input
                                    (#.Cons [[artifact-id artifact-category] input'])
                                    (case (do !
                                            [data (try.from-maybe (dictionary.get (format (%.nat artifact-id) extension) actual))
                                             #let [context [module-id artifact-id]
                                                   directive (\ host ingest context data)]]
                                            (case artifact-category
                                              #artifact.Anonymous
                                              (do !
                                                [_ (\ host re-learn context directive)]
                                                (wrap [definitions
                                                       [analysers
                                                        synthesizers
                                                        generators
                                                        directives]]))
                                              
                                              (#artifact.Definition name)
                                              (if (text\= $/program.name name)
                                                (wrap [definitions
                                                       [analysers
                                                        synthesizers
                                                        generators
                                                        directives]])
                                                (do !
                                                  [value (\ host re-load context directive)]
                                                  (wrap [(dictionary.put name value definitions)
                                                         [analysers
                                                          synthesizers
                                                          generators
                                                          directives]])))

                                              (#artifact.Analyser extension)
                                              (do !
                                                [value (\ host re-load context directive)]
                                                (wrap [definitions
                                                       [(dictionary.put extension (:coerce analysis.Handler value) analysers)
                                                        synthesizers
                                                        generators
                                                        directives]]))

                                              (#artifact.Synthesizer extension)
                                              (do !
                                                [value (\ host re-load context directive)]
                                                (wrap [definitions
                                                       [analysers
                                                        (dictionary.put extension (:coerce synthesis.Handler value) synthesizers)
                                                        generators
                                                        directives]]))

                                              (#artifact.Generator extension)
                                              (do !
                                                [value (\ host re-load context directive)]
                                                (wrap [definitions
                                                       [analysers
                                                        synthesizers
                                                        (dictionary.put extension (:coerce generation.Handler value) generators)
                                                        directives]]))

                                              (#artifact.Directive extension)
                                              (do !
                                                [value (\ host re-load context directive)]
                                                (wrap [definitions
                                                       [analysers
                                                        synthesizers
                                                        generators
                                                        (dictionary.put extension (:coerce directive.Handler value) directives)]]))))
                                      (#try.Success [definitions' bundles'])
                                      (recur input' definitions' bundles')

                                      failure
                                      failure)
                                    
                                    #.None
                                    (#try.Success [definitions bundles])))))
     content (document.read $.key document)
     definitions (monad.map ! (function (_ [def-name def-global])
                                (case def-global
                                  (#.Alias alias)
                                  (wrap [def-name (#.Alias alias)])
                                  
                                  (#.Definition [exported? type annotations _])
                                  (do !
                                    [value (try.from-maybe (dictionary.get def-name definitions))]
                                    (wrap [def-name (#.Definition [exported? type annotations value])]))))
                            (get@ #.definitions content))]
    (wrap [(document.write $.key (set@ #.definitions definitions content))
           bundles])))

(def: (load-definitions system static module-id host-environment [descriptor document])
  (All [expression directive]
    (-> (file.System Promise) Static archive.ID (generation.Host expression directive)
        [Descriptor (Document .Module)]
        (Promise (Try [[Descriptor (Document .Module)]
                       Bundles]))))
  (do (try.with promise.monad)
    [actual (cached-artifacts system static module-id)
     #let [expected (|> descriptor (get@ #descriptor.registry) artifact.artifacts)]
     [document bundles] (promise\wrap (loaded-document (get@ #static.artifact-extension static) host-environment module-id expected actual document))]
    (wrap [[descriptor document] bundles])))

(def: (purge! system static [module-name module-id])
  (-> (file.System Promise) Static [Module archive.ID] (Promise (Try Any)))
  (do {! (try.with promise.monad)}
    [cache (!.use (\ system directory) [(..module system static module-id)])
     artifacts (!.use (\ cache files) [])
     _ (monad.map ! (function (_ artifact)
                      (!.use (\ artifact delete) []))
                  artifacts)]
    (!.use (\ cache discard) [])))

(def: (valid-cache? expected actual)
  (-> Descriptor Input Bit)
  (and (text\= (get@ #descriptor.name expected)
               (get@ #////.module actual))
       (text\= (get@ #descriptor.file expected)
               (get@ #////.file actual))
       (n.= (get@ #descriptor.hash expected)
            (get@ #////.hash actual))))

(type: Purge
  (Dictionary Module archive.ID))

(def: initial-purge
  (-> (List [Bit [Module [archive.ID [Descriptor (Document .Module)]]]])
      Purge)
  (|>> (list.all (function (_ [valid-cache? [module-name [module-id _]]])
                   (if valid-cache?
                     #.None
                     (#.Some [module-name module-id]))))
       (dictionary.from-list text.hash)))

(def: (full-purge caches load-order)
  (-> (List [Bit [Module [archive.ID [Descriptor (Document .Module)]]]])
      dependency.Order
      Purge)
  (list\fold (function (_ [module-name [module-id [descriptor document]]] purge)
               (let [purged? (: (Predicate Module)
                                (function (_ module)
                                  (dictionary.contains? module purge)))]
                 (if (purged? module-name)
                   purge
                   (if (|> descriptor
                           (get@ #descriptor.references)
                           set.to-list
                           (list.any? purged?))
                     (dictionary.put module-name module-id purge)
                     purge))))
             (..initial-purge caches)
             load-order))

(def: (load-every-reserved-module host-environment system static import contexts archive)
  (All [expression directive]
    (-> (generation.Host expression directive) (file.System Promise) Static Import (List Context) Archive
        (Promise (Try [Archive .Lux Bundles]))))
  (do {! (try.with promise.monad)}
    [pre-loaded-caches (|> archive
                           archive.reservations
                           (monad.map ! (function (_ [module-name module-id])
                                          (do !
                                            [data (..read-module-descriptor system static module-id)
                                             [descriptor document] (promise\wrap (<b>.run ..parser data))]
                                            (if (text\= archive.runtime-module module-name)
                                              (wrap [true
                                                     [module-name [module-id [descriptor document]]]])
                                              (do !
                                                [input (//context.read system import contexts (get@ #static.host-module-extension static) module-name)]
                                                (wrap [(..valid-cache? descriptor input)
                                                       [module-name [module-id [descriptor document]]]])))))))
     load-order (|> pre-loaded-caches
                    (list\map product.right)
                    (monad.fold try.monad
                                (function (_ [module [module-id descriptor,document]] archive)
                                  (archive.add module descriptor,document archive))
                                archive)
                    (\ try.monad map (dependency.load-order $.key))
                    (\ try.monad join)
                    promise\wrap)
     #let [purge (..full-purge pre-loaded-caches load-order)]
     _ (|> purge
           dictionary.entries
           (monad.map ! (..purge! system static)))
     loaded-caches (|> load-order
                       (list.filter (function (_ [module-name [module-id [descriptor document]]])
                                      (not (dictionary.contains? module-name purge))))
                       (monad.map ! (function (_ [module-name [module-id descriptor,document]])
                                      (do !
                                        [[descriptor,document bundles] (..load-definitions system static module-id host-environment descriptor,document)]
                                        (wrap [[module-name descriptor,document]
                                               bundles])))))]
    (promise\wrap
     (do {! try.monad}
       [archive (monad.fold !
                            (function (_ [[module descriptor,document] _bundle] archive)
                              (archive.add module descriptor,document archive))
                            archive
                            loaded-caches)
        analysis-state (..analysis-state (get@ #static.host static) archive)]
       (wrap [archive
              analysis-state
              (list\fold (function (_ [_ [+analysers +synthesizers +generators +directives]]
                                      [analysers synthesizers generators directives])
                           [(dictionary.merge +analysers analysers)
                            (dictionary.merge +synthesizers synthesizers)
                            (dictionary.merge +generators generators)
                            (dictionary.merge +directives directives)])
                         ..empty-bundles
                         loaded-caches)])))))

(def: #export (thaw host-environment system static import contexts)
  (All [expression directive]
    (-> (generation.Host expression directive) (file.System Promise) Static Import (List Context)
        (Promise (Try [Archive .Lux Bundles]))))
  (do promise.monad
    [file (!.use (\ system file) (..general-descriptor system static))]
    (case file
      (#try.Success file)
      (do (try.with promise.monad)
        [binary (!.use (\ file content) [])
         archive (promise\wrap (archive.import ///.version binary))]
        (..load-every-reserved-module host-environment system static import contexts archive))
      
      (#try.Failure error)
      (wrap (#try.Success [archive.empty
                           (fresh-analysis-state (get@ #static.host static))
                           ..empty-bundles])))))
