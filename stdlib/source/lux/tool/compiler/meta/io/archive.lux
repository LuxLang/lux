(.module:
  [lux (#- Module)
   ["@" target (#+ Host)]
   [abstract
    [monad (#+ do)]]
   [control
    ["." try (#+ Try)]
    ["." exception (#+ exception:)]
    [concurrency
     ["." promise (#+ Promise)]]
    [security
     ["!" capability (#+ capability:)]]]
   [data
    [binary (#+ Binary)]
    ["." text
     ["%" format (#+ format)]]]
   [world
    ["." file (#+ Path File System)]]]
  ["." // (#+ Module)])

(exception: #export (cannot-prepare {archive Path}
                                    {module Module}
                                    {error Text})
  (exception.report
   ["Archive" archive]
   ["Module" module]
   ["Error" error]))

(def: #export (archive system host root)
  (-> (System Promise) Host Path Path)
  (format root (:: system separator) host))

(def: #export (document system host root module)
  (-> (System Promise) Host Path Module Path)
  (format (..archive system host root)
          (:: system separator)
          (//.sanitize system module)))

(def: #export (artifact system host root module name)
  (-> (System Promise) Host Path Module Text Path)
  (format (document system host root module)
          (:: system separator)
          (//.sanitize system name)))

(def: #export (prepare system host root module)
  (-> (System Promise) Host Path Module (Promise (Try Any)))
  (do promise.monad
    [#let [document (..document system host root module)]
     document-exists? (file.exists? promise.monad system document)]
    (if document-exists?
      (wrap (#try.Success []))
      (do @
        [outcome (!.use (:: system create-directory) document)]
        (case outcome
          (#try.Success output)
          (wrap (#try.Success []))

          (#try.Failure error)
          (wrap (exception.throw ..cannot-prepare [(..archive system host root)
                                                   module
                                                   error])))))))

(def: #export (write system host root module name content)
  (-> (System Promise) Host Path Module Text Binary (Promise (Try Any)))
  (do (try.with promise.monad)
    [artifact (: (Promise (Try (File Promise)))
                 (file.get-file promise.monad system
                                (..artifact system host root module name)))]
    (!.use (:: artifact over-write) content)))

(def: #export (module system host root document)
  (-> (System Promise) Host Path Path (Maybe Module))
  (case (text.split-with (..archive system host root) document)
    (#.Some ["" post])
    (let [raw (text.replace-all (:: system separator) "/" post)]
      (if (text.starts-with? "/" raw)
        (text.clip' 1 raw)
        (#.Some raw)))

    _
    #.None))
