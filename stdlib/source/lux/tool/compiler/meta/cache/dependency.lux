(.module:
  [lux (#- Module)
   [data
    ["." text]
    [collection
     ["." list ("#@." functor fold)]
     ["." dictionary (#+ Dictionary)]]]]
  [///io (#+ Module)]
  [///archive (#+ Archive)])

(type: #export Graph
  (Dictionary Module (List Module)))

(def: #export empty
  Graph
  (dictionary.new text.hash))

(def: #export (add to from)
  (-> Module Module Graph Graph)
  (|>> (dictionary.update~ from (list) (|>> (#.Cons to)))
       (dictionary.update~ to (list) id)))

(def: dependents
  (-> Module Graph (Maybe (List Module)))
  dictionary.get)

(def: #export (remove module dependency)
  (-> Module Graph Graph)
  (case (dependents module dependency)
    (#.Some dependents)
    (list@fold remove (dictionary.remove module dependency) dependents)

    #.None
    dependency))

(type: #export Dependency
  {#module Module
   #imports (List Module)})

(def: #export (dependency [module imports])
  (-> Dependency Graph)
  (list@fold (..add module) ..empty imports))

(def: #export graph
  (-> (List Dependency) Graph)
  (|>> (list@map ..dependency)
       (list@fold dictionary.merge empty)))

(def: #export (prune archive graph)
  (-> Archive Graph Graph)
  (list@fold (function (_ module graph)
               (if (dictionary.contains? module archive)
                 graph
                 (..remove module graph)))
             graph
             (dictionary.keys graph)))
