(.module:
  [lux (#- Module)
   [control
    ["ex" exception (#+ exception:)]
    ["." equivalence (#+ Equivalence)]
    ["." monad (#+ do)]]
   [data
    ["." error (#+ Error)]
    ["." name]
    ["." text
     format]
    [collection
     ["." dictionary (#+ Dictionary)]]]
   [type
    abstract]
   [world
    [file (#+ File)]]]
  [///
   [default (#+ Version)]]
  [/
   ["." signature (#+ Signature)]
   ["." key (#+ Key)]
   ["." descriptor (#+ Module Descriptor)]
   ["." document (#+ Document)]])

## Archive
(exception: #export (unknown-document {name Module})
  (ex.report ["Module" name]))

(exception: #export (cannot-replace-document {name Module}
                                             {old (Document Any)}
                                             {new (Document Any)})
  (ex.report ["Module" name]
             ["Old key" (signature.description (document.signature old))]
             ["New key" (signature.description (document.signature new))]))

(abstract: #export Archive
  {}
  
  (Dictionary Text [Descriptor (Document Any)])

  (def: #export empty
    Archive
    (:abstraction (dictionary.new text.hash)))

  (def: #export (add name [descriptor document] archive)
    (-> Module [Descriptor (Document Any)] Archive (Error Archive))
    (case (dictionary.get name (:representation archive))
      (#.Some [existing-descriptor existing-document])
      (if (is? document existing-document)
        (#error.Success archive)
        (ex.throw cannot-replace-document [name existing-document document]))
      
      #.None
      (#error.Success (|> archive
                          :representation
                          (dictionary.put name [descriptor document])
                          :abstraction))))

  (def: #export (find name archive)
    (-> Module Archive (Error [Descriptor (Document Any)]))
    (case (dictionary.get name (:representation archive))
      (#.Some document)
      (#error.Success document)
      
      #.None
      (ex.throw unknown-document [name])))

  (def: #export (merge additions archive)
    (-> Archive Archive (Error Archive))
    (monad.fold error.monad
                (function (_ [name' descriptor+document'] archive')
                  (..add name' descriptor+document' archive'))
                archive
                (dictionary.entries (:representation additions))))
  )
