(.module:
  [lux (#- Module)
   [abstract
    ["." equivalence (#+ Equivalence)]
    ["." monad (#+ do)]]
   [control
    ["ex" exception (#+ exception:)]]
   [data
    ["." error (#+ Error)]
    ["." name]
    ["." text]
    [collection
     ["." dictionary (#+ Dictionary)]]]
   [type
    abstract]
   [world
    [file (#+ File)]]]
  [///
   [default (#+ Version)]]
  [/
   ["." signature (#+ Signature)]
   ["." key (#+ Key)]
   ["." descriptor (#+ Module Descriptor)]
   ["." document (#+ Document)]])

## Archive
(exception: #export (unknown-document {module Module})
  (ex.report ["Module" module]))

(exception: #export (cannot-replace-document {module Module}
                                             {old (Document Any)}
                                             {new (Document Any)})
  (ex.report ["Module" module]
             ["Old key" (signature.description (document.signature old))]
             ["New key" (signature.description (document.signature new))]))

(abstract: #export Archive
  {}
  
  (Dictionary Module [Descriptor (Document Any)])

  (def: #export empty
    Archive
    (:abstraction (dictionary.new text.hash)))

  (def: #export (add module [descriptor document] archive)
    (-> Module [Descriptor (Document Any)] Archive (Error Archive))
    (case (dictionary.get module (:representation archive))
      (#.Some [existing-descriptor existing-document])
      (if (is? document existing-document)
        (#error.Success archive)
        (ex.throw cannot-replace-document [module existing-document document]))
      
      #.None
      (#error.Success (|> archive
                          :representation
                          (dictionary.put module [descriptor document])
                          :abstraction))))

  (def: #export (find module archive)
    (-> Module Archive (Error [Descriptor (Document Any)]))
    (case (dictionary.get module (:representation archive))
      (#.Some document)
      (#error.Success document)
      
      #.None
      (ex.throw unknown-document [module])))

  (def: #export (archived? archive module)
    (-> Archive Module Bit)
    (case (find module archive)
      (#error.Success _)
      yes

      (#error.Failure _)
      no))

  (def: #export archived
    (-> Archive (List Module))
    (|>> :representation dictionary.keys))

  (def: #export (merge additions archive)
    (-> Archive Archive (Error Archive))
    (monad.fold error.monad
                (function (_ [module' descriptor+document'] archive')
                  (..add module' descriptor+document' archive'))
                archive
                (dictionary.entries (:representation additions))))
  )
