(.module:
  [lux #*
   [data
    [text
     ["%" format]]]
   [math
    [number
     ["n" nat]]]])

(type: #export Version
  Nat)

(def: range 100)

(def: level
  (n.% ..range))

(def: current
  (-> Nat Nat)
  (|>>))

(def: next
  (n./ ..range))

(def: #export patch
  (-> Version Nat)
  (|>> ..current ..level))

(def: #export minor
  (-> Version Nat)
  (|>> ..next ..level))

(def: #export major
  (-> Version Nat)
  (|>> ..next ..next ..level))

(def: separator ".")

(def: (padded value)
  (-> Nat Text)
  (if (n.< 10 value)
    (%.format "0" (%.nat value))
    (%.nat value)))

(def: #export (format version)
  (%.Format Version)
  (%.format (..padded (..major version))
            ..separator
            (..padded (..minor version))
            ..separator
            (..padded (..patch version))))
