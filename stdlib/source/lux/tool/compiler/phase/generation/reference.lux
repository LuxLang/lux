(.module:
  [lux #*
   [control
    pipe]
   [data
    [text
     ["%" format (#+ format)]]]
   [type (#+ :share)]]
  ["." //
   ["#/" // ("#@." monad)
    ["#/" // #_
     [synthesis (#+ Synthesis)]
     ["#." reference (#+ Register Variable Reference)]]]])

(signature: #export (System expression)
  (: (-> Register expression)
     local)
  (: (-> Register expression)
     foreign)
  (: (All [anchor directive]
       (-> Variable (//.Operation anchor expression directive)))
     variable)
  (: (All [anchor directive]
       (-> Name (//.Operation anchor expression directive)))
     constant)
  (: (All [anchor directive]
       (-> Reference (//.Operation anchor expression directive)))
     reference))

(def: (variable-maker prefix variable)
  (All [expression]
    (-> Text (-> Text expression)
        (-> Register expression)))
  (|>> %.nat (format prefix) variable))

(def: #export foreign
  (All [expression]
    (-> (-> Text expression)
        (-> Register expression)))
  (variable-maker "f"))

(def: #export local
  (All [expression]
    (-> (-> Text expression)
        (-> Register expression)))
  (variable-maker "l"))

(def: #export (system constant variable)
  (All [expression]
    (-> (-> Text expression) (-> Text expression)
        (System expression)))
  (let [local (..local variable)
        foreign (..foreign variable)
        variable (:share [expression]
                         {(-> Text expression)
                          variable}
                         {(All [anchor directive]
                            (-> Variable (//.Operation anchor expression directive)))
                          (|>> (case> (#////reference.Local register)
                                      (local register)
                                      
                                      (#////reference.Foreign register)
                                      (foreign register))
                               ///@wrap)})
        constant (:share [expression]
                         {(-> Text expression)
                          constant}
                         {(All [anchor directive]
                            (-> Name (//.Operation anchor expression directive)))
                          (|>> //.remember (///@map constant))})]
    (structure
     (def: local local)
     (def: foreign foreign)
     (def: variable variable)
     (def: constant constant)
     (def: reference
       (|>> (case> (#////reference.Constant value)
                   (constant value)
                   
                   (#////reference.Variable value)
                   (variable value)))))))
