(.module:
  [lux #*
   [host (#+ import:)]
   [abstract
    ["." monad (#+ do)]]
   [control
    ["ex" exception (#+ exception:)]
    [parser
     ["s" code]]]
   [data
    ["." product]
    ["." text]
    [number (#+ hex)
     ["f" frac]]
    [collection
     ["." list ("#;." functor)]
     ["dict" dictionary (#+ Dictionary)]]]
   ["." macro (#+ with-gensyms)
    ["." code]
    [syntax (#+ syntax:)]]
   [target
    ["_" scheme (#+ Expression Computation)]]]
  ["." /// #_
   ["#." runtime (#+ Operation Phase Handler Bundle)]
   ["#//" ///
    ["#." extension
     ["." bundle]]
    ["#/" // #_
     ["#." synthesis (#+ Synthesis)]]]])

(syntax: (Vector {size s.nat} elemT)
  (wrap (list (` [(~+ (list.repeat size elemT))]))))

(type: #export Nullary (-> (Vector 0 Expression) Computation))
(type: #export Unary   (-> (Vector 1 Expression) Computation))
(type: #export Binary  (-> (Vector 2 Expression) Computation))
(type: #export Trinary (-> (Vector 3 Expression) Computation))
(type: #export Variadic (-> (List Expression) Computation))

(syntax: (arity: {name s.local-identifier} {arity s.nat})
  (with-gensyms [g!_ g!extension g!name g!phase g!inputs]
    (do @
      [g!input+ (monad.seq @ (list.repeat arity (macro.gensym "input")))]
      (wrap (list (` (def: #export ((~ (code.local-identifier name)) (~ g!extension))
                       (-> (-> (..Vector (~ (code.nat arity)) Expression) Computation)
                           Handler)
                       (function ((~ g!_) (~ g!name) (~ g!phase) (~ g!inputs))
                         (case (~ g!inputs)
                           (^ (list (~+ g!input+)))
                           (do /////.monad
                             [(~+ (|> g!input+
                                      (list;map (function (_ g!input)
                                                  (list g!input (` ((~ g!phase) (~ g!input))))))
                                      list.concat))]
                             ((~' wrap) ((~ g!extension) [(~+ g!input+)])))

                           (~' _)
                           (/////.throw /////extension.incorrect-arity [(~ g!name) 1 (list.size (~ g!inputs))]))))))))))

(arity: nullary 0)
(arity: unary 1)
(arity: binary 2)
(arity: trinary 3)

(def: #export (variadic extension)
  (-> Variadic Handler)
  (function (_ extension-name)
    (function (_ phase inputsS)
      (do /////.monad
        [inputsI (monad.map @ phase inputsS)]
        (wrap (extension inputsI))))))

(def: bundle::lux
  Bundle
  (|> bundle.empty
      (bundle.install "is?" (binary (product.uncurry _.eq?/2)))
      (bundle.install "try" (unary ///runtime.lux//try))))

(template [<name> <op>]
  [(def: (<name> [subjectO paramO])
     Binary
     (<op> paramO subjectO))]
  
  [i64::and _.bit-and/2]
  [i64::or  _.bit-or/2]
  [i64::xor _.bit-xor/2]
  )

(def: (i64::left-shift [subjectO paramO])
  Binary
  (_.arithmetic-shift/2 (_.remainder/2 (_.int +64) paramO)
                        subjectO))

(def: (i64::arithmetic-right-shift [subjectO paramO])
  Binary
  (_.arithmetic-shift/2 (|> paramO (_.remainder/2 (_.int +64)) (_.*/2 (_.int -1)))
                        subjectO))

(def: (i64::logical-right-shift [subjectO paramO])
  Binary
  (///runtime.i64//logical-right-shift (_.remainder/2 (_.int +64) paramO) subjectO))

(import: java/lang/Double
  (#static MIN_VALUE double)
  (#static MAX_VALUE double))

(template [<name> <const> <encode>]
  [(def: (<name> _)
     Nullary
     (<encode> <const>))]

  [f64::smallest (Double::MIN_VALUE)            _.float]
  [f64::min      (f.* -1.0 (Double::MAX_VALUE)) _.float]
  [f64::max      (Double::MAX_VALUE)            _.float]
  )

(template [<name> <op>]
  [(def: (<name> [subjectO paramO])
     Binary
     (|> subjectO (<op> paramO)))]

  [i64::+ _.+/2]
  [i64::- _.-/2]
  [i64::* _.*/2]
  [i64::/ _.quotient/2]
  [i64::% _.remainder/2]
  )

(template [<name> <op>]
  [(def: (<name> [subjectO paramO])
     Binary
     (<op> paramO subjectO))]

  [f64::+ _.+/2]
  [f64::- _.-/2]
  [f64::* _.*/2]
  [f64::/ _.//2]
  [f64::% _.mod/2]
  [f64::= _.=/2]
  [f64::< _.</2]

  [text::= _.string=?/2]
  [text::< _.string<?/2]
  )

(template [<name> <cmp>]
  [(def: (<name> [subjectO paramO])
     Binary
     (<cmp> paramO subjectO))]

  [i64::= _.=/2]
  [i64::< _.</2]
  )

(def: i64::char (|>> _.integer->char/1 _.string/1))

(def: bundle::i64
  Bundle
  (<| (bundle.prefix "i64")
      (|> bundle.empty
          (bundle.install "and" (binary i64::and))
          (bundle.install "or" (binary i64::or))
          (bundle.install "xor" (binary i64::xor))
          (bundle.install "left-shift" (binary i64::left-shift))
          (bundle.install "logical-right-shift" (binary i64::logical-right-shift))
          (bundle.install "arithmetic-right-shift" (binary i64::arithmetic-right-shift))
          (bundle.install "+" (binary i64::+))
          (bundle.install "-" (binary i64::-))
          (bundle.install "*" (binary i64::*))
          (bundle.install "/" (binary i64::/))
          (bundle.install "%" (binary i64::%))
          (bundle.install "=" (binary i64::=))
          (bundle.install "<" (binary i64::<))
          (bundle.install "f64" (unary (|>> (_.//2 (_.float +1.0)))))
          (bundle.install "char" (unary i64::char)))))

(def: bundle::f64
  Bundle
  (<| (bundle.prefix "f64")
      (|> bundle.empty
          (bundle.install "+" (binary f64::+))
          (bundle.install "-" (binary f64::-))
          (bundle.install "*" (binary f64::*))
          (bundle.install "/" (binary f64::/))
          (bundle.install "%" (binary f64::%))
          (bundle.install "=" (binary f64::=))
          (bundle.install "<" (binary f64::<))
          (bundle.install "smallest" (nullary f64::smallest))
          (bundle.install "min" (nullary f64::min))
          (bundle.install "max" (nullary f64::max))
          (bundle.install "i64" (unary _.exact/1))
          (bundle.install "encode" (unary _.number->string/1))
          (bundle.install "decode" (unary ///runtime.frac//decode)))))

(def: (text::char [subjectO paramO])
  Binary
  (_.string/1 (_.string-ref/2 subjectO paramO)))

(def: (text::clip [subjectO startO endO])
  Trinary
  (_.substring/3 subjectO startO endO))

(def: bundle::text
  Bundle
  (<| (bundle.prefix "text")
      (|> bundle.empty
          (bundle.install "=" (binary text::=))
          (bundle.install "<" (binary text::<))
          (bundle.install "concat" (binary (product.uncurry _.string-append/2)))
          (bundle.install "size" (unary _.string-length/1))
          (bundle.install "char" (binary text::char))
          (bundle.install "clip" (trinary text::clip)))))

(def: (io::log input)
  Unary
  (_.begin (list (_.display/1 input)
                 _.newline/0)))

(def: (void code)
  (-> Expression Computation)
  (_.begin (list code (_.string //////synthesis.unit))))

(def: bundle::io
  Bundle
  (<| (bundle.prefix "io")
      (|> bundle.empty
          (bundle.install "log" (unary (|>> io::log ..void)))
          (bundle.install "error" (unary _.raise/1))
          (bundle.install "exit" (unary _.exit/1))
          (bundle.install "current-time" (nullary (function (_ _) (///runtime.io//current-time (_.string //////synthesis.unit))))))))

(def: #export bundle
  Bundle
  (<| (bundle.prefix "lux")
      (|> bundle::lux
          (dict.merge bundle::i64)
          (dict.merge bundle::f64)
          (dict.merge bundle::text)
          (dict.merge bundle::io)
          )))
