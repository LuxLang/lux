(.module:
  [lux (#- Type type)
   [data
    [collection
     ["." list ("#@." functor)]
     ["." row]]]
   [target
    [jvm
     ["." modifier (#+ Modifier) ("#@." monoid)]
     ["." field (#+ Field)]
     ["_" bytecode (#+ Bytecode)]
     [type (#+ Type)
      [category (#+ Value Class)]]
     [constant
      [pool (#+ Resource)]]]]]
  ["." //// #_
   ["#." type]
   ["#." reference]
   [////
    [reference (#+ Register)]]])

(def: #export type ////type.value)

(def: #export (get class name)
  (-> (Type Class) Text (Bytecode Any))
  ($_ _.compose
      ////reference.this
      (_.getfield class name ..type)
      ))

(def: #export (put naming class register value)
  (-> (-> Register Text) (Type Class) Register (Bytecode Any) (Bytecode Any))
  ($_ _.compose
      ////reference.this
      value
      (_.putfield class (naming register) ..type)))

(def: modifier
  (Modifier Field)
  ($_ modifier@compose
      field.private
      field.final
      ))

(def: #export (variable name type)
  (-> Text (Type Value) (Resource Field))
  (field.field ..modifier name type (row.row)))

(def: #export (variables naming amount)
  (-> (-> Register Text) Nat (List (Resource Field)))
  (|> amount
      list.indices
      (list@map (function (_ register)
                  (..variable (naming register) ..type)))))
