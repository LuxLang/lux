(.module:
  [lux (#- inc)
   [abstract
    [monad (#+ do)]]
   [control
    ["." function]
    ["p" parser
     ["s" code]]]
   [data
    [number (#+ hex)
     ["." i64]]
    ["." text
     format]
    [collection
     ["." list ("#@." functor)]]]
   ["." macro
    ["." code]
    [syntax (#+ syntax:)]]
   [host
    ["_" python (#+ Expression SVar Computation Literal Statement)]]]
  ["." ///
   ["//." //
    [//
     ["/////." name]
     ["." synthesis]]]]
  )

(template [<name> <base>]
  [(type: #export <name>
     (<base> SVar (Expression Any) (Statement Any)))]

  [Operation ///.Operation]
  [Phase ///.Phase]
  [Handler ///.Handler]
  [Bundle ///.Bundle]
  )

(def: prefix Text "LuxRuntime")

(def: #export unit (_.string synthesis.unit))

(def: (flag value)
  (-> Bit Literal)
  (if value
    (_.string "")
    _.none))

(def: (variant' tag last? value)
  (-> (Expression Any) (Expression Any) (Expression Any) Literal)
  (_.tuple (list tag last? value)))

(def: #export (variant tag last? value)
  (-> Nat Bit (Expression Any) Literal)
  (variant' (_.int (.int tag))
            (flag last?)
            value))

(def: #export none
  Literal
  (..variant 0 #0 unit))

(def: #export some
  (-> (Expression Any) Literal)
  (..variant 1 #1))

(def: #export left
  (-> (Expression Any) Literal)
  (..variant 0 #0))

(def: #export right
  (-> (Expression Any) Literal)
  (..variant 1 #1))

(def: runtime-name
  (-> Text SVar)
  (|>> /////name.normalize
       (format ..prefix "_")
       _.var))

(def: (feature name definition)
  (-> SVar (-> SVar (Statement Any)) (Statement Any))
  (definition name))

(syntax: #export (with-vars {vars (s.tuple (p.some s.local-identifier))}
                   body)
  (wrap (list (` (let [(~+ (|> vars
                               (list@map (function (_ var)
                                           (list (code.local-identifier var)
                                                 (` (_.var (~ (code.text (/////name.normalize var))))))))
                               list.concat))]
                   (~ body))))))

(syntax: (runtime: {declaration (p.or s.local-identifier
                                      (s.form (p.and s.local-identifier
                                                     (p.some s.local-identifier))))}
           code)
  (case declaration
    (#.Left name)
    (macro.with-gensyms [g!_]
      (let [nameC (code.local-identifier name)
            code-nameC (code.local-identifier (format "@" name))
            runtime-nameC (` (runtime-name (~ (code.text name))))]
        (wrap (list (` (def: #export (~ nameC) SVar (~ runtime-nameC)))
                    (` (def: (~ code-nameC)
                         (Statement Any)
                         (..feature (~ runtime-nameC)
                                    (function ((~ g!_) (~ nameC))
                                      (~ code)))))))))
    
    (#.Right [name inputs])
    (macro.with-gensyms [g!_]
      (let [nameC (code.local-identifier name)
            code-nameC (code.local-identifier (format "@" name))
            runtime-nameC (` (runtime-name (~ (code.text name))))
            inputsC (list@map code.local-identifier inputs)
            inputs-typesC (list@map (function.constant (` (_.Expression Any)))
                                    inputs)]
        (wrap (list (` (def: #export ((~ nameC) (~+ inputsC))
                         (-> (~+ inputs-typesC) (Computation Any))
                         (_.apply/* (~ runtime-nameC) (list (~+ inputsC)))))
                    (` (def: (~ code-nameC)
                         (Statement Any)
                         (..feature (~ runtime-nameC)
                                    (function ((~ g!_) (~ g!_))
                                      (..with-vars [(~+ inputsC)]
                                        (_.def (~ g!_) (list (~+ inputsC))
                                               (~ code)))))))))))))

(runtime: (lux//try op)
  (with-vars [error value]
    (_.try ($_ _.then
               (_.set (list value) (_.apply/* op (list unit)))
               (_.return (right value)))
           (list [(list (_.var "Exception")) error
                  (_.return (left (_.str/1 error)))]))))

(runtime: (lux//program-args program-args)
  (with-vars [inputs value]
    ($_ _.then
        (_.set (list inputs) none)
        (<| (_.for-in value program-args)
            (_.set (list inputs)
                   (some (_.tuple (list value inputs)))))
        (_.return inputs))))

(def: runtime//lux
  (Statement Any)
  ($_ _.then
      @lux//try
      @lux//program-args))

(runtime: (io//log! message)
  ($_ _.then
      (_.print message)
      (_.return ..unit)))

(runtime: (io//throw! message)
  ($_ _.then
      (_.raise (_.Exception/1 message))
      (_.return ..unit)))

(runtime: (io//exit! code)
  ($_ _.then
      (_.import "sys")
      (_.statement (|> (_.var "sys") (_.do "exit" (list code))))
      (_.return ..unit)))

(runtime: (io//current-time! _)
  ($_ _.then
      (_.import "time")
      (_.return (|> (_.var "time")
                    (_.do "time" (list))
                    (_.* (_.int +1,000))
                    _.int/1))))

(def: runtime//io
  (Statement Any)
  ($_ _.then
      @io//log!
      @io//throw!
      @io//exit!
      @io//current-time!))

(def: last-index
  (|>> _.len/1 (_.- (_.int +1))))

(with-expansions [<recur> (as-is ($_ _.then
                                     (_.set (list lefts) (_.- last-index-right lefts))
                                     (_.set (list tuple) (_.nth last-index-right tuple))))]
  (runtime: (tuple//left lefts tuple)
    (with-vars [last-index-right]
      (<| (_.while (_.bool true))
          ($_ _.then
              (_.set (list last-index-right) (..last-index tuple))
              (_.if (_.> lefts last-index-right)
                ## No need for recursion
                (_.return (_.nth lefts tuple))
                ## Needs recursion
                <recur>)))))

  (runtime: (tuple//right lefts tuple)
    (with-vars [last-index-right right-index]
      (<| (_.while (_.bool true))
          ($_ _.then
              (_.set (list last-index-right) (..last-index tuple))
              (_.set (list right-index) (_.+ (_.int +1) lefts))
              (_.cond (list [(_.= right-index last-index-right)
                             (_.return (_.nth right-index tuple))]
                            [(_.> right-index last-index-right)
                             ## Needs recursion.
                             <recur>])
                      (_.return (_.slice-from right-index tuple)))
              )))))

(runtime: (sum//get sum wantsLast wantedTag)
  (let [no-match! (_.return _.none)
        sum-tag (_.nth (_.int +0) sum)
        sum-flag (_.nth (_.int +1) sum)
        sum-value (_.nth (_.int +2) sum)
        is-last? (_.= (_.string "") sum-flag)
        test-recursion! (_.if is-last?
                          ## Must recurse.
                          (_.return (sum//get sum-value wantsLast (_.- sum-tag wantedTag)))
                          no-match!)]
    (_.cond (list [(_.= sum-tag wantedTag)
                   (_.if (_.= wantsLast sum-flag)
                     (_.return sum-value)
                     test-recursion!)]

                  [(_.> sum-tag wantedTag)
                   test-recursion!]

                  [(_.and (_.< sum-tag wantedTag)
                          (_.= (_.string "") wantsLast))
                   (_.return (variant' (_.- wantedTag sum-tag) sum-flag sum-value))])

            no-match!)))

(def: runtime//adt
  (Statement Any)
  ($_ _.then
      @tuple//left
      @tuple//right
      @sum//get))

(def: full-64-bits
  Literal
  (_.manual "0xFFFFFFFFFFFFFFFF"))

(runtime: (i64//64 input)
  (with-vars [capped]
    (_.cond (list [(|> input (_.> full-64-bits))
                   (_.return (|> input (_.bit-and full-64-bits) i64//64))]
                  [(|> input (_.> (: Literal (_.manual "0x7FFFFFFFFFFFFFFF"))))
                   ($_ _.then
                       (_.set (list capped)
                              (_.int/1 (|> (: Literal (_.manual "0x10000000000000000"))
                                           (_.- input))))
                       (_.if (|> capped (_.<= (: Literal (_.manual "9223372036854775807L"))))
                         (_.return (|> capped (_.* (_.int -1))))
                         (_.return (: Literal (_.manual "-9223372036854775808L")))))])
            (_.return input))))

(runtime: (i64//logic-right-shift param subject)
  (let [mask (|> (_.int +1)
                 (_.bit-shl (_.- param (_.int +64)))
                 (_.- (_.int +1)))]
    (_.return (|> subject
                  (_.bit-shr param)
                  (_.bit-and mask)))))

(def: runtime//i64
  (Statement Any)
  ($_ _.then
      @i64//64
      @i64//logic-right-shift))

(runtime: (frac//decode input)
  (with-vars [ex]
    (_.try
     (_.return (..some (_.float/1 input)))
     (list [(list (_.var "Exception")) ex
            (_.return ..none)]))))

(def: runtime//frac
  (Statement Any)
  ($_ _.then
      @frac//decode))

(runtime: (text//index subject param start)
  (with-vars [idx]
    ($_ _.then
        (_.set (list idx) (|> subject (_.do "find" (list param start))))
        (_.if (_.= (_.int -1) idx)
          (_.return ..none)
          (_.return (..some idx))))))

(def: inc (|>> (_.+ (_.int +1))))

(def: (within? top value)
  (-> (Expression Any) (Expression Any) (Computation Any))
  (_.and (|> value (_.>= (_.int +0)))
         (|> value (_.< top))))

(runtime: (text//clip @text @from @to)
  (_.return (|> @text (_.slice @from (inc @to)))))

(runtime: (text//char idx text)
  (_.if (|> idx (within? (_.len/1 text)))
    (_.return (..some (_.ord/1 (|> text (_.slice idx (inc idx))))))
    (_.return ..none)))

(def: runtime//text
  (Statement Any)
  ($_ _.then
      @text//index
      @text//clip
      @text//char))

(runtime: (array//get array idx)
  (with-vars [temp]
    ($_ _.then
        (_.set (list temp) (_.nth idx array))
        (_.if (_.= _.none temp)
          (_.return ..none)
          (_.return (..some temp))))))

(runtime: (array//put array idx value)
  ($_ _.then
      (_.set (list (_.nth idx array)) value)
      (_.return array)))

(def: runtime//array
  (Statement Any)
  ($_ _.then
      @array//get
      @array//put))

(runtime: (box//write value box)
  ($_ _.then
      (_.set (list (_.nth (_.int +0) box)) value)
      (_.return ..unit)))

(def: runtime//box
  (Statement Any)
  @box//write)

(def: runtime
  (Statement Any)
  ($_ _.then
      runtime//lux
      runtime//adt
      runtime//i64
      runtime//frac
      runtime//text
      runtime//array
      runtime//box
      runtime//io
      ))

(def: #export artifact ..prefix)

(def: #export generate
  (Operation Any)
  (///.with-buffer
    (do ////.monad
      [_ (///.save! true ["" ..prefix]
                    (<| (_.comment "-*- coding: utf-8 -*-")
                        ..runtime))]
      (///.save-buffer! ..artifact))))
