(.module:
  [lux #*
   [host (#+ import:)]
   [abstract
    ["." monad (#+ do)]]
   [control
    ["." function]]
   [data
    ["." product]
    [collection
     ["." dictionary]]]
   [target
    ["_" lua (#+ Expression Literal)]]]
  ["." /// #_
   ["#." runtime (#+ Operation Phase Handler Bundle)]
   ["#." primitive]
   [//
    [extension (#+ Nullary Unary Binary Trinary
                   nullary unary binary trinary)]
    [//
     [extension
      ["." bundle]]]]])

(template: (!unary function)
  (|>> list _.apply/* (|> (_.var function))))

(def: lux-procs
  Bundle
  (|> bundle.empty
      (bundle.install "is" (binary (product.uncurry _.=)))
      (bundle.install "try" (unary ///runtime.lux//try))))

(def: i64-procs
  Bundle
  (<| (bundle.prefix "i64")
      (|> bundle.empty
          (bundle.install "and" (binary (product.uncurry _.bit-and)))
          (bundle.install "or" (binary (product.uncurry _.bit-or)))
          (bundle.install "xor" (binary (product.uncurry _.bit-xor)))
          (bundle.install "left-shift" (binary (product.uncurry _.bit-shl)))
          (bundle.install "logical-right-shift" (binary (product.uncurry ///runtime.i64//logic-right-shift)))
          (bundle.install "arithmetic-right-shift" (binary (product.uncurry _.bit-shr)))
          (bundle.install "=" (binary (product.uncurry _.=)))
          (bundle.install "+" (binary (product.uncurry _.+)))
          (bundle.install "-" (binary (product.uncurry _.-)))
          )))

(def: int-procs
  Bundle
  (<| (bundle.prefix "int")
      (|> bundle.empty
          (bundle.install "<" (binary (product.uncurry _.<)))
          (bundle.install "*" (binary (product.uncurry _.*)))
          (bundle.install "/" (binary (product.uncurry _./)))
          (bundle.install "%" (binary (product.uncurry _.%)))
          (bundle.install "frac" (unary (_./ (_.float +1.0))))
          (bundle.install "char" (unary (!unary "string.char"))))))

(import: #long java/lang/Double
  (#static MIN_VALUE double)
  (#static MAX_VALUE double))

(template [<name> <const>]
  [(def: (<name> _)
     (Nullary Literal)
     (_.float <const>))]

  [frac//smallest (java/lang/Double::MIN_VALUE)]
  [frac//min      (f/* -1.0 (java/lang/Double::MAX_VALUE))]
  [frac//max      (java/lang/Double::MAX_VALUE)]
  )

(def: frac//decode
  (Unary (Expression Any))
  (|>> list _.apply/* (|> (_.var "tonumber")) _.return (_.closure (list)) ///runtime.lux//try))

(def: frac-procs
  Bundle
  (<| (bundle.prefix "frac")
      (|> bundle.empty
          (bundle.install "+" (binary (product.uncurry _.+)))
          (bundle.install "-" (binary (product.uncurry _.-)))
          (bundle.install "*" (binary (product.uncurry _.*)))
          (bundle.install "/" (binary (product.uncurry _./)))
          (bundle.install "%" (binary (product.uncurry _.%)))
          (bundle.install "=" (binary (product.uncurry _.=)))
          (bundle.install "<" (binary (product.uncurry _.<)))
          (bundle.install "smallest" (nullary frac//smallest))
          (bundle.install "min" (nullary frac//min))
          (bundle.install "max" (nullary frac//max))
          (bundle.install "int" (unary (!unary "math.floor")))
          (bundle.install "encode" (unary (!unary "tostring")))
          (bundle.install "decode" (unary ..frac//decode)))))

(def: (text//char [subjectO paramO])
  (Binary (Expression Any))
  (///runtime.text//char subjectO paramO))

(def: (text//clip [paramO extraO subjectO])
  (Trinary (Expression Any))
  (///runtime.text//clip subjectO paramO extraO))

(def: (text//index [startO partO textO])
  (Trinary (Expression Any))
  (///runtime.text//index textO partO startO))

(def: text-procs
  Bundle
  (<| (bundle.prefix "text")
      (|> bundle.empty
          (bundle.install "=" (binary (product.uncurry _.=)))
          (bundle.install "<" (binary (product.uncurry _.<)))
          (bundle.install "concat" (binary (product.uncurry (function.flip _.concat))))
          (bundle.install "index" (trinary text//index))
          (bundle.install "size" (unary (|>> list _.apply/* (|> (_.var "string.len")))))
          (bundle.install "char" (binary (product.uncurry ///runtime.text//char)))
          (bundle.install "clip" (trinary text//clip))
          )))

(def: (io//log! messageO)
  (Unary (Expression Any))
  (_.or (_.apply/* (list messageO) (_.var "print"))
        ///runtime.unit))

(def: io-procs
  Bundle
  (<| (bundle.prefix "io")
      (|> bundle.empty
          (bundle.install "log" (unary ..io//log!))
          (bundle.install "error" (unary (!unary "error")))
          (bundle.install "exit" (unary (!unary "os.exit")))
          (bundle.install "current-time" (nullary (function (_ _)
                                                    (|> (_.var "os.time")
                                                        (_.apply/* (list))
                                                        (_.* (_.int +1,000)))))))))

(def: #export bundle
  Bundle
  (<| (bundle.prefix "lux")
      (|> lux-procs
          (dictionary.merge i64-procs)
          (dictionary.merge int-procs)
          (dictionary.merge frac-procs)
          (dictionary.merge text-procs)
          (dictionary.merge io-procs)
          )))
