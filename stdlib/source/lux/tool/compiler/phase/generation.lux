(.module:
  [lux (#- Module)
   [abstract
    [monad (#+ do)]]
   [control
    ["." try (#+ Try)]
    ["." exception (#+ exception:)]]
   [data
    ["." product]
    ["." name ("#@." equivalence)]
    ["." text
     ["%" format (#+ format)]]
    [collection
     ["." row (#+ Row)]
     ["." dictionary (#+ Dictionary)]
     ["." list ("#@." functor)]]]]
  ["." //
   ["." extension]
   [//
    [synthesis (#+ Synthesis)]
    [meta
     [archive
      [descriptor (#+ Module)]]]]])

(type: #export Registry
  (Dictionary Name Text))

(exception: #export (cannot-interpret {error Text})
  (exception.report
   ["Error" error]))

(exception: #export (unknown-lux-name {name Name} {registry Registry})
  (exception.report
   ["Name" (%.name name)]
   ["Registry" (|> registry
                   dictionary.keys
                   (list.sort (:: name.order <))
                   (list@map %.name)
                   (text.join-with text.new-line))]))

(exception: #export (cannot-overwrite-lux-name {lux-name Name}
                                               {old-host-name Text}
                                               {new-host-name Text})
  (exception.report
   ["Lux Name" (%.name lux-name)]
   ["Old Host Name" old-host-name]
   ["New Host Name" new-host-name]))

(template [<name>]
  [(exception: #export (<name> {name Name})
     (exception.report
      ["Output" (%.name name)]))]

  [cannot-overwrite-output]
  [no-buffer-for-saving-code]
  )

(type: #export Context
  {#scope-name Text
   #inner-functions Nat})

(signature: #export (Host expression directive)
  (: (-> Text expression (Try Any))
     evaluate!)
  (: (-> Text directive (Try Any))
     execute!)
  (: (-> Name expression (Try [Text Any directive]))
     define!))

(type: #export (Buffer directive) (Row [Name directive]))
(type: #export (Output directive) (Row [Module (Buffer directive)]))

(type: #export (State anchor expression directive)
  {#context Context
   #anchor (Maybe anchor)
   #host (Host expression directive)
   #buffer (Maybe (Buffer directive))
   #output (Output directive)
   #counter Nat
   #name-cache Registry})

(template [<special> <general>]
  [(type: #export (<special> anchor expression directive)
     (<general> (State anchor expression directive) Synthesis expression))]

  [State+    extension.State]
  [Operation extension.Operation]
  [Phase     extension.Phase]
  [Handler   extension.Handler]
  [Bundle    extension.Bundle]
  )

(def: #export (state host)
  (All [anchor expression directive]
    (-> (Host expression directive)
        (..State anchor expression directive)))
  {#context {#scope-name ""
             #inner-functions 0}
   #anchor #.None
   #host host
   #buffer #.None
   #output row.empty
   #counter 0
   #name-cache (dictionary.new name.hash)})

(def: #export (with-context expr)
  (All [anchor expression directive output]
    (-> (Operation anchor expression directive output)
        (Operation anchor expression directive [Text output])))
  (function (_ [bundle state])
    (let [[old-scope old-inner] (get@ #context state)
          new-scope (format old-scope "c" (%.nat old-inner))]
      (case (expr [bundle (set@ #context [new-scope 0] state)])
        (#try.Success [[bundle' state'] output])
        (#try.Success [[bundle' (set@ #context [old-scope (inc old-inner)] state')]
                       [new-scope output]])

        (#try.Failure error)
        (#try.Failure error)))))

(def: #export context
  (All [anchor expression directive]
    (Operation anchor expression directive Text))
  (extension.read (|>> (get@ #context)
                       (get@ #scope-name))))

(def: #export empty-buffer Buffer row.empty)

(template [<tag>
           <with-declaration> <with-type> <with-value>
           <set> <get> <get-type> <exception>]
  [(exception: #export <exception>)

   (def: #export <with-declaration>
     (All [anchor expression directive output] <with-type>)
     (function (_ body)
       (function (_ [bundle state])
         (case (body [bundle (set@ <tag> (#.Some <with-value>) state)])
           (#try.Success [[bundle' state'] output])
           (#try.Success [[bundle' (set@ <tag> (get@ <tag> state) state')]
                          output])

           (#try.Failure error)
           (#try.Failure error)))))

   (def: #export <get>
     (All [anchor expression directive]
       (Operation anchor expression directive <get-type>))
     (function (_ (^@ stateE [bundle state]))
       (case (get@ <tag> state)
         (#.Some output)
         (#try.Success [stateE output])

         #.None
         (exception.throw <exception> []))))

   (def: #export (<set> value)
     (All [anchor expression directive]
       (-> <get-type> (Operation anchor expression directive Any)))
     (function (_ [bundle state])
       (#try.Success [[bundle (set@ <tag> (#.Some value) state)]
                      []])))]

  [#anchor
   (with-anchor anchor)
   (-> anchor (Operation anchor expression directive output)
       (Operation anchor expression directive output))
   anchor
   set-anchor anchor anchor no-anchor]

  [#buffer
   with-buffer
   (-> (Operation anchor expression directive output)
       (Operation anchor expression directive output))
   ..empty-buffer
   set-buffer buffer (Buffer directive) no-active-buffer]
  )

(def: #export output
  (All [anchor expression directive]
    (Operation anchor expression directive (Output directive)))
  (extension.read (get@ #output)))

(def: #export next
  (All [anchor expression directive]
    (Operation anchor expression directive Nat))
  (do //.monad
    [count (extension.read (get@ #counter))
     _ (extension.update (update@ #counter inc))]
    (wrap count)))

(def: #export (gensym prefix)
  (All [anchor expression directive]
    (-> Text (Operation anchor expression directive Text)))
  (:: //.monad map (|>> %.nat (format prefix)) ..next))

(template [<name> <inputT>]
  [(def: #export (<name> label code)
     (All [anchor expression directive]
       (-> Text <inputT> (Operation anchor expression directive Any)))
     (function (_ (^@ state+ [bundle state]))
       (case (:: (get@ #host state) <name> label code)
         (#try.Success output)
         (#try.Success [state+ output])

         (#try.Failure error)
         (exception.throw cannot-interpret error))))]

  [evaluate! expression]
  [execute!  directive]
  )

(def: #export (define! name code)
  (All [anchor expression directive]
    (-> Name expression (Operation anchor expression directive [Text Any directive])))
  (function (_ (^@ stateE [bundle state]))
    (case (:: (get@ #host state) define! name code)
      (#try.Success output)
      (#try.Success [stateE output])

      (#try.Failure error)
      (exception.throw cannot-interpret error))))

(def: #export (save! execute? name code)
  (All [anchor expression directive]
    (-> Bit Name directive (Operation anchor expression directive Any)))
  (do //.monad
    [label (..gensym "save")
     _ (if execute?
         (execute! label code)
         (wrap []))
     ?buffer (extension.read (get@ #buffer))]
    (case ?buffer
      (#.Some buffer)
      (if (row.any? (|>> product.left (name@= name)) buffer)
        (//.throw cannot-overwrite-output name)
        (extension.update (set@ #buffer (#.Some (row.add [name code] buffer)))))
      
      #.None
      (//.throw no-buffer-for-saving-code name))))

(def: #export (save-buffer! target)
  (All [anchor expression directive]
    (-> Module (Operation anchor expression directive Any)))
  (do //.monad
    [buffer ..buffer]
    (extension.update (update@ #output (row.add [target buffer])))))

(def: #export (remember lux-name)
  (All [anchor expression directive]
    (-> Name (Operation anchor expression directive Text)))
  (function (_ (^@ stateE [_ state]))
    (let [cache (get@ #name-cache state)]
      (case (dictionary.get lux-name cache)
        (#.Some host-name)
        (#try.Success [stateE host-name])
        
        #.None
        (exception.throw unknown-lux-name [lux-name cache])))))

(def: #export (learn lux-name host-name)
  (All [anchor expression directive]
    (-> Name Text (Operation anchor expression directive Any)))
  (function (_ [bundle state])
    (let [cache (get@ #name-cache state)]
      (case (dictionary.get lux-name cache)
        #.None
        (#try.Success [[bundle
                        (update@ #name-cache
                                 (dictionary.put lux-name host-name)
                                 state)]
                       []])

        (#.Some old-host-name)
        (exception.throw cannot-overwrite-lux-name [lux-name old-host-name host-name])))))
