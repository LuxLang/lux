(.module:
  [lux #*
   [abstract
    [monad (#+ do)]]
   [control
    ["." exception (#+ exception:)]]
   [data
    ["." error (#+ Error)]
    ["." text
     format]
    [collection
     [array (#+ Array)]
     ["." list ("#@." functor)]]]
   ["." macro]]
  ["." //])

(exception: #export (expansion-failed {macro Name} {inputs (List Code)} {error Text})
  (exception.report
   ["Macro" (%name macro)]
   ["Inputs" (exception.enumerate %code inputs)]
   ["Error" error]))

(exception: #export (must-have-single-expansion {macro Name} {inputs (List Code)} {outputs (List Code)})
  (exception.report
   ["Macro" (%name macro)]
   ["Inputs" (exception.enumerate %code inputs)]
   ["Outputs" (exception.enumerate %code outputs)]))

(type: #export Expander
  (-> Macro (List Code) Lux (Error (Error [Lux (List Code)]))))

(def: #export (expand expander name macro inputs)
  (-> Expander Name Macro (List Code) (Meta (List Code)))
  (function (_ state)
    (do error.monad
      [output (expander macro inputs state)]
      (case output
        (#error.Success output)
        (#error.Success output)
        
        (#error.Failure error)
        ((//.throw expansion-failed [name inputs error]) state)))))

(def: #export (expand-one expander name macro inputs)
  (-> Expander Name Macro (List Code) (Meta Code))
  (do macro.monad
    [expansion (expand expander name macro inputs)]
    (case expansion
      (^ (list single))
      (wrap single)

      _
      (//.throw must-have-single-expansion [name inputs expansion]))))
