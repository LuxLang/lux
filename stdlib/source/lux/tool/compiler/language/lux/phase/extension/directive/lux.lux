(.module:
  [lux #*
   [abstract
    ["." monad (#+ do)]]
   [control
    [io (#+ IO)]
    ["." try]
    ["." exception (#+ exception:)]
    ["p" parser
     ["s" code (#+ Parser)]]]
   [data
    ["." product]
    ["." maybe]
    ["." text
     ["%" format (#+ format)]]
    [collection
     ["." dictionary]]]
   ["." macro
    ["." code]]
   ["." type (#+ :share :by-example) ("#@." equivalence)
    ["." check]]]
  ["." /// (#+ Extender)
   ["#." bundle]
   ["#." analysis]
   ["/#" // #_
    [analysis
     ["." module]
     [".A" type]]
    ["/#" // #_
     ["#." analysis
      [macro (#+ Expander)]
      ["#/." evaluation]]
     ["#." synthesis (#+ Synthesis)]
     ["#." generation (#+ Context)]
     ["#." directive (#+ Import Requirements Phase Operation Handler Bundle)]
     ["#." program (#+ Program)]
     [///
      ["." phase]
      [meta
       ["." archive (#+ Archive)]]]]]])

(def: #export (custom [syntax handler])
  (All [anchor expression directive s]
    (-> [(Parser s)
         (-> Text
             (Phase anchor expression directive)
             Archive
             s
             (Operation anchor expression directive Requirements))]
        (Handler anchor expression directive)))
  (function (_ extension-name phase archive inputs)
    (case (s.run syntax inputs)
      (#try.Success inputs)
      (handler extension-name phase archive inputs)

      (#try.Failure error)
      (phase.throw ///.invalid-syntax [extension-name %.code inputs]))))

## TODO: Inline "evaluate!'" into "evaluate!" ASAP
(def: (evaluate!' archive generate code//type codeS)
  (All [anchor expression directive]
    (-> Archive
        (/////generation.Phase anchor expression directive)
        Type
        Synthesis
        (Operation anchor expression directive [Type expression Any])))
  (/////directive.lift-generation
   (do phase.monad
     [module /////generation.module
      id /////generation.next
      codeG (generate archive codeS)
      codeV (/////generation.evaluate! (/////analysis/evaluation.id "directive" module id)
                                       codeG)]
     (wrap [code//type codeG codeV]))))

(def: #export (evaluate! archive type codeC)
  (All [anchor expression directive]
    (-> Archive Type Code (Operation anchor expression directive [Type expression Any])))
  (do phase.monad
    [state (///.lift phase.get-state)
     #let [analyse (get@ [#/////directive.analysis #/////directive.phase] state)
           synthesize (get@ [#/////directive.synthesis #/////directive.phase] state)
           generate (get@ [#/////directive.generation #/////directive.phase] state)]
     [_ codeA] (/////directive.lift-analysis
                (/////analysis.with-scope
                  (typeA.with-fresh-env
                    (typeA.with-type type
                      (analyse archive codeC)))))
     codeS (/////directive.lift-synthesis
            (synthesize archive codeA))]
    (evaluate!' archive generate type codeS)))

## TODO: Inline "definition'" into "definition" ASAP
(def: (definition' archive generate [module name] code//type codeS)
  (All [anchor expression directive]
    (-> Archive
        (/////generation.Phase anchor expression directive)
        Name
        Type
        Synthesis
        (Operation anchor expression directive [Type expression Any])))
  (/////directive.lift-generation
   (do phase.monad
     [codeG (generate archive codeS)
      id (/////generation.learn name)
      module-id (phase.lift (archive.id module archive))
      [target-name value directive] (/////generation.define! [module-id id] codeG)
      _ (/////generation.save! false [(%.nat module-id) (%.nat id)] directive)]
     (wrap [code//type codeG value]))))

(def: (definition archive name expected codeC)
  (All [anchor expression directive]
    (-> Archive Name (Maybe Type) Code
        (Operation anchor expression directive [Type expression Any])))
  (do {@ phase.monad}
    [state (///.lift phase.get-state)
     #let [analyse (get@ [#/////directive.analysis #/////directive.phase] state)
           synthesize (get@ [#/////directive.synthesis #/////directive.phase] state)
           generate (get@ [#/////directive.generation #/////directive.phase] state)]
     [_ code//type codeA] (/////directive.lift-analysis
                           (/////analysis.with-scope
                             (typeA.with-fresh-env
                               (case expected
                                 #.None
                                 (do @
                                   [[code//type codeA] (typeA.with-inference
                                                         (analyse archive codeC))
                                    code//type (typeA.with-env
                                                 (check.clean code//type))]
                                   (wrap [code//type codeA]))

                                 (#.Some expected)
                                 (do @
                                   [codeA (typeA.with-type expected
                                            (analyse archive codeC))]
                                   (wrap [expected codeA]))))))
     codeS (/////directive.lift-synthesis
            (synthesize archive codeA))]
    (definition' archive generate name code//type codeS)))

(template [<full> <partial> <learn>]
  [## TODO: Inline "<partial>" into "<full>" ASAP
   (def: (<partial> archive generate extension codeT codeS)
     (All [anchor expression directive]
       (-> Archive
           (/////generation.Phase anchor expression directive)
           Text
           Type
           Synthesis
           (Operation anchor expression directive [expression Any])))
     (do phase.monad
       [current-module (/////directive.lift-analysis
                        (///.lift macro.current-module-name))]
       (/////directive.lift-generation
        (do phase.monad
          [codeG (generate archive codeS)
           module-id (phase.lift (archive.id current-module archive))
           id (<learn> extension)
           [target-name value directive] (/////generation.define! [module-id id] codeG)
           _ (/////generation.save! false [(%.nat module-id) (%.nat id)] directive)]
          (wrap [codeG value])))))

   (def: #export (<full> archive extension codeT codeC)
     (All [anchor expression directive]
       (-> Archive Text Type Code
           (Operation anchor expression directive [expression Any])))
     (do phase.monad
       [state (///.lift phase.get-state)
        #let [analyse (get@ [#/////directive.analysis #/////directive.phase] state)
              synthesize (get@ [#/////directive.synthesis #/////directive.phase] state)
              generate (get@ [#/////directive.generation #/////directive.phase] state)]
        [_ codeA] (/////directive.lift-analysis
                   (/////analysis.with-scope
                     (typeA.with-fresh-env
                       (typeA.with-type codeT
                         (analyse archive codeC)))))
        codeS (/////directive.lift-synthesis
               (synthesize archive codeA))]
       (<partial> archive generate extension codeT codeS)))]

  [analyser analyser' /////generation.learn-analyser]
  [synthesizer synthesizer' /////generation.learn-synthesizer]
  [generator generator' /////generation.learn-generator]
  [directive directive' /////generation.learn-directive]
  )

(def: (refresh expander host-analysis)
  (All [anchor expression directive]
    (-> Expander /////analysis.Bundle (Operation anchor expression directive Any)))
  (do phase.monad
    [[bundle state] phase.get-state
     #let [eval (/////analysis/evaluation.evaluator expander
                                                    (get@ [#/////directive.synthesis #/////directive.state] state)
                                                    (get@ [#/////directive.generation #/////directive.state] state)
                                                    (get@ [#/////directive.generation #/////directive.phase] state))]]
    (phase.set-state [bundle
                      (update@ [#/////directive.analysis #/////directive.state]
                               (: (-> /////analysis.State+ /////analysis.State+)
                                  (|>> product.right
                                       [(///analysis.bundle eval host-analysis)]))
                               state)])))

(def: (announce-definition! name)
  (All [anchor expression directive]
    (-> Name (Operation anchor expression directive Any)))
  (/////directive.lift-generation
   (/////generation.log! (format "Definition " (%.name name)))))

(def: (lux::def expander host-analysis)
  (-> Expander /////analysis.Bundle Handler)
  (function (_ extension-name phase archive inputsC+)
    (case inputsC+
      (^ (list [_ (#.Identifier ["" short-name])] valueC annotationsC [_ (#.Bit exported?)]))
      (do phase.monad
        [current-module (/////directive.lift-analysis
                         (///.lift macro.current-module-name))
         #let [full-name [current-module short-name]]
         [type valueT value] (..definition archive full-name #.None valueC)
         [_ annotationsT annotations] (evaluate! archive Code annotationsC)
         _ (/////directive.lift-analysis
            (module.define short-name (#.Right [exported? type (:coerce Code annotations) value])))
         _ (..refresh expander host-analysis)
         _ (..announce-definition! full-name)]
        (wrap /////directive.no-requirements))

      _
      (phase.throw ///.invalid-syntax [extension-name %.code inputsC+]))))

(def: (def::type-tagged expander host-analysis)
  (-> Expander /////analysis.Bundle Handler)
  (..custom
   [($_ p.and s.local-identifier s.any s.any (s.tuple (p.some s.text)) s.bit)
    (function (_ extension-name phase archive [short-name valueC annotationsC tags exported?])
      (do phase.monad
        [current-module (/////directive.lift-analysis
                         (///.lift macro.current-module-name))
         #let [full-name [current-module short-name]]
         [_ annotationsT annotations] (evaluate! archive Code annotationsC)
         #let [annotations (:coerce Code annotations)]
         [type valueT value] (..definition archive full-name (#.Some .Type) valueC)
         _ (/////directive.lift-analysis
            (do phase.monad
              [_ (module.define short-name (#.Right [exported? type annotations value]))]
              (module.declare-tags tags exported? (:coerce Type value))))
         _ (..refresh expander host-analysis)
         _ (..announce-definition! full-name)]
        (wrap /////directive.no-requirements)))]))

(def: imports
  (Parser (List Import))
  (|> (s.tuple (p.and s.text s.text))
      p.some
      s.tuple))

(def: def::module
  Handler
  (..custom
   [($_ p.and s.any ..imports)
    (function (_ extension-name phase archive [annotationsC imports])
      (do {@ phase.monad}
        [[_ annotationsT annotationsV] (evaluate! archive Code annotationsC)
         #let [annotationsV (:coerce Code annotationsV)]
         _ (/////directive.lift-analysis
            (do @
              [_ (monad.map @ (function (_ [module alias])
                                (do @
                                  [_ (module.import module)]
                                  (case alias
                                    "" (wrap [])
                                    _ (module.alias alias module))))
                            imports)]
              (module.set-annotations annotationsV)))]
        (wrap {#/////directive.imports imports
               #/////directive.referrals (list)})))]))

(exception: #export (cannot-alias-an-alias {local Alias} {foreign Alias} {target Name})
  (exception.report
   ["Local alias" (%.name local)]
   ["Foreign alias" (%.name foreign)]
   ["Target definition" (%.name target)]))

(def: (define-alias alias original)
  (-> Text Name (/////analysis.Operation Any))
  (do phase.monad
    [current-module (///.lift macro.current-module-name)
     constant (///.lift (macro.find-def original))]
    (case constant
      (#.Left de-aliased)
      (phase.throw ..cannot-alias-an-alias [[current-module alias] original de-aliased])
      
      (#.Right [exported? original-type original-annotations original-value])
      (module.define alias (#.Left original)))))

(def: def::alias
  Handler
  (..custom
   [($_ p.and s.local-identifier s.identifier)
    (function (_ extension-name phase archive [alias def-name])
      (do phase.monad
        [_ (///.lift
            (phase.sub [(get@ [#/////directive.analysis #/////directive.state])
                        (set@ [#/////directive.analysis #/////directive.state])]
                       (define-alias alias def-name)))]
        (wrap /////directive.no-requirements)))]))

(template [<description> <mame> <type> <scope> <definer>]
  [(def: (<mame> extender)
     (All [anchor expression directive]
       (-> Extender
           (Handler anchor expression directive)))
     (function (handler extension-name phase archive inputsC+)
       (case inputsC+
         (^ (list nameC valueC))
         (do phase.monad
           [[_ _ name] (evaluate! archive Text nameC)
            [_ handlerV] (<definer> archive (:coerce Text name)
                                    (:by-example [anchor expression directive]
                                                 {(Handler anchor expression directive)
                                                  handler}
                                                 <type>)
                                    valueC)
            _ (<| <scope>
                  (///.install extender (:coerce Text name))
                  (:share [anchor expression directive]
                          {(Handler anchor expression directive)
                           handler}
                          {<type>
                           (:assume handlerV)}))
            _ (/////directive.lift-generation
               (/////generation.log! (format <description> " " (%.text (:coerce Text name)))))]
           (wrap /////directive.no-requirements))

         _
         (phase.throw ///.invalid-syntax [extension-name %.code inputsC+]))))]

  ["Analysis"   def::analysis   /////analysis.Handler                                 /////directive.lift-analysis   ..analyser]
  ["Synthesis"  def::synthesis  /////synthesis.Handler                                /////directive.lift-synthesis  ..synthesizer]
  ["Generation" def::generation (/////generation.Handler anchor expression directive) /////directive.lift-generation ..generator]
  ["Directive"  def::directive  (/////directive.Handler anchor expression directive)  (<|)                           ..directive]
  )

## TODO; Both "prepare-program" and "define-program" exist only
## because the old compiler couldn't handle a fully-inlined definition
## for "def::program". Inline them ASAP.
(def: (prepare-program archive analyse synthesize programC)
  (All [anchor expression directive output]
    (-> Archive
        /////analysis.Phase
        /////synthesis.Phase
        Code
        (Operation anchor expression directive Synthesis)))
  (do phase.monad
    [[_ programA] (/////directive.lift-analysis
                   (/////analysis.with-scope
                     (typeA.with-fresh-env
                       (typeA.with-type (type (-> (List Text) (IO Any)))
                         (analyse archive programC)))))]
    (/////directive.lift-synthesis
     (synthesize archive programA))))

(def: (define-program archive module-id generate program programS)
  (All [anchor expression directive output]
    (-> Archive
        archive.ID
        (/////generation.Phase anchor expression directive)
        (Program expression directive)
        Synthesis
        (/////generation.Operation anchor expression directive Any)))
  (do phase.monad
    [artifact-id (/////generation.learn /////program.name)
     programG (generate archive programS)]
    (/////generation.save! false [(%.nat module-id) (%.nat artifact-id)] (program [module-id artifact-id] programG))))

(def: (def::program program)
  (All [anchor expression directive]
    (-> (Program expression directive) (Handler anchor expression directive)))
  (function (handler extension-name phase archive inputsC+)
    (case inputsC+
      (^ (list programC))
      (do phase.monad
        [state (///.lift phase.get-state)
         #let [analyse (get@ [#/////directive.analysis #/////directive.phase] state)
               synthesize (get@ [#/////directive.synthesis #/////directive.phase] state)
               generate (get@ [#/////directive.generation #/////directive.phase] state)]
         programS (prepare-program archive analyse synthesize programC)
         current-module (/////directive.lift-analysis
                         (///.lift macro.current-module-name))
         module-id (phase.lift (archive.id current-module archive))
         _ (/////directive.lift-generation
            (define-program archive module-id generate program programS))]
        (wrap /////directive.no-requirements))

      _
      (phase.throw ///.invalid-syntax [extension-name %.code inputsC+]))))

(def: (bundle::def expander host-analysis program extender)
  (All [anchor expression directive]
    (-> Expander
        /////analysis.Bundle
        (Program expression directive)
        Extender
        (Bundle anchor expression directive)))
  (<| (///bundle.prefix "def")
      (|> ///bundle.empty
          (dictionary.put "module" def::module)
          (dictionary.put "alias" def::alias)
          (dictionary.put "type tagged" (def::type-tagged expander host-analysis))
          (dictionary.put "analysis" (def::analysis extender))
          (dictionary.put "synthesis" (def::synthesis extender))
          (dictionary.put "generation" (def::generation extender))
          (dictionary.put "directive" (def::directive extender))
          (dictionary.put "program" (def::program program))
          )))

(def: #export (bundle expander host-analysis program extender)
  (All [anchor expression directive]
    (-> Expander
        /////analysis.Bundle
        (Program expression directive)
        Extender
        (Bundle anchor expression directive)))
  (<| (///bundle.prefix "lux")
      (|> ///bundle.empty
          (dictionary.put "def" (lux::def expander host-analysis))
          (dictionary.merge (..bundle::def expander host-analysis program extender)))))
