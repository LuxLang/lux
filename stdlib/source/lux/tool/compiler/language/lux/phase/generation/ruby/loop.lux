(.module:
  [lux (#- Scope)
   [abstract
    ["." monad (#+ do)]]
   [data
    ["." product]
    [number
     ["n" nat]]
    [text
     ["%" format (#+ format)]]
    [collection
     ["." list ("#@." functor)]]]
   [target
    ["_" ruby (#+ Expression LVar)]]]
  ["." // #_
   [runtime (#+ Operation Phase Generator)]
   ["#." case]
   ["///#" //// #_
    [synthesis (#+ Scope Synthesis)]
    ["#." generation]
    ["//#" /// #_
     ["#." phase]]]])

(def: loop-name
  (-> Nat LVar)
  (|>> %.nat (format "loop") _.local))

(def: #export (scope generate archive [start initsS+ bodyS])
  (Generator (Scope Synthesis))
  (do {! ///////phase.monad}
    [@loop (:: ! map ..loop-name /////generation.next)
     initsO+ (monad.map ! (generate archive) initsS+)
     bodyO (/////generation.with-anchor @loop
             (generate archive bodyS))]
    (wrap (|> (_.return bodyO)
              (_.lambda (#.Some @loop)
                        (|> initsS+
                            list.enumeration
                            (list@map (|>> product.left (n.+ start) //case.register))))
              (_.apply/* initsO+)))))

(def: #export (recur generate archive argsS+)
  (Generator (List Synthesis))
  (do {! ///////phase.monad}
    [@scope /////generation.anchor
     argsO+ (monad.map ! (generate archive) argsS+)]
    (wrap (_.apply/* argsO+ @scope))))
