(.module:
  [lux #*
   [abstract
    ["." monad (#+ do)]]
   [control
    ["." function]
    ["<>" parser
     ["<s>" synthesis (#+ Parser)]]]
   [data
    [collection
     ["." dictionary]]]
   [target
    ["_" js (#+ Expression)]]]
  ["." // #_
   ["#." common (#+ custom)]
   ["//#" /// #_
    ["/" bundle]
    ["/#" // #_
     ["." extension]
     [generation
      [extension (#+ Nullary Unary Binary Trinary
                     nullary unary binary trinary)]
      ["//" js #_
       ["#." runtime (#+ Operation Phase Handler Bundle
                         with-vars)]]]
     ["///#" //// #_
      ["#." phase]]]]])

(def: array::new
  (Unary Expression)
  (|>> //runtime.i64//to-number list (_.new (_.var "Array"))))

(def: array::length
  (Unary Expression)
  (|>> (_.the "length") //runtime.i64//from-number))

(def: (array::read [indexG arrayG])
  (Binary Expression)
  (_.at indexG arrayG))

(def: (array::write [indexG valueG arrayG])
  (Trinary Expression)
  (//runtime.array//write indexG valueG arrayG))

(def: (array::delete [indexG arrayG])
  (Binary Expression)
  (//runtime.array//delete indexG arrayG))

(def: array
  Bundle
  (<| (/.prefix "array")
      (|> /.empty
          (/.install "new" (unary array::new))
          (/.install "length" (unary array::length))
          (/.install "read" (binary array::read))
          (/.install "write" (trinary array::write))
          (/.install "delete" (binary array::delete))
          )))

(def: object::new
  (custom
   [($_ <>.and <s>.any (<>.some <s>.any))
    (function (_ extension phase [constructorS inputsS])
      (do ////////phase.monad
        [constructorG (phase constructorS)
         inputsG (monad.map @ phase inputsS)]
        (wrap (_.new constructorG inputsG))))]))

(def: object::get
  Handler
  (custom
   [($_ <>.and <s>.text <s>.any)
    (function (_ extension phase [fieldS objectS])
      (do ////////phase.monad
        [objectG (phase objectS)]
        (wrap (_.the fieldS objectG))))]))

(def: object::do
  Handler
  (custom
   [($_ <>.and <s>.text <s>.any (<>.some <s>.any))
    (function (_ extension phase [methodS objectS inputsS])
      (do ////////phase.monad
        [objectG (phase objectS)
         inputsG (monad.map @ phase inputsS)]
        (wrap (_.do methodS inputsG objectG))))]))

(template [<!> <?> <unit>]
  [(def: <!> (Nullary Expression) (function.constant <unit>))
   (def: <?> (Unary Expression) (_.= <unit>))]

  [object::null object::null? _.null]
  [object::undefined object::undefined? _.undefined]
  )

(def: object
  Bundle
  (<| (/.prefix "object")
      (|> /.empty
          (/.install "new" object::new)
          (/.install "get" object::get)
          (/.install "do" object::do)
          (/.install "null" (nullary object::null))
          (/.install "null?" (unary object::null?))
          (/.install "undefined" (nullary object::undefined))
          (/.install "undefined?" (unary object::undefined?))
          )))

(def: js::constant
  (custom
   [<s>.text
    (function (_ extension phase name)
      (do ////////phase.monad
        []
        (wrap (_.var name))))]))

(def: js::apply
  (custom
   [($_ <>.and <s>.any (<>.some <s>.any))
    (function (_ extension phase [abstractionS inputsS])
      (do ////////phase.monad
        [abstractionG (phase abstractionS)
         inputsG (monad.map @ phase inputsS)]
        (wrap (_.apply/* abstractionG inputsG))))]))

(def: #export bundle
  Bundle
  (<| (/.prefix "js")
      (|> /.empty
          (/.install "constant" js::constant)
          (/.install "apply" js::apply)
          (/.install "type-of" (unary _.type-of))
          (dictionary.merge ..array)
          (dictionary.merge ..object)
          )))
