(.module:
  [lux #*
   [abstract
    [monad (#+ do)]]
   [control
    ["ex" exception (#+ exception:)]]
   [data
    [text
     ["%" format (#+ format)]]]
   ["." macro]]
  ["." / #_
   ["#." type]
   ["#." primitive]
   ["#." structure]
   ["#." reference]
   ["#." case]
   ["#." function]
   ["/#" // #_
    ["#." extension]
    ["/#" // #_
     ["/" analysis (#+ Analysis Operation Phase)
      ["#." macro (#+ Expander)]]
     [///
      ["//" phase]
      ["." reference]
      [meta
       [archive (#+ Archive)]]]]]])

(exception: #export (unrecognized-syntax {code Code})
  (ex.report ["Code" (%.code code)]))

## TODO: Had to split the 'compile' function due to compilation issues
## with old-luxc. Must re-combine all the code ASAP

(type: (Fix a)
  (-> a a))

(def: (compile|primitive else code')
  (Fix (-> (Code' (Ann Cursor)) (Operation Analysis)))
  (case code'
    (^template [<tag> <analyser>]
      (<tag> value)
      (<analyser> value))
    ([#.Bit  /primitive.bit]
     [#.Nat  /primitive.nat]
     [#.Int  /primitive.int]
     [#.Rev  /primitive.rev]
     [#.Frac /primitive.frac]
     [#.Text /primitive.text])

    _
    (else code')))

(def: (compile|structure archive compile else code')
  (-> Archive Phase (Fix (-> (Code' (Ann Cursor)) (Operation Analysis))))
  (case code'
    (^template [<tag> <analyser>]
      (^ (#.Form (list& [_ (<tag> tag)]
                        values)))
      (case values
        (#.Cons value #.Nil)
        (<analyser> compile tag archive value)

        _
        (<analyser> compile tag archive (` [(~+ values)]))))
    ([#.Nat /structure.sum]
     [#.Tag /structure.tagged-sum])

    (#.Tag tag)
    (/structure.tagged-sum compile tag archive (' []))

    (^ (#.Tuple (list)))
    /primitive.unit

    (^ (#.Tuple (list singleton)))
    (compile archive singleton)

    (^ (#.Tuple elems))
    (/structure.product archive compile elems)

    (^ (#.Record pairs))
    (/structure.record archive compile pairs)

    _
    (else code')))

(def: (compile|others expander archive compile code')
  (-> Expander Archive Phase (-> (Code' (Ann Cursor)) (Operation Analysis)))
  (case code'
    (#.Identifier reference)
    (/reference.reference reference)

    (^ (#.Form (list [_ (#.Record branches)] input)))
    (/case.case compile branches archive input)

    (^ (#.Form (list& [_ (#.Text extension-name)] extension-args)))
    (//extension.apply archive compile [extension-name extension-args])

    (^ (#.Form (list [_ (#.Tuple (list [_ (#.Identifier ["" function-name])]
                                       [_ (#.Identifier ["" arg-name])]))]
                     body)))
    (/function.function compile function-name arg-name archive body)

    (^ (#.Form (list& functionC argsC+)))
    (do //.monad
      [[functionT functionA] (/type.with-inference
                               (compile archive functionC))]
      (case functionA
        (#/.Reference (#reference.Constant def-name))
        (do @
          [?macro (//extension.lift (macro.find-macro def-name))]
          (case ?macro
            (#.Some macro)
            (do @
              [expansion (//extension.lift (/macro.expand-one expander def-name macro argsC+))]
              (compile archive expansion))

            _
            (/function.apply compile argsC+ functionT functionA archive functionC)))

        _
        (/function.apply compile argsC+ functionT functionA archive functionC)))

    _
    (//.throw unrecognized-syntax [.dummy-cursor code'])))

(def: #export (phase expander)
  (-> Expander Phase)
  (function (compile archive code)
    (let [[cursor code'] code]
      ## The cursor must be set in the state for the sake
      ## of having useful error messages.
      (/.with-cursor cursor
        (compile|primitive (compile|structure archive compile
                                              (compile|others expander archive compile))
                           code')))))
