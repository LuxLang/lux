(.module:
  [lux #*
   [abstract
    ["." monad (#+ do)]]
   [data
    [number
     ["." i32]]
    [collection
     ["." list]]]
   [target
    [jvm
     ["_" bytecode (#+ Bytecode)]
     ["." type]]]]
  ["." // #_
   ["#." runtime (#+ Operation Phase Generator)]
   ["#." primitive]
   ["///#" //// #_
    [analysis (#+ Variant Tuple)]
    ["#." synthesis (#+ Synthesis)]
    [///
     ["." phase]]]])

(def: $Object
  (type.class "java.lang.Object" (list)))

(def: unitG
  (Bytecode Any)
  (//primitive.text /////synthesis.unit))

(def: #export (tuple generate archive membersS)
  (Generator (Tuple Synthesis))
  (case membersS
    #.Nil
    (:: phase.monad wrap ..unitG)

    (#.Cons singletonS #.Nil)
    (generate archive singletonS)

    _
    (do {@ phase.monad}
      [membersI (|> membersS
                    list.enumerate
                    (monad.map @ (function (_ [idx member])
                                   (do @
                                     [memberI (generate archive member)]
                                     (wrap (do _.monad
                                             [_ _.dup
                                              _ (_.int (.i64 idx))
                                              _ memberI]
                                             _.aastore))))))]
      (wrap (do {@ _.monad}
              [_ (_.int (.i64 (list.size membersS)))
               _ (_.anewarray $Object)]
              (monad.seq @ membersI))))))

(def: (flagG right?)
  (-> Bit (Bytecode Any))
  (if right?
    ..unitG
    _.aconst-null))

(def: #export (variant generate archive [lefts right? valueS])
  (Generator (Variant Synthesis))
  (do phase.monad
    [valueI (generate archive valueS)
     #let [tagI (case (if right?
                        (.inc lefts)
                        lefts)
                  0 _.iconst-0
                  1 _.iconst-1
                  2 _.iconst-2
                  3 _.iconst-3
                  4 _.iconst-4
                  5 _.iconst-5
                  tag (_.int (.i64 tag)))]]
    (wrap (do _.monad
            [_ tagI
             _ (flagG right?)
             _ valueI]
            (_.invokestatic //runtime.class "variant"
                            (type.method [(list type.int $Object $Object)
                                          (type.array $Object)
                                          (list)]))))))
