(.module:
  [lux (#- Scope)
   [abstract
    ["." monad (#+ do)]]
   [data
    ["." product]
    ["." text]
    [number
     ["n" nat]]
    [collection
     ["." list ("#@." functor)]]]
   [target
    ["_" js (#+ Computation Var)]]]
  ["." // #_
   [runtime (#+ Operation Phase Generator)]
   ["#." case]
   ["///#" //// #_
    [synthesis (#+ Scope Synthesis)]
    ["#." generation]
    ["//#" /// #_
     ["#." phase]]]])

(def: @scope (_.var "scope"))

(def: #export (scope generate archive [start initsS+ bodyS])
  (Generator (Scope Synthesis))
  (do ///////phase.monad
    [initsO+ (monad.map @ (generate archive) initsS+)
     bodyO (/////generation.with-anchor @scope
             (generate archive bodyS))
     #let [closure (_.function @scope
                     (|> initsS+
                         list.enumerate
                         (list@map (|>> product.left (n.+ start) //case.register)))
                     (_.return bodyO))]]
    (wrap (_.apply/* closure initsO+))))

(def: #export (recur generate archive argsS+)
  (Generator (List Synthesis))
  (do ///////phase.monad
    [@scope /////generation.anchor
     argsO+ (monad.map @ (generate archive) argsS+)]
    (wrap (_.apply/* @scope argsO+))))
