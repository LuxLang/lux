(.module:
  [lux (#- Scope)
   [abstract
    ["." monad (#+ do)]]
   [data
    ["." product]
    ["." text
     ["%" format (#+ format)]]
    [collection
     ["." list ("#\." functor fold)]
     ["." set (#+ Set)]]]
   [math
    [number
     ["n" nat]]]
   [target
    ["_" scheme (#+ Computation Var)]]]
  ["." // #_
   [runtime (#+ Operation Phase Generator)]
   ["#." case]
   ["/#" // #_
    ["#." reference]
    ["/#" // #_
     [synthesis
      ["." case]]
     ["/#" // #_
      ["."synthesis (#+ Scope Synthesis)]
      ["#." generation]
      ["//#" /// #_
       ["#." phase]
       [meta
        [archive (#+ Archive)]]
       [reference
        [variable (#+ Register)]]]]]]])

(def: @scope
  (_.var "scope"))

(def: #export (scope expression archive [start initsS+ bodyS])
  (Generator (Scope Synthesis))
  (case initsS+
    ## function/false/non-independent loop
    #.Nil
    (expression archive bodyS)

    ## true loop
    _
    (do {! ///////phase.monad}
      [initsO+ (monad.map ! (expression archive) initsS+)
       bodyO (/////generation.with_anchor @scope
               (expression archive bodyS))]
      (wrap (_.letrec (list [@scope (_.lambda [(|> initsS+
                                                   list.enumeration
                                                   (list\map (|>> product.left (n.+ start) //case.register)))
                                               #.None]
                                              bodyO)])
                      (_.apply/* initsO+ @scope))))))

(def: #export (recur expression archive argsS+)
  (Generator (List Synthesis))
  (do {! ///////phase.monad}
    [@scope /////generation.anchor
     argsO+ (monad.map ! (expression archive) argsS+)]
    (wrap (_.apply/* argsO+ @scope))))
