(.module:
  [lux #*
   [abstract
    ["." monad (#+ do)]]
   [control
    ["." exception (#+ exception:)]]
   [data
    [text
     ["%" format (#+ format)]]
    [collection
     ["." list ("#@." fold monoid)]]]
   ["." meta]]
  ["." // #_
   ["#." extension]
   ["#." analysis
    ["#/." type]]
   ["/#" // #_
    ["/" directive (#+ Phase)]
    ["#." analysis
     ["#/." macro (#+ Expander)]]
    [///
     [reference (#+)]
     ["//" phase]]]])

(exception: #export (not-a-directive {code Code})
  (exception.report
   ["Directive" (%.code code)]))

(exception: #export (invalid-macro-call {code Code})
  (exception.report
   ["Code" (%.code code)]))

(exception: #export (macro-was-not-found {name Name})
  (exception.report
   ["Name" (%.name name)]))

(with-expansions [<lux_def_module> (as-is [|form-location| (#.Form (list& [|text-location| (#.Text "lux def module")] annotations))])]
  (def: #export (phase expander)
    (-> Expander Phase)
    (let [analyze (//analysis.phase expander)]
      (function (recur archive code)
        (case code
          (^ [_ (#.Form (list& [_ (#.Text name)] inputs))])
          (//extension.apply archive recur [name inputs])

          (^ [_ (#.Form (list& macro inputs))])
          (do {! //.monad}
            [expansion (/.lift-analysis
                        (do !
                          [macroA (//analysis/type.with-type Macro
                                    (analyze archive macro))]
                          (case macroA
                            (^ (///analysis.constant macro-name))
                            (do !
                              [?macro (//extension.lift (meta.find-macro macro-name))
                               macro (case ?macro
                                       (#.Some macro)
                                       (wrap macro)
                                       
                                       #.None
                                       (//.throw ..macro-was-not-found macro-name))]
                              (//extension.lift (///analysis/macro.expand expander macro-name macro inputs)))
                            
                            _
                            (//.throw ..invalid-macro-call code))))]
            (case expansion
              (^ (list& <lux_def_module> referrals))
              (|> (recur archive <lux_def_module>)
                  (:: ! map (update@ #/.referrals (list@compose referrals))))

              _
              (|> expansion
                  (monad.map ! (recur archive))
                  (:: ! map (list@fold /.merge-requirements /.no-requirements)))))

          _
          (//.throw ..not-a-directive code))))))
