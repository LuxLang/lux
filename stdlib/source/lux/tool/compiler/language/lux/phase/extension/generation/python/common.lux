(.module:
  [lux #*
   [host (#+ import:)]
   [abstract
    ["." monad (#+ do)]]
   [control
    ["." function]]
   [data
    ["." product]
    [number
     ["f" frac]]
    [collection
     ["." dictionary]]]
   [target
    ["_" python (#+ Expression)]]]
  [////
   ["/" bundle]
   [//
    [generation
     [extension (#+ Nullary Unary Binary Trinary
                    nullary unary binary trinary)]
     ["//" python #_
      ["#." runtime (#+ Operation Phase Handler Bundle)]]]]])

(def: lux-procs
  Bundle
  (|> /.empty
      (/.install "is" (binary (product.uncurry _.is)))
      (/.install "try" (unary //runtime.lux//try))))

(def: i64-procs
  Bundle
  (<| (/.prefix "i64")
      (|> /.empty
          (/.install "and" (binary (product.uncurry _.bit-and)))
          (/.install "or" (binary (product.uncurry _.bit-or)))
          (/.install "xor" (binary (product.uncurry _.bit-xor)))
          (/.install "left-shift" (binary (function.compose //runtime.i64//64 (product.uncurry _.bit-shl))))
          (/.install "logical-right-shift" (binary (product.uncurry //runtime.i64//logic-right-shift)))
          (/.install "arithmetic-right-shift" (binary (product.uncurry _.bit-shr)))
          (/.install "=" (binary (product.uncurry _.=)))
          (/.install "+" (binary (product.uncurry _.+)))
          (/.install "-" (binary (product.uncurry _.-)))
          )))

(def: int-procs
  Bundle
  (<| (/.prefix "int")
      (|> /.empty
          (/.install "<" (binary (product.uncurry _.<)))
          (/.install "*" (binary (product.uncurry _.*)))
          (/.install "/" (binary (product.uncurry _./)))
          (/.install "%" (binary (product.uncurry _.%)))
          (/.install "frac" (unary _.float/1))
          (/.install "char" (unary _.chr/1)))))

(import: #long java/lang/Double
  (#static MIN_VALUE double)
  (#static MAX_VALUE double))

(template [<name> <const>]
  [(def: (<name> _)
     (Nullary (Expression Any))
     (_.float <const>))]

  [frac//smallest (java/lang/Double::MIN_VALUE)]
  [frac//min      (f.* -1.0 (java/lang/Double::MAX_VALUE))]
  [frac//max      (java/lang/Double::MAX_VALUE)]
  )

(def: frac-procs
  Bundle
  (<| (/.prefix "frac")
      (|> /.empty
          (/.install "+" (binary (product.uncurry _.+)))
          (/.install "-" (binary (product.uncurry _.-)))
          (/.install "*" (binary (product.uncurry _.*)))
          (/.install "/" (binary (product.uncurry _./)))
          (/.install "%" (binary (product.uncurry _.%)))
          (/.install "=" (binary (product.uncurry _.=)))
          (/.install "<" (binary (product.uncurry _.<)))
          (/.install "smallest" (nullary frac//smallest))
          (/.install "min" (nullary frac//min))
          (/.install "max" (nullary frac//max))
          (/.install "int" (unary _.int/1))
          (/.install "encode" (unary _.repr/1))
          (/.install "decode" (unary //runtime.frac//decode)))))

(def: (text//clip [paramO extraO subjectO])
  (Trinary (Expression Any))
  (//runtime.text//clip subjectO paramO extraO))

(def: (text//index [startO partO textO])
  (Trinary (Expression Any))
  (//runtime.text//index textO partO startO))

(def: text-procs
  Bundle
  (<| (/.prefix "text")
      (|> /.empty
          (/.install "=" (binary (product.uncurry _.=)))
          (/.install "<" (binary (product.uncurry _.<)))
          (/.install "concat" (binary (product.uncurry _.+)))
          (/.install "index" (trinary text//index))
          (/.install "size" (unary _.len/1))
          (/.install "char" (binary (product.uncurry //runtime.text//char)))
          (/.install "clip" (trinary text//clip))
          )))

(def: io-procs
  Bundle
  (<| (/.prefix "io")
      (|> /.empty
          (/.install "log" (unary //runtime.io//log!))
          (/.install "error" (unary //runtime.io//throw!))
          (/.install "exit" (unary //runtime.io//exit!))
          (/.install "current-time" (nullary (function.constant (//runtime.io//current-time! //runtime.unit)))))))

(def: #export bundle
  Bundle
  (<| (/.prefix "lux")
      (|> lux-procs
          (dictionary.merge i64-procs)
          (dictionary.merge int-procs)
          (dictionary.merge frac-procs)
          (dictionary.merge text-procs)
          (dictionary.merge io-procs)
          )))
