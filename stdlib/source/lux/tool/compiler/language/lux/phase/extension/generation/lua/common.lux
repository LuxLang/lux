(.module:
  [lux #*
   [host (#+ import:)]
   [abstract
    ["." monad (#+ do)]]
   [control
    ["." function]]
   [data
    ["." product]
    [number
     ["f" frac]]
    [collection
     ["." dictionary]]]
   [target
    ["_" lua (#+ Expression Literal)]]]
  [////
   ["/" bundle]
   [//
    [generation
     [extension (#+ Nullary Unary Binary Trinary
                    nullary unary binary trinary)]
     ["//" lua #_
      ["#." runtime (#+ Operation Phase Handler Bundle)]]]]])

(template: (!unary function)
  (|>> list _.apply/* (|> (_.var function))))

(def: lux-procs
  Bundle
  (|> /.empty
      (/.install "is" (binary (product.uncurry _.=)))
      (/.install "try" (unary //runtime.lux//try))))

(def: i64-procs
  Bundle
  (<| (/.prefix "i64")
      (|> /.empty
          (/.install "and" (binary (product.uncurry _.bit-and)))
          (/.install "or" (binary (product.uncurry _.bit-or)))
          (/.install "xor" (binary (product.uncurry _.bit-xor)))
          (/.install "left-shift" (binary (product.uncurry _.bit-shl)))
          (/.install "logical-right-shift" (binary (product.uncurry //runtime.i64//logic-right-shift)))
          (/.install "arithmetic-right-shift" (binary (product.uncurry _.bit-shr)))
          (/.install "=" (binary (product.uncurry _.=)))
          (/.install "+" (binary (product.uncurry _.+)))
          (/.install "-" (binary (product.uncurry _.-)))
          )))

(def: int-procs
  Bundle
  (<| (/.prefix "int")
      (|> /.empty
          (/.install "<" (binary (product.uncurry _.<)))
          (/.install "*" (binary (product.uncurry _.*)))
          (/.install "/" (binary (product.uncurry _./)))
          (/.install "%" (binary (product.uncurry _.%)))
          (/.install "frac" (unary (_./ (_.float +1.0))))
          (/.install "char" (unary (!unary "string.char"))))))

(import: java/lang/Double
  (#static MIN_VALUE double)
  (#static MAX_VALUE double))

(template [<name> <const>]
  [(def: (<name> _)
     (Nullary Literal)
     (_.float <const>))]

  [frac//smallest (java/lang/Double::MIN_VALUE)]
  [frac//min      (f.* -1.0 (java/lang/Double::MAX_VALUE))]
  [frac//max      (java/lang/Double::MAX_VALUE)]
  )

(def: frac//decode
  (Unary (Expression Any))
  (|>> list _.apply/* (|> (_.var "tonumber")) _.return (_.closure (list)) //runtime.lux//try))

(def: frac-procs
  Bundle
  (<| (/.prefix "frac")
      (|> /.empty
          (/.install "+" (binary (product.uncurry _.+)))
          (/.install "-" (binary (product.uncurry _.-)))
          (/.install "*" (binary (product.uncurry _.*)))
          (/.install "/" (binary (product.uncurry _./)))
          (/.install "%" (binary (product.uncurry _.%)))
          (/.install "=" (binary (product.uncurry _.=)))
          (/.install "<" (binary (product.uncurry _.<)))
          (/.install "smallest" (nullary frac//smallest))
          (/.install "min" (nullary frac//min))
          (/.install "max" (nullary frac//max))
          (/.install "int" (unary (!unary "math.floor")))
          (/.install "encode" (unary (!unary "tostring")))
          (/.install "decode" (unary ..frac//decode)))))

(def: (text//char [subjectO paramO])
  (Binary (Expression Any))
  (//runtime.text//char subjectO paramO))

(def: (text//clip [paramO extraO subjectO])
  (Trinary (Expression Any))
  (//runtime.text//clip subjectO paramO extraO))

(def: (text//index [startO partO textO])
  (Trinary (Expression Any))
  (//runtime.text//index textO partO startO))

(def: text-procs
  Bundle
  (<| (/.prefix "text")
      (|> /.empty
          (/.install "=" (binary (product.uncurry _.=)))
          (/.install "<" (binary (product.uncurry _.<)))
          (/.install "concat" (binary (product.uncurry (function.flip _.concat))))
          (/.install "index" (trinary text//index))
          (/.install "size" (unary (|>> list _.apply/* (|> (_.var "string.len")))))
          (/.install "char" (binary (product.uncurry //runtime.text//char)))
          (/.install "clip" (trinary text//clip))
          )))

(def: (io//log! messageO)
  (Unary (Expression Any))
  (_.or (_.apply/* (list messageO) (_.var "print"))
        //runtime.unit))

(def: io-procs
  Bundle
  (<| (/.prefix "io")
      (|> /.empty
          (/.install "log" (unary ..io//log!))
          (/.install "error" (unary (!unary "error")))
          (/.install "exit" (unary (!unary "os.exit")))
          (/.install "current-time" (nullary (function (_ _)
                                               (|> (_.var "os.time")
                                                   (_.apply/* (list))
                                                   (_.* (_.int +1,000)))))))))

(def: #export bundle
  Bundle
  (<| (/.prefix "lux")
      (|> lux-procs
          (dictionary.merge i64-procs)
          (dictionary.merge int-procs)
          (dictionary.merge frac-procs)
          (dictionary.merge text-procs)
          (dictionary.merge io-procs)
          )))
