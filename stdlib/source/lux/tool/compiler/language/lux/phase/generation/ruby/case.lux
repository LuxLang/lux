(.module:
  [lux (#- case let if)
   [abstract
    [monad (#+ do)]]
   [control
    ["ex" exception (#+ exception:)]]
   [data
    ["." text]
    [number
     ["n" nat]
     ["i" int]]
    [collection
     ["." list ("#@." functor fold)]
     ["." set]]]
   [target
    ["_" ruby (#+ Expression Statement)]]]
  ["." // #_
   ["#." runtime (#+ Operation Phase Generator)]
   ["#." primitive]
   ["/#" // #_
    ["#." reference]
    ["/#" // #_
     [synthesis
      ["." case]]
     ["/#" // #_
      ["#." synthesis (#+ Synthesis Path)]
      ["#." generation]
      ["//#" /// #_
       ["#." reference (#+ Register)]
       ["#." phase ("#@." monad)]
       [meta
        [archive (#+ Archive)]]]]]]])

(def: #export register
  (///reference.local _.local))

(def: #export capture
  (///reference.foreign _.local))

(def: #export (let generate archive [valueS register bodyS])
  (Generator [Synthesis Register Synthesis])
  (do ///////phase.monad
    [valueO (generate archive valueS)
     bodyO (generate archive bodyS)]
    ## TODO: Find some way to do 'let' without paying the price of the closure.
    (wrap (|> bodyO
              _.return
              (_.lambda #.None (list (..register register)))
              (_.do "call" (list valueO))))))

(def: #export (record-get generate archive [valueS pathP])
  (Generator [Synthesis (List (Either Nat Nat))])
  (do ///////phase.monad
    [valueO (generate archive valueS)]
    (wrap (list@fold (function (_ side source)
                       (.let [method (.case side
                                       (^template [<side> <accessor>]
                                         (<side> lefts)
                                         (<accessor> (_.int (.int lefts))))
                                       ([#.Left //runtime.tuple//left]
                                        [#.Right //runtime.tuple//right]))]
                         (method source)))
                     valueO
                     pathP))))

(def: #export (if generate archive [testS thenS elseS])
  (Generator [Synthesis Synthesis Synthesis])
  (do ///////phase.monad
    [testO (generate archive testS)
     thenO (generate archive thenS)
     elseO (generate archive elseS)]
    (wrap (_.? testO thenO elseO))))

(def: @savepoint (_.local "lux_pm_savepoint"))
(def: @cursor (_.local "lux_pm_cursor"))
(def: @temp (_.local "lux_pm_temp"))

(def: (push! value)
  (-> (Expression Any) (Statement Any))
  (_.statement (|> @cursor (_.do "push" (list value)))))

(def: peek-and-pop
  (Expression Any)
  (|> @cursor (_.do "pop" (list))))

(def: pop!
  (Statement Any)
  (_.statement ..peek-and-pop))

(def: peek
  (Expression Any)
  (_.nth (_.int -1) @cursor))

(def: save!
  (Statement Any)
  (.let [cursor (_.array-range (_.int +0) (_.int -1) @cursor)]
    (_.statement (|> @savepoint (_.do "push" (list cursor))))))

(def: restore!
  (Statement Any)
  (_.set (list @cursor) (|> @savepoint (_.do "pop" (list)))))

(def: fail! _.break)

(def: (multi-pop! pops)
  (-> Nat (Statement Any))
  (_.statement (_.do "slice!" (list (_.int (i.* -1 (.int pops)))
                                    (_.int (.int pops)))
                 @cursor)))

(template [<name> <flag> <prep>]
  [(def: (<name> simple? idx)
     (-> Bit Nat (Statement Any))
     ($_ _.then
         (_.set (list @temp) (|> idx <prep> .int _.int (//runtime.sum//get ..peek <flag>)))
         (.if simple?
           (_.when (_.= _.nil @temp)
                   fail!)
           (_.if (_.= _.nil @temp)
             fail!
             (..push! @temp)))))]

  [left-choice  _.nil         (<|)]
  [right-choice (_.string "") inc]
  )

(def: (alternation pre! post!)
  (-> (Statement Any) (Statement Any) (Statement Any))
  ($_ _.then
      (_.while (_.bool true)
               ($_ _.then
                   ..save!
                   pre!))
      ($_ _.then
          ..restore!
          post!)))

(def: (pattern-matching' generate archive pathP)
  (-> Phase Archive Path (Operation (Statement Any)))
  (.case pathP
    (^ (/////synthesis.path/then bodyS))
    (///////phase@map _.return (generate archive bodyS))

    #/////synthesis.Pop
    (///////phase@wrap ..pop!)

    (#/////synthesis.Bind register)
    (///////phase@wrap (_.set (list (..register register)) ..peek))

    (^template [<tag> <format>]
      (^ (<tag> value))
      (///////phase@wrap (_.when (|> value <format> (_.= ..peek) _.not)
                                 fail!)))
    ([/////synthesis.path/bit  //primitive.bit]
     [/////synthesis.path/i64  //primitive.i64]
     [/////synthesis.path/f64  //primitive.f64]
     [/////synthesis.path/text //primitive.text])

    (^template [<complex> <simple> <choice>]
      (^ (<complex> idx))
      (///////phase@wrap (<choice> false idx))

      (^ (<simple> idx nextP))
      (|> nextP
          (pattern-matching' generate archive)
          (///////phase@map (_.then (<choice> true idx)))))
    ([/////synthesis.side/left  /////synthesis.simple-left-side  ..left-choice]
     [/////synthesis.side/right /////synthesis.simple-right-side ..right-choice])

    (^ (/////synthesis.member/left 0))
    (///////phase@wrap (|> ..peek (_.nth (_.int +0)) ..push!))
    
    (^template [<pm> <getter>]
      (^ (<pm> lefts))
      (///////phase@wrap (|> ..peek (<getter> (_.int (.int lefts))) ..push!)))
    ([/////synthesis.member/left  //runtime.tuple//left]
     [/////synthesis.member/right //runtime.tuple//right])

    (^ (/////synthesis.!bind-top register thenP))
    (do ///////phase.monad
      [then! (pattern-matching' generate archive thenP)]
      (///////phase@wrap ($_ _.then
                             (_.set (list (..register register)) ..peek-and-pop)
                             then!)))

    (^ (/////synthesis.!multi-pop nextP))
    (.let [[extra-pops nextP'] (case.count-pops nextP)]
      (do ///////phase.monad
        [next! (pattern-matching' generate archive nextP')]
        (///////phase@wrap ($_ _.then
                               (..multi-pop! (n.+ 2 extra-pops))
                               next!))))

    (^template [<tag> <combinator>]
      (^ (<tag> preP postP))
      (do ///////phase.monad
        [pre! (pattern-matching' generate archive preP)
         post! (pattern-matching' generate archive postP)]
        (wrap (<combinator> pre! post!))))
    ([/////synthesis.path/seq _.then]
     [/////synthesis.path/alt ..alternation])))

(def: (pattern-matching generate archive pathP)
  (-> Phase Archive Path (Operation (Statement Any)))
  (do ///////phase.monad
    [pattern-matching! (pattern-matching' generate archive pathP)]
    (wrap ($_ _.then
              (_.while (_.bool true)
                       pattern-matching!)
              (_.statement (_.raise (_.string case.pattern-matching-error)))))))

(def: #export (case generate archive [valueS pathP])
  (Generator [Synthesis Path])
  (do ///////phase.monad
    [initG (generate archive valueS)
     pattern-matching! (pattern-matching generate archive pathP)]
    (wrap (|> ($_ _.then
                  (_.set (list @cursor) (_.array (list initG)))
                  (_.set (list @savepoint) (_.array (list)))
                  pattern-matching!)
              (_.lambda #.None (list))
              (_.do "call" (list))))))
