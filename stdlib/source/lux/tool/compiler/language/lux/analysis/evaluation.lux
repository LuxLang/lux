(.module:
  [lux (#- Module)
   [abstract
    [monad (#+ do)]]
   [control
    ["." try]]
   [data
    ["." text
     ["%" format (#+ format)]]]
   ["." macro]]
  [// (#+ Operation)
   [macro (#+ Expander)]
   [//
    [phase
     [".P" extension]
     [".P" synthesis]
     [".P" analysis
      ["." type]]
     [//
      ["." synthesis]
      ["." generation]
      [///
       ["." phase]
       [meta
        [archive (#+ Archive)
         [descriptor (#+ Module)]]]]]]]])

(type: #export Eval
  (-> Archive Nat Type Code (Operation Any)))

(def: #export (id prefix module count)
  (-> Text Module Nat Text)
  (format prefix
          "$"
          (text.replace-all "/" "$" module)
          "$"
          (%.nat count)))

(def: #export (evaluator expander synthesis-state generation-state generate)
  (All [anchor expression artifact]
    (-> Expander
        synthesis.State+
        (generation.State+ anchor expression artifact)
        (generation.Phase anchor expression artifact)
        Eval))
  (let [analyze (analysisP.phase expander)]
    (function (eval archive count type exprC)
      (do phase.monad
        [exprA (type.with-type type
                 (analyze archive exprC))
         module (extensionP.lift
                 macro.current-module-name)]
        (phase.lift (do try.monad
                      [exprS (|> exprA (synthesisP.phase archive) (phase.run synthesis-state))]
                      (phase.run generation-state
                                 (do phase.monad
                                   [exprO (generate archive exprS)]
                                   (generation.evaluate! (..id "analysis" module count)
                                                         exprO)))))))))
