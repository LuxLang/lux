(.module:
  [lux #*
   [control
    ["." equivalence]
    ["." monoid]
    ["." parser]]
   [data
    ["." number
     ["." i64]]
    [format
     ["." binary]]
    [collection
     [list ("list/." Functor<List>)]]]
   [type
    ["." abstract]]
   [macro (#+ with-gensyms)
    ["." code]
    ["s" syntax (#+ Syntax syntax:)]]]
  [//
   ["//." encoding]])

(type: Modifier
  {#code Text
   #name Text})

(def: modifier
  (Syntax Modifier)
  (s.tuple (parser.and s.text
                       s.local-identifier)))

(def: (code modifier)
  (-> Modifier Code)
  (code.tuple (list (code.text (get@ #code modifier))
                    (code.local-identifier (get@ #name modifier)))))

(syntax: #export (modifiers: {options (parser.many ..modifier)})
  (with-gensyms [g!parameter g!subject g!<name> g!<code>]
    (let [g!name (' Modifier)
          g!combine (' combine)
          g!empty (' empty)
          g!format (' modifier-format)
          typeC (` (abstract.abstract: (~' #export) (~ g!name)
                     {}

                     //encoding.U2

                     (.def: (~' #export) (~' code)
                       (.-> (~ g!name) //encoding.U2)
                       (.|>> abstract.:representation))

                     (.def: (~' #export) ((~ g!combine) (~ g!parameter) (~ g!subject))
                       (.-> (~ g!name) (~ g!name) (~ g!name))
                       (abstract.:abstraction (//encoding.to-u2 (i64.and (//encoding.from-u2 (abstract.:representation (~ g!parameter)))
                                                                         (//encoding.from-u2 (abstract.:representation (~ g!subject)))))))

                     (.do-template [(~ g!<code>) (~ g!<name>)]
                       [(.def: (~' #export) (~ g!<name>)
                          (~ g!name)
                          (.|> (number.hex (~ g!<code>)) //encoding.to-u2 abstract.:abstraction))]

                       ["0000" (~ g!empty)]
                       (~+ (list/map ..code options))
                       )

                     (.structure: (~' #export) (~' _) (equivalence.Equivalence (~ g!name))
                       (.def: ((~' =) (~' reference) (~' sample))
                         (.:: //encoding.Equivalence<U2> (~' =)
                           (abstract.:representation (~' reference))
                           (abstract.:representation (~' sample)))))

                     (.def: (~' #export) (~ g!format)
                       (binary.Format (~ g!name))
                       (.let [(.^open "_/.") //encoding.u2-format]
                         {#binary.reader (|> (~' _/reader)
                                             (:: parser.Functor<Parser> (~' map)
                                                 (|>> abstract.:abstraction)))
                          #binary.writer (|>> abstract.:representation
                                              (~' _/writer))}))))
          monoidC (` (.structure: (~' #export) (~' _) (monoid.Monoid (~ g!name))
                       (.def: (~' identity) (~ g!empty))
                       (.def: (~' compose) (~ g!combine))))]
      (wrap (list typeC monoidC)))))
