(.module:
  [lux #*
   [control
    [monoid (#+)]
    [parser (#+)]
    ["." monad (#+ do)]
    ["." state (#+ State)]]
   [data
    [number (#+)
     [i64 (#+)]]
    [format
     ["." binary (#+ Format)]]
    [collection
     ["." row (#+ Row)]]]
   [type
    [abstract (#+)]]]
  [//
   ["//." encoding (#+)]
   ["//." modifier (#+ modifiers:)]
   ["//." version (#+ Version Minor Major)]
   ["//." name (#+ Internal)]
   ["//." magic (#+ Magic)]
   ["//." index (#+ Index)]
   ["//." attribute (#+ Attribute)]
   ["//." field (#+ Field)]
   ["//." method (#+ Method)]
   ["//." constant (#+ Constant)
    ["//." pool (#+ Pool)]]])

(modifiers:
  ["0001" public]
  ["0010" final]
  ["0020" super]
  ["0200" interface]
  ["0400" abstract]
  ["1000" synthetic]
  ["2000" annotation]
  ["4000" enum]
  )

(type: #export Class
  {#magic Magic
   #minor-version Minor
   #major-version Major
   #constant-pool Pool
   #access-flags Modifier
   #this (Index //constant.Class)
   #super (Index //constant.Class)
   #interfaces (Row (Index //constant.Class))
   #fields (Row Field)
   #methods (Row Method)
   #attributes (Row Attribute)})

(def: default-minor-version Minor (//version.version 0))

(def: (install-classes this super interfaces)
  (-> Internal Internal (List Internal)
      (State Pool [(Index //constant.Class) (Index //constant.Class) (Row (Index //constant.Class))]))
  (do state.Monad<State>
    [@this (//pool.class (//name.read this))
     @super (//pool.class (//name.read super))
     @interfaces (: (State Pool (Row (Index //constant.Class)))
                    (monad.fold @ (function (_ interface @interfaces)
                                    (do @
                                      [@interface (//pool.class (//name.read interface))]
                                      (wrap (row.add @interface @interfaces))))
                                row.empty
                                interfaces))]
    (wrap [@this @super @interfaces])))

(def: #export (class version access
                     super this interfaces
                     fields methods attributes)
  (-> Major Modifier
      Internal Internal (List Internal)
      (Row Field) (Row Method) (Row Attribute)
      Class)
  (let [[pool [@this @super @interfaces]] (state.run (: Pool row.empty)
                                                     (install-classes this super interfaces))]
    {#magic //magic.code
     #minor-version ..default-minor-version
     #major-version version
     #constant-pool pool
     #access-flags access
     #this @this
     #super @super
     #interfaces @interfaces
     #fields fields
     #methods methods
     #attributes attributes}))

(def: #export format
  (Format Class)
  ($_ binary.and
      //magic.format
      //version.format
      //version.format
      //pool.format
      ..modifier-format
      //index.format
      //index.format
      (binary.row/16 //index.format)
      (binary.row/16 //field.format)
      (binary.row/16 //method.format)
      (binary.row/16 //attribute.format)))
