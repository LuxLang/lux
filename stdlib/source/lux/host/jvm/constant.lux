(.module:
  [lux #*
   [control
    [monad (#+ do)]
    ["." parser]
    ["." equivalence (#+ Equivalence)]]
   [data
    ["." number]
    ["." text]
    [format
     ["." binary (#+ Format) ("mutation/." Monoid<Mutation>)]]
    [collection
     ["." row (#+ Row)]]]
   [type
    abstract]]
  [//
   [encoding (#+ U4)]
   ["//." index (#+ Index)]
   [descriptor (#+ Descriptor)]]
  [/
   ["/." tag ("tag/." Equivalence<Tag>)]])

(type: #export UTF8 Text)

(def: utf8-format
  (Format UTF8)
  binary.utf8/16)

(abstract: #export Class
  {}
  
  (Index UTF8)
  
  (def: #export class
    (-> (Index UTF8) Class)
    (|>> :abstraction))

  (def: #export Equivalence<Class>
    (Equivalence Class)
    (:: equivalence.Contravariant<Equivalence> map-1
        (|>> :representation)
        //index.Equivalence<Index>))

  (def: class-format
    (Format Class)
    (binary.adapt (|>> :abstraction)
                  (|>> :representation)
                  //index.format))
  )

(abstract: #export (Value kind)

  {}

  kind

  (def: #export value
    (All [kind] (-> (Value kind) kind))
    (|>> :representation))

  (def: #export (Equivalence<Value> Equivalence<kind>)
    (All [kind]
      (-> (Equivalence kind)
          (Equivalence (Value kind))))
    (:: equivalence.Contravariant<Equivalence> map-1
        (|>> :representation)
        Equivalence<kind>))

  (do-template [<constructor> <type> <marker>]
    [(type: #export <type> (Value <marker>))

     (def: #export <constructor>
       (-> <marker> <type>)
       (|>> :abstraction))]

    [integer Integer U4]
    [long    Long    .Int]
    [float   Float   Nothing]
    [double  Double  Frac]
    [string  String  (Index UTF8)]
    )

  (do-template [<name> <type> <read> <write> <base>]
    [(def: <name>
       (Format <type>)
       (binary.adapt (|>> <read> :abstraction)
                     (|>> :representation <write>)
                     <base>))]

    [long-format Long .int (<|) binary.bits/64]
    [double-format Double number.bits-to-frac number.frac-to-bits binary.bits/64]
    [string-format String (<|) (<|) //index.format]
    )
  )

(type: #export Name-And-Type
  {#name (Index UTF8)
   #descriptor (Index (Descriptor Any))})

(type: #export Reference
  {#class (Index Class)
   #name-and-type (Index Name-And-Type)})

(do-template [<type> <equivalence> <format>]
  [(def: #export <equivalence>
     (Equivalence <type>)
     ($_ equivalence.product
         //index.Equivalence<Index>
         //index.Equivalence<Index>))

   (def: #export <format>
     (Format <type>)
     ($_ binary.and
         //index.format
         //index.format))]

  [Name-And-Type Equivalence<Name-And-Type> name-and-type-format]
  [Reference Equivalence<Reference> reference-format]
  )

(type: #export Constant
  (#UTF8 UTF8)
  (#Long Long)
  (#Double Double)
  (#Class Class)
  (#String String)
  (#Field Reference)
  (#Method Reference)
  (#Interface-Method Reference)
  (#Name-And-Type Name-And-Type))

(def: #export Equivalence<Constant>
  (Equivalence Constant)
  ($_ equivalence.sum
      ## #UTF8
      text.Equivalence<Text>
      ## #Long
      (..Equivalence<Value> number.Equivalence<Int>)
      ## #Double
      (..Equivalence<Value> number.Equivalence<Frac>)
      ## #Class
      ..Equivalence<Class>
      ## #String
      (..Equivalence<Value> //index.Equivalence<Index>)
      ## #Field
      ..Equivalence<Reference>
      ## #Method
      ..Equivalence<Reference>
      ## #Interface-Method
      ..Equivalence<Reference>
      ## #Name-And-Type
      ..Equivalence<Name-And-Type>
      ))

(def: #export format
  (Format Constant)
  (with-expansions [<constants> (as-is [#UTF8             /tag.utf8             ..utf8-format]
                                       ## TODO: Integer
                                       ## TODO: Float
                                       [#Long             /tag.long             ..long-format]
                                       [#Double           /tag.double           ..double-format]
                                       [#Class            /tag.class            ..class-format]
                                       [#String           /tag.string           ..string-format]
                                       [#Field            /tag.field            ..reference-format]
                                       [#Method           /tag.method           ..reference-format]
                                       [#Interface-Method /tag.interface-method ..reference-format]
                                       [#Name-And-Type    /tag.name-and-type    ..name-and-type-format]
                                       ## TODO: Method-Handle
                                       ## TODO: Method-Type
                                       ## TODO: Invoke-Dynamic
                                       )]
    {#binary.reader (do parser.Monad<Parser>
                      [tag (get@ #binary.reader /tag.format)]
                      (`` (cond (~~ (do-template [<case> <tag> <format>]
                                      [(tag/= <tag> tag)
                                       (:: @ map (|>> <case>) (get@ #binary.reader <format>))]

                                      <constants>))
                                
                                ## else
                                (parser.fail "Cannot parse constant."))))
     #binary.writer (function (_ value)
                      (case value
                        (^template [<case> <tag> <format>]
                          (<case> value)
                          (mutation/compose ((get@ #binary.writer /tag.format) <tag>)
                                            ((get@ #binary.writer <format>) value)))
                        (<constants>)
                        ))}))
