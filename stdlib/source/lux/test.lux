(;module: {#;doc "Tools for unit & property-based/generative testing."}
  lux
  (lux [macro #+ Monad<Lux> with-gensyms]
       (macro ["s" syntax #+ syntax: Syntax]
              [code])
       (control functor
                applicative
                ["M" monad #+ do Monad]
                ["p" parser])
       (concurrency [promise #+ Promise Monad<Promise>])
       (data (coll [list "L/" Monad<List> Fold<List>])
             [product]
             [text]
             text/format
             ["E" result])
       [io #- run]
       ["R" math/random]))

## [Host]
(def: now
  (IO Int)
  (io (_lux_proc ["io" "current-time"] [])))

(do-template [<name> <signal>]
  [(def: #hidden <name> (IO Bottom)
     (io (_lux_proc ["io" "exit"] [<signal>])))]

  [exit 0]
  [die  1]
  )

## [Types]
(type: Counters [Nat Nat])

(type: #export Test
  {#;doc "Tests are asynchronous process which may fail."}
  (Promise [Counters Text]))

## [Values]
(def: success Counters [+1 +0])
(def: failure Counters [+0 +1])
(def: start Counters [+0 +0])

(def: (add-counters [s f] [ts tf])
  (-> Counters Counters Counters)
  [(n.+ s ts) (n.+ f tf)])

(def: (fail message)
  (All [a] (-> Text Test))
  (:: Monad<Promise> wrap [failure (format "  [Error] " (%t message))]))

(def: #export (test message condition)
  {#;doc "Check that a condition is true, and fail with the given message otherwise."}
  (-> Text Bool Test)
  (if condition
    (:: Monad<Promise> wrap [success (format "[Success] " message)])
    (:: Monad<Promise> wrap [failure (format "  [Error] " message)])))

(def: #hidden (run' tests)
  (-> (List [Text (IO Test) Text]) (Promise Counters))
  (do Monad<Promise>
    [test-runs (|> tests
                   (L/map (: (-> [Text (IO Test) Text] (Promise Counters))
                             (function [[module test description]]
                               (do @
                                 [#let [pre (io;run now)]
                                  [counters documentation] (io;run test)
                                  #let [post (io;run now)
                                        _ (log! (format "@ " module " (" (%i (i.- pre post)) "ms" ") " "\n"
                                                        description "\n"
                                                        "\n" documentation "\n"))]]
                                 (wrap counters)))))
                   (M;seq @))]
    (wrap (L/fold add-counters start test-runs))))

(def: pcg-32-magic-inc Nat +12345)

(type: #export Seed
  {#;doc "The seed value used for random testing (if that feature is used)."}
  Nat)

(def: failed?
  (-> Counters Bool)
  (|>. product;right (n.> +0)))

(def: (try seed random-test)
  (-> Seed (R;Random Test) (Promise [Seed [Counters Text]]))
  (let [[prng [new-seed test]] (R;run (R;pcg-32 [pcg-32-magic-inc seed])
                                      (do R;Monad<Random>
                                        [test random-test
                                         next-seed R;nat]
                                        (wrap [next-seed test])))]
    (do Monad<Promise>
      [result test]
      (wrap [new-seed result]))))

(def: (repeat' seed times random-test)
  (-> Seed Nat (R;Random Test) Test)
  (if (n.= +0 times)
    (fail "Cannot try a test 0 times.")
    (do Monad<Promise>
      [[seed' [counters documentation]] (try seed random-test)]
      (cond (failed? counters)
            (wrap [counters
                   (format "Context failed with this seed: " (%n seed) "\n" documentation)])

            (n.= +1 times)
            (wrap [counters documentation])

            ## else
            (repeat' seed' (n.dec times) random-test)))))

(def: #hidden (repeat ?seed times random-test)
  (-> (Maybe Nat) Nat (R;Random Test) Test)
  (repeat' (default (int-to-nat (io;run now))
             ?seed)
           (case ?seed
             #;None times
             (#;Some _) +1)
           random-test))

## [Syntax]
(type: Test-Config
  (#Seed Nat)
  (#Times Nat))

(type: Property-Test
  {#seed (Maybe Test-Config)
   #bindings (List [Code Code])
   #body Code})

(type: Test-Kind
  (#Property Property-Test)
  (#Simple Code))

(def: config^
  (Syntax Test-Config)
  (p;alt (do p;Monad<Parser>
           [_ (s;this (' #seed))]
           s;nat)
         (do p;Monad<Parser>
           [_ (s;this (' #times))]
           s;nat)))

(def: property-test^
  (Syntax Property-Test)
  ($_ p;seq
      (p;opt config^)
      (s;tuple (p;some (p;seq s;any s;any)))
      s;any))

(def: test^
  (Syntax Test-Kind)
  (p;alt property-test^
         s;any))

(def: (pair-to-list [x y])
  (All [a] (-> [a a] (List a)))
  (list x y))

(def: #hidden (try-body lazy-body)
  (-> (IO Test) Test)
  (case (_lux_proc ["lux" "try"] [lazy-body])
    (#E;Success output)
    output

    (#E;Error error)
    (test error false)))

(syntax: #export (context: description [body test^])
  {#;doc (doc "Macro for definint tests."
              (context: "Simple macros and constructs"
                ($_ seq
                    (test "Can write easy loops for iterative programming."
                          (i.= 1000
                               (loop [counter 0
                                      value 1]
                                 (if (i.< 3 counter)
                                   (recur (i.inc counter) (i.* 10 value))
                                   value))))

                    (test "Can create lists easily through macros."
                          (and (case (list 1 2 3)
                                 (#;Cons 1 (#;Cons 2 (#;Cons 3 #;Nil)))
                                 true

                                 _
                                 false)
                               
                               (case (list& 1 2 3 (list 4 5 6))
                                 (#;Cons 1 (#;Cons 2 (#;Cons 3 (#;Cons 4 (#;Cons 5 (#;Cons 6 #;Nil))))))
                                 true

                                 _
                                 false)))

                    (test "Can have defaults for Maybe values."
                          (and (is "yolo" (default "yolo"
                                            #;None))
                               
                               (is "lol" (default "yolo"
                                           (#;Some "lol")))))
                    ))
              "Also works with random generation of values for property-based testing."
              (context: "Addition & Substraction"
                [x (:: @ map <prep> rand-gen)
                 y (:: @ map <prep> rand-gen)]
                (test ""
                      (and (|> x (- y) (+ y) (= x))
                           (|> x (+ y) (- y) (= x)))))
              "By default, random tests will be tried 100 times, you can specify the amount you want:"
              (context: "Addition & Substraction"
                #times +1234
                [x (:: @ map <prep> rand-gen)
                 y (:: @ map <prep> rand-gen)]
                (test ""
                      (and (|> x (- y) (+ y) (= x))
                           (|> x (+ y) (- y) (= x)))))
              "If a test fails, you'll be shown a seed that you can then use to reproduce a failing scenario."
              (context: "Addition & Substraction"
                #seed +987654321
                [x (:: @ map <prep> rand-gen)
                 y (:: @ map <prep> rand-gen)]
                (test ""
                      (and (|> x (- y) (+ y) (= x))
                           (|> x (+ y) (- y) (= x)))))
              )}
  (let [body (case body
               (#Property config bindings body)
               (let [[=seed =times] (case config
                                      #;None
                                      [(` #;None) +100]
                                      
                                      (#;Some (#Seed value))
                                      [(` (#;Some (~ (code;nat value)))) +100]

                                      (#;Some (#Times value))
                                      [(` #;None) value])
                     bindings' (|> bindings (L/map pair-to-list) L/join)]
                 (` (repeat (~ =seed)
                            (~ (code;nat =times))
                            (do R;Monad<Random>
                              [(~@ bindings')]
                              ((~' wrap) (;;try-body (io;io (~ body))))))))
               
               (#Simple body)
               body)]
    (with-gensyms [g!test]
      (wrap (list (` (def: #export (~ g!test)
                       {#;;test (#;TextA (~ description))}
                       (IO Test)
                       (io (~ body)))))))))

(def: (exported-tests module-name)
  (-> Text (Lux (List [Text Text Text])))
  (do Monad<Lux>
    [defs (macro;exports module-name)]
    (wrap (|> defs
              (L/map (function [[def-name [_ def-anns _]]]
                       (case (macro;get-text-ann (ident-for #;;test) def-anns)
                         (#;Some description)
                         [true module-name def-name description]

                         _
                         [false module-name def-name ""])))
              (list;filter product;left)
              (L/map product;right)))))

(def: #hidden _appendT_ (-> Text Text Text) (:: text;Monoid<Text> append))
(def: #hidden _%i_ (-> Int Text) %i)

(syntax: #export (run)
  {#;doc (doc "Runs all the tests defined on the current module, and in all imported modules."
              (run))}
  (with-gensyms [g!successes g!failures g!total-successes g!total-failures]
    (do @
      [current-module macro;current-module-name
       modules (macro;imported-modules current-module)
       tests (: (Lux (List [Text Text Text]))
                (|> (#;Cons current-module modules)
                    list;reverse
                    (M;map @ exported-tests)
                    (:: @ map L/join)))
       #let [tests+ (L/map (function [[module-name test desc]]
                             (` [(~ (code;text module-name)) (~ (code;symbol [module-name test])) (~ (code;text desc))]))
                           tests)
             num-tests (list;size tests+)
             groups (list;split-all promise;concurrency-level tests+)]]
      (wrap (list (` (: (IO Unit)
                        (io (exec (do Monad<Promise>
                                    [(~' #let) [(~ g!total-successes) +0
                                                (~ g!total-failures) +0]
                                     (~@ (L/join (L/map (function [group]
                                                          (list (` [(~ g!successes) (~ g!failures)]) (` (run' (list (~@ group))))
                                                                (' #let) (` [(~ g!total-successes) (n.+ (~ g!successes) (~ g!total-successes))
                                                                             (~ g!total-failures) (n.+ (~ g!failures) (~ g!total-failures))])))
                                                        groups)))]
                                    (exec (log! ($_ _appendT_
                                                    "Test-suite finished."
                                                    "\n"
                                                    (_%i_ (nat-to-int (~ g!total-successes)))
                                                    " out of "
                                                    (_%i_ (nat-to-int (n.+ (~ g!total-failures)
                                                                           (~ g!total-successes))))
                                                    " tests passed."
                                                    "\n"
                                                    (_%i_ (nat-to-int (~ g!total-failures))) " tests failed."))
                                      (promise;future (if (n.> +0 (~ g!total-failures))
                                                        ;;die
                                                        ;;exit))))
                              [])))))))))

(def: #export (seq left right)
  {#;doc "Sequencing combinator."}
  (-> Test Test Test)
  (do Monad<Promise>
    [[l-counter l-documentation] left
     [r-counter r-documentation] right]
    (wrap [(add-counters l-counter r-counter)
           (format l-documentation "\n" r-documentation)])))
