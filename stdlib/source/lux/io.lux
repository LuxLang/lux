(.module: {#.doc "A method for abstracting I/O and effectful computations to make it safe while writing pure functional code."}
  lux
  (lux (control [functor #+ Functor]
                [apply #+ Apply]
                [monad #+ do Monad]
                ["ex" exception #+ Exception])
       (data ["e" error #+ Error]
             (coll [list]))))

(type: #export (IO a)
  {#.doc "A type that represents synchronous, effectful computations that may interact with the outside world."}
  (-> Bottom a))

(macro: #export (io tokens state)
  {#.doc (doc "Delays the evaluation of an expression, by wrapping it in an IO 'thunk'."
              "Great for wrapping effectful computations (which will not be performed until the IO is \"run\")."
              (io (exec
                    (log! msg)
                    "Some value...")))}
  (case tokens
    (^ (list value))
    (let [blank (: Code [["" +0 +0] (#.Symbol ["" ""])])]
      (#.Right [state (list (` ("lux function" (~ blank) (~ blank) (~ value))))]))

    _
    (#.Left "Wrong syntax for io")))

(struct: #export _ (Functor IO)
  (def: (map f ma)
    (io (f (ma (:! Bottom []))))))

(struct: #export _ (Apply IO)
  (def: functor Functor<IO>)

  (def: (apply ff fa)
    (io ((ff (:! Bottom [])) (fa (:! Bottom []))))))

(struct: #export _ (Monad IO)
  (def: functor Functor<IO>)

  (def: (wrap x)
    (io x))
  
  (def: (join mma)
    (io ((mma (:! Bottom [])) (:! Bottom [])))))

(def: #export (run action)
  {#.doc "A way to execute IO computations and perform their side-effects."}
  (All [a] (-> (IO a) a))
  (action (:! Bottom [])))

## Process
(type: #export (Process a)
  (IO (Error a)))

(struct: #export _ (Functor Process)
  (def: (map f ma)
    (io (:: e.Functor<Error> map f (run ma)))))

(struct: #export _ (Apply Process)
  (def: functor Functor<Process>)

  (def: (apply ff fa)
    (io (:: e.Apply<Error> apply (run ff) (run fa)))))

(struct: #export _ (Monad Process)
  (def: functor Functor<Process>)

  (def: (wrap x)
    (io (:: e.Monad<Error> wrap x)))
  
  (def: (join mma)
    (case (run mma)
      (#e.Success ma)
      ma
      
      (#e.Error error)
      (io (#e.Error error)))))

(def: #export (fail error)
  (All [a] (-> Text (Process a)))
  (io (#e.Error error)))

(def: #export (throw exception message)
  (All [e a] (-> (Exception e) e (Process a)))
  (io (ex.throw exception message)))
