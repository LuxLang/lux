(.module:
  [lux #*
   [control
    [monoid (#+ Monoid)]
    [equivalence (#+ Equivalence)]
    hash
    [codec (#+ Codec)]]
   function])

## [Structures]
(structure: #export _ (Equivalence Bit)
  (def: (= x y)
    (if x
      y
      (not y))))

(structure: #export _ (Hash Bit)
  (def: eq Equivalence<Bit>)
  (def: (hash value)
    (case value
      true +1
      false +0)))

(do-template [<name> <identity> <op>]
  [(structure: #export <name> (Monoid Bit)
     (def: identity <identity>)
     (def: (compose x y)
       (<op> x y)))]

  [ Or@Monoid<Bit> false or]
  [And@Monoid<Bit> true and]
  )

(structure: #export _ (Codec Text Bit)
  (def: (encode x)
    (if x
      "true"
      "false"))

  (def: (decode input)
    (case input
      "true"  (#.Right true)
      "false" (#.Right false)
      _       (#.Left "Wrong syntax for Bit."))))

## [Values]
(def: #export complement
  {#.doc "Generates the complement of a predicate.
          That is a predicate that returns the oposite of the original predicate."}
  (All [a] (-> (-> a Bit) (-> a Bit)))
  (compose not))
