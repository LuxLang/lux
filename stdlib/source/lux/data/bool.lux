(;module:
  lux
  (lux (control [monoid #+ Monoid]
                [eq #+ Eq]
                hash
                [codec #+ Codec])
       function))

## [Structures]
(struct: #export _ (Eq Bool)
  (def: (= x y)
    (if x
      y
      (not y))))

(struct: #export _ (Hash Bool)
  (def: eq Eq<Bool>)
  (def: (hash value)
    (case value
      true +1
      false +0)))

(do-template [<name> <identity> <op>]
  [(struct: #export <name> (Monoid Bool)
     (def: identity <identity>)
     (def: (compose x y)
       (<op> x y)))]

  [ Or@Monoid<Bool> false or]
  [And@Monoid<Bool> true and]
  )

(struct: #export _ (Codec Text Bool)
  (def: (encode x)
    (if x
      "true"
      "false"))

  (def: (decode input)
    (case input
      "true"  (#;Right true)
      "false" (#;Right false)
      _       (#;Left "Wrong syntax for Bool."))))

## [Values]
(def: #export complement
  {#;doc "Generates the complement of a predicate.
          That is a predicate that returns the oposite of the original predicate."}
  (All [a] (-> (-> a Bool) (-> a Bool)))
  (compose not))
