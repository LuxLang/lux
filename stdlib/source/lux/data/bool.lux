(.module:
  [lux #*
   [control
    [monoid (#+ Monoid)]
    [equivalence (#+ Equivalence)]
    hash
    [codec (#+ Codec)]]
   function])

## [Structures]
(structure: #export _ (Equivalence Bool)
  (def: (= x y)
    (if x
      y
      (not y))))

(structure: #export _ (Hash Bool)
  (def: eq Equivalence<Bool>)
  (def: (hash value)
    (case value
      true +1
      false +0)))

(do-template [<name> <identity> <op>]
  [(structure: #export <name> (Monoid Bool)
     (def: identity <identity>)
     (def: (compose x y)
       (<op> x y)))]

  [ Or@Monoid<Bool> false or]
  [And@Monoid<Bool> true and]
  )

(structure: #export _ (Codec Text Bool)
  (def: (encode x)
    (if x
      "true"
      "false"))

  (def: (decode input)
    (case input
      "true"  (#.Right true)
      "false" (#.Right false)
      _       (#.Left "Wrong syntax for Bool."))))

## [Values]
(def: #export complement
  {#.doc "Generates the complement of a predicate.
          That is a predicate that returns the oposite of the original predicate."}
  (All [a] (-> (-> a Bool) (-> a Bool)))
  (compose not))
