(;module:
  lux
  (lux (control functor
                applicative
                ["M" monad #*])))

## [Types]
(type: #export (Result a)
  (#Error Text)
  (#Success a))

## [Structures]
(struct: #export _ (Functor Result)
  (def: (map f ma)
    (case ma
      (#Error msg)     (#Error msg)
      (#Success datum) (#Success (f datum)))))

(struct: #export _ (Applicative Result)
  (def: functor Functor<Result>)

  (def: (wrap a)
    (#Success a))

  (def: (apply ff fa)
    (case ff
      (#Success f)
      (case fa
        (#Success a)
        (#Success (f a))

        (#Error msg)
        (#Error msg))

      (#Error msg)
      (#Error msg))
    ))

(struct: #export _ (Monad Result)
  (def: applicative Applicative<Result>)

  (def: (join mma)
    (case mma
      (#Error msg) (#Error msg)
      (#Success ma) ma)))

(struct: #export (ResultT Monad<M>)
  (All [M] (-> (Monad M) (Monad (All [a] (M (Result a))))))
  (def: applicative (compA (get@ #M;applicative Monad<M>) Applicative<Result>))
  (def: (join MeMea)
    (do Monad<M>
      [eMea MeMea]
      (case eMea
        (#Error error)
        (wrap (#Error error))

        (#Success Mea)
        Mea))))

(def: #export (lift-result Monad<M>)
  (All [M a] (-> (Monad M) (-> (M a) (M (Result a)))))
  (liftM Monad<M> (:: Monad<Result> wrap)))

(def: #export (succeed value)
  (All [a] (-> a (Result a)))
  (#Success value))

(def: #export (fail message)
  (All [a] (-> Text (Result a)))
  (#Error message))
