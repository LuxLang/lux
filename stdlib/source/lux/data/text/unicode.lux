(.module:
  [lux #*
   [abstract
    [equivalence (#+ Equivalence)]]
   [data
    [collection
     ["." list ("#\." fold functor)]
     ["." set ("#\." equivalence)]
     ["." tree #_
      ["#" finger (#+ Tree)]]]]
   [type (#+ :by-example)
    abstract]]
  ["." / #_
   ["#." segment (#+ Segment)]
   [// (#+ Char)]])

(def: builder
  (tree.builder /segment.monoid))

(def: :@:
  (:by-example [@]
               {(tree.Builder @ Segment)
                ..builder}
               @))

(abstract: #export Set
  (Tree :@: Segment [])

  (def: #export (compose left right)
    (-> Set Set Set)
    (:abstraction
     (\ builder branch
        (:representation left)
        (:representation right))))

  (def: (singleton segment)
    (-> Segment Set)
    (:abstraction
     (\ builder leaf segment [])))

  (def: #export (set [head tail])
    (-> [Segment (List Segment)] Set)
    (list\fold ..compose (..singleton head) (list\map ..singleton tail)))

  (def: half/0
    (..set [/segment.basic-latin
            (list /segment.latin-1-supplement
                  /segment.latin-extended-a
                  /segment.latin-extended-b
                  /segment.ipa-extensions
                  /segment.spacing-modifier-letters
                  /segment.combining-diacritical-marks
                  /segment.greek-and-coptic
                  /segment.cyrillic
                  /segment.cyrillic-supplementary
                  /segment.armenian
                  /segment.hebrew
                  /segment.arabic
                  /segment.syriac
                  /segment.thaana
                  /segment.devanagari
                  /segment.bengali
                  /segment.gurmukhi
                  /segment.gujarati
                  /segment.oriya
                  /segment.tamil
                  /segment.telugu
                  /segment.kannada
                  /segment.malayalam
                  /segment.sinhala
                  /segment.thai
                  /segment.lao
                  /segment.tibetan
                  /segment.myanmar
                  /segment.georgian
                  /segment.hangul-jamo
                  /segment.ethiopic
                  /segment.cherokee
                  /segment.unified-canadian-aboriginal-syllabics
                  /segment.ogham
                  /segment.runic
                  /segment.tagalog
                  /segment.hanunoo
                  /segment.buhid
                  /segment.tagbanwa
                  /segment.khmer
                  /segment.mongolian
                  /segment.limbu
                  /segment.tai-le
                  /segment.khmer-symbols
                  /segment.phonetic-extensions
                  /segment.latin-extended-additional
                  /segment.greek-extended
                  /segment.general-punctuation
                  /segment.superscripts-and-subscripts
                  /segment.currency-symbols
                  /segment.combining-diacritical-marks-for-symbols
                  /segment.letterlike-symbols
                  /segment.number-forms
                  /segment.arrows
                  /segment.mathematical-operators
                  /segment.miscellaneous-technical
                  /segment.control-pictures
                  /segment.optical-character-recognition
                  /segment.enclosed-alphanumerics
                  /segment.box-drawing
                  )]))

  (def: half/1
    (..set [/segment.block-elements
            (list /segment.geometric-shapes
                  /segment.miscellaneous-symbols
                  /segment.dingbats
                  /segment.miscellaneous-mathematical-symbols-a
                  /segment.supplemental-arrows-a
                  /segment.braille-patterns
                  /segment.supplemental-arrows-b
                  /segment.miscellaneous-mathematical-symbols-b
                  /segment.supplemental-mathematical-operators
                  /segment.miscellaneous-symbols-and-arrows
                  /segment.cjk-radicals-supplement
                  /segment.kangxi-radicals
                  /segment.ideographic-description-characters
                  /segment.cjk-symbols-and-punctuation
                  /segment.hiragana
                  /segment.katakana
                  /segment.bopomofo
                  /segment.hangul-compatibility-jamo
                  /segment.kanbun
                  /segment.bopomofo-extended
                  /segment.katakana-phonetic-extensions
                  /segment.enclosed-cjk-letters-and-months
                  /segment.cjk-compatibility
                  /segment.cjk-unified-ideographs-extension-a
                  /segment.yijing-hexagram-symbols
                  /segment.cjk-unified-ideographs
                  /segment.yi-syllables
                  /segment.yi-radicals
                  /segment.hangul-syllables
                  ## /segment.high-surrogates
                  ## /segment.high-private-use-surrogates
                  ## /segment.low-surrogates
                  ## /segment.private-use-area
                  /segment.cjk-compatibility-ideographs
                  /segment.alphabetic-presentation-forms
                  /segment.arabic-presentation-forms-a
                  /segment.variation-selectors
                  /segment.combining-half-marks
                  /segment.cjk-compatibility-forms
                  /segment.small-form-variants
                  /segment.arabic-presentation-forms-b
                  /segment.halfwidth-and-fullwidth-forms
                  /segment.specials
                  ## /segment.linear-b-syllabary
                  ## /segment.linear-b-ideograms
                  ## /segment.aegean-numbers
                  ## /segment.old-italic
                  ## /segment.gothic
                  ## /segment.ugaritic
                  ## /segment.deseret
                  ## /segment.shavian
                  ## /segment.osmanya
                  ## /segment.cypriot-syllabary
                  ## /segment.byzantine-musical-symbols
                  ## /segment.musical-symbols
                  ## /segment.tai-xuan-jing-symbols
                  ## /segment.mathematical-alphanumeric-symbols
                  ## /segment.cjk-unified-ideographs-extension-b
                  ## /segment.cjk-compatibility-ideographs-supplement
                  ## /segment.tags
                  )]))

  (def: #export full
    (..compose ..half/0 ..half/1))

  (def: #export (range set)
    (-> Set [Char Char])
    (let [tag (tree.tag (:representation set))]
      [(/segment.start tag)
       (/segment.end tag)]))

  (def: #export (member? set character)
    (-> Set Char Bit)
    (loop [tree (:representation set)]
      (if (/segment.within? (tree.tag tree) character)
        (case (tree.root tree)
          (0 #0 _)
          true
          
          (0 #1 left right)
          (or (recur left)
              (recur right)))
        false)))

  (structure: #export equivalence
    (Equivalence Set)

    (def: (= reference subject)
      (set\= (set.from-list /segment.hash (tree.tags (:representation reference)))
             (set.from-list /segment.hash (tree.tags (:representation subject))))))
  )

(template [<name> <segments>]
  [(def: #export <name>
     (..set <segments>))]

  [ascii             [/segment.basic-latin (list)]]
  [ascii/alpha       [/segment.basic-latin/upper-alpha (list /segment.basic-latin/lower-alpha)]]
  [ascii/alpha-num   [/segment.basic-latin/upper-alpha (list /segment.basic-latin/lower-alpha /segment.basic-latin/decimal)]]
  [ascii/upper-alpha [/segment.basic-latin/upper-alpha (list)]]
  [ascii/lower-alpha [/segment.basic-latin/lower-alpha (list)]]
  )
