(.module:
  [lux #*
   [abstract
    monad]
   [control
    ["." try]
    ["<>" parser ("#@." monad)
     ["<t>" text (#+ Parser)]
     ["<c>" code]]]
   [data
    ["." product]
    ["." maybe]
    [number (#+ hex)
     ["n" nat ("#@." decimal)]]
    [collection
     ["." list ("#@." fold monad)]]]
   ["." macro (#+ with-gensyms)
    [syntax (#+ syntax:)]
    ["." code]]]
  ["." //
   ["%" format (#+ format)]])

(def: regex-char^
  (Parser Text)
  (<t>.none-of "\.|&()[]{}"))

(def: escaped-char^
  (Parser Text)
  (do <>.monad
    [? (<>.parses? (<t>.this "\"))]
    (if ?
      <t>.any
      regex-char^)))

(def: (refine^ refinement^ base^)
  (All [a] (-> (Parser a) (Parser Text) (Parser Text)))
  (do <>.monad
    [output base^
     _ (<t>.local output refinement^)]
    (wrap output)))

(def: word^
  (Parser Text)
  (<>.either <t>.alpha-num
             (<t>.one-of "_")))

(def: (copy reference)
  (-> Text (Parser Text))
  (<>.after (<t>.this reference) (<>@wrap reference)))

(def: (join-text^ part^)
  (-> (Parser (List Text)) (Parser Text))
  (do <>.monad
    [parts part^]
    (wrap (//.join-with "" parts))))

(def: name-char^
  (Parser Text)
  (<t>.none-of (format "[]{}()s#.<>" //.double-quote)))

(def: name-part^
  (Parser Text)
  (do <>.monad
    [head (refine^ (<t>.not <t>.decimal)
                   name-char^)
     tail (<t>.some name-char^)]
    (wrap (format head tail))))

(def: (name^ current-module)
  (-> Text (Parser Name))
  ($_ <>.either
      (<>.and (<>@wrap current-module) (<>.after (<t>.this "..") name-part^))
      (<>.and name-part^ (<>.after (<t>.this ".") name-part^))
      (<>.and (<>@wrap "lux") (<>.after (<t>.this ".") name-part^))
      (<>.and (<>@wrap "") name-part^)))

(def: (re-var^ current-module)
  (-> Text (Parser Code))
  (do <>.monad
    [name (<t>.enclosed ["\@<" ">"] (name^ current-module))]
    (wrap (` (: (Parser Text) (~ (code.identifier name)))))))

(def: re-range^
  (Parser Code)
  (do {@ <>.monad}
    [from (|> regex-char^ (:: @ map (|>> (//.nth 0) maybe.assume)))
     _ (<t>.this "-")
     to (|> regex-char^ (:: @ map (|>> (//.nth 0) maybe.assume)))]
    (wrap (` (<t>.range (~ (code.nat from)) (~ (code.nat to)))))))

(def: re-char^
  (Parser Code)
  (do <>.monad
    [char escaped-char^]
    (wrap (` ((~! ..copy) (~ (code.text char)))))))

(def: re-options^
  (Parser Code)
  (do <>.monad
    [options (<t>.many escaped-char^)]
    (wrap (` (<t>.one-of (~ (code.text options)))))))

(def: re-user-class^'
  (Parser Code)
  (do <>.monad
    [negate? (<>.maybe (<t>.this "^"))
     parts (<>.many ($_ <>.either
                        re-range^
                        re-options^))]
    (wrap (case negate?
            (#.Some _) (` (<t>.not ($_ <>.either (~+ parts))))
            #.None     (` ($_ <>.either (~+ parts)))))))

(def: re-user-class^
  (Parser Code)
  (do <>.monad
    [_ (wrap [])
     init re-user-class^'
     rest (<>.some (<>.after (<t>.this "&&") (<t>.enclosed ["[" "]"] re-user-class^')))]
    (wrap (list@fold (function (_ refinement base)
                       (` ((~! refine^) (~ refinement) (~ base))))
                     init
                     rest))))

(def: blank^
  (Parser Text)
  (<t>.one-of (format " " //.tab)))

(def: ascii^
  (Parser Text)
  (<t>.range (hex "0") (hex "7F")))

(def: control^
  (Parser Text)
  (<>.either (<t>.range (hex "0") (hex "1F"))
             (<t>.one-of (//.from-code (hex "7F")))))

(def: punct^
  (Parser Text)
  (<t>.one-of (format "!#$%&'()*+,-./:;<=>?@[\]^_`{|}~"
                      //.double-quote)))

(def: graph^
  (Parser Text)
  (<>.either punct^ <t>.alpha-num))

(def: print^
  (Parser Text)
  (<>.either graph^
             (<t>.one-of (//.from-code (hex "20")))))

(def: re-system-class^
  (Parser Code)
  (do <>.monad
    []
    ($_ <>.either
        (<>.after (<t>.this ".") (wrap (` <t>.any)))
        (<>.after (<t>.this "\d") (wrap (` <t>.decimal)))
        (<>.after (<t>.this "\D") (wrap (` (<t>.not <t>.decimal))))
        (<>.after (<t>.this "\s") (wrap (` <t>.space)))
        (<>.after (<t>.this "\S") (wrap (` (<t>.not <t>.space))))
        (<>.after (<t>.this "\w") (wrap (` (~! word^))))
        (<>.after (<t>.this "\W") (wrap (` (<t>.not (~! word^)))))

        (<>.after (<t>.this "\p{Lower}") (wrap (` <t>.lower)))
        (<>.after (<t>.this "\p{Upper}") (wrap (` <t>.upper)))
        (<>.after (<t>.this "\p{Alpha}") (wrap (` <t>.alpha)))
        (<>.after (<t>.this "\p{Digit}") (wrap (` <t>.decimal)))
        (<>.after (<t>.this "\p{Alnum}") (wrap (` <t>.alpha-num)))
        (<>.after (<t>.this "\p{Space}") (wrap (` <t>.space)))
        (<>.after (<t>.this "\p{HexDigit}") (wrap (` <t>.hexadecimal)))
        (<>.after (<t>.this "\p{OctDigit}") (wrap (` <t>.octal)))
        (<>.after (<t>.this "\p{Blank}") (wrap (` (~! blank^))))
        (<>.after (<t>.this "\p{ASCII}") (wrap (` (~! ascii^))))
        (<>.after (<t>.this "\p{Contrl}") (wrap (` (~! control^))))
        (<>.after (<t>.this "\p{Punct}") (wrap (` (~! punct^))))
        (<>.after (<t>.this "\p{Graph}") (wrap (` (~! graph^))))
        (<>.after (<t>.this "\p{Print}") (wrap (` (~! print^))))
        )))

(def: re-class^
  (Parser Code)
  (<>.either re-system-class^
             (<t>.enclosed ["[" "]"] re-user-class^)))

(def: number^
  (Parser Nat)
  (|> (<t>.many <t>.decimal)
      (<>.codec n.decimal)))

(def: re-back-reference^
  (Parser Code)
  (<>.either (do <>.monad
               [_ (<t>.this "\")
                id number^]
               (wrap (` ((~! ..copy) (~ (code.identifier ["" (n@encode id)]))))))
             (do <>.monad
               [_ (<t>.this "\k<")
                captured-name name-part^
                _ (<t>.this ">")]
               (wrap (` ((~! ..copy) (~ (code.identifier ["" captured-name]))))))))

(def: (re-simple^ current-module)
  (-> Text (Parser Code))
  ($_ <>.either
      re-class^
      (re-var^ current-module)
      re-back-reference^
      re-char^
      ))

(def: (re-simple-quantified^ current-module)
  (-> Text (Parser Code))
  (do <>.monad
    [base (re-simple^ current-module)
     quantifier (<t>.one-of "?*+")]
    (case quantifier
      "?"
      (wrap (` (<>.default "" (~ base))))
      
      "*"
      (wrap (` ((~! join-text^) (<>.some (~ base)))))
      
      ## "+"
      _
      (wrap (` ((~! join-text^) (<>.many (~ base)))))
      )))

(def: (re-counted-quantified^ current-module)
  (-> Text (Parser Code))
  (do {@ <>.monad}
    [base (re-simple^ current-module)]
    (<t>.enclosed ["{" "}"]
                  ($_ <>.either
                      (do @
                        [[from to] (<>.and number^ (<>.after (<t>.this ",") number^))]
                        (wrap (` ((~! join-text^) (<>.between (~ (code.nat from))
                                                              (~ (code.nat to))
                                                              (~ base))))))
                      (do @
                        [limit (<>.after (<t>.this ",") number^)]
                        (wrap (` ((~! join-text^) (<>.at-most (~ (code.nat limit)) (~ base))))))
                      (do @
                        [limit (<>.before (<t>.this ",") number^)]
                        (wrap (` ((~! join-text^) (<>.at-least (~ (code.nat limit)) (~ base))))))
                      (do @
                        [limit number^]
                        (wrap (` ((~! join-text^) (<>.exactly (~ (code.nat limit)) (~ base))))))))))

(def: (re-quantified^ current-module)
  (-> Text (Parser Code))
  (<>.either (re-simple-quantified^ current-module)
             (re-counted-quantified^ current-module)))

(def: (re-complex^ current-module)
  (-> Text (Parser Code))
  ($_ <>.either
      (re-quantified^ current-module)
      (re-simple^ current-module)))

(type: Re-Group
  #Non-Capturing
  (#Capturing [(Maybe Text) Nat]))

(def: (re-sequential^ capturing? re-scoped^ current-module)
  (-> Bit
      (-> Text (Parser [Re-Group Code]))
      Text
      (Parser [Nat Code]))
  (do <>.monad
    [parts (<>.many (<>.or (re-complex^ current-module)
                           (re-scoped^ current-module)))
     #let [g!total (code.identifier ["" "0total"])
           g!temp (code.identifier ["" "0temp"])
           [_ names steps] (list@fold (: (-> (Either Code [Re-Group Code])
                                             [Nat (List Code) (List (List Code))]
                                             [Nat (List Code) (List (List Code))])
                                         (function (_ part [idx names steps])
                                           (case part
                                             (^or (#.Left complex) (#.Right [#Non-Capturing complex]))
                                             [idx
                                              names
                                              (list& (list g!temp complex
                                                           (' #let) (` [(~ g!total) (:: (~! //.monoid) (~' compose) (~ g!total) (~ g!temp))]))
                                                     steps)]
                                             
                                             (#.Right [(#Capturing [?name num-captures]) scoped])
                                             (let [[idx! name!] (case ?name
                                                                  (#.Some _name)
                                                                  [idx (code.identifier ["" _name])]

                                                                  #.None
                                                                  [(inc idx) (code.identifier ["" (n@encode idx)])])
                                                   access (if (n.> 0 num-captures)
                                                            (` ((~! product.left) (~ name!)))
                                                            name!)]
                                               [idx!
                                                (list& name! names)
                                                (list& (list name! scoped
                                                             (' #let) (` [(~ g!total) (:: (~! //.monoid) (~' compose) (~ g!total) (~ access))]))
                                                       steps)])
                                             )))
                                      [0
                                       (: (List Code) (list))
                                       (: (List (List Code)) (list))]
                                      parts)]]
    (wrap [(if capturing?
             (list.size names)
             0)
           (` (do <>.monad
                [(~ (' #let)) [(~ g!total) ""]
                 (~+ (|> steps list.reverse list@join))]
                ((~ (' wrap)) [(~ g!total) (~+ (list.reverse names))])))])
    ))

(def: (unflatten^ lexer)
  (-> (Parser Text) (Parser [Text Any]))
  (<>.and lexer (:: <>.monad wrap [])))

(def: (|||^ left right)
  (All [l r] (-> (Parser [Text l]) (Parser [Text r]) (Parser [Text (| l r)])))
  (function (_ input)
    (case (left input)
      (#try.Success [input' [lt lv]])
      (#try.Success [input' [lt (0 lv)]])

      (#try.Failure _)
      (case (right input)
        (#try.Success [input' [rt rv]])
        (#try.Success [input' [rt (1 rv)]])

        (#try.Failure error)
        (#try.Failure error)))))

(def: (|||_^ left right)
  (All [l r] (-> (Parser [Text l]) (Parser [Text r]) (Parser Text)))
  (function (_ input)
    (case (left input)
      (#try.Success [input' [lt lv]])
      (#try.Success [input' lt])

      (#try.Failure _)
      (case (right input)
        (#try.Success [input' [rt rv]])
        (#try.Success [input' rt])

        (#try.Failure error)
        (#try.Failure error)))))

(def: (prep-alternative [num-captures alt])
  (-> [Nat Code] Code)
  (if (n.> 0 num-captures)
    alt
    (` ((~! unflatten^) (~ alt)))))

(def: (re-alternative^ capturing? re-scoped^ current-module)
  (-> Bit
      (-> Text (Parser [Re-Group Code]))
      Text
      (Parser [Nat Code]))
  (do <>.monad
    [#let [sub^ (re-sequential^ capturing? re-scoped^ current-module)]
     head sub^
     tail (<>.some (<>.after (<t>.this "|") sub^))]
    (if (list.empty? tail)
      (wrap head)
      (wrap [(list@fold n.max (product.left head) (list@map product.left tail))
             (` ($_ ((~ (if capturing?
                          (` (~! |||^))
                          (` (~! |||_^)))))
                    (~ (prep-alternative head))
                    (~+ (list@map prep-alternative tail))))]))))

(def: (re-scoped^ current-module)
  (-> Text (Parser [Re-Group Code]))
  ($_ <>.either
      (do <>.monad
        [_ (<t>.this "(?:")
         [_ scoped] (re-alternative^ #0 re-scoped^ current-module)
         _ (<t>.this ")")]
        (wrap [#Non-Capturing scoped]))
      (do <>.monad
        [complex (re-complex^ current-module)]
        (wrap [#Non-Capturing complex]))
      (do <>.monad
        [_ (<t>.this "(?<")
         captured-name name-part^
         _ (<t>.this ">")
         [num-captures pattern] (re-alternative^ #1 re-scoped^ current-module)
         _ (<t>.this ")")]
        (wrap [(#Capturing [(#.Some captured-name) num-captures]) pattern]))
      (do <>.monad
        [_ (<t>.this "(")
         [num-captures pattern] (re-alternative^ #1 re-scoped^ current-module)
         _ (<t>.this ")")]
        (wrap [(#Capturing [#.None num-captures]) pattern]))))

(def: (regex^ current-module)
  (-> Text (Parser Code))
  (:: <>.monad map product.right (re-alternative^ #1 re-scoped^ current-module)))

(syntax: #export (regex {pattern <c>.text})
  {#.doc (doc "Create lexers using regular-expression syntax."
              "For example:"
              
              "Literals"
              (regex "a")
              
              "Wildcards"
              (regex ".")
              
              "Escaping"
              (regex "\.")
              
              "Character classes"
              (regex "\d")
              (regex "\p{Lower}")
              (regex "[abc]")
              (regex "[a-z]")
              (regex "[a-zA-Z]")
              (regex "[a-z&&[def]]")
              
              "Negation"
              (regex "[^abc]")
              (regex "[^a-z]")
              (regex "[^a-zA-Z]")
              (regex "[a-z&&[^bc]]")
              (regex "[a-z&&[^m-p]]")
              
              "Combinations"
              (regex "aa")
              (regex "a?")
              (regex "a*")
              (regex "a+")
              
              "Specific amounts"
              (regex "a{2}")
              
              "At least"
              (regex "a{1,}")
              
              "At most"
              (regex "a{,1}")
              
              "Between"
              (regex "a{1,2}")
              
              "Groups"
              (regex "a(.)c")
              (regex "a(b+)c")
              (regex "(\d{3})-(\d{3})-(\d{4})")
              (regex "(\d{3})-(?:\d{3})-(\d{4})")
              (regex "(?<code>\d{3})-\k<code>-(\d{4})")
              (regex "(?<code>\d{3})-\k<code>-(\d{4})-\0")
              (regex "(\d{3})-((\d{3})-(\d{4}))")
              
              "Alternation"
              (regex "a|b")
              (regex "a(.)(.)|b(.)(.)")
              )}
  (do macro.monad
    [current-module macro.current-module-name]
    (case (<t>.run (regex^ current-module)
                   pattern)
      (#try.Failure error)
      (macro.fail (format "Error while parsing regular-expression:" //.new-line
                          error))

      (#try.Success regex)
      (wrap (list regex))
      )))

(syntax: #export (^regex {[pattern bindings] (<c>.form (<>.and <c>.text (<>.maybe <c>.any)))}
                         body
                         {branches (<>.many <c>.any)})
  {#.doc (doc "Allows you to test text against regular expressions."
              (case some-text
                (^regex "(\d{3})-(\d{3})-(\d{4})"
                        [_ country-code area-code place-code])
                do-some-thing-when-number

                (^regex "\w+")
                do-some-thing-when-word

                _
                do-something-else))}
  (with-gensyms [g!temp]
    (wrap (list& (` (^multi (~ g!temp)
                            [((~! <t>.run) (..regex (~ (code.text pattern))) (~ g!temp))
                             (#try.Success (~ (maybe.default g!temp bindings)))]))
                 body
                 branches))))
