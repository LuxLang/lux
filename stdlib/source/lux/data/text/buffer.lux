(.module:
  [lux #*
   [host (#+ import:)]
   ["@" target]
   [control
    ["." function]]
   [data
    ["." product]
    [text
     ["%" format (#+ format)]]
    [collection
     ["." row (#+ Row) ("#\." fold)]]]
   [math
    [number
     ["n" nat]]]
   [type
    abstract]]
  ["." //])

(with_expansions [<jvm> (as_is (import: java/lang/CharSequence)

                               (import: java/lang/Appendable
                                 ["#::."
                                  (append [java/lang/CharSequence] java/lang/Appendable)])

                               (import: java/lang/String
                                 ["#::."
                                  (new [int])
                                  (toString [] java/lang/String)])

                               (import: java/lang/StringBuilder
                                 ["#::."
                                  (new [int])
                                  (toString [] java/lang/String)]))]
  (`` (for {@.old (as_is <jvm>)
            @.jvm (as_is <jvm>)})))

(`` (abstract: #export Buffer
      (for {@.old [Nat (-> java/lang/StringBuilder java/lang/StringBuilder)]
            @.jvm [Nat (-> java/lang/StringBuilder java/lang/StringBuilder)]}
           ## default
           (Row Text))

      {#.doc "Immutable text buffer for efficient text concatenation."}

      (def: #export empty
        Buffer
        (:abstraction (with_expansions [<jvm> [0 function.identity]]
                        (for {@.old <jvm>
                              @.jvm <jvm>}
                             ## default
                             row.empty))))

      (def: #export (append chunk buffer)
        (-> Text Buffer Buffer)
        (with_expansions [<jvm> (let [[capacity transform] (:representation buffer)
                                      append! (: (-> Text java/lang/StringBuilder java/lang/StringBuilder)
                                                 (function (_ chunk builder)
                                                   (exec (java/lang/Appendable::append (:coerce java/lang/CharSequence chunk)
                                                                                       builder)
                                                     builder)))]
                                  (:abstraction [(n.+ (//.size chunk) capacity)
                                                 (|>> transform (append! chunk))]))]
          (for {@.old <jvm>
                @.jvm <jvm>}
               ## default
               (|> buffer :representation (row.add chunk) :abstraction))))

      (def: #export size
        (-> Buffer Nat)
        (with_expansions [<jvm> (|>> :representation product.left)]
          (for {@.old <jvm>
                @.jvm <jvm>}
               ## default
               (|>> :representation
                    (row\fold (function (_ chunk total)
                                (n.+ (//.size chunk) total))
                              0)))))

      (def: #export (text buffer)
        (-> Buffer Text)
        (with_expansions [<jvm> (let [[capacity transform] (:representation buffer)]
                                  (|> (java/lang/StringBuilder::new (.int capacity))
                                      transform
                                      java/lang/StringBuilder::toString))]
          (for {@.old <jvm>
                @.jvm <jvm>}
               ## default
               (row\fold (function (_ chunk total)
                           (format total chunk))
                         ""
                         (:representation buffer)))))
      ))
