##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;module:
  lux
  (lux/control (functor #as F #refer #all)
               (applicative #as A #refer #all)
               (monad #as M #refer #all)
               (comonad #as CM #refer #all)))

## [Types]
(type: #export (Identity a)
  a)

## [Structures]
(struct: #export _ (Functor Identity)
  (def: map id))

(struct: #export _ (Applicative Identity)
  (def: functor Functor<Identity>)

  (def: wrap id)

  (def: (apply ff fa)
    (ff fa)))

(struct: #export _ (Monad Identity)
  (def: applicative Applicative<Identity>)
  
  (def: join id))

(struct: #export _ (CoMonad Identity)
  (def: functor Functor<Identity>)
  (def: unwrap id)
  (def: split id))
