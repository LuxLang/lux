(.module:
  [lux #*
   [control
    [functor (#+ Functor)]
    [apply (#+ Apply)]
    [monad (#+ do Monad)]
    [equivalence (#+ Equivalence)]
    monoid
    fold
    ["p" parser]
    [predicate (#+ Predicate)]]
   [data
    ["." maybe]
    ["." product]
    [number
     ["." i64]]
    [collection
     ["." list ("#@." fold functor monoid)]
     ["." array (#+ Array) ("#@." functor fold)]]]
   [macro (#+ with-gensyms)
    ["." code]
    ["s" syntax (#+ syntax: Syntax)]]])

(type: (Node a)
  (#Base (Array a))
  (#Hierarchy (Array (Node a))))

(type: (Base a) (Array a))
(type: (Hierarchy a) (Array (Node a)))

(type: Level Nat)

(type: Index Nat)

(def: branching-exponent
  Nat
  5)

(def: root-level
  Level
  0)

(template [<name> <op>]
  [(def: <name>
     (-> Level Level)
     (<op> branching-exponent))]

  [level-up   n/+]
  [level-down n/-]
  )

(def: full-node-size
  Nat
  (i64.left-shift branching-exponent 1))

(def: branch-idx-mask
  Nat
  (dec full-node-size))

(def: branch-idx
  (-> Index Index)
  (i64.and branch-idx-mask))

(def: (new-hierarchy _)
  (All [a] (-> Any (Hierarchy a)))
  (array.new full-node-size))

(def: (tail-off row-size)
  (-> Nat Nat)
  (if (n/< full-node-size row-size)
    0
    (|> (dec row-size)
        (i64.logic-right-shift branching-exponent)
        (i64.left-shift branching-exponent))))

(def: (new-path level tail)
  (All [a] (-> Level (Base a) (Node a)))
  (if (n/= 0 level)
    (#Base tail)
    (|> (new-hierarchy [])
        (array.write 0 (new-path (level-down level) tail))
        #Hierarchy)))

(def: (new-tail singleton)
  (All [a] (-> a (Base a)))
  (|> (array.new 1)
      (array.write 0 singleton)))

(def: (push-tail size level tail parent)
  (All [a] (-> Nat Level (Base a) (Hierarchy a) (Hierarchy a)))
  (let [sub-idx (branch-idx (i64.logic-right-shift level (dec size)))
        ## If we're currently on a bottom node
        sub-node (if (n/= branching-exponent level)
                   ## Just add the tail to it
                   (#Base tail)
                   ## Otherwise, check whether there's a vacant spot
                   (case (array.read sub-idx parent)
                     ## If so, set the path to the tail
                     #.None
                     (new-path (level-down level) tail)
                     ## If not, push the tail onto the sub-node.
                     (#.Some (#Hierarchy sub-node))
                     (#Hierarchy (push-tail size (level-down level) tail sub-node))

                     _
                     (undefined))
                   )]
    (|> (array.clone parent)
        (array.write sub-idx sub-node))))

(def: (expand-tail val tail)
  (All [a] (-> a (Base a) (Base a)))
  (let [tail-size (array.size tail)]
    (|> (array.new (inc tail-size))
        (array.copy tail-size 0 tail 0)
        (array.write tail-size val))))

(def: (put' level idx val hierarchy)
  (All [a] (-> Level Index a (Hierarchy a) (Hierarchy a)))
  (let [sub-idx (branch-idx (i64.logic-right-shift level idx))]
    (case (array.read sub-idx hierarchy)
      (#.Some (#Hierarchy sub-node))
      (|> (array.clone hierarchy)
          (array.write sub-idx (#Hierarchy (put' (level-down level) idx val sub-node))))

      (^multi (#.Some (#Base base))
              (n/= 0 (level-down level)))
      (|> (array.clone hierarchy)
          (array.write sub-idx (|> (array.clone base)
                                   (array.write (branch-idx idx) val)
                                   #Base)))

      _
      (undefined))))

(def: (pop-tail size level hierarchy)
  (All [a] (-> Nat Level (Hierarchy a) (Maybe (Hierarchy a))))
  (let [sub-idx (branch-idx (i64.logic-right-shift level (n/- 2 size)))]
    (cond (n/= 0 sub-idx)
          #.None

          (n/> branching-exponent level)
          (do maybe.monad
            [base|hierarchy (array.read sub-idx hierarchy)
             sub (case base|hierarchy
                   (#Hierarchy sub)
                   (pop-tail size (level-down level) sub)

                   (#Base _)
                   (undefined))]
            (|> (array.clone hierarchy)
                (array.write sub-idx (#Hierarchy sub))
                #.Some))

          ## Else...
          (|> (array.clone hierarchy)
              (array.delete sub-idx)
              #.Some)
          )))

(def: (to-list' node)
  (All [a] (-> (Node a) (List a)))
  (case node
    (#Base base)
    (array.to-list base)
    
    (#Hierarchy hierarchy)
    (|> hierarchy
        array.to-list
        list.reverse
        (list@fold (function (_ sub acc) (list@compose (to-list' sub) acc))
                   #.Nil))))

(type: #export (Row a)
  {#level Level
   #size Nat
   #root (Hierarchy a)
   #tail (Base a)})

(def: #export empty
  Row
  {#level (level-up root-level)
   #size 0
   #root (array.new full-node-size)
   #tail (array.new 0)})

(def: #export (size row)
  (All [a] (-> (Row a) Nat))
  (get@ #size row))

(def: #export (add val row)
  (All [a] (-> a (Row a) (Row a)))
  ## Check if there is room in the tail.
  (let [row-size (get@ #size row)]
    (if (|> row-size (n/- (tail-off row-size)) (n/< full-node-size))
      ## If so, append to it.
      (|> row
          (update@ #size inc)
          (update@ #tail (expand-tail val)))
      ## Otherwise, push tail into the tree
      ## --------------------------------------------------------
      ## Will the root experience an overflow with this addition?
      (|> (if (n/> (i64.left-shift (get@ #level row) 1)
                   (i64.logic-right-shift branching-exponent row-size))
            ## If so, a brand-new root must be established, that is
            ## 1-level taller.
            (|> row
                (set@ #root (|> (: (Hierarchy ($ 0))
                                   (new-hierarchy []))
                                ## TODO: Remove version above once new-luxc becomes the standard compiler.
                                ## (new-hierarchy [])
                                (array.write 0 (#Hierarchy (get@ #root row)))
                                (array.write 1 (new-path (get@ #level row) (get@ #tail row)))))
                (update@ #level level-up))
            ## Otherwise, just push the current tail onto the root.
            (|> row
                (update@ #root (push-tail row-size (get@ #level row) (get@ #tail row)))))
          ## Finally, update the size of the row and grow a new
          ## tail with the new element as it's sole member.
          (update@ #size inc)
          (set@ #tail (new-tail val)))
      )))

(def: (base-for idx row)
  (All [a] (-> Index (Row a) (Maybe (Base a))))
  (let [row-size (get@ #size row)]
    (if (and (n/>= 0 idx)
             (n/< row-size idx))
      (if (n/>= (tail-off row-size) idx)
        (#.Some (get@ #tail row))
        (loop [level (get@ #level row)
               hierarchy (get@ #root row)]
          (case [(n/> branching-exponent level)
                 (array.read (branch-idx (i64.logic-right-shift level idx)) hierarchy)]
            [#1 (#.Some (#Hierarchy sub))]
            (recur (level-down level) sub)

            [#0 (#.Some (#Base base))]
            (#.Some base)

            [_ #.None]
            #.None

            _
            (error! "Incorrect row structure."))))
      #.None)))

(def: #export (nth idx row)
  (All [a] (-> Nat (Row a) (Maybe a)))
  (do maybe.monad
    [base (base-for idx row)]
    (array.read (branch-idx idx) base)))

(def: #export (put idx val row)
  (All [a] (-> Nat a (Row a) (Row a)))
  (let [row-size (get@ #size row)]
    (if (and (n/>= 0 idx)
             (n/< row-size idx))
      (if (n/>= (tail-off row-size) idx)
        (|> row
            ## (update@ #tail (|>> array.clone (array.write (branch-idx idx) val)))
            ## TODO: Remove once new-luxc becomes the standard compiler.
            (update@ #tail (: (-> (Base ($ 0)) (Base ($ 0)))
                              (|>> array.clone (array.write (branch-idx idx) val))))
            )
        (|> row
            (update@ #root (put' (get@ #level row) idx val))))
      row)))

(def: #export (update idx f row)
  (All [a] (-> Nat (-> a a) (Row a) (Row a)))
  (case (nth idx row)
    (#.Some val)
    (put idx (f val) row)

    #.None
    row))

(def: #export (pop row)
  (All [a] (-> (Row a) (Row a)))
  (case (get@ #size row)
    0
    empty

    1
    empty

    row-size
    (if (|> row-size (n/- (tail-off row-size)) (n/> 1))
      (let [old-tail (get@ #tail row)
            new-tail-size (dec (array.size old-tail))]
        (|> row
            (update@ #size dec)
            (set@ #tail (|> (array.new new-tail-size)
                            (array.copy new-tail-size 0 old-tail 0)))))
      (maybe.assume
       (do maybe.monad
         [new-tail (base-for (n/- 2 row-size) row)
          #let [[level' root'] (let [init-level (get@ #level row)]
                                 (loop [level init-level
                                        root (maybe.default (new-hierarchy [])
                                                            (pop-tail row-size init-level (get@ #root row)))]
                                   (if (n/> branching-exponent level)
                                     (case [(array.read 1 root) (array.read 0 root)]
                                       [#.None (#.Some (#Hierarchy sub-node))]
                                       (recur (level-down level) sub-node)

                                       ## [#.None (#.Some (#Base _))]
                                       ## (undefined)

                                       _
                                       [level root])
                                     [level root])))]]
         (wrap (|> row
                   (update@ #size dec)
                   (set@ #level level')
                   (set@ #root root')
                   (set@ #tail new-tail))))))
    ))

(def: #export (to-list row)
  (All [a] (-> (Row a) (List a)))
  (list@compose (to-list' (#Hierarchy (get@ #root row)))
                (to-list' (#Base (get@ #tail row)))))

(def: #export from-list
  (All [a] (-> (List a) (Row a)))
  (list@fold ..add ..empty))

(def: #export (member? a/Equivalence row val)
  (All [a] (-> (Equivalence a) (Row a) a Bit))
  (list.member? a/Equivalence (to-list row) val))

(def: #export empty?
  (All [a] (-> (Row a) Bit))
  (|>> (get@ #size) (n/= 0)))

(syntax: #export (row {elems (p.some s.any)})
  {#.doc (doc "Row literals."
              (row +10 +20 +30 +40))}
  (wrap (list (` (from-list (list (~+ elems)))))))

(structure: #export (node-equivalence Equivalence<a>) (All [a] (-> (Equivalence a) (Equivalence (Node a))))
  (def: (= v1 v2)
    (case [v1 v2]
      [(#Base b1) (#Base b2)]
      (:: (array.equivalence Equivalence<a>) = b1 b2)
      
      [(#Hierarchy h1) (#Hierarchy h2)]
      (:: (array.equivalence (node-equivalence Equivalence<a>)) = h1 h2)

      _
      #0)))

(structure: #export (equivalence Equivalence<a>) (All [a] (-> (Equivalence a) (Equivalence (Row a))))
  (def: (= v1 v2)
    (and (n/= (get@ #size v1) (get@ #size v2))
         (let [(^open "Node/.") (node-equivalence Equivalence<a>)]
           (and (Node/= (#Base (get@ #tail v1))
                        (#Base (get@ #tail v2)))
                (Node/= (#Hierarchy (get@ #root v1))
                        (#Hierarchy (get@ #root v2))))))))

(structure: node-fold (Fold Node)
  (def: (fold f init xs)
    (case xs
      (#Base base)
      (array@fold f init base)
      
      (#Hierarchy hierarchy)
      (array@fold (function (_ node init') (fold f init' node))
                  init
                  hierarchy))))

(structure: #export fold (Fold Row)
  (def: (fold f init xs)
    (let [(^open ".") node-fold]
      (fold f
            (fold f
                  init
                  (#Hierarchy (get@ #root xs)))
            (#Base (get@ #tail xs))))))

(structure: #export monoid (All [a] (Monoid (Row a)))
  (def: identity ..empty)
  
  (def: (compose xs ys)
    (list@fold add xs (..to-list ys))))

(structure: node-functor (Functor Node)
  (def: (map f xs)
    (case xs
      (#Base base)
      (#Base (array@map f base))
      
      (#Hierarchy hierarchy)
      (#Hierarchy (array@map (map f) hierarchy)))))

(structure: #export functor (Functor Row)
  (def: (map f xs)
    {#level (get@ #level xs)
     #size (get@ #size xs)
     #root (|> xs (get@ #root) (array@map (:: node-functor map f)))
     #tail (|> xs (get@ #tail) (array@map f))}))

(structure: #export apply (Apply Row)
  (def: &functor ..functor)

  (def: (apply ff fa)
    (let [(^open ".") ..functor
          (^open ".") ..fold
          (^open ".") ..monoid
          results (map (function (_ f) (map f fa))
                       ff)]
      (fold compose identity results))))

(structure: #export monad (Monad Row)
  (def: &functor ..functor)

  (def: wrap (|>> row))

  (def: join
    (let [(^open ".") ..fold
          (^open ".") ..monoid]
      (fold (function (_ post pre) (compose pre post)) identity))))

(def: #export reverse
  (All [a] (-> (Row a) (Row a)))
  (|>> ..to-list list.reverse (list@fold add ..empty)))

(template [<name> <array> <init> <op>]
  [(def: #export <name>
     (All [a]
       (-> (Predicate a) (Row a) Bit))
     (let [help (: (All [a]
                     (-> (Predicate a) (Node a) Bit))
                   (function (help predicate node)
                     (case node
                       (#Base base)
                       (<array> predicate base)

                       (#Hierarchy hierarchy)
                       (<array> (help predicate) hierarchy))))]
       (function (<name> predicate row)
         (let [(^slots [#root #tail]) row]
           (<op> (help predicate (#Hierarchy root))
                 (help predicate (#Base tail)))))))]

  [every? array.every? #1 and]
  [any?   array.any?   #0 or]
  )
