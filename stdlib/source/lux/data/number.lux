(.module:
  [lux #*
   [control
    [codec (#+ Codec)]]
   [data
    ["." error (#+ Error)]
    ["." text]]]
  [/
   ["/." nat]
   ["/." int]
   ["/." rev]
   ["/." frac]])

(macro: (encoding-doc tokens state)
  (case tokens
    (^ (list [cursor (#.Text encoding)] example-1 example-2))
    (let [encoding ($_ "lux text concat"
                       "Given syntax for a "
                       encoding
                       " number, generates a Nat, an Int, a Rev or a Frac.")
          underscore "Allows for the presence of underscore in the numbers."
          description [cursor (#.Text ($_ "lux text concat" encoding " " underscore))]]
      (#error.Success [state (list (` (doc (~ description)
                                           (~ example-1)
                                           (~ example-2))))]))

    _
    (#error.Failure "Wrong syntax for 'encoding-doc'.")))

(def: (underscore-prefixed? number)
  (-> Text Bit)
  (case ("lux text index" number "_" 0)
    (#.Some 0)
    #1

    _
    #0))

(def: clean-underscores
  (-> Text Text)
  (text.replace-all "_" ""))

(do-template [<macro> <nat> <int> <rev> <frac> <error> <doc>]
  [(macro: #export (<macro> tokens state)
     {#.doc <doc>}
     (case tokens
       (#.Cons [meta (#.Text repr')] #.Nil)
       (if (underscore-prefixed? repr')
         (#error.Failure <error>)
         (let [repr (clean-underscores repr')]
           (case (:: <nat> decode repr)
             (#error.Success value)
             (#error.Success [state (list [meta (#.Nat value)])])

             (^multi (#error.Failure _)
                     [(:: <int> decode repr) (#error.Success value)])
             (#error.Success [state (list [meta (#.Int value)])])

             (^multi (#error.Failure _)
                     [(:: <rev> decode repr) (#error.Success value)])
             (#error.Success [state (list [meta (#.Rev value)])])

             (^multi (#error.Failure _)
                     [(:: <frac> decode repr) (#error.Success value)])
             (#error.Success [state (list [meta (#.Frac value)])])

             _
             (#error.Failure <error>))))

       _
       (#error.Failure <error>)))]

  [bin /nat.binary /int.binary /rev.binary /frac.binary
   "Invalid binary syntax."
   (encoding-doc "binary" (bin "+11001001") (bin "+11_00_10_01"))]
  [oct /nat.octal  /int.octal  /rev.octal  /frac.octal
   "Invalid octal syntax."
   (encoding-doc "octal" (oct "+615243") (oct "+615_243"))]
  [hex /nat.hex    /int.hex    /rev.hex    /frac.hex
   "Invalid hexadecimal syntax."
   (encoding-doc "hexadecimal" (hex "deadBEEF") (hex "dead_BEEF"))]
  )
