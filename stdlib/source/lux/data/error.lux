(.module:
  [lux #*
   [control
    ["." functor (#+ Functor)]
    [apply (#+ Apply)]
    ["." monad (#+ Monad do)]
    [equivalence (#+ Equivalence)]]])

(type: #export (Error a)
  (#Failure Text)
  (#Success a))

(structure: #export functor (Functor Error)
  (def: (map f ma)
    (case ma
      (#Failure msg)
      (#Failure msg)
      
      (#Success datum)
      (#Success (f datum)))))

(structure: #export apply (Apply Error)
  (def: &functor ..functor)

  (def: (apply ff fa)
    (case ff
      (#Success f)
      (case fa
        (#Success a)
        (#Success (f a))

        (#Failure msg)
        (#Failure msg))

      (#Failure msg)
      (#Failure msg))
    ))

(structure: #export monad (Monad Error)
  (def: &functor ..functor)

  (def: (wrap a)
    (#Success a))

  (def: (join mma)
    (case mma
      (#Failure msg)
      (#Failure msg)
      
      (#Success ma)
      ma)))

(structure: #export (with monad)
  ## TODO: Replace (All [a] (M (Error a))) with (functor.Then M Error)
  (All [M] (-> (Monad M) (Monad (All [a] (M (Error a))))))
  
  (def: &functor (functor.compose (get@ #monad.&functor monad) ..functor))

  (def: wrap (|>> (:: ..monad wrap) (:: monad wrap)))
  
  (def: (join MeMea)
    (do monad
      [eMea MeMea]
      (case eMea
        (#Failure error)
        (wrap (#Failure error))

        (#Success Mea)
        Mea))))

(def: #export (lift monad)
  (All [M a] (-> (Monad M) (-> (M a) (M (Error a)))))
  (monad.lift monad (:: ..monad wrap)))

(structure: #export (equivalence (^open ",@."))
  (All [a] (-> (Equivalence a) (Equivalence (Error a))))
  (def: (= reference sample)
    (case [reference sample]
      [(#Success reference) (#Success sample)]
      (,@= reference sample)

      [(#Failure reference) (#Failure sample)]
      ("lux text =" reference sample)

      _
      false
      )))

(def: #export (succeed value)
  (All [a] (-> a (Error a)))
  (#Success value))

(def: #export (fail message)
  (-> Text Error)
  (#Failure message))

(def: #export (assume error)
  (All [a] (-> (Error a) a))
  (case error
    (#Success value)
    value

    (#Failure message)
    (error! message)))

(macro: #export (default tokens compiler)
  {#.doc (doc "Allows you to provide a default value that will be used"
              "if a (Error x) value turns out to be #Failure."
              (= "bar"
                 (default "foo" (#Success "bar")))
              (= "foo"
                 (default "foo" (#Failure "KABOOM!"))))}
  (case tokens
    (^ (list else error))
    (#Success [compiler (list (` (case (~ error)
                                   (#..Success (~' g!temp))
                                   (~' g!temp)

                                   (#..Failure (~ [dummy-cursor (#.Identifier ["" ""])]))
                                   (~ else))))])

    _
    (#Failure "Wrong syntax for default")))
