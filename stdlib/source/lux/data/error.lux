(.module:
  [lux #*
   [control
    ["F" functor]
    ["A" apply]
    ["M" monad (#+ do Monad)]]])

## [Types]
(type: #export (Error a)
  (#Failure Text)
  (#Success a))

## [Structures]
(structure: #export _ (F.Functor Error)
  (def: (map f ma)
    (case ma
      (#Failure msg)
      (#Failure msg)
      
      (#Success datum)
      (#Success (f datum)))))

(structure: #export _ (A.Apply Error)
  (def: functor Functor<Error>)

  (def: (apply ff fa)
    (case ff
      (#Success f)
      (case fa
        (#Success a)
        (#Success (f a))

        (#Failure msg)
        (#Failure msg))

      (#Failure msg)
      (#Failure msg))
    ))

(structure: #export _ (Monad Error)
  (def: functor Functor<Error>)

  (def: (wrap a)
    (#Success a))

  (def: (join mma)
    (case mma
      (#Failure msg)
      (#Failure msg)
      
      (#Success ma)
      ma)))

(structure: #export (ErrorT Monad<M>)
  (All [M] (-> (Monad M) (Monad (All [a] (M (Error a))))))
  
  (def: functor (F.compose (get@ #M.functor Monad<M>) Functor<Error>))

  (def: wrap (|>> (:: Monad<Error> wrap) (:: Monad<M> wrap)))
  
  (def: (join MeMea)
    (do Monad<M>
      [eMea MeMea]
      (case eMea
        (#Failure error)
        (wrap (#Failure error))

        (#Success Mea)
        Mea))))

(def: #export (lift Monad<M>)
  (All [M a] (-> (Monad M) (-> (M a) (M (Error a)))))
  (M.lift Monad<M> (:: Monad<Error> wrap)))

(def: #export (succeed value)
  (All [a] (-> a (Error a)))
  (#Success value))

(def: #export (fail message)
  (All [a] (-> Text (Error a)))
  (#Failure message))

(def: #export (assume error)
  (All [a] (-> (Error a) a))
  (case error
    (#Success value)
    value

    (#Failure message)
    (error! message)))

(macro: #export (default tokens compiler)
  {#.doc (doc "Allows you to provide a default value that will be used"
              "if a (Error x) value turns out to be #Failure."
              (is? +10
                   (default +20 (#Success +10)))
              (is? +20
                   (default +20 (#Failure "KABOOM!"))))}
  (case tokens
    (^ (list else error))
    (#Success [compiler (list (` (case (~ error)
                                   (#..Success (~' g!temp))
                                   (~' g!temp)

                                   (#..Failure (~ [dummy-cursor (#.Identifier ["" ""])]))
                                   (~ else))))])

    _
    (#Failure "Wrong syntax for default")))
