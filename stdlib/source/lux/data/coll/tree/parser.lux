(;module:
  lux
  (lux (control ["p" parser]
                ["ex" exception #+ exception:])
       (data ["R" result]))
  (.. ["T" rose]
      ["Z" zipper]))

(type: #export (Parser t a)
  (p;Parser (Z;Zipper t) a))

(def: #export (run-zipper zipper parser)
  (All [t a] (-> (Z;Zipper t) (Parser t a) (R;Result a)))
  (case (p;run zipper parser)
    (#R;Success [zipper output])
    (#R;Success output)

    (#R;Error error)
    (#R;Error error)))

(def: #export (run tree parser)
  (All [t a] (-> (T;Tree t) (Parser t a) (R;Result a)))
  (run-zipper (Z;zip tree) parser))

(def: #export value
  (All [t] (Parser t t))
  (function [zipper]
    (#R;Success [zipper (Z;value zipper)])))

(exception: #export Cannot-Move-Further)

(do-template [<name> <direction>]
  [(def: #export <name>
     (All [t] (Parser t []))
     (function [zipper]
       (let [next (<direction> zipper)]
         (if (is zipper next)
           (ex;throw Cannot-Move-Further "")
           (#R;Success [next []])))))]

  [up        Z;up]
  [down      Z;down]
  [left      Z;left]
  [right     Z;right]
  [root      Z;root]
  [rightmost Z;rightmost]
  [leftmost  Z;leftmost]
  [next      Z;next]
  [prev      Z;prev]
  )
