(.module: {#.doc "Functionality for reading, generating and processing values in the XML format."}
  [lux #*
   [control
    monad
    [equivalence (#+ Equivalence)]
    codec
    ["p" parser ("#;." monad)]
    ["ex" exception (#+ exception:)]]
   [data
    ["." error (#+ Error)]
    ["." product]
    ["." name ("#;." equivalence codec)]
    [number
     ["." int]]
    ["." text ("#;." equivalence monoid)
     ["l" lexer]]
    [collection
     ["." list ("#;." monad)]
     ["d" dictionary]]]])

(type: #export Tag Name)
(type: #export Attrs (d.Dictionary Name Text))

(def: #export attrs Attrs (d.new name.hash))

(type: #export #rec XML
  (#Text Text)
  (#Node Tag Attrs (List XML)))

(def: xml-standard-escape-char^
  (l.Lexer Text)
  ($_ p.either
      (p.after (l.this "&lt;") (p;wrap "<"))
      (p.after (l.this "&gt;") (p;wrap ">"))
      (p.after (l.this "&amp;") (p;wrap "&"))
      (p.after (l.this "&apos;") (p;wrap "'"))
      (p.after (l.this "&quot;") (p;wrap text.double-quote))))

(def: xml-unicode-escape-char^
  (l.Lexer Text)
  (|> (do p.monad
        [hex? (p.maybe (l.this "x"))
         code (case hex?
                #.None
                (p.codec int.decimal (l.many l.decimal))

                (#.Some _)
                (p.codec int.decimal (l.many l.hexadecimal)))]
        (wrap (|> code .nat text.from-code)))
      (p.before (l.this ";"))
      (p.after (l.this "&#"))))

(def: xml-escape-char^
  (l.Lexer Text)
  (p.either xml-standard-escape-char^
            xml-unicode-escape-char^))

(def: xml-char^
  (l.Lexer Text)
  (p.either (l.none-of ($_ text;compose "<>&'" text.double-quote))
            xml-escape-char^))

(def: xml-identifier
  (l.Lexer Text)
  (do p.monad
    [head (p.either (l.one-of "_")
                    l.alpha)
     tail (l.some (p.either (l.one-of "_.-")
                            l.alpha-num))]
    (wrap ($_ text;compose head tail))))

(def: namespaced-symbol^
  (l.Lexer Name)
  (do p.monad
    [first-part xml-identifier
     ?second-part (<| p.maybe (p.after (l.this ":")) xml-identifier)]
    (case ?second-part
      #.None
      (wrap ["" first-part])

      (#.Some second-part)
      (wrap [first-part second-part]))))

(def: tag^ namespaced-symbol^)
(def: attr-name^ namespaced-symbol^)

(def: spaced^
  (All [a] (-> (l.Lexer a) (l.Lexer a)))
  (let [white-space^ (p.some l.space)]
    (|>> (p.before white-space^)
         (p.after white-space^))))

(def: attr-value^
  (l.Lexer Text)
  (let [value^ (l.some xml-char^)]
    (p.either (l.enclosed [text.double-quote text.double-quote] value^)
              (l.enclosed ["'" "'"] value^))))

(def: attrs^
  (l.Lexer Attrs)
  (<| (:: p.monad map (d.from-list name.hash))
      p.some
      (p.and (spaced^ attr-name^))
      (p.after (l.this "="))
      (spaced^ attr-value^)))

(def: (close-tag^ expected)
  (-> Tag (l.Lexer []))
  (do p.monad
    [actual (|> tag^
                spaced^
                (p.after (l.this "/"))
                (l.enclosed ["<" ">"]))]
    (p.assert ($_ text;compose "Close tag does not match open tag." text.new-line
                  "Expected: " (name;encode expected) text.new-line
                  "  Actual: " (name;encode actual) text.new-line)
              (name;= expected actual))))

(def: comment^
  (l.Lexer Text)
  (|> (l.not (l.this "--"))
      l.some
      (l.enclosed ["<--" "-->"])
      spaced^))

(def: xml-header^
  (l.Lexer Attrs)
  (|> (spaced^ attrs^)
      (p.before (l.this "?>"))
      (p.after (l.this "<?xml"))
      spaced^))

(def: cdata^
  (l.Lexer Text)
  (let [end (l.this "]]>")]
    (|> (l.some (l.not end))
        (p.after end)
        (p.after (l.this "<![CDATA["))
        spaced^)))

(def: text^
  (l.Lexer XML)
  (|> (p.either cdata^
                (l.many xml-char^))
      (p;map (|>> #Text))))

(def: xml^
  (l.Lexer XML)
  (|> (p.rec
       (function (_ node^)
         (p.either text^
                   (spaced^
                    (do p.monad
                      [_ (l.this "<")
                       tag (spaced^ tag^)
                       attrs (spaced^ attrs^)
                       #let [no-children^ (do p.monad
                                            [_ (l.this "/>")]
                                            (wrap (#Node tag attrs (list))))
                             with-children^ (do p.monad
                                              [_ (l.this ">")
                                               children (p.some node^)
                                               _ (close-tag^ tag)]
                                              (wrap (#Node tag attrs children)))]]
                      (p.either no-children^
                                with-children^))))))
      ## This is put outside of the call to "rec" because comments
      ## cannot be located inside of XML nodes.
      ## This way, the comments can only be before or after the main document.
      (p.before (p.some comment^))
      (p.after (p.some comment^))
      (p.after (p.maybe xml-header^))))

(def: #export (read input)
  (-> Text (Error XML))
  (l.run input xml^))

(def: (sanitize-value input)
  (-> Text Text)
  (|> input
      (text.replace-all "&" "&amp;")
      (text.replace-all "<" "&lt;")
      (text.replace-all ">" "&gt;")
      (text.replace-all "'" "&apos;")
      (text.replace-all text.double-quote "&quot;")))

(def: (write-tag [namespace name])
  (-> Tag Text)
  (case namespace
    "" name
    _ ($_ text;compose namespace ":" name)))

(def: (write-attrs attrs)
  (-> Attrs Text)
  (|> attrs
      d.entries
      (list;map (function (_ [key value])
                  ($_ text;compose (write-tag key) "=" text.double-quote (sanitize-value value) text.double-quote)))
      (text.join-with " ")))

(def: xml-header
  Text
  ($_ text;compose "<?xml version=" text.double-quote "1.0" text.double-quote " encoding=" text.double-quote "UTF-8" text.double-quote "?>"))

(def: #export (write input)
  (-> XML Text)
  ($_ text;compose xml-header
      (loop [input input]
        (case input
          (#Text value)
          (sanitize-value value)
          
          (#Node xml-tag xml-attrs xml-children)
          (let [tag (write-tag xml-tag)
                attrs (if (d.empty? xml-attrs)
                        ""
                        ($_ text;compose " " (write-attrs xml-attrs)))]
            (if (list.empty? xml-children)
              ($_ text;compose "<" tag attrs "/>")
              ($_ text;compose "<" tag attrs ">"
                  (|> xml-children
                      (list;map recur)
                      (text.join-with ""))
                  "</" tag ">")))))))

(structure: #export codec (Codec Text XML)
  (def: encode write)
  (def: decode read))

(structure: #export equivalence (Equivalence XML)
  (def: (= reference sample)
    (case [reference sample]
      [(#Text reference/value) (#Text sample/value)]
      (text;= reference/value sample/value)

      [(#Node reference/tag reference/attrs reference/children)
       (#Node sample/tag sample/attrs sample/children)]
      (and (name;= reference/tag sample/tag)
           (:: (d.equivalence text.equivalence) = reference/attrs sample/attrs)
           (n/= (list.size reference/children)
                (list.size sample/children))
           (|> (list.zip2 reference/children sample/children)
               (list.every? (product.uncurry =))))

      _
      #0)))

(type: #export (Reader a)
  (p.Parser (List XML) a))

(exception: #export empty-input)
(exception: #export unexpected-input)
(exception: #export unknown-attribute)

(exception: #export (wrong-tag {tag Name})
  (name;encode tag))

(def: blank-line ($_ text;compose text.new-line text.new-line))

(exception: #export (unconsumed-inputs {inputs (List XML)})
  (|> inputs
      (list;map (:: ..codec encode))
      (text.join-with blank-line)))

(def: #export text
  (Reader Text)
  (function (_ docs)
    (case docs
      #.Nil
      (ex.throw empty-input [])
      
      (#.Cons head tail)
      (case head
        (#Text value)
        (#error.Success [tail value])
        
        (#Node _)
        (ex.throw unexpected-input [])))))

(def: #export (attr name)
  (-> Name (Reader Text))
  (function (_ docs)
    (case docs
      #.Nil
      (ex.throw empty-input [])
      
      (#.Cons head _)
      (case head
        (#Text _)
        (ex.throw unexpected-input [])
        
        (#Node tag attrs children)
        (case (d.get name attrs)
          #.None
          (ex.throw unknown-attribute [])
          
          (#.Some value)
          (#error.Success [docs value]))))))

(def: (run' docs reader)
  (All [a] (-> (List XML) (Reader a) (Error a)))
  (case (p.run docs reader)
    (#error.Success [remaining output])
    (if (list.empty? remaining)
      (#error.Success output)
      (ex.throw unconsumed-inputs remaining))
    
    (#error.Failure error)
    (#error.Failure error)))

(def: #export (node tag)
  (-> Name (Reader Any))
  (function (_ docs)
    (case docs
      #.Nil
      (ex.throw empty-input [])
      
      (#.Cons head _)
      (case head
        (#Text _)
        (ex.throw unexpected-input [])
        
        (#Node _tag _attrs _children)
        (if (name;= tag _tag)
          (#error.Success [docs []])
          (ex.throw wrong-tag tag))))))

(def: #export (children reader)
  (All [a] (-> (Reader a) (Reader a)))
  (function (_ docs)
    (case docs
      #.Nil
      (ex.throw empty-input [])
      
      (#.Cons head tail)
      (case head
        (#Text _)
        (ex.throw unexpected-input [])
        
        (#Node _tag _attrs _children)
        (do error.monad
          [output (run' _children reader)]
          (wrap [tail output]))))))

(def: #export ignore
  (Reader Any)
  (function (_ docs)
    (case docs
      #.Nil
      (ex.throw empty-input [])
      
      (#.Cons head tail)
      (#error.Success [tail []]))))

(def: #export (run document reader)
  (All [a] (-> XML (Reader a) (Error a)))
  (run' (list document) reader))
