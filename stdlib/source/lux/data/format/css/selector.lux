(.module:
  [lux (#- or and for is? not)
   [data
    ["." text
     format]]
   [type
    abstract]
   [macro
    ["." template]]
   ["." locale (#+ Locale)]])

(type: #export Label Text)

(type: #export Tag Label)
(type: #export ID Label)
(type: #export Class Label)
(type: #export Attribute Label)

(abstract: #export (Generic brand) {} Any)

(do-template [<generic> <brand>]
  [(abstract: <brand> {} Any)
   (type: #export <generic> (Generic <brand>))]

  [Can-Chain Can-Chain']
  [Cannot-Chain Cannot-Chain']
  )

(abstract: #export Unique {} Any)
(abstract: #export Specific {} Any)
(abstract: #export Composite {} Any)

(abstract: #export (Selector kind)
  {}
  
  Text

  (def: #export selector
    (-> (Selector Any) Text)
    (|>> :representation))

  (def: #export any
    (Selector Cannot-Chain)
    (:abstraction "*"))

  (def: #export tag
    (-> Tag (Selector Cannot-Chain))
    (|>> :abstraction))

  (do-template [<name> <type> <prefix> <kind>]
    [(def: #export <name>
       (-> <type> (Selector <kind>))
       (|>> (format <prefix>) :abstraction))]

    [id ID "#" Unique]
    [class Class "." Can-Chain]
    )

  (do-template [<right> <left> <combo> <combinator>+]
    [(`` (do-template [<combinator> <name>]
           [(def: #export (<name> right left)
              (-> (Selector <right>) (Selector <left>) (Selector <combo>))
              (:abstraction (format (:representation left)
                                    <combinator>
                                    (:representation right))))]

           (~~ (template.splice <combinator>+))))]

    [Can-Chain (Generic Any) Can-Chain
     [["" and]]]
    [Unique (Generic Any) Composite
     [["" for]]]
    [Specific (Generic Any) Composite
     [["" at]]]
    [Any Any Composite
     [["," or]
      [" " in]
      [">" sub]
      ["+" next]
      ["~" later]]]
    )

  (type: #export Combinator
    (-> (Selector Any) (Selector Any) (Selector Composite)))

  (def: #export (with? attribute)
    (-> Attribute (Selector Can-Chain))
    (:abstraction (format "[" attribute "]")))

  (do-template [<check> <name>]
    [(def: #export (<name> attribute value)
       (-> Attribute Text (Selector Can-Chain))
       (:abstraction (format "[" attribute <check> value "]")))]

    ["=" is?]
    ["~=" has?]
    ["|=" has-start?]
    ["^=" starts?]
    ["$=" ends?]
    ["*=" contains?]
    )

  (do-template [<kind> <pseudo>+]
    [(`` (do-template [<name> <pseudo>]
           [(def: #export <name>
              (Selector Can-Chain)
              (:abstraction <pseudo>))]

           (~~ (template.splice <pseudo>+))))]

    [Can-Chain
     [[active ":active"]
      [checked ":checked"]
      [default ":default"]
      [disabled ":disabled"]
      [empty ":empty"]
      [enabled ":enabled"]
      [first-child ":first-child"]
      [first-of-type ":first-of-type"]
      [focused ":focus"]
      [hovered ":hover"]
      [in-range ":in-range"]
      [indeterminate ":indeterminate"]
      [invalid ":invalid"]
      [last-child ":last-child"]
      [last-of-type ":last-of-type"]
      [link ":link"]
      [only-of-type ":only-of-type"]
      [only-child ":only-child"]
      [optional ":optional"]
      [out-of-range ":out-of-range"]
      [read-only ":read-only"]
      [read-write ":read-write"]
      [required ":required"]
      [root ":root"]
      [target ":target"]
      [valid ":valid"]
      [visited ":visited"]]]
    
    [Specific
     [[after "::after"]
      [before "::before"]
      [first-letter "::first-letter"]
      [first-line "::first-line"]
      [placeholder "::placeholder"]
      [selection "::selection"]]]
    )

  (def: #export (language locale)
    (-> Locale (Selector Can-Chain))
    (|> locale
        locale.code
        (text.enclose ["(" ")"])
        (format ":lang")
        :abstraction))

  (def: #export not
    (-> (Selector Any) (Selector Can-Chain))
    (|>> :representation
         (text.enclose ["(" ")"])
         (format ":not")
         :abstraction))

  (abstract: #export Index
    {}
    
    Text

    (def: #export index
      (-> Nat Index)
      (|>> %n :abstraction))

    (do-template [<name> <index>]
      [(def: #export <name> Index (:abstraction <index>))]
      
      [odd "odd"]
      [even "even"]
      )

    (type: #export Formula
      {#constant Int
       #variable Int})

    (def: #export (formula input)
      (-> Formula Index)
      (let [(^slots [#constant #variable]) input]
        (:abstraction (format (if (i/< +0 variable)
                                (%i variable)
                                (%n (.nat variable)))
                              (%i constant)))))
    
    (do-template [<name> <pseudo>]
      [(def: #export (<name> index)
         (-> Index (Selector Can-Chain))
         (|> (:representation index)
             (text.enclose ["(" ")"])
             (format <pseudo>)
             (:abstraction Selector)))]

      [nth-child ":nth-child"]
      [nth-last-child ":nth-last-child"]
      [nth-last-of-type ":nth-last-of-type"]
      [nth-of-type ":nth-of-type"]
      )
    )
  )
