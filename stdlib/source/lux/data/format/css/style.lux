(.module:
  [lux #*
   [data
    [text
     ["%" format (#+ format)]]]
   [type
    abstract]]
  ["." // #_
   ["#." value (#+ Value)]
   ["#." property (#+ Property)]])

(abstract: #export Style
  Text

  {#.doc "The style associated with a CSS selector."}

  (def: #export empty Style (:abstraction ""))

  (def: #export separator " ")

  (def: #export (with [property value])
    (All [brand]
      (-> [(Property brand) (Value brand)]
          (-> Style Style)))
    (|>> :representation
         (format (//property.name property) ": " (//value.value value) ";" ..separator)
         :abstraction))

  (def: #export inline
    (-> Style Text)
    (|>> :representation))
  )
