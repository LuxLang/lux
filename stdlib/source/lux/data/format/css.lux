(.module:
  [lux #*
   [data
    [color (#+ Color)]
    [number]
    ["." text
     format]
    [collection [list ("list/" Functor<List> Monoid<List>)]]]])

(type: #export Selector
  Text)

(type: #export Property Text)
(type: #export Value Text)

(type: #export Style
  {#.doc "The style associated with a CSS selector."}
  (List [Property Value]))

(type: #export Rule [Selector Style])

(type: #export Sheet (List Rule))

(type: #export CSS Text)

(def: #export (inline style)
  (-> Style Text)
  (|> style
      (list/map (function (_ [key val]) (format key ": " val)))
      (text.join-with "; ")))

(def: #export (css sheet)
  (-> Sheet CSS)
  (|> sheet
      (list/map (function (_ [selector style])
                  (if (list.empty? style)
                    ""
                    (format selector "{" (inline style) "}"))))
      (text.join-with "\n")))

(def: #export (rgb color)
  (-> Color Value)
  (let [[red green blue] (color.unpack color)]
    (format "rgb(" (|> red .int %i)
            "," (|> green .int %i)
            "," (|> blue .int %i)
            ")")))

(def: #export (rgba color alpha)
  (-> Color Rev Value)
  (let [[red green blue] (color.unpack color)]
    (format "rgba(" (|> red .int %i)
            "," (|> green .int %i)
            "," (|> blue .int %i)
            "," (if (r/= (:: number.Interval<Rev> top) alpha)
                  "1.0"
                  (format "0" (%r alpha)))
            ")")))

(def: #export (rule selector style children)
  (-> Selector Style Sheet Sheet)
  (list& [selector style]
         (list/map (function (_ [sub-selector sub-style])
                     [(format selector sub-selector) sub-style])
                   children)))

(do-template [<name> <type>]
  [(def: #export <name>
     (-> <type> <type> <type>)
     list/compose)]

  [merge   Style]
  [cascade Sheet]
  )

(do-template [<name> <suffix>]
  [(def: #export (<name> value)
     (-> Frac Value)
     (format (%f value) <suffix>))]

  [em "em"]
  [ex "ex"]
  [rem "rem"]
  [ch "ch"]
  [vw "vw"]
  [vh "vh"]
  [vmin "vmin"]
  [vmax "vmax"]
  [% "%"]
  [cm "cm"]
  [mm "mm"]
  [in "in"]
  [px "px"]
  [pt "pt"]
  [pc "pc"]
  )
