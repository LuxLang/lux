(;module:
  lux
  (lux (control ["p" parser]
                ["ex" exception #+ exception:]
                [monad #+ do])
       (data ["R" result]
             (coll ["d" dict]))))

(exception: #export Unknown-Property)

(type: #export Context
  (d;Dict Text Text))

(type: #export (Property a)
  (p;Parser Context a))

(def: #export (property name)
  (-> Text (Property Text))
  (function [context]
    (case (d;get name context)
      (#;Some value)
      (ex;return [context value])
      
      #;None
      (ex;throw Unknown-Property name))))

(def: #export (run context property)
  (All [a] (-> Context (Property a) (R;Result a)))
  (case (property context)
    (#R;Success [_ output])
    (#R;Success output)

    (#R;Error error)
    (#R;Error error)))
