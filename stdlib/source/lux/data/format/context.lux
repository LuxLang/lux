(.module:
  [lux #*
   [abstract
    [monad (#+ do)]]
   [control
    [parser (#+ Parser)]
    ["ex" exception (#+ exception:)]]
   [data
    ["." error (#+ Error)]
    ["." text
     ["%" format (#+ format)]]
    [collection
     ["." dictionary (#+ Dictionary)]]]])

(exception: #export (unknown-property {property Text})
  (ex.report ["Property" (%.text property)]))

(type: #export Context
  (Dictionary Text Text))

(type: #export (Property a)
  (Parser Context a))

(def: #export empty
  Context
  (dictionary.new text.hash))

(def: #export (property name)
  (-> Text (Property Text))
  (function (_ context)
    (case (dictionary.get name context)
      (#.Some value)
      (ex.return [context value])
      
      #.None
      (ex.throw unknown-property name))))

(def: #export (run context property)
  (All [a] (-> Context (Property a) (Error a)))
  (case (property context)
    (#error.Success [_ output])
    (#error.Success output)

    (#error.Failure error)
    (#error.Failure error)))
