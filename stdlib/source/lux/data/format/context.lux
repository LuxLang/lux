(.module:
  lux
  (lux (control ["p" parser]
                ["ex" exception #+ exception:]
                [monad #+ do])
       (data ["E" error]
             (coll (dictionary ["dict" unordered #+ Dictionary])))))

(exception: #export (unknown-property {property Text})
  property)

(type: #export Context
  (Dictionary Text Text))

(type: #export (Property a)
  (p.Parser Context a))

(def: #export (property name)
  (-> Text (Property Text))
  (function (_ context)
    (case (dict.get name context)
      (#.Some value)
      (ex.return [context value])
      
      #.None
      (ex.throw unknown-property name))))

(def: #export (run context property)
  (All [a] (-> Context (Property a) (E.Error a)))
  (case (property context)
    (#E.Success [_ output])
    (#E.Success output)

    (#E.Error error)
    (#E.Error error)))
