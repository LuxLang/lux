(;module:
  lux
  (lux (control ["m" monoid]
                functor
                comonad)
       [macro]))

(type: #export (Trace t a)
  {#monoid (m;Monoid t)
   #trace (-> t a)})

(struct: #export Functor<Trace> (All [t] (Functor (Trace t)))
  (def: (map f fa)
    (update@ #trace (. f) fa)))

(struct: #export CoMonad<Trace> (All [t] (CoMonad (Trace t)))
  (def: functor Functor<Trace>)

  (def: (unwrap wa)
    ((get@ #trace wa)
     (get@ [#monoid #m;unit] wa)))

  (def: (split wa)
    (let [monoid (get@ #monoid wa)]
      {#monoid monoid
       #trace (lambda [t1]
                {#monoid monoid
                 #trace (lambda [t2] ((get@ #trace wa)
                                 (:: monoid append t1 t2)))})})))

(def: #export (run context tracer)
  (All [t a] (-> t (Trace t a) a))
  (:: tracer trace context))
