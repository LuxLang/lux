... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except list nat int rev symbol type)
   [abstract
    [monad (.only do)]
    [functor
     ["[0]" contravariant]]]
   [control
    ["<>" projection]
    ["[0]" maybe]]
   [data
    ["[0]" bit]
    ["[0]" text]
    [format
     ["[0]" xml]
     ["[0]" json]]
    [collection
     ["[0]" list (.use "[1]#[0]" monad)]]]
   [math
    [number
     ["[0]" nat]
     ["[0]" int]
     ["[0]" rev]
     ["[0]" dec]
     ["[0]" frac]
     ["[0]" complex]]
    [arithmetic
     ["[0]" modular]]]
   [meta
    ["[0]" location]
    ["[0]" symbol]
    ["[0]" type]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [macro
     [syntax (.only syntax)]
     ["[0]" template]]]
   [world
    ["[0]" time (.only)
     ["[0]" instant]
     ["[0]" duration]
     ["[0]" date]
     ["[0]" day]
     ["[0]" month]]]]])

(.every .public (Injection of)
  (-> of
      Text))

(the .public functor
  (contravariant.Functor Injection)
  (implementation
   (the (each f fb)
     (|>> f fb))))

... https://en.wikipedia.org/wiki/Message
(the .public message
  (syntax (_ [fragments (<>.many <code>.any)])
    (in (.list (` (.text_composite# (,* fragments)))))))

(with_template [<name> <type> <injection>]
  [(the .public <name>
     (Injection <type>)
     <injection>)]

  [bit      Bit               (of bit.codec injection)]
  [nat      Nat               (of nat.decimal injection)]
  [int      Int               (of int.decimal injection)]
  [rev      Rev               (of rev.decimal injection)]
  [dec      Dec               (of dec.decimal injection)]
  [text     Text              text.injection]
  
  [frac     frac.Frac         (of frac.codec injection)]
  [complex  complex.Complex   complex.injection]
  
  [symbol   Symbol            (of symbol.absolute injection)]
  [location Location          location.injection]
  [code     Code              code.absolute]
  [type     Type              type.absolute_injection]
  
  [instant  instant.Instant   (of instant.codec injection)]
  [duration duration.Duration (of duration.codec injection)]
  [date     date.Date         (of date.codec injection)]
  [time     time.Time         (of time.codec injection)]
  [day      day.Day           (of day.codec injection)]
  [month    month.Month       (of month.codec injection)]
  
  [xml      xml.XML           (of xml.codec injection)]
  [json     json.JSON         (of json.codec injection)]
  )

(with_template [<name>]
  [(the .public <name>
     (Injection Dec)
     (let [suffix (|> +0.0
                      (of (template.symbol [dec._] [<name>]) injection)
                      (text.clip_since 4)
                      (maybe.else ""))]
       (|>> ((template.symbol [dec._] [as_ <name>]))
            dec.int
            ..int
            (text.suffix suffix))))]

  [degree]
  [percentage]
  [permille]
  [permyriad]
  )

(with_template [<type> <injection>,<codec>]
  [(`` (with_template [<injection> <codec>]
         [(the .public <injection>
            (Injection <type>)
            (of <codec> injection))]

         (,, (template.spliced <injection>,<codec>))))]

  [Nat
   [[nat_2 nat.binary]
    [nat_8 nat.octal]
    [nat_10 nat.decimal]
    [nat_16 nat.hex]]]
  [Int
   [[int_2 int.binary]
    [int_8 int.octal]
    [int_10 int.decimal]
    [int_16 int.hex]]]
  [Rev
   [[rev_2 rev.binary]
    [rev_8 rev.octal]
    [rev_10 rev.decimal]
    [rev_16 rev.hex]]]
  [Dec
   [[dec_2 dec.binary]
    [dec_8 dec.octal]
    [dec_10 dec.decimal]
    [dec_16 dec.hex]]]
  )

(the .public (padded padding injection)
  (-> Nat (Injection Nat)
      (Injection Nat))
  (of (nat.padded padding
                  (implementation
                   (the injection injection)
                   (the (projection _) (undefined))))
      injection))

(the .public (mod modular)
  (for_any (_ %)
    (Injection (modular.Mod %)))
  (let [codec (modular.codec (modular.modulus modular))]
    (of codec injection modular)))

(the .public (list injection)
  (for_any (_ of)
    (-> (Injection of)
        (Injection (List of))))
  (|>> (list#each (|>> injection (..message " ")))
       text.together
       (text.enclosed ["(list" ")"])))

(the .public (maybe injection)
  (for_any (_ of)
    (-> (Injection of)
        (Injection (Maybe of))))
  (function (_ value)
    (when value
      {.#None}
      "{.#None}"

      {.#Some value}
      (..message "{.#Some " (injection value) "}"))))
