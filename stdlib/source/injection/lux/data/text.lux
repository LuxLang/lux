... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except list natural integer revolution name type)
   [abstract
    [monad (.only do)]]
   [control
    ["<>" projection]
    ["[0]" maybe]]
   [data
    ["[0]" product]
    ["[0]" text (.only Injection)]
    [collection
     ["[0]" list (.use "[1]#[0]" monad)]]]
   [math
    ["[0]" unit]
    [number
     ["[0]" natural]
     ["[0]" integer]
     ["[0]" revolution]
     ["[0]" decimal]]
    [arithmetic
     ["[0]" modular]]]
   [meta
    ["[0]" code
     ["<[1]>" \\projection]]
    [macro
     ["[0]" syntax]
     ["[0]" template]]]]])

... https://en.wikipedia.org/wiki/Message
(the .public message
  (syntax.macro (_ [fragments (<>.many <code>.any)])
    (in (.list (` (.text_composite# (,* fragments)))))))

(template.with [<name> <type> <injection>]
  [(the .public <name>
     (Injection <type>)
     <injection>)]

  [natural Natural (by natural.base_10 injection)]
  [integer Integer (by integer.base_10 injection)]
  [revolution Revolution (by revolution.base_10 injection)]
  [decimal Decimal (by decimal.base_10 injection)]
  [text Text text.injection]
  )

(template.with [,name]
  [(the .public ,name
     (Injection Decimal)
     (let [suffix (its unit.#suffix (template.name [unit._] [,name]))]
       (|>> (by (template.name [decimal._] [,name]) injection)
            (text.split_by decimal.delimiter)
            (maybe.else ["" ""])
            product.left
            (text.suffix suffix))))]

  [degree]
  [percentage]
  [permille]
  [permyriad]
  )

(template.with [<type> <injection>,<format>]
  [(`` (template.with [<injection> <format>]
         [(the .public <injection>
            (Injection <type>)
            (by <format> injection))]

         (,, (template.spliced <injection>,<format>))))]

  [Natural
   [[natural_02 natural.base_02]
    [natural_08 natural.base_08]
    [natural_10 natural.base_10]
    [natural_16 natural.base_16]]]
  [Integer
   [[integer_02 integer.base_02]
    [integer_08 integer.base_08]
    [integer_10 integer.base_10]
    [integer_16 integer.base_16]]]
  [Revolution
   [[revolution_02 revolution.base_02]
    [revolution_08 revolution.base_08]
    [revolution_10 revolution.base_10]
    [revolution_16 revolution.base_16]]]
  [Decimal
   [[decimal_02 decimal.base_02]
    [decimal_08 decimal.base_08]
    [decimal_10 decimal.base_10]
    [decimal_16 decimal.base_16]]]
  )

(the .public (padded padding injection)
  (-> Natural (Injection Natural)
      (Injection Natural))
  (by (natural.padded padding
                      (implementation
                       (the injection injection)
                       (the (projection _) (undefined))))
      injection))

(the .public (mod modular)
  (for_any (_ %)
    (Injection (modular.Mod %)))
  (let [format (modular.format (modular.modulus modular))]
    (by format injection modular)))

(the .public (list injection)
  (for_any (_ of)
    (-> (Injection of)
        (Injection (List of))))
  (|>> (list#each (|>> injection (..message " ")))
       text.together
       (text.enclosed ["(list" ")"])))

(the .public (maybe injection)
  (for_any (_ of)
    (-> (Injection of)
        (Injection (Maybe of))))
  (function (_ value)
    (when value
      {.#None}
      "{.#None}"

      {.#Some value}
      (..message "{.#Some " (injection value) "}"))))
