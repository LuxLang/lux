(.module:
  [library
   [lux "*"
    ["_" test {"+" [Test]}]
    [abstract
     [monad {"+" [do]}]]
    [math
     ["[0]" random {"+" [Random]}]]]]
  [\\library
   ["[0]" /
    [//
     [equivalence {"+" [Equivalence]}]]]])

(def: .public (spec (^open "\[0]") (^open "\[0]") gen_sample)
  (All (_ a) (-> (Equivalence a) (/.Monoid a) (Random a) Test))
  (do random.monad
    [sample gen_sample
     left gen_sample
     mid gen_sample
     right gen_sample]
    (<| (_.for [/.Monoid])
        ($_ _.and
            (_.test "Left identity."
                    (\= sample
                        (\composite \identity sample)))
            (_.test "Right identity."
                    (\= sample
                        (\composite sample \identity)))
            (_.test "Associativity."
                    (\= (\composite left (\composite mid right))
                        (\composite (\composite left mid) right)))
            ))))
