(.module:
  [library
   [lux #*
    ["_" test (#+ Test)]
    [math
     ["." random]
     [number
      ["n" nat]]]]]
  [\\library
   ["." / (#+ do)]]
  [//
   [functor (#+ Injection Comparison)]])

(def: (left_identity injection comparison (^open "_//."))
  (All (_ f) (-> (Injection f) (Comparison f) (/.Monad f) Test))
  (do {! random.monad}
    [sample random.nat
     morphism (\ ! each (function (_ diff)
                          (|>> (n.+ diff) _//in))
                 random.nat)]
    (_.test "Left identity."
            ((comparison n.=)
             (|> (injection sample) (_//each morphism) _//conjoint)
             (morphism sample)))))

(def: (right_identity injection comparison (^open "_//."))
  (All (_ f) (-> (Injection f) (Comparison f) (/.Monad f) Test))
  (do random.monad
    [sample random.nat]
    (_.test "Right identity."
            ((comparison n.=)
             (|> (injection sample) (_//each _//in) _//conjoint)
             (injection sample)))))

(def: (associativity injection comparison (^open "_//."))
  (All (_ f) (-> (Injection f) (Comparison f) (/.Monad f) Test))
  (do {! random.monad}
    [sample random.nat
     increase (\ ! each (function (_ diff)
                          (|>> (n.+ diff) _//in))
                 random.nat)
     decrease (\ ! each (function (_ diff)
                          (|>> (n.- diff) _//in))
                 random.nat)]
    (_.test "Associativity."
            ((comparison n.=)
             (|> (injection sample) (_//each increase) _//conjoint (_//each decrease) _//conjoint)
             (|> (injection sample) (_//each (|>> increase (_//each decrease) _//conjoint)) _//conjoint)))))

(def: .public (spec injection comparison monad)
  (All (_ f) (-> (Injection f) (Comparison f) (/.Monad f) Test))
  (<| (_.for [/.Monad])
      ($_ _.and
          (..left_identity injection comparison monad)
          (..right_identity injection comparison monad)
          (..associativity injection comparison monad)
          )))
