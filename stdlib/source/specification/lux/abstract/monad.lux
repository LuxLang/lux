(.require
 [library
  [lux (.except)
   [abstract
    [functor
     [\\test (.only Injection Comparison)]]]
   [math
    ["[0]" random]
    [number
     ["n" nat]]]
   [test
    ["_" property (.only Test)]]]]
 [\\library
  ["[0]" / (.only do)]])

(def (left_identity injection comparison (open "_//[0]"))
  (All (_ f) (-> (Injection f) (Comparison f) (/.Monad f) Test))
  (do [! random.monad]
    [sample random.nat
     morphism (of ! each (function (_ diff)
                           (|>> (n.+ diff) _//in))
                  random.nat)]
    (_.test "Left identity."
      ((comparison n.=)
       (|> (injection sample) (_//each morphism) _//conjoint)
       (morphism sample)))))

(def (right_identity injection comparison (open "_//[0]"))
  (All (_ f) (-> (Injection f) (Comparison f) (/.Monad f) Test))
  (do random.monad
    [sample random.nat]
    (_.test "Right identity."
      ((comparison n.=)
       (|> (injection sample) (_//each _//in) _//conjoint)
       (injection sample)))))

(def (associativity injection comparison (open "_//[0]"))
  (All (_ f) (-> (Injection f) (Comparison f) (/.Monad f) Test))
  (do [! random.monad]
    [sample random.nat
     increase (of ! each (function (_ diff)
                           (|>> (n.+ diff) _//in))
                  random.nat)
     decrease (of ! each (function (_ diff)
                           (|>> (n.- diff) _//in))
                  random.nat)]
    (_.test "Associativity."
      ((comparison n.=)
       (|> (injection sample) (_//each increase) _//conjoint (_//each decrease) _//conjoint)
       (|> (injection sample) (_//each (|>> increase (_//each decrease) _//conjoint)) _//conjoint)))))

(def .public (spec injection comparison monad)
  (All (_ f) (-> (Injection f) (Comparison f) (/.Monad f) Test))
  (<| (_.for [/.Monad])
      (all _.and
           (..left_identity injection comparison monad)
           (..right_identity injection comparison monad)
           (..associativity injection comparison monad)
           )))
