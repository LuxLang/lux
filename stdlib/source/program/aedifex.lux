... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except local)
   ["[0]" program]
   ["[0]" debug]
   [abstract
    [monad (.only do)]]
   [concurrency
    ["[0]" future (.only Future) (.use "[1]#[0]" monad)]]
   [control
    ["[0]" io (.only IO)]
    ["[0]" maybe]]
   [error
    ["[0]" try (.only Try) (.use "[1]#[0]" functor)]
    ["[0]" exception (.only Exception)]]
   [data
    [binary (.only Binary)]
    ["[0]" text (.only)
     ["%" \\injection]
     [encoding
      ["[0]" utf8]]]
    [format
     ["[0]" xml]]
    [collection
     ["[0]" set]
     ["[0]" dictionary (.only Dictionary)]
     ["[0]" stack (.use "[1]#[0]" functor)]]]
   [macro
    ["[0]" template]]
   [world
    ["[0]" shell (.only Exit Shell)]
    ["[0]" console (.only Console)]
    ["[0]" environment]
    ["[0]" file (.only Path)
     ["[0]" watch]]
    [net
     ["[0]" http
      ["[1]" client]]]]]]
 ["[0]" /
  ["[1]" profile]
  ["[1][0]" action (.only Action)]
  ["[1][0]" project (.only Project)]
  ["[1][0]" input]
  ["[1][0]" pom]
  ["[1][0]" cli]
  ["[1][0]" dependency
   ["[1]" resolution (.only Resolution)]]
  ["[1][0]" repository (.only Repository)
   ["[1]/[0]" remote (.only Address)]
   ["[1]/[0]" local]]
  ["[1][0]" command (.only Command)
   ["[1]/[0]" version]
   ["[1]/[0]" clean]
   ["[1]/[0]" pom]
   ["[1]/[0]" install]
   ["[1]/[0]" deps]
   ["[1]/[0]" build]
   ["[1]/[0]" test]
   ["[1]/[0]" auto]
   ["[1]/[0]" deploy]]])

(the repositories
  (-> /.Profile (Stack (Repository Future)))
  (|>> (its /.#repositories)
       set.stack
       (stack#each (|>> (/repository/remote.repository http.default {.#None}) /repository.future))))

(the environment (environment.future environment.default))
(the fs (file.future file.default))
(the local (/repository/local.repository ..environment ..fs))

(the (with_dependencies environment console command profile)
  (for_any (_ a)
    (-> (environment.Environment Future) (Console Future)
        (-> (Console Future) (environment.Environment Future) (file.System Future) (Shell Future) Resolution (Command [Exit a]))
        (Command a)))
  (do /action.monad
    [resolution (/command/deps.do! console
                                   (/repository/local.repository environment (file.future file.default))
                                   (..repositories profile)
                                   (|>> (/repository/remote.repository http.default {.#None})
                                        /repository.future)
                                   profile)
     [exit_code output] ((command console environment (file.future file.default) (shell.future shell.default) resolution) profile)
     _ (when exit_code
         shell.normal
         (in [])

         _
         (do future.monad
           [_ (by environment exit exit_code)]
           (in {try.#Failure ""})))]
    (in output)))

(exception.the (cannot_find_repository [repository options])
  (Exception [Text (Dictionary Text Address)])
  (exception.report
   (stack ["Repository" (%.text repository)]
          ["Options" (exception.listing (function (_ [name repo])
                                          (text (%.text name) " := " (%.text repo)))
                                        (dictionary.entries options))])))

(the succeed!
  (IO Any)
  (by environment.default exit shell.normal))

(the (fail! error)
  (-> Text (IO Any))
  (exec
    (debug.log! error)
    (by environment.default exit shell.error)))

(the (command action)
  (for_any (_ a)
    (-> (Future (Try a))
        (IO Any)))
  (exec
    (do future.monad
      [outcome action]
      (future.soon
       (when outcome
         {try.#Success _}
         ..succeed!

         {try.#Failure error}
         (..fail! error))))
    (by io.monad in [])))

(exception.the .public invalid_operation)

(`` (the write_only
      (Console IO)
      (implementation
       (,, (template.with [<name>]
             [(the (<name> _)
                (io.io (exception.except ..invalid_operation [])))]

             [read]
             [read_line]
             [close]
             ))

       (the (write it)
         (<| io.io
             {try.#Success}
             debug.log!
             (if (text.ends_with? text.\n it)
               (maybe.trusted (text.clip 0 (-- (text.size it)) it))
               it))))))

(the _
  (program.program [[profiles operation] /cli.command]
    (do [! io.monad]
      [console (by ! each (|>> (try.else ..write_only) console.future)
                   console.default)]
      (when operation
        {/cli.#Version}
        (..command
         (/command/version.do! console (by /.monoid identity)))

        _
        (do !
          [?profile (/input.read io.monad file.default profiles)]
          (when ?profile
            {try.#Failure error}
            (..fail! error)

            {try.#Success profile}
            (when operation
              {/cli.#Version}
              (in [])
              
              {/cli.#Clean}
              (..command
               (/command/clean.do! console (file.future file.default) profile))

              {/cli.#POM}
              (..command
               (/command/pom.do! console (file.future file.default) profile))
              
              {/cli.#Install}
              (..command
               (/command/install.do! console ..fs ..local profile))

              {/cli.#Deploy repository identity}
              (..command
               (when (its /.#identity profile)
                 {.#Some artifact}
                 (when (dictionary.value repository (its /.#deploy_repositories profile))
                   {try.#Success remote}
                   (/command/deploy.do! ..environment
                                        (shell.future shell.default)
                                        console
                                        ..local
                                        (/repository.future (/repository/remote.repository http.default {.#Some identity} remote))
                                        (file.future file.default)
                                        artifact
                                        profile)
                   
                   not_found!
                   (future#in (exception.except ..cannot_find_repository [repository (its /.#deploy_repositories profile)])))
                 
                 {.#None}
                 (future#in (exception.except /.no_identity []))))
              
              {/cli.#Dependencies}
              (..command
               (/command/deps.do! console
                                  (/repository/local.repository ..environment (file.future file.default))
                                  (..repositories profile)
                                  (|>> (/repository/remote.repository http.default {.#None})
                                       /repository.future)
                                  profile))

              {/cli.#Compilation compilation}
              (when compilation
                {/cli.#Build} (..command
                               (..with_dependencies ..environment console /command/build.do! profile))
                {/cli.#Test} (..command
                              (..with_dependencies ..environment console /command/test.do! profile)))

              {/cli.#Auto auto}
              (do !
                [?watcher watch.default]
                (when ?watcher
                  {try.#Failure error}
                  (..fail! error)
                  
                  {try.#Success watcher}
                  (..command
                   (when auto
                     {/cli.#Build} (..with_dependencies ..environment console (/command/auto.do! /command/auto.delay watcher /command/build.do!) profile)
                     {/cli.#Test} (..with_dependencies ..environment console (/command/auto.do! /command/auto.delay watcher /command/test.do!) profile))))))
            ))))))
