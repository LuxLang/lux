(.module:
  [lux #*
   [data
    ["." text
     format]
    [collection
     ["." list ("#;." monoid)]]]]
  ["." // (#+ Commercial)
   ["_" term]
   [//
    ["$" document]]])

(def: #export cannot-sell
  (let [preamble (format "Without limiting other conditions in " _.license)
        condition (format "the grant of rights under " _.license
                          " will not include, and " _.license
                          " does not grant to " _.recipient
                          ", the right to " _.sell " " _.work)]
    ($.sentence (format preamble ", " condition))))

(def: #export require-contributor-attribution
  ($.sentence (format "All advertising materials mentioning features or use of " _.work
                      " must include an acknowledgement of the authorship of every " _.contributor)))

(def: #export disallow-contributor-endorsement
  (let [subject (format "The name of no " _.contributor)
        capability "endorse or promote products"
        source (format "any " _.extension)
        condition "specific prior written permission"]
    ($.sentence (format subject " may be used to " capability " derived from " source " without " condition))))
