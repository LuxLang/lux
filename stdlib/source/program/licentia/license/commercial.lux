(.module:
  [lux #*
   [data
    ["." text
     ["%" format (#+ format)]]]]
  ["." // (#+ Commercial)
   ["_" term]
   [//
    ["$" document]]])

(def: #export cannot-sell
  (let [preamble (format "Without limiting other conditions in " _.license)
        direct-condition (format "the grant of rights under " _.license
                                 " will not include, and does not grant to " _.recipient
                                 ", the right to " _.sell " " _.work)
        derivative-condition (format "or any " _.derivative-work)]
    ($.sentence (format preamble
                        ", " direct-condition
                        ", " derivative-condition))))

(def: #export require-contributor-attribution
  ($.sentence (format "All advertising materials mentioning features or use of " _.work
                      " must include an acknowledgement of the authorship of every " _.contributor)))

(def: #export disallow-contributor-endorsement
  (let [subject (format "The name of no " _.contributor)
        capability "endorse or promote products"
        source (format "any " _.extension)
        condition "specific prior written permission"]
    ($.sentence (format subject " may be used to " capability " derived from " source " without " condition))))
