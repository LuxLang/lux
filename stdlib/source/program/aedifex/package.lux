... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except local)
   [abstract
    [equivalence (.only Equivalence)]]
   [control
    ["[0]" try (.only Try) (.use "[1]#[0]" functor)]]
   [data
    ["[0]" sum]
    ["[0]" product]
    ["[0]" binary (.only Binary)]
    [text
     [encoding
      ["[0]" utf8]]]
    [format
     ["[0]" xml (.only XML)
      ["<[1]>" \\projection]]]
    [collection
     [set (.only Set)]]]
   [macro
    ["[0]" template]]]]
 ["[0]" //
  ["/" profile]
  ["[1][0]" hash]
  ["[1][0]" pom]
  [dependency (.only Dependency)
   ["[1][0]" status (.only Status)]]
  [repository
   [remote (.only Address)]
   ["[1][0]" origin (.only Origin)]]])

(every .public Package
  (Record
   [#origin Origin
    #library [Binary Status]
    #pom [XML Binary Status]]))

(template.with [<name> <tag>]
  [(the .public (<name> package)
     (-> Package Bit)
     (when (its #origin package)
       {<tag> _}
       true
       
       _
       false))]

  [local? //origin.#Local]
  [remote? //origin.#Remote]
  )

(the .public (local pom library)
  (-> XML Binary Package)
  [#origin {//origin.#Local ""}
   #library [library
             {//status.#Verified (//hash.sha1 library)
                                 (//hash.md5 library)}]
   #pom (let [binary_pom (|> pom
                             (by xml.format injection)
                             (by utf8.format injection))]
          [pom
           binary_pom
           {//status.#Verified (//hash.sha1 binary_pom)
                               (//hash.md5 binary_pom)}])])

(the .public dependencies
  (-> Package (Try (Set Dependency)))
  (|>> (its #pom)
       product.left
       list
       (<xml>.value //pom.projection)
       (try#each (its /.#dependencies))))

(the .public repositories
  (-> Package (Try (Set Address)))
  (|>> (its #pom)
       product.left
       list
       (<xml>.value //pom.projection)
       (try#each (its /.#repositories))))

(the .public equivalence
  (Equivalence Package)
  (all product.equivalence
       //origin.equivalence
       (all product.equivalence
            binary.equivalence
            //status.equivalence)
       (all product.equivalence
            xml.equivalence
            binary.equivalence
            //status.equivalence)
       ))

(alias [=]
       ..equivalence)
