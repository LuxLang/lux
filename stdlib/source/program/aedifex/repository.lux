(.module:
  [lux #*
   ["." host (#+ import:)]
   [abstract
    [monad (#+ do)]]
   [control
    ["." io (#+ IO)]
    ["." try (#+ Try)]
    ["." exception (#+ exception:)]
    [concurrency
     ["." promise (#+ Promise)]
     ["." stm]]]
   [data
    ["." binary (#+ Binary)]
    ["." text
     ["%" format (#+ format)]
     ["." encoding]]
    [number
     ["n" nat]]]
   [tool
    [compiler
     ["." version]
     ["." language #_
      ["#/." lux #_
       ["#" version]]]]]
   [world
    [net (#+ URL)
     ["." uri (#+ URI)]]]]
  ["." // #_
   ["#." artifact (#+ Artifact)
    ["#/." extension (#+ Extension)]]])

(type: #export Address
  URL)

(type: #export User
  Text)

(type: #export Password
  Text)

(type: #export Identity
  {#user User
   #password Password})

(signature: #export (Repository !)
  (: (-> URI (! (Try Binary)))
     download)
  (: (-> Identity URI Binary (! (Try Any)))
     upload))

(def: #export (async repository)
  (-> (Repository IO) (Repository Promise))
  (structure
   (def: (download uri)
     (promise.future (\ repository download uri)))

   (def: (upload identity uri content)
     (promise.future (\ repository upload identity uri content)))
   ))

(signature: #export (Simulation s)
  (: (-> URI s (Try [s Binary]))
     on-download)
  (: (-> Identity URI Binary s (Try s))
     on-upload))

(def: #export (mock simulation init)
  (All [s] (-> (Simulation s) s (Repository Promise)))
  (let [state (stm.var init)]
    (structure
     (def: (download uri)
       (stm.commit
        (do {! stm.monad}
          [|state| (stm.read state)]
          (case (\ simulation on-download uri |state|)
            (#try.Success [|state| output])
            (do !
              [_ (stm.write |state| state)]
              (wrap (#try.Success output)))
            
            (#try.Failure error)
            (wrap (#try.Failure error))))))

     (def: (upload identity uri content)
       (stm.commit
        (do {! stm.monad}
          [|state| (stm.read state)]
          (case (\ simulation on-upload identity uri content |state|)
            (#try.Success |state|)
            (do !
              [_ (stm.write |state| state)]
              (wrap (#try.Success [])))
            
            (#try.Failure error)
            (wrap (#try.Failure error))))))
     )))

(import: java/lang/String)

(import: java/lang/AutoCloseable
  ["#::."
   (close [] #io #try void)])

(import: java/io/InputStream)

(import: java/io/OutputStream
  ["#::."
   (flush [] #io #try void)
   (write [[byte]] #io #try void)])

(import: java/net/URLConnection
  ["#::."
   (setDoOutput [boolean] #io #try void)
   (setRequestProperty [java/lang/String java/lang/String] #io #try void)
   (getInputStream [] #io #try java/io/InputStream)
   (getOutputStream [] #io #try java/io/OutputStream)])

(import: java/net/HttpURLConnection
  ["#::."
   (setRequestMethod [java/lang/String] #io #try void)
   (getResponseCode [] #io #try int)])

(import: java/net/URL
  ["#::."
   (new [java/lang/String])
   (openConnection [] #io #try java/net/URLConnection)])

(import: java/util/Base64$Encoder
  ["#::."
   (encodeToString [[byte]] java/lang/String)])

(import: java/util/Base64
  ["#::."
   (#static getEncoder [] java/util/Base64$Encoder)])

(import: java/io/BufferedInputStream
  ["#::."
   (new [java/io/InputStream])
   (read [[byte] int int] #io #try int)])

(exception: #export (deployment-failure {code Int})
  (exception.report
   ["Code" (%.int code)]))

(def: (basic-auth user password)
  (-> User Password Text)
  (format "Basic " (java/util/Base64$Encoder::encodeToString (\ encoding.utf8 encode (format user ":" password))
                                                             (java/util/Base64::getEncoder))))

(def: #export (uri artifact extension)
  (-> Artifact Extension URI)
  (format (//artifact.uri artifact) extension))

(def: buffer-size
  (n.* 512 1,024))

(def: user-agent
  (format "LuxAedifex/" (version.format language/lux.version)))

(structure: #export (remote address)
  (All [s] (-> Address (Repository IO)))

  (def: (download uri)
    (do {! (try.with io.monad)}
      [connection (|> (format address uri)
                      java/net/URL::new
                      java/net/URL::openConnection)
       #let [connection (:coerce java/net/HttpURLConnection connection)]
       _ (java/net/HttpURLConnection::setRequestMethod "GET" connection)
       _ (java/net/URLConnection::setRequestProperty "User-Agent" ..user-agent connection)
       input (|> connection
                 java/net/URLConnection::getInputStream
                 (\ ! map (|>> java/io/BufferedInputStream::new)))
       #let [buffer (binary.create ..buffer-size)]]
      (loop [output (\ binary.monoid identity)]
        (do !
          [bytes-read (java/io/BufferedInputStream::read buffer +0 (.int ..buffer-size) input)]
          (case bytes-read
            -1 (do !
                 [_ (java/lang/AutoCloseable::close input)]
                 (wrap output))
            _ (if (n.= ..buffer-size bytes-read)
                (recur (\ binary.monoid compose output buffer))
                (do !
                  [chunk (\ io.monad wrap (binary.slice 0 (.nat bytes-read) buffer))]
                  (recur (\ binary.monoid compose output chunk)))))))))

  (def: (upload [user password] uri content)
    (do (try.with io.monad)
      [connection (|> (format address uri)
                      java/net/URL::new
                      java/net/URL::openConnection)
       #let [connection (:coerce java/net/HttpURLConnection connection)]
       _ (java/net/HttpURLConnection::setRequestMethod "PUT" connection)
       _ (java/net/URLConnection::setDoOutput true connection)
       _ (java/net/URLConnection::setRequestProperty "Authorization" (..basic-auth user password) connection)
       stream (java/net/URLConnection::getOutputStream connection)
       _ (java/io/OutputStream::write content stream)
       _ (java/io/OutputStream::flush stream)
       _ (java/lang/AutoCloseable::close stream)
       code (java/net/HttpURLConnection::getResponseCode connection)]
      (case code
        +201 (wrap [])
        _ (\ io.monad wrap (exception.throw ..deployment-failure [code])))))
  )
