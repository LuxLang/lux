... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   ["[0]" ffi (.only import)]
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\format (.only format)]
     [encoding
      ["[0]" utf8]]]]]])

(every .public User
  Text)

(every .public Password
  Text)

(every .public Identity
  (Record
   [#user User
    #password Password]))

(the .public equivalence
  (Equivalence Identity)
  (all product.equivalence
       text.equivalence
       text.equivalence
       ))

(import java/util/Base64$Encoder
  "[1]::[0]"
  (encodeToString [[byte]] java/lang/String))

(import java/util/Base64
  "[1]::[0]"
  ("static" getEncoder [] java/util/Base64$Encoder))

(the .public (basic_auth user password)
  (-> User Password Text)
  (let [credentials (of utf8.codec injection (format user ":" password))]
    (|> (java/util/Base64::getEncoder)
        (java/util/Base64$Encoder::encodeToString credentials)
        ffi.of_string
        (format "Basic "))))
