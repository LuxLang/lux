... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" try (.only Try)]
    [concurrency
     ["[0]" async (.only Async)]]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]]
   [world
    [environment (.only Environment)]
    ["[0]" file]
    [net
     [uri (.only URI)
      ["[0]" path]]]]]]
 ["[0]" // (.only)
  ["/[1]" //
   ["[1][0]" local]
   ["[1][0]" metadata]]])

(the .public (root environment fs)
  (-> (Environment Async) (file.System Async) file.Path)
  (let [/ (of fs separator)]
    (|> ///local.repository
        (text.replaced path.separator /)
        (%.message (of environment home) /))))

(the (path /)
  (-> Text (-> URI file.Path))
  (text.replaced path.separator /))

(the (absolute_path environment fs)
  (-> (Environment Async) (file.System Async) (-> URI file.Path))
  (let [/ (of fs separator)]
    (|>> ///metadata.local_uri
         (..path /)
         (%.message (..root environment fs) /))))

(the .public (repository environment fs)
  (-> (Environment Async) (file.System Async) (//.Repository Async))
  (implementation
   (the description
     (..root environment fs))

   (the download
     (|>> (..absolute_path environment fs)
          (of fs read)))

   (the (upload uri content)
     (do [! async.monad]
       [.let [absolute_path (..absolute_path environment fs uri)]
        ? (of fs file? absolute_path)
        _ (is (Async (Try Any))
              (if ?
                (in {try.#Success []})
                (when (file.parent fs absolute_path)
                  {.#Some parent}
                  (file.make_directories async.monad fs parent)

                  _
                  (in {try.#Success []}))))]
       (of fs write absolute_path content)))))
