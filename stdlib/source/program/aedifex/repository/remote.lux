... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["[0]" io (.only IO)]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" product]
    [text
     ["%" \\injection]]
    [collection
     ["[0]" list]]]
   ["[0]" meta
    ["[1]/[0]" version]
    [macro
     ["[0]" template]]
    [compiler
     ["[0]" version]]]
   [world
    [net (.only URL)
     [uri (.only URI)]
     ["[0]" http
      ["[0]" response (.only Response)]
      ["[1]" client]
      ["[1]/[0]" status]
      ["@[1]" /]]]]]]
 ["[0]" // (.only)
  ["[1][0]" identity (.only Identity)]
  ["/[1]" //
   ["[1][0]" artifact (.only Version Artifact)
    [extension (.only Extension)]]]])

(every .public Address
  URL)

(template.with [<name>]
  [(exception.the .public (<name> [url status])
     (Exception [URL Nat])
     (exception.report
      (list ["URL" (%.text url)]
            ["Status Code" (%.nat status)])))]

  [download_failure]
  [upload_failure]
  )

(the .public (uri version_template artifact extension)
  (-> Version Artifact Extension URI)
  (%.message (///artifact.uri version_template artifact) extension))

(the .public user_agent
  (%.message "Lux_Aedifex/" (version.injection meta/version.latest)))

(the base_headers
  (List [Text Text])
  (list ["User-Agent" ..user_agent]))

(the .public (repository http identity address)
  (for_any (_ s)
    (-> (http.Client IO) (Maybe Identity) Address
        (//.Repository IO)))
  (implementation
   (the description
     address)
   (the (download uri)
     (do [! (try.with io.monad)]
       [[status message] (is (IO (Try (Response IO)))
                             (http.get (%.message address uri)
                                       (http.headers ..base_headers)
                                       {.#None}
                                       http))]
       (when status
         http/status.ok
         (of ! each product.right ((its @http.#body message) {.#None}))
         
         _
         (do !
           [_ ((its @http.#body message) {.#Some 0})]
           (of io.monad in (exception.except ..download_failure [(%.message address uri) status]))))))

   (the (upload uri content)
     (do (try.with io.monad)
       [[status message] (is (IO (Try (Response IO)))
                             (http.put (%.message address uri)
                                       (http.headers (when identity
                                                       {.#None}
                                                       ..base_headers
                                                       
                                                       {.#Some [user password]}
                                                       (list.partial ["Authorization" (//identity.basic_auth user password)]
                                                                     ..base_headers)))
                                       {.#Some content}
                                       http))
        _ ((its @http.#body message) {.#Some 0})]
       (when status
         http/status.created
         (in [])
         
         _
         (of io.monad in (exception.except ..upload_failure [(%.message address uri) status])))))
   ))
