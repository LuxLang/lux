... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Name)
   [abstract
    [equivalence (.only Equivalence)]
    [monoid (.only Monoid)]
    [monad (.only do)]]
   [error
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]]
    [collection
     ["[0]" dictionary (.only Dictionary)]
     ["[0]" set (.only Set)]
     ["[0]" stack (.use "[1]#[0]" mix)]]]]]
 ["[0]" //
  ["[1]" profile (.only Name Profile)]])

(the .public file
  "project.lux")

(every .public Project
  (Dictionary Name Profile))

(the .public (project name profile)
  (-> Name Profile
      Project)
  (dictionary.of_list text.hash (list [name profile])))

(the .public equivalence
  (Equivalence Project)
  (dictionary.equivalence //.equivalence))

(alias [=]
       ..equivalence)

(the .public monoid
  (Monoid Project)
  (implementation
   (the identity
     (dictionary.empty text.hash))

   (the composite
     (dictionary.composite_with (by //.monoid composite)))))

(exception.the .public (unknown_profile name)
  (Exception Name)
  (exception.report
   (list ["Name" (%.text name)])))

(exception.the .public (circular_dependency [dependee dependent])
  (Exception [Name Name])
  (exception.report
   (list ["Dependent" (%.text dependent)]
         ["Dependee" (%.text dependee)])))

(the (profile' lineage project name)
  (-> (Set Name) Project Name (Try Profile))
  (when (dictionary.value name project)
    {try.#Success profile}
    (when (stack.example (set.member? lineage)
                         (its //.#parents profile))
      {try.#Success ouroboros}
      (exception.except ..circular_dependency [ouroboros name])
      
      failure!
      (do [! try.monad]
        [parents (stack.each' ! (profile' (set.has name lineage) project)
                              (its //.#parents profile))]
        (in (stack#mix (function (_ parent child)
                         (by //.monoid composite child parent))
                       (has //.#parents (stack) profile)
                       parents))))
    
    failure!
    (exception.except ..unknown_profile [name])))

(the .public profile
  (-> Project Name (Try Profile))
  (..profile' (set.empty text.hash)))
