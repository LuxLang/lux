... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Type)
   [abstract
    [equivalence (.only Equivalence)]
    [order (.only Order)]
    [hash (.only Hash)]]
   [data
    ["[0]" product]
    ["[0]" text (.use "[1]#[0]" order)]]]]
 ["[0]" //
  ["[1]" artifact (.only Artifact) (.use "[1]#[0]" order)
   [type (.only Type)]]])

(every .public Dependency
  (Record
   [#artifact Artifact
    #type Type]))

(the .public hash
  (Hash Dependency)
  (all product.hash
       //.hash
       text.hash
       ))

(the .public equivalence
  (Equivalence Dependency)
  (of hash equivalence))

(alias [=]
       ..equivalence)

(the .public order
  (Order Dependency)
  (implementation
   (the equivalence
     ..equivalence)

   (the (< expected actual)
     (<| (or (//#< (its #artifact expected)
                   (its #artifact actual)))
         
         (and (//.= (its #artifact expected)
                    (its #artifact actual)))
         (text#< (its #type expected)
                 (its #type actual))))))
