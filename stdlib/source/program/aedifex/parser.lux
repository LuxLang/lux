... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except type)
   [abstract
    [monad (.only do)]]
   [control
    ["<>" parser (.use "[1]#[0]" functor)]
    ["[0]" maybe]]
   [data
    ["[0]" text]
    [collection
     ["[0]" set (.only Set)]
     ["[0]" dictionary (.only Dictionary)]
     [list
      ["[0]" property]]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\parser (.only Parser)]]
    [compiler
     [meta
      [cli
       [compiler (.only Compiler)]]]]]
   [world
    [net (.only URL)]]]]
 ["[0]" //
  ["/" profile]
  ["[1][0]" runtime (.only Runtime)]
  ["[1][0]" project (.only Project)]
  ["[1][0]" dependency]
  ["[1][0]" format]
  ["[1][0]" repository
   ["[1]" remote]]
  ["[1][0]" artifact (.only Artifact)
   ["[1]/[0]" type]]])

(the (singular input tag parser)
  (All (_ a) (-> (Dictionary Text Code) Text (Parser a) (Parser a)))
  (<code>.locally (maybe.list (dictionary.value tag input))
                  parser))

(the (plural input tag parser)
  (All (_ a) (-> (Dictionary Text Code) Text (Parser a) (Parser (List a))))
  (<code>.locally (maybe.list (dictionary.value tag input))
                  (<code>.tuple (<>.some parser))))

(the group
  (Parser //artifact.Group)
  <code>.text)

(the name
  (Parser //artifact.Name)
  <code>.text)

(the version
  (Parser //artifact.Version)
  <code>.text)

(the artifact'
  (Parser //artifact.Artifact)
  (all <>.and ..group ..name ..version))

(the artifact
  (Parser //artifact.Artifact)
  (<code>.tuple ..artifact'))

(the url
  (Parser URL)
  <code>.text)

(the scm
  (Parser /.SCM)
  ..url)

(the description
  (Parser Text)
  <code>.text)

(the license
  (Parser /.License)
  (do [! <>.monad]
    [input (of ! each
               (dictionary.of_list text.hash)
               (<code>.tuple (<>.some (<>.and <code>.text
                                              <code>.any))))]
    (all <>.and
         (..singular input "name" ..name)
         (..singular input "url" ..url)
         (<>.else {/.#Repo}
                  (..singular input "type"
                              (<>.or (<code>.this (' "repo"))
                                     (<code>.this (' "manual"))))))))

(the organization
  (Parser /.Organization)
  (do [! <>.monad]
    [input (of ! each
               (dictionary.of_list text.hash)
               (<code>.tuple (<>.some (<>.and <code>.text
                                              <code>.any))))]
    (all <>.and
         (..singular input "name" ..name)
         (..singular input "url" ..url))))

(the developer
  (Parser /.Developer)
  (do [! <>.monad]
    [input (of ! each
               (dictionary.of_list text.hash)
               (<code>.tuple (<>.some (<>.and <code>.text
                                              <code>.any))))]
    (all <>.and
         (..singular input "name" ..name)
         (..singular input "url" ..url)
         (<>.maybe (..singular input "organization" ..organization))
         )))

(the contributor
  (Parser /.Contributor)
  ..developer)

(the info
  (Parser /.Info)
  (do [! <>.monad]
    [input (of ! each
               (dictionary.of_list text.hash)
               (<code>.tuple (<>.some (<>.and <code>.text
                                              <code>.any))))]
    (all <>.and
         (<>.maybe (..singular input "url" ..url))
         (<>.maybe (..singular input "scm" ..scm))
         (<>.maybe (..singular input "description" ..description))
         (<>.else (list) (..plural input "licenses" ..license))
         (<>.maybe (..singular input "organization" ..organization))
         (<>.else (list) (..plural input "developers" ..developer))
         (<>.else (list) (..plural input "contributors" ..contributor))
         )))

(the repository
  (Parser //repository.Address)
  ..url)

(the type
  (Parser //artifact/type.Type)
  <code>.text)

(the dependency
  (Parser //dependency.Dependency)
  (<code>.tuple
   (all <>.and
        ..artifact'
        (<>.else //artifact/type.lux_library ..type)
        )))

(the compiler
  (Parser Compiler)
  (<code>.tuple
   (all <>.and
        <code>.global
        (<>.some <code>.text)
        )))

(the source
  (Parser /.Source)
  <code>.text)

(the target
  (Parser /.Target)
  <code>.text)

(the module_or_program
  (Parser Symbol)
  <code>.symbol)

(the program
  (Parser Symbol)
  <code>.global)

(the deploy_repository
  (Parser [Text //repository.Address])
  (<>.and <code>.text
          ..repository))

(the configuration/1
  (Parser [Text Text])
  (<>.and <code>.text
          <code>.text))

(the runtime
  (Parser Runtime)
  (let [environment (<>#each (dictionary.of_list text.hash)
                             (<>.some (<code>.tuple (<>.and <code>.text <code>.text))))]
    (<code>.tuple
     (all <>.and
          environment
          <code>.text
          (<>.some <code>.text)))))

(the profile
  (Parser /.Profile)
  (do [! <>.monad]
    [input (of ! each
               (dictionary.of_list text.hash)
               (<code>.tuple (<>.some (<>.and <code>.text
                                              <code>.any))))
     .let [^parents (is (Parser (List /.Name))
                        (<>.else (list)
                                 (..plural input "parents" <code>.text)))
           ^identity (is (Parser (Maybe Artifact))
                         (<>.maybe
                          (..singular input "identity" ..artifact)))
           ^info (is (Parser (Maybe /.Info))
                     (<>.maybe
                      (..singular input "info" ..info)))
           ^repositories (is (Parser (Set //repository.Address))
                             (|> (..plural input "repositories" ..repository)
                                 (of ! each (set.of_list text.hash))
                                 (<>.else (set.empty text.hash))
                                 (of ! each (set.has /.default_repository))))
           ^dependencies (is (Parser (Set //dependency.Dependency))
                             (|> (..plural input "dependencies" ..dependency)
                                 (of ! each (set.of_list //dependency.hash))
                                 (<>.else (set.empty //dependency.hash))))
           ^lux (|> ..dependency
                    (..singular input //format.lux_compiler_label)
                    (<>.else /.default_lux))
           ^compilers (|> ..compiler
                          (..plural input "compilers")
                          (<>.else (list)))
           ^sources (is (Parser (Set /.Source))
                        (|> (..plural input "sources" ..source)
                            (of ! each (set.of_list text.hash))
                            (<>.else (set.of_list text.hash (list /.default_source)))))
           ^target (is (Parser /.Target)
                       (|> ..target
                           (..singular input "target")
                           (<>.else /.default_target)))
           ^program (is (Parser (Maybe Symbol))
                        (<>.maybe
                         (..singular input "program" ..module_or_program)))
           ^test (is (Parser (Maybe Symbol))
                     (<>.maybe
                      (..singular input "test" ..program)))
           ^deploy_repositories (is (Parser (Dictionary Text //repository.Address))
                                    (<| (of ! each (dictionary.of_list text.hash))
                                        (<>.else (list))
                                        (..plural input "deploy_repositories" ..deploy_repository)))
           ^configuration (is (Parser (property.List Text))
                              (<| (<>.else (list))
                                  (..plural input "configuration" ..configuration/1)))
           ^java (|> ..runtime
                     (..singular input "java")
                     (<>.else //runtime.default_java))
           ^js (|> ..runtime
                   (..singular input "js")
                   (<>.else //runtime.default_js))
           ^python (|> ..runtime
                       (..singular input "python")
                       (<>.else //runtime.default_python))
           ^lua (|> ..runtime
                    (..singular input "lua")
                    (<>.else //runtime.default_lua))
           ^ruby (|> ..runtime
                     (..singular input "ruby")
                     (<>.else //runtime.default_ruby))]]
    (all <>.and
         ^parents
         ^identity
         ^info
         ^repositories
         ^dependencies
         ^lux
         ^compilers
         ^sources
         ^target
         ^program
         ^test
         ^deploy_repositories
         ^configuration
         ^java
         ^js
         ^python
         ^lua
         ^ruby
         )))

(the .public project
  (Parser Project)
  (of <>.monad each
      (dictionary.of_list text.hash)
      (<code>.tuple (<>.some (<>.and <code>.text
                                     ..profile)))))
