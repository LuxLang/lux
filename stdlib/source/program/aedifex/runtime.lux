... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except for)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" product]
    ["[0]" text]
    [collection
     ["[0]" list (.use "[1]#[0]" monoid)]
     ["[0]" dictionary (.only Dictionary)]]]
   [meta
    [macro
     ["[0]" template]]]
   [world
    ["[0]" file]]]])

(every .public Runtime
  (Record
   [#environment (Dictionary Text Text)
    #program Text
    #parameters (List Text)]))

(the .public equivalence
  (Equivalence Runtime)
  (all product.equivalence
       (dictionary.equivalence text.equivalence)
       text.equivalence
       (list.equivalence text.equivalence)
       ))

(alias [=]
       ..equivalence)

(template.with [<name> <command> <environment> <parameters>]
  [(the .public <name>
     Runtime
     [#environment (dictionary.of_list text.hash (`` (list (,, (template.spliced <environment>)))))
      #program <command>
      #parameters (`` (list (,, (template.spliced <parameters>))))])]

  [default_java "java" [] ["-Xss16m" "--add-opens" "java.base/java.lang=ALL-UNNAMED" "-jar"]]
  [default_js "node" [] ["--stack_size=8192"]]
  [default_python "python3" [] []]
  [default_lua "lua" [] []]
  ... 2^23
  [default_ruby "ruby" [["RUBY_THREAD_VM_STACK_SIZE" "8388608"]] []]
  )

(the .public (for runtime path)
  (-> Runtime file.Path Runtime)
  (revised #parameters
           (function (_ parameters)
             (list#composite parameters (list path)))
           runtime))
