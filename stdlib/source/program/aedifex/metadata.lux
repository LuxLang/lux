(.module:
  [lux #*
   [data
    ["." text
     ["%" format (#+ format)]]]
   [world
    [file (#+ Path)]
    [net
     ["." uri (#+ URI)]]]]
  ["." // #_
   ["#." artifact (#+ Artifact)]])

(def: remote_file
  Path
  "maven-metadata.xml")

(def: #export (remote_artifact_uri artifact)
  (-> Artifact URI)
  (let [/ uri.separator]
    (format (get@ #//artifact.group artifact)
            / (get@ #//artifact.name artifact)
            / (get@ #//artifact.version artifact)
            / ..remote_file)))

(def: #export (remote_project_uri artifact)
  (-> Artifact URI)
  (let [/ uri.separator]
    (format (get@ #//artifact.group artifact)
            / (get@ #//artifact.name artifact)
            / ..remote_file)))

(def: local_file
  Path
  "maven-metadata-local.xml")

(def: #export (local_uri remote_uri)
  (-> URI URI)
  (text.replace_once ..remote_file ..local_file remote_uri))

(def: #export (remote_uri local_uri)
  (-> URI URI)
  (text.replace_once ..local_file ..remote_file local_uri))
