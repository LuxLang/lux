(.module:
  [lux #*
   [data
    ["." text
     ["%" format (#+ format)]]]
   [world
    [file (#+ Path)]
    [net
     ["." uri (#+ URI)]]]]
  [//
   ["." artifact (#+ Artifact)]])

(def: #export file
  Path
  "maven-metadata.xml")

(def: (project' separator artifact)
  (-> Text Artifact Text)
  (format (artifact.directory separator (get@ #artifact.group artifact))
          separator
          (get@ #artifact.name artifact)))

(def: (version' separator artifact)
  (-> Text Artifact Text)
  (format (..project' separator artifact)
          separator
          (get@ #artifact.version artifact)))

(template [<public> <private>]
  [(def: #export (<public> artifact)
     (-> Artifact URI)
     (let [/ uri.separator]
       (format (<private> / artifact) / ..file)))]

  [project ..project']
  [version ..version']
  )
