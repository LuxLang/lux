... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except #version
                local)
   [abstract
    [equivalence (.only Equivalence)]
    [order (.only Order)]
    [hash (.only Hash)]]
   [data
    ["[0]" product]
    ["[0]" text (.use "[1]#[0]" order)
     ["%" \\injection (.only Injection)]]
    [collection
     ["[0]" list (.use "[1]#[0]" monoid)]]]
   [world
    ["[0]" file (.only Path)]
    [net
     [uri (.only URI)
      ["[0]" path]]]]]])

(every .public Group
  Text)

(every .public Name
  Text)

(every .public Version
  Text)

(every .public Artifact
  (Record
   [#group Group
    #name Name
    #version Version]))

(the .public hash
  (Hash Artifact)
  (all product.hash
       text.hash
       text.hash
       text.hash
       ))

(the .public equivalence
  (Equivalence Artifact)
  (of ..hash equivalence))

(the .public order
  (Order Artifact)
  (implementation
   (the equivalence
     ..equivalence)

   (the (< expected actual)
     (<| (or (text#< (its #group expected)
                     (its #group actual)))

         (and (text#= (its #group expected)
                      (its #group actual)))
         (or (text#< (its #name expected)
                     (its #name actual)))
         
         (and (text#= (its #name expected)
                      (its #name actual)))
         (text#< (its #version expected)
                 (its #version actual))))))

(with_template [<separator> <definition>]
  [(the <definition>
     Text
     <separator>)]

  ["." group_separator]
  ["-" version_separator]
  [":" identity_separator]
  )

(the .public (identity artifact)
  (-> Artifact Text)
  (%.message (its #name artifact)
             ..version_separator
             (its #version artifact)))

(the .public (injection value)
  (Injection Artifact)
  (%.message (its #group value)
             ..identity_separator
             (..identity value)))

(the .public (directory separator group)
  (-> Text Group Text)
  (|> group
      (text.all_split_by ..group_separator)
      (text.interposed separator)))

(the .public (bundle version artifact)
  (-> Version Artifact URI)
  (let [/ path.separator
        group (..directory / (its #group artifact))
        name (its #name artifact)]
    (%.message group / name / version)))

(the .public (uri version artifact)
  (-> Version Artifact URI)
  (let [/ path.separator]
    (%.message (..bundle version artifact) / (..identity artifact))))

(the .public (local artifact)
  (-> Artifact (List Text))
  (list#composite (|> artifact
                      (its #group)
                      (text.all_split_by ..group_separator))
                  (list (its #name artifact)
                        (its #version artifact))))
