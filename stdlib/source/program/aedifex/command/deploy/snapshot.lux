... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [concurrency
    ["[0]" future (.only Future)]]
   [function
    [predicate (.only Predicate)]]
   [data
    ["[0]" binary
     ["[1]" \\injection]]
    ["[0]" text (.only)
     [encoding
      ["[0]" utf8]]]
    [collection
     ["[0]" set]]
    [format
     ["[0]" tar]
     ["[0]" xml]]]
   [compiler
    [meta
     ["[0]" export]]]
   [world
    [console (.only Console)]
    ["[0]" file]]]]
 ["[0]" ////
  [command (.only Command)]
  ["/" profile]
  ["[1][0]" action]
  ["[1][0]" pom]
  ["[1][0]" package]
  ["[1][0]" dependency
   ["[1]/[0]" deployment]
   ["[1]/[0]" status]]
  ["[1][0]" repository (.only Repository)
   ["[1]/[0]" origin]]
  ["[1][0]" artifact (.only Artifact)
   ["[1]/[0]" type]]])

(the .public snapshot?
  (Predicate Artifact)
  (|>> (its ////artifact.#version)
       (text.contains? "-SNAPSHOT")))

(the .public (do! console remote fs artifact profile)
  (-> (Console Future) (Repository Future) (file.System Future) Artifact (Command Any))
  (do [! ////action.monad]
    [library (|> profile
                 (its /.#sources)
                 set.as_list
                 list.as_stack
                 (export.library fs)
                 (by ! each (binary.value tar.as_binary)))
     pom (by future.monad in (////pom.write profile))
     _ (////dependency/deployment.one
        remote
        [artifact ////artifact/type.lux_library]
        (let [pom_data (|> pom
                           (by xml.text as)
                           (by utf8.binary as))]
          [////package.#origin {////repository/origin.#Remote ""}
           ////package.#library [library
                                 (////dependency/status.verified library)]
           ////package.#pom [pom
                             pom_data
                             (////dependency/status.verified pom_data)]]))]
    (in [])))
