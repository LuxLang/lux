... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    [function
     [predicate (.only Predicate)]]
    [concurrency
     ["[0]" async (.only Async)]]]
   [data
    ["[0]" binary
     ["[1]" \\format]]
    ["[0]" text (.only)
     [encoding
      ["[0]" utf8]]]
    [collection
     ["[0]" set]]
    [format
     ["[0]" tar]
     ["[0]" xml]]]
   [meta
    [compiler
     [meta
      ["[0]" export]]]]
   [world
    [console (.only Console)]
    ["[0]" file]]]]
 ["[0]" ////
  [command (.only Command)]
  ["/" profile]
  ["[1][0]" action]
  ["[1][0]" pom]
  ["[1][0]" package]
  ["[1][0]" dependency
   ["[1]/[0]" deployment]
   ["[1]/[0]" status]]
  ["[1][0]" repository (.only Repository)
   ["[1]/[0]" origin]]
  ["[1][0]" artifact (.only Artifact)
   ["[1]/[0]" type]]])

(the .public snapshot?
  (Predicate Artifact)
  (|>> (its ////artifact.#version)
       (text.contains? "-SNAPSHOT")))

(the .public (do! console remote fs artifact profile)
  (-> (Console Async) (Repository Async) (file.System Async) Artifact (Command Any))
  (do [! ////action.monad]
    [library (|> profile
                 (its /.#sources)
                 set.list
                 (export.library fs)
                 (of ! each (binary.value tar.format)))
     pom (of async.monad in (////pom.write profile))
     _ (////dependency/deployment.one
        remote
        [artifact ////artifact/type.lux_library]
        (let [pom_data (|> pom
                           (of xml.codec encoded)
                           (of utf8.codec encoded))]
          [////package.#origin {////repository/origin.#Remote ""}
           ////package.#library [library
                                 (////dependency/status.verified library)]
           ////package.#pom [pom
                             pom_data
                             (////dependency/status.verified pom_data)]]))]
    (in [])))
