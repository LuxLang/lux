... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]]
   [concurrency
    ["[0]" future (.only Future)]]
   [control
    ["[0]" try (.only Try)]]
   [data
    [collection
     ["[0]" list]
     ["[0]" set]]]
   [macro
    ["[0]" expansion]]
   [world
    [environment (.only Environment)]
    [shell (.only Exit Shell)]
    [console (.only Console)]
    ["[0]" file (.only)
     ["[0]" watch (.only Watcher)]]]]]
 ["[0]" //
  ["/[1]" //
   [command (.only Command)]
   ["[1]" profile]
   ["[1][0]" action]
   [dependency
    [resolution (.only Resolution)]]]])

(the (targets fs path)
  (-> (file.System Future) file.Path (Future (List file.Path)))
  (let [! future.monad]
    (|> path
        (by fs sub_directories)
        (by ! each (|>> (try.else (list))
                        (monad.each ! (targets fs))))
        (by ! conjoint)
        (by ! each (|>> list.together (list.partial path))))))

(the .public delay
  Natural
  1,000)

(the (pause delay)
  (-> Natural (Future (Try Any)))
  (future.after delay {try.#Success []}))

(the .public (do! delay watcher command)
  (for_any (_ a)
    (-> Natural (Watcher Future)
        (-> (Console Future) (Environment Future) (file.System Future) (Shell Future) Resolution (Command [Exit a]))
        (-> (Console Future) (Environment Future) (file.System Future) (Shell Future) Resolution (Command [Exit Any]))))
  (function (_ console environment fs shell resolution)
    (function (_ profile)
      (expansion.let [<call> ((command console environment fs shell resolution) profile)]
        (do [! future.monad]
          [targets (|> profile
                       (its ///.#sources)
                       set.list
                       (monad.each ! (..targets fs))
                       (by ! each list.together))]
          (do [! ///action.monad]
            [_ (monad.each ! (by watcher start watch.modification) targets)
             _ <call>]
            (loop (again [_ []])
              (do !
                [_ (..pause delay)
                 events (by watcher poll [])]
                (when events
                  {.#Item _}
                  (do !
                    [_ <call>]
                    (again []))

                  {.#End}
                  (again []))))))))))
