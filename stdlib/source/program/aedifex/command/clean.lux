(.module:
  [library
   [lux #*
    [abstract
     ["." monad (#+ do)]]
    [control
     [try (#+ Try)]
     [concurrency
      ["." async (#+ Async)]]]
    [data
     [text
      ["%" format (#+ format)]]]
    [world
     ["." file (#+ Path)]
     ["." console (#+ Console)]]]]
  ["." /// #_
   [command (#+ Command)]
   ["#" profile]
   ["#." action (#+ Action)]])

(def: (clean_files! fs root)
  (-> (file.System Async) Path (Async (Try Any)))
  (let [! ///action.monad]
    (|> root
        (\ fs directory_files)
        (\ ! map (monad.map ! (\ fs delete)))
        (\ ! join))))

(def: .public success
  (-> ///.Target Text)
  (|>> (format "Successfully cleaned target directory: ")))

(def: .public (do! console fs profile)
  (-> (Console Async) (file.System Async) (Command Any))
  (do {! async.monad}
    [.let [target (get@ #///.target profile)]
     ? (\ fs directory? target)
     _ (let [! ///action.monad]
         (if ?
           (loop [root target]
             (do !
               [_ (..clean_files! fs root)
                _ (|> root
                      (\ fs sub_directories)
                      (\ ! map (monad.map ! recur))
                      (\ ! join))]
               (\ fs delete root)))
           (\ ! in [])))]
    (console.write_line (..success target) console)))
