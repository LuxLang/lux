... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [concurrency
    ["[0]" future (.only Future)]]
   [error
    [try (.only Try)]]
   [data
    [text
     ["%" \\injection]]
    [collection
     ["[0]" list]]]
   [world
    ["[0]" file (.only Path)]
    ["[0]" console (.only Console)]]]]
 ["[0]" ///
  [command (.only Command)]
  ["[1]" profile]
  ["[1][0]" action (.only Action)]])

(the (clean_files! fs root)
  (-> (file.System Future) Path (Future (Try Any)))
  (do [! ///action.monad]
    [_ (|> root
           (by fs directory_files)
           (by ! each (list.each' ! (by fs delete)))
           (by ! conjoint))]
    (pure [])))

(the .public success
  (-> ///.Target Text)
  (|>> (text "Successfully cleaned target directory: ")))

(the .public (do! console fs profile)
  (-> (Console Future) (file.System Future) (Command Any))
  (do [! future.monad]
    [.let [target (its ///.#target profile)]
     ? (by fs directory? target)
     _ (let [! ///action.monad]
         (if ?
             (loop (again [root target])
               (do !
                 [_ (..clean_files! fs root)
                  _ (|> root
                        (by fs sub_directories)
                        (by ! each (list.each' ! again))
                        (by ! conjoint))]
                 (by fs delete root)))
             (by ! pure [])))]
    (console.write_line (..success target) console)))
