... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only do)]]
   [concurrency
    ["[0]" async (.only Async)]]
   [control
    [try (.only Try)]]
   [data
    [text
     ["%" \\injection]]]
   [world
    ["[0]" file (.only Path)]
    ["[0]" console (.only Console)]]]]
 ["[0]" ///
  [command (.only Command)]
  ["[1]" profile]
  ["[1][0]" action (.only Action)]])

(the (clean_files! fs root)
  (-> (file.System Async) Path (Async (Try Any)))
  (do [! ///action.monad]
    [_ (|> root
           (by fs directory_files)
           (by ! each (monad.each ! (by fs delete)))
           (by ! conjoint))]
    (in [])))

(the .public success
  (-> ///.Target Text)
  (|>> (%.message "Successfully cleaned target directory: ")))

(the .public (do! console fs profile)
  (-> (Console Async) (file.System Async) (Command Any))
  (do [! async.monad]
    [.let [target (its ///.#target profile)]
     ? (by fs directory? target)
     _ (let [! ///action.monad]
         (if ?
           (loop (again [root target])
             (do !
               [_ (..clean_files! fs root)
                _ (|> root
                      (by fs sub_directories)
                      (by ! each (monad.each ! again))
                      (by ! conjoint))]
               (by fs delete root)))
           (by ! in [])))]
    (console.write_line (..success target) console)))
