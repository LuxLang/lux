(.module:
  [lux #*
   [abstract
    [monad (#+ do)]]
   [control
    ["." try (#+ Try)]
    [security
     ["!" capability]]
    [concurrency
     ["." promise (#+ Promise) ("#\." monad)]]]
   [data
    ["." text
     ["%" format (#+ format)]
     [encoding
      ["." utf8]]]
    [format
     ["." xml]]]
   [world
    ["." file (#+ Path File)]
    ["." console (#+ Console)]]]
  ["." // #_
   ["#." clean]
   ["/#" // #_
    [command (#+ Command)]
    ["#." action (#+ Action)]
    ["#." pom]]])

(def: #export success
  (format "Successfully created POM file: " ///pom.file))

(def: #export (do! console fs profile)
  (-> (Console Promise) (file.System Promise) (Command Path))
  (do ///action.monad
    [pom (promise\wrap (///pom.write profile))
     file (: (Promise (Try (File Promise)))
             (file.get_file promise.monad fs ///pom.file))
     outcome (|> pom
                 (\ xml.codec encode)
                 (\ utf8.codec encode)
                 (!.use (\ file over_write)))
     _ (console.write_line ..success console)]
    (wrap ///pom.file)))
