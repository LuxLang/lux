(.module:
  [lux (#- Name)
   [abstract
    [monad (#+ do)]]
   [control
    ["." try (#+ Try)]
    ["." exception (#+ exception:)]
    ["." io (#+ IO)]
    [concurrency
     ["." promise (#+ Promise) ("#\." monad)]]
    [security
     ["!" capability]]]
   [data
    ["." product]
    ["." maybe]
    ["." text ("#\." equivalence)
     ["%" format (#+ format)]]
    [collection
     ["." list ("#\." functor)]
     ["." dictionary]
     ["." set]]]
   [math
    [number
     ["i" int]]]
   [world
    [program (#+ Program)]
    ["." file (#+ Path)]
    ["." shell (#+ Shell)]
    ["." console (#+ Console)]
    [net
     ["." uri]]]]
  ["." /// #_
   ["#" profile]
   ["#." action]
   ["#." command (#+ Command)]
   ["#." local]
   ["#." repository]
   ["#." runtime]
   ["#." dependency (#+ Dependency)
    ["#/." resolution (#+ Resolution)]]
   ["#." artifact (#+ Group Name Artifact)
    ["#/." type]]])

(type: Finder
  (-> Resolution (Maybe Dependency)))

(def: (dependency_finder group name)
  (-> Group Name Finder)
  (|>> dictionary.entries
       (list.one (function (_ [dependency package])
                   (if (and (text\= group (get@ [#///dependency.artifact #///artifact.group] dependency))
                            (text\= name (get@ [#///dependency.artifact #///artifact.name] dependency)))
                     (#.Some dependency)
                     #.None)))))

(def: #export lux_group
  Group
  "com.github.luxlang")

(def: #export jvm_compiler_name
  Name
  "lux-jvm")

(def: #export js_compiler_name
  Name
  "lux-js")

(template [<finder> <name>]
  [(def: <finder>
     Finder
     (..dependency_finder ..lux_group <name>))]

  [jvm_compiler ..jvm_compiler_name]
  [js_compiler ..js_compiler_name]
  )

(exception: #export no_available_compiler)
(exception: #export no_specified_program)
(exception: #export no_specified_target)

(type: #export Compiler
  (#JVM Artifact)
  (#JS Artifact))

(def: (remove_dependency dependency)
  (-> Dependency (-> Resolution Resolution))
  (|>> dictionary.entries
       (list.filter (|>> product.left (is? dependency) not))
       (dictionary.from_list ///dependency.hash)))

(def: (compiler resolution)
  (-> Resolution (Try [Resolution Compiler]))
  (case [(..jvm_compiler resolution)
         (..js_compiler resolution)]
    [(#.Some dependency) _]
    (#try.Success [(..remove_dependency dependency resolution)
                   (#JVM (get@ #///dependency.artifact dependency))])
    
    [_ (#.Some dependency)]
    (#try.Success [(..remove_dependency dependency resolution)
                   (#JS (get@ #///dependency.artifact dependency))])

    _
    (exception.throw ..no_available_compiler [])))

(def: (path fs home artifact)
  (All [!] (-> (file.System !) Path Artifact Path))
  (let [/ (\ fs separator)]
    (|> artifact
        (///local.uri (get@ #///artifact.version artifact))
        (text.replace_all uri.separator /)
        (format home /))))

(def: (libraries fs home)
  (All [!] (-> (file.System !) Path Resolution (List Path)))
  (|>> dictionary.keys
       (list.filter (|>> (get@ #///dependency.type) (text\= ///artifact/type.lux_library)))
       (list\map (|>> (get@ #///dependency.artifact) (..path fs home)))))

(def: (singular name)
  (-> Text Text (List Text))
  (|>> (list name)))

(def: (plural name)
  (-> Text (List Text) (List Text))
  (|>> (list\map (|>> (list name))) list.concat))

(def: #export start "[BUILD STARTED]")
(def: #export success "[BUILD ENDED]")
(def: #export failure "[BUILD FAILED]")

(def: #export (do! console program fs shell resolution profile)
  (-> (Console Promise) (Program Promise) (file.System Promise) (Shell Promise) Resolution (Command [Compiler Path]))
  (case [(get@ #///.program profile)
         (get@ #///.target profile)]
    [#.None _]
    (promise\wrap (exception.throw ..no_specified_program []))

    [_ #.None]
    (promise\wrap (exception.throw ..no_specified_target []))
    
    [(#.Some program_module) (#.Some target)]
    (do promise.monad
      [environment (\ program environment [])
       home (\ program home [])
       working_directory (\ program directory [])]
      (do ///action.monad
        [[resolution compiler] (promise\wrap (..compiler resolution))
         #let [[command output] (let [[compiler output] (case compiler
                                                          (#JVM artifact) [(///runtime.java (..path fs home artifact))
                                                                           "program.jar"]
                                                          (#JS artifact) [(///runtime.node (..path fs home artifact))
                                                                          "program.js"])]
                                  [(format compiler " build") output])
               / (\ fs separator)
               cache_directory (format working_directory / target)]
         _ (console.write_line ..start console)
         process (!.use (\ shell execute)
                        [environment
                         working_directory
                         command
                         (list.concat (list (..plural "--library" (..libraries fs home resolution))
                                            (..plural "--source" (set.to_list (get@ #///.sources profile)))
                                            (..singular "--target" cache_directory)
                                            (..singular "--module" program_module)))])
         exit (!.use (\ process await) [])
         _ (console.write_line (if (i.= shell.normal exit)
                                 ..success
                                 ..failure)
                               console)]
        (wrap [compiler
               (format cache_directory / output)])))))
