... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]
    [equivalence (.only Equivalence)]]
   [control
    ["<>" projection]
    ["[0]" pipe]
    ["[0]" try (.only Try)]
    [concurrency
     ["[0]" async (.only Async)]]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]
     ["<[1]>" \\projection]
     [encoding
      ["[0]" utf8]]]
    [format
     ["[0]" xml (.only XML)
      ["<[1]>" \\projection (.only Projection)]]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    [number
     ["n" natural]]]
   [meta
    [macro
     ["[0]" template]]]
   [world
    [net
     ["[0]" uri (.only URI)]]
    ["[0]" time (.only Time)
     ["[0]" instant (.only Instant)]
     ["[0]" date (.only Date)]
     ["[0]" year]
     ["[0]" month]]]]]
 ["[0]" // (.only)
  ["/[1]" //
   [repository (.only Repository)]
   ["[1][0]" artifact (.only Group Name Version Artifact)]]])

(every .public Metadata
  (Record
   [#group Group
    #name Name
    #versions (List Version)
    #last_updated Instant]))

(the (pad value)
  (-> Natural Text)
  (if (n.< 10 value)
    (%.message "0" (%.natural value))
    (%.natural value)))

(the (date_injection value)
  (text.Injection Date)
  (%.message (|> value date.year year.value .natural %.natural)
             (|> value date.month month.number ..pad)
             (|> value date.day_of_month ..pad)))

(the (time_injection value)
  (text.Injection Time)
  (let [(open "[0]") (time.clock value)]
    (%.message (..pad #hour)
               (..pad #minute)
               (..pad #second))))

(the (instant_injection value)
  (text.Injection Instant)
  (%.message (..date_injection (instant.date value))
             (..time_injection (instant.time value))))

(template.with [<definition> <tag>]
  [(the <definition> xml.Tag ["" <tag>])]

  [<group> "groupId"]
  [<name> "artifactId"]
  [<version> "version"]
  [<versioning> "versioning"]
  [<versions> "versions"]
  [<last_updated> "lastUpdated"]
  [<metadata> "metadata"]
  )

(template.with [<name> <type> <tag> <pre>]
  [(the <name>
     (-> <type> XML)
     (|>> <pre> {xml.#Text} list {xml.#Node <tag> xml.attributes}))]

  [group_injection Group ..<group> (|>)]
  [name_injection Name ..<name> (|>)]
  [version_injection Version ..<version> (|>)]
  [last_updated_injection Instant ..<last_updated> ..instant_injection]
  )

(the versions_injection
  (-> (List Version) XML)
  (|>> (list#each ..version_injection) {xml.#Node ..<versions> xml.attributes}))

(the .public (injection value)
  (-> Metadata XML)
  {xml.#Node ..<metadata>
             xml.attributes
             (list (..group_injection (its #group value))
                   (..name_injection (its #name value))
                   {xml.#Node ..<versioning>
                              xml.attributes
                              (list (..versions_injection (its #versions value))
                                    (..last_updated_injection (its #last_updated value)))})})

(the (text tag)
  (-> xml.Tag (Projection Text))
  (<| (<xml>.node tag)
      <xml>.text))

(the date_projection
  (<text>.Projection Date)
  (do <>.monad
    [year (<>.format n.base_10 (<text>.exactly 4 <text>.decimal))
     year (<>.of_try (year.year (.int year)))
     month (<>.format n.base_10 (<text>.exactly 2 <text>.decimal))
     month (<>.of_try (month.by_number month))
     day_of_month (<>.format n.base_10 (<text>.exactly 2 <text>.decimal))]
    (<>.of_try (date.date year month day_of_month))))

(the time_projection
  (<text>.Projection Time)
  (do <>.monad
    [hour (<>.format n.base_10 (<text>.exactly 2 <text>.decimal))
     minute (<>.format n.base_10 (<text>.exactly 2 <text>.decimal))
     second (<>.format n.base_10 (<text>.exactly 2 <text>.decimal))]
    (<>.of_try (time.time
                [time.#hour hour
                 time.#minute minute
                 time.#second second
                 time.#milli_second 0]))))

(the last_updated_projection
  (Projection Instant)
  (<text>.then (do <>.monad
                 [date ..date_projection
                  time ..time_projection]
                 (in (instant.of_date_time date time)))
               (..text ..<last_updated>)))

(the .public projection
  (Projection Metadata)
  (<| (<xml>.node ..<metadata>)
      (all <>.and
           (<xml>.somewhere (..text ..<group>))
           (<xml>.somewhere (..text ..<name>))
           (<| (<xml>.node ..<versioning>)
               ... Handle any ignorable tag.
               (<>.before (<>.some <xml>.any))
               (all <>.and
                    (<| <xml>.somewhere
                        (<xml>.node ..<versions>)
                        (<>.many (..text ..<version>)))
                    (<xml>.somewhere ..last_updated_projection)
                    )))))

(the .public equivalence
  (Equivalence Metadata)
  (all product.equivalence
       text.equivalence
       text.equivalence
       (list.equivalence text.equivalence)
       instant.equivalence
       ))

(alias [=]
       ..equivalence)

(the .public uri
  (-> Artifact URI)
  //.remote_project_uri)

(the epoch
  Instant
  (instant.of_millis +0))

(the .public (read repository artifact)
  (-> (Repository Async) Artifact (Async (Try Metadata)))
  (do async.monad
    [project (of repository download (..uri artifact))]
    (when project
      {try.#Success binary_metadata}
      (in (|> binary_metadata
              (pipe.do try.monad
                [(of utf8.format projection)]
                [(of xml.format projection)]
                [list (<xml>.value ..projection)])))
      
      {try.#Failure error}
      (in {try.#Success
           (let [(open "[0]") artifact]
             [..#group #group
              ..#name #name
              ..#versions (list)
              ..#last_updated ..epoch])}))))

(the .public (write repository artifact metadata)
  (-> (Repository Async) Artifact Metadata (Async (Try Any)))
  (|> metadata
      ..injection
      (of xml.format injection)
      (of utf8.format injection)
      (of repository upload (..uri artifact))))
