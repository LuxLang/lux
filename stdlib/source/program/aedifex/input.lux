... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["[0]" monad (.only Monad
                        do)]]
   [algorithm
    ["[0]" mix]]
   [control
    ["[0]" pipe]
    ["[0]" try (.only Try)]]
   [data
    [binary (.only Binary)]
    ["[0]" text (.only)
     [encoding
      ["[0]" utf8]]]
    [collection
     ["[0]" list]]]
   [meta
    ["[0]" location]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]
    [compiler
     [language
      [lux
       ["[0]" syntax]]]]]
   [world
    ["[0]" file]]]]
 ["[0]" //
  ["[1][0]" profile (.only Name Profile)]
  ["[1][0]" project (.only Project)]
  ["[1][0]" projection]])

(the (lux_projection source_code)
  (-> Text (Try Code))
  (let [parse (syntax.parse ""
                            syntax.no_aliases
                            (text.size source_code))]
    (when (parse [location.dummy 0 source_code])
      {.#Left [_ error]}
      {try.#Failure error}
      
      {.#Right [_ lux_code]}
      {try.#Success lux_code})))

(the project_projection
  (-> Binary (Try Project))
  (|>> (pipe.do try.monad
         [(by utf8.format projection)]
         [..lux_projection]
         [(list) (<code>.value //projection.project)])))

(the .public (read monad fs profiles)
  (for_any (_ !)
    (-> (Monad !) (file.System !) (List Name)
        (! (Try Profile))))
  (|> //project.file
      (by fs read)
      (by monad each
          (function (_ it)
            (do [! try.monad]
              [it it
               it (..project_projection it)
               it (monad.each ! (//project.profile it) (list.partial //profile.default profiles))]
              (in (mix.with_monoid //profile.monoid list.mix it)))))))
