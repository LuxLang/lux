... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Name)
   [abstract
    [monad (.only Monad do)]]
   [function
    [polytypism
     ["[0]" mix]]]
   [control
    ["[0]" pipe]]
   [error
    ["[0]" try (.only Try)]]
   [data
    [binary (.only Binary)]
    ["[0]" text (.only)
     [encoding
      ["[0]" utf8]]]
    [collection
     ["[0]" stack]]]
   [meta
    ["[0]" provenance]
    ["[0]" code (.only)
     ["<[1]>" \\projection]]]
   [compiler
    [language
     [lux
      ["[0]" syntax]]]]
   [world
    ["[0]" file]]]]
 ["[0]" //
  ["[1][0]" profile (.only Name Profile)]
  ["[1][0]" project (.only Project)]
  ["[1][0]" projection]])

(the (lux_of source_code)
  (-> Text
      (Try Code))
  (let [parse (syntax.parse source_code
                            ""
                            syntax.no_aliases)]
    (when (parse [provenance.dummy 0 source_code])
      {.#Left [_ error]}
      {try.#Failure error}
      
      {.#Right [_ lux_code]}
      {try.#Success lux_code})))

(the project_of
  (-> Binary (Try Project))
  (|>> (pipe.do try.monad
         [(by utf8.binary of)]
         [..lux_of]
         [(stack) (<code>.value //projection.project)])))

(the .public (read monad fs profiles)
  (for_any (_ !)
    (-> (Monad !) (file.System !) (Stack Name)
        (! (Try Profile))))
  (|> //project.file
      (by fs read)
      (by monad each
          (function (_ it)
            (do [! try.monad]
              [it it
               it (..project_of it)
               it (stack.each' ! (//project.profile it) (stack.partial //profile.default profiles))]
              (in (mix.with_monoid //profile.monoid stack.mix it)))))))
