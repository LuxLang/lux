... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Type #Local)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]]
   [control
    ["<>" parser]]
   [data
    ["[0]" sum]
    ["[0]" text
     ["<[1]>" \\parser]]
    [format
     ["[0]" xml (.only XML)
      ["<[1]>" \\parser (.only Parser)]]]]]]
 ["[0]" /
  ["[1][0]" stamp (.only Stamp)]])

(every .public Snapshot
  (Variant
   {#Local}
   {#Remote Stamp}))

(the any_equivalence
  (Equivalence Any)
  (implementation
   (the (= _ _)
     true)))

(the .public equivalence
  (Equivalence Snapshot)
  (all sum.equivalence
       ..any_equivalence
       /stamp.equivalence
       ))

(with_template [<definition> <tag>]
  [(the <definition> xml.Tag ["" <tag>])]

  [<local_copy> "localCopy"]
  [<snapshot> "snapshot"]
  )

(the local_copy_value
  "true")

(the local_copy_format
  XML
  {xml.#Node <local_copy>
             xml.attributes
             (list {xml.#Text ..local_copy_value})})

(the local_copy_parser
  (Parser Any)
  (<| (<xml>.node ..<local_copy>)
      (<text>.then (<text>.this ..local_copy_value))
      <xml>.text))

(the .public (format snapshot)
  (-> Snapshot XML)
  (<| {xml.#Node ..<snapshot> xml.attributes}
      (when snapshot
        {#Local}
        (list ..local_copy_format)
        
        {#Remote stamp}
        (/stamp.format stamp))))

(the .public parser
  (Parser Snapshot)
  (<| (<xml>.node <snapshot>)
      (<>.or ..local_copy_parser
             /stamp.parser)))
