... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]
    ["<>" projection]]
   [data
    ["[0]" text
     ["%" \\injection]
     ["<[1]>" \\projection]]
    [format
     ["[0]" xml (.only XML)
      ["<[1]>" \\projection (.only Projection)]]]]
   [math
    [number
     ["[0]" natural]]]]])

(every .public Build
  Natural)

(the .public equivalence
  (Equivalence Build)
  natural.equivalence)

(alias [=]
       ..equivalence)

(the tag
  xml.Tag
  ["" "buildNumber"])

(the .public as_xml
  (-> Build
      XML)
  (|>> %.natural
       {xml.#Text}
       stack
       {xml.#Node ..tag xml.attributes}))

(the .public of_xml
  (Projection Build)
  (<| (<xml>.node ..tag)
      (<text>.then (<>.of natural.base_10 (<text>.many <text>.decimal)))
      <xml>.text))
