... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]]
   [control
    ["<>" projection]]
   [data
    ["[0]" product]
    ["[0]" text
     ["<[1]>" \\projection]]
    [format
     ["[0]" xml (.only XML)
      ["<[1]>" \\projection (.only Projection)]]]]]]
 ["[0]" //
  ["[1][0]" time (.only Time)]
  ["[1][0]" build (.only Build)]])

(every .public Stamp
  (Record
   [#time Time
    #build Build]))

(the .public equivalence
  (Equivalence Stamp)
  (all product.equivalence
       //time.equivalence
       //build.equivalence
       ))

(the <timestamp>
  xml.Tag
  ["" "timestamp"])

(the time_format
  (-> Time XML)
  (|>> //time.format
       {xml.#Text}
       list
       {xml.#Node ..<timestamp> xml.attributes}))

(the .public (format (open "_[0]"))
  (-> Stamp (List XML))
  (list (..time_format _#time)
        (//build.format _#build)))

(the time_projection
  (Projection Time)
  (<| (<xml>.node <timestamp>)
      (<text>.then //time.projection)
      <xml>.text))

(the .public projection
  (Projection Stamp)
  (<>.and (<xml>.somewhere ..time_projection)
          (<xml>.somewhere //build.projection)))
