... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except #version)
   [abstract
    [equivalence (.only Equivalence)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\injection]]]]]
 ["[0]" /// (.only Snapshot)
  ["[1][0]" time]
  ["[1][0]" stamp]])

(every .public Value
  (Record
   [#version Text
    #snapshot Snapshot]))

(the .public equivalence
  (Equivalence Value)
  (all product.equivalence
       text.equivalence
       ///.equivalence
       ))

(the separator
  "-")

(the .public snapshot
  "SNAPSHOT")

(the .public (format (open "/[0]"))
  (%.Injection Value)
  (when /#snapshot
    {///.#Local}
    /#version
    
    {///.#Remote stamp}
    (let [(open "/[0]") stamp]
      (%.format (text.replaced ..snapshot
                               (///time.format /#time)
                               /#version)
                ..separator
                (%.nat /#build)))))
