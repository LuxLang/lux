... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]]
   [control
    ["<>" projection]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]
     ["<[1]>" \\projection]]
    [format
     ["[0]" xml (.only XML)
      ["<[1]>" \\projection (.only Projection)]]]]
   [time
    ["[0]" instant (.only Instant)]]]]
 ["[0]" ///
  ["[1][0]" time (.only)
   ["[1]/[0]" date]
   ["[1]/[0]" time]]])

(every .public Time
  ///time.Time)

(the .public equivalence
  (Equivalence Time)
  ///time.equivalence)

(alias [=]
       ..equivalence)

... https://en.wikipedia.org/wiki/Delimiter
(the delimiter
  ".")

(the .public (as_text [date time])
  (text.Injection Time)
  (%.message (///time/date.as_text date)
             ..delimiter
             (///time/time.as_text time)))

(the .public of_text
  (<text>.Projection Time)
  (do <>.monad
    [date ///time/date.of_text
     _ (<text>.this ..delimiter)
     time ///time/time.of_text]
    (in [date time])))
