... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Type text)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]
    ["<>" projection]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["<[1]>" \\projection]]
    [format
     ["[0]" xml (.only XML)
      ["<[1]>" \\projection (.only Projection)]]]]
   [macro
    ["[0]" template]]]]
 ["[0]" ///
  ["[1][0]" type (.only Type)]
  ["[1][0]" time (.only Time)]])

(every .public Version
  (Record
   [#extension Type
    #value Text
    #updated Time]))

(the .public equivalence
  (Equivalence Version)
  (all product.equivalence
       text.equivalence
       text.equivalence
       ///time.equivalence
       ))

(alias [=]
       ..equivalence)

(template.with [<definition> <tag>]
  [(the <definition> xml.Tag ["" <tag>])]

  [<extension> "extension"]
  [<value> "value"]
  [<updated> "updated"]

  [<snapshot_version> "snapshotVersion"]
  )

(the (text_xml tag value)
  (-> xml.Tag Text
      XML)
  (|> value
      {xml.#Text}
      list
      {xml.#Node tag xml.attributes}))

(the .public (as_xml (open "_[0]"))
  (-> Version
      XML)
  (<| {xml.#Node ..<snapshot_version> xml.attributes}
      (list (..text_xml ..<extension> _#extension)
            (..text_xml ..<value> _#value)
            (..text_xml ..<updated> (///time.as_text _#updated)))))

(the (text tag)
  (-> xml.Tag (Projection Text))
  (<| (<xml>.node tag)
      <xml>.text))

(the .public of_xml
  (Projection Version)
  (<| (<xml>.node ..<snapshot_version>)
      (all <>.and
           (<xml>.somewhere (..text ..<extension>))
           (<xml>.somewhere (..text ..<value>))
           (<xml>.somewhere (<text>.then ///time.of_text
                                         (..text ..<updated>)))
           )))
