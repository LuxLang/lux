(.using
 [library
  [lux (.except Type)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]]
   [control
    ["<>" parser (.only)
     ["<[0]>" xml (.only Parser)]]]
   [data
    ["[0]" product]
    ["[0]" text
     ["<[1]>" \\parser]]
    [format
     ["[0]" xml (.only XML)]]]]]
 ["[0]" ///
  ["[1][0]" type (.only Type)]
  ["[1][0]" time (.only Time)]])

(type: .public Version
  (Record
   [#extension Type
    #value Text
    #updated Time]))

(def .public equivalence
  (Equivalence Version)
  (all product.equivalence
       text.equivalence
       text.equivalence
       ///time.equivalence
       ))

(with_template [<definition> <tag>]
  [(def <definition> xml.Tag ["" <tag>])]

  [<extension> "extension"]
  [<value> "value"]
  [<updated> "updated"]

  [<snapshot_version> "snapshotVersion"]
  )

(def (text_format tag value)
  (-> xml.Tag Text XML)
  (|> value {xml.#Text} list {xml.#Node tag xml.attributes}))

(def .public (format (open "_[0]"))
  (-> Version XML)
  (<| {xml.#Node ..<snapshot_version> xml.attributes}
      (list (..text_format ..<extension> _#extension)
            (..text_format ..<value> _#value)
            (..text_format ..<updated> (///time.format _#updated)))))

(def (text tag)
  (-> xml.Tag (Parser Text))
  (<| (<xml>.node tag)
      <xml>.text))

(def .public parser
  (Parser Version)
  (<| (<xml>.node ..<snapshot_version>)
      (all <>.and
           (<xml>.somewhere (..text ..<extension>))
           (<xml>.somewhere (..text ..<value>))
           (<xml>.somewhere (<text>.then ///time.parser
                                         (..text ..<updated>)))
           )))
