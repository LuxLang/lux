... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Type)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]]
   [control
    ["<>" projection]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["<[1]>" \\projection]]
    [format
     ["[0]" xml (.only XML)
      ["<[1]>" \\projection (.only Projection)]]]]
   [meta
    [macro
     ["[0]" template]]]]]
 ["[0]" ///
  ["[1][0]" type (.only Type)]
  ["[1][0]" time (.only Time)]])

(every .public Version
  (Record
   [#extension Type
    #value Text
    #updated Time]))

(the .public equivalence
  (Equivalence Version)
  (all product.equivalence
       text.equivalence
       text.equivalence
       ///time.equivalence
       ))

(template.with [<definition> <tag>]
  [(the <definition> xml.Tag ["" <tag>])]

  [<extension> "extension"]
  [<value> "value"]
  [<updated> "updated"]

  [<snapshot_version> "snapshotVersion"]
  )

(the (text_injection tag value)
  (-> xml.Tag Text XML)
  (|> value {xml.#Text} list {xml.#Node tag xml.attributes}))

(the .public (injection (open "_[0]"))
  (-> Version XML)
  (<| {xml.#Node ..<snapshot_version> xml.attributes}
      (list (..text_injection ..<extension> _#extension)
            (..text_injection ..<value> _#value)
            (..text_injection ..<updated> (///time.injection _#updated)))))

(the (text tag)
  (-> xml.Tag (Projection Text))
  (<| (<xml>.node tag)
      <xml>.text))

(the .public projection
  (Projection Version)
  (<| (<xml>.node ..<snapshot_version>)
      (all <>.and
           (<xml>.somewhere (..text ..<extension>))
           (<xml>.somewhere (..text ..<value>))
           (<xml>.somewhere (<text>.then ///time.projection
                                         (..text ..<updated>)))
           )))
