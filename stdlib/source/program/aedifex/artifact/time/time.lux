... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [monad (.only do)]]
   [control
    ["<>" projection (.only)]]
   [data
    ["[0]" text (.only)
     ["%" \\injection]
     ["<[1]>" \\projection (.only Projection)]]]
   [math
    [number
     ["n" natural]]]
   ["[0]" time]]]
 ["[0]" //
  ["[1]" date]])

(every .public Time
  time.Time)

(the .public (injection value)
  (text.Injection Time)
  (let [(open "_[0]") (time.clock value)]
    (%.message (//.pad _#hour)
               (//.pad _#minute)
               (//.pad _#second))))

(the .public projection
  (<text>.Projection Time)
  (do <>.monad
    [hour (<>.format n.base_10 (<text>.exactly 2 <text>.decimal))
     minute (<>.format n.base_10 (<text>.exactly 2 <text>.decimal))
     second (<>.format n.base_10 (<text>.exactly 2 <text>.decimal))]
    (<>.of_try (time.time
                [time.#hour hour
                 time.#minute minute
                 time.#second second
                 time.#milli_second 0]))))
