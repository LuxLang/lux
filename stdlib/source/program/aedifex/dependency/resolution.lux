... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Tag
                all)
   ["[0]" debug]
   ["[0]" ffi (.only import)]
   [abstract
    [format (.only Format)]
    [equivalence (.only Equivalence)]
    [monad (.only do)]]
   [concurrency
    ["[0]" future (.only Future)]]
   [error
    ["[0]" try (.only Try) (.use "[1]#[0]" functor)]
    ["[0]" exception (.only Exception)]]
   [function
    ["[0]" predicate (.only Predicate)]]
   [data
    ["[0]" binary (.only Binary)]
    ["[0]" text (.only \n)
     ["%" \\injection]
     [encoding
      ["[0]" utf8]]]
    [format
     ["[0]" xml (.only Tag XML)
      ["<[1]>" \\projection]]]
    [collection
     ["[0]" dictionary (.only Dictionary)]
     ["[0]" set]
     ["[0]" stack (.use "[1]#[0]" functor monoid)]]]
   [math
    [number
     ["n" natural]
     ["[0]" i64]]]
   [macro
    ["[0]" template]]
   [world
    [console (.only Console)]
    [net (.only URL)
     ["[0]" uri]
     ["[0]" http
      ["[1]" client]]]]]]
 ["[0]" // (.only Dependency)
  ["[1][0]" status (.only Status)]
  ["/[1]" //
   ["/" profile]
   ["[1][0]" hash (.only Hash SHA1 MD5)]
   ["[1][0]" pom]
   ["[1][0]" package (.only Package)]
   ["[1][0]" artifact (.only Version Artifact)
    ["[1]/[0]" extension (.only Extension)]
    ["[1]/[0]" versioning]
    ["[0]" snapshot (.only)
     [version
      ["[0]" value]]]]
   ["[1][0]" repository (.only Repository)
    ["[1]/[0]" remote (.only Address)]
    ["[1]/[0]" origin (.only Origin)]]
   ["[1][0]" metadata
    ["[1]/[0]" snapshot]]]])

(template.with [<name>]
  [(exception.the .public (<name> [artifact extension hash])
     (Exception [Artifact Extension Text])
     (exception.report
      (list ["Artifact" (///artifact.as_text artifact)]
            ["Extension" (%.text extension)]
            ["Hash" (%.text hash)])))]

  [sha1_does_not_match]
  [md5_does_not_match]
  )

(import java/lang/String
  "[1]::[0]"
  (trim [] java/lang/String))

(the (verified_hash library repository version_template artifact extension hash format exception)
  (for_any (_ h)
    (-> Binary (Repository Future) Version Artifact Extension
        (-> Binary (Hash h)) (Format Text (Hash h))
        (Exception [Artifact Extension Text])
        (Future (Try (Maybe (Hash h))))))
  (do future.monad
    [?actual (by repository download (///repository/remote.uri version_template artifact extension))]
    (when ?actual
      {try.#Success actual}
      (in (do [! try.monad]
            [output (by ! each (|>> ffi.as_string (java/lang/String::trim []) ffi.of_string)
                        (by utf8.binary of actual))
             actual (|> output
                        (text.all_split_by " ")
                        stack.head
                        (try.else output)
                        (by format of))
             _ (exception.assertion exception [artifact extension output]
                                    (by ///hash.equivalence = (hash library) actual))]
            (in {.#Some actual})))
      
      {try.#Failure error}
      (in {try.#Success {.#None}}))))

(the (hashed repository version_template artifact extension)
  (-> (Repository Future) Version Artifact Extension (Future (Try [Binary Status])))
  (do (try.with future.monad)
    [data (by repository download (///repository/remote.uri version_template artifact extension))
     ?sha1 (..verified_hash data
                            repository version_template artifact (text extension ///artifact/extension.sha1)
                            ///hash.sha1 ///hash.sha1_format ..sha1_does_not_match)
     ?md5 (..verified_hash data
                           repository version_template artifact (text extension ///artifact/extension.md5)
                           ///hash.md5 ///hash.md5_format ..md5_does_not_match)]
    (in [data (when [?sha1 ?md5]
                [{.#Some sha1} {.#Some md5}]
                {//status.#Verified sha1 md5}

                [{.#Some sha1} {.#None}]
                {//status.#Partial {.#Left sha1}}

                [{.#None} {.#Some md5}]
                {//status.#Partial {.#Right md5}}

                [{.#None} {.#None}]
                {//status.#Unverified})])))

(the .public (one repository dependency)
  (-> (Repository Future) Dependency (Future (Try Package)))
  (let [[artifact type] dependency
        extension (///artifact/extension.extension type)]
    (do (try.with future.monad)
      [snapshot (///metadata/snapshot.read repository artifact)
       .let [version_template (its [///metadata/snapshot.#artifact ///artifact.#version] snapshot)
             artifact_version (value.as_text [value.#version version_template
                                              value.#snapshot (its [///metadata/snapshot.#versioning ///artifact/versioning.#snapshot] snapshot)])
             artifact (has ///artifact.#version artifact_version artifact)]
       [pom_data pom_status] (..hashed repository version_template artifact ///artifact/extension.pom)
       library_&_status (..hashed repository version_template artifact extension)]
      (by future.monad in
          (do try.monad
            [pom (by utf8.binary of pom_data)
             pom (by xml.text of pom)
             profile (<xml>.value ///pom.of_xml (stack pom))]
            (in [///package.#origin {///repository/origin.#Remote ""}
                 ///package.#library library_&_status
                 ///package.#pom [pom pom_data pom_status]]))))))

(every .public Resolution
  (Dictionary Dependency Package))

(the .public empty
  Resolution
  (dictionary.empty //.hash))

(the .public equivalence
  (Equivalence Resolution)
  (dictionary.equivalence ///package.equivalence))

(alias [=]
       ..equivalence)

(exception.the .public (cannot_resolve dependency)
  (Exception Dependency)
  (exception.report
   (list ["Artifact" (%.text (///artifact.as_text (its //.#artifact dependency)))]
         ["Type" (%.text (its //.#type dependency))])))

(template.with [<sigil> <name> <doing> <at>]
  [(the (<name> console repository artifact)
     (-> (Console Future) (Repository Future) Artifact (Future (Try Any)))
     (by console write (text "[" <sigil> "]"
                             " " <doing>
                             " " (///artifact.as_text artifact)
                             " " <at>
                             " " (%.text (by repository description))
                             \n)))]

  ["?" announce_fetching "Fetching" "from"]
  ["O" announce_success "Found" "at"]
  ["X" announce_failure "Missed" "from"]
  )

(the .public (any console repositories dependency)
  (-> (Console Future) (Stack (Repository Future)) Dependency (Future (Try Package)))
  (when repositories
    {.#Empty}
    (|> dependency
        (exception.except ..cannot_resolve)
        (by future.monad in))

    {.#Top repository alternatives}
    (do [! future.monad]
      [_ (..announce_fetching console repository (its //.#artifact dependency))
       outcome (..one repository dependency)]
      (when outcome
        {try.#Success package}
        (do !
          [_ (..announce_success console repository (its //.#artifact dependency))]
          (in outcome))

        {try.#Failure error}
        (do !
          [_ (..announce_failure console repository (its //.#artifact dependency))]
          (any console alternatives dependency))))))

(the .public (all console repositories new_repository dependencies resolution)
  (-> (Console Future) (Stack (Repository Future)) (-> URL (Repository Future)) (Stack Dependency) Resolution
      (Future [(Stack Dependency)
               (Stack Dependency)
               Resolution]))
  (loop (again [repositories repositories
                successes (is (Stack Dependency) (stack))
                failures (is (Stack Dependency) (stack))
                dependencies dependencies
                resolution resolution])
    (when dependencies
      {.#Empty}
      (by future.monad in
          [successes failures resolution])

      {.#Top head tail}
      (when (its [//.#artifact ///artifact.#version] head)
        ... Skip if there is no version
        "" (again repositories
                  successes
                  failures
                  tail
                  resolution)
        _ (do [! future.monad]
            [?package (is (Future (Try Package))
                          (when (dictionary.value head resolution)
                            {try.#Success package}
                            (in {try.#Success package})

                            not_found!
                            (..any console repositories head)))]
            (when ?package
              {try.#Success package}
              (do !
                [.let [redundant? (is (Predicate Dependency)
                                      (predicate.or (by //.equivalence = head)
                                                    (dictionary.key? resolution)))
                       sub_dependencies (|> package
                                            ///package.dependencies
                                            (try#each (|>> set.stack
                                                           (stack.only (|>> redundant? not))))
                                            (try.else (stack)))
                       ... For security reasons, it's not a good idea to allow dependencies to introduce repositories.
                       ... package_repositories (|> package
                       ...                          ///package.repositories
                       ...                          (try#each set.stack)
                       ...                          (try.else (stack))
                       ...                          (stack#each new_repository))
                       ... sub_repositories (stack#composite repositories package_repositories)
                       ]]
                (again repositories
                       {.#Top head successes}
                       failures
                       (set.stack (set.union (set.of_stack //.hash tail)
                                             (set.of_stack //.hash sub_dependencies)))
                       (dictionary.has head package resolution)))

              {try.#Failure error}
              (again repositories
                     successes
                     {.#Top head failures}
                     tail
                     resolution)))))))
