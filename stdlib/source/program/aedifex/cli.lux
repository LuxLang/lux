... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except #Build)
   [abstract
    [equivalence (.only Equivalence)]
    [monad (.only do)]]
   [control
    ["<>" parser]]
   [data
    ["[0]" sum]
    ["[0]" product]
    ["[0]" text]]
   [program
    ["cli" \\parser (.only Parser)]]]]
 [//
  [repository
   [identity (.only Identity)]]
  ["/" profile (.only Name)]])

(every .public Compilation
  (Variant
   {#Build}
   {#Test}))

(the any_equivalence
  (Equivalence Any)
  (implementation
   (the (= reference subject)
     true)))

(the compilation_equivalence
  (Equivalence Compilation)
  (sum.equivalence ..any_equivalence
                   ..any_equivalence))

(the compilation
  (Parser Compilation)
  (<>.or (cli.this "build")
         (cli.this "test")))

(every .public Command
  (Variant
   {#Version}
   {#Clean}
   {#POM}
   {#Dependencies}
   {#Install}
   {#Deploy Text Identity}
   {#Compilation Compilation}
   {#Auto Compilation}))

(the .public equivalence
  (Equivalence Command)
  (all sum.equivalence
       ... #Version
       ..any_equivalence
       ... #Clean
       ..any_equivalence
       ... #POM
       ..any_equivalence
       ... #Dependencies
       ..any_equivalence
       ... #Install
       ..any_equivalence
       ... #Deploy
       (all product.equivalence
            text.equivalence
            text.equivalence
            text.equivalence)
       ... #Compilation
       ..compilation_equivalence
       ... #Auto
       ..compilation_equivalence))

(the command'
  (Parser Command)
  (all <>.or
       (cli.this "version")
       (cli.this "clean")
       (cli.this "pom")
       (cli.this "deps")
       (cli.this "install")
       (<>.after (cli.this "deploy")
                 (<>.and cli.any
                         (<>.and cli.any
                                 cli.any)))
       ..compilation
       (<>.after (cli.this "auto")
                 ..compilation)
       ))

(the .public command
  (Parser [(List Name) Command])
  (<>.rec
   (function (_ command)
     (all <>.either
          (<>.after (cli.this "with")
                    (do <>.monad
                      [head cli.any
                       [tail command] command]
                      (in [{.#Item head tail} command])))
          (of <>.monad each (|>> [(list)])
              ..command')
          ))))
