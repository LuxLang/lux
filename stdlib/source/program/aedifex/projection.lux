... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except type name)
   [abstract
    [monad (.only do)]
    ["<>" projection (.use "[1]#[0]" functor)]]
   [error
    ["[0]" try (.use "[1]#[0]" functor)]]
   [data
    ["[0]" text]
    [collection
     ["[0]" set (.only Set)]
     ["[0]" dictionary (.only Dictionary)]
     ["[0]" list]
     [stack
      ["[0]" property]]]]
   [meta
    ["[0]" code (.only)
     ["<[1]>" \\projection (.only Projection)]]]
   [compiler
    [meta
     [cli
      [compiler (.only Compiler)]]]]
   [world
    [net (.only URL)]]]]
 ["[0]" //
  ["/" profile]
  ["[1][0]" runtime (.only Runtime)]
  ["[1][0]" project (.only Project)]
  ["[1][0]" dependency]
  ["[1][0]" injection]
  ["[1][0]" repository
   ["[1]" remote]]
  ["[1][0]" artifact (.only Artifact)
   ["[1]/[0]" type]]])

(the (singular input tag projection)
  (for_any (_ a)
    (-> (Dictionary Text Code) Text (Projection a)
        (Projection a)))
  (<code>.locally (|> input
                      (dictionary.value tag)
                      (try#each (|>> (list)))
                      (try.else (list))
                      list.as_stack)
                  projection))

(the (plural input tag projection)
  (for_any (_ a)
    (-> (Dictionary Text Code) Text (Projection a)
        (Projection (List a))))
  (<code>.locally (|> input
                      (dictionary.value tag)
                      (try#each (|>> (list)))
                      (try.else (list))
                      list.as_stack)
                  (<code>.tuple (<>.some projection))))

(the group
  (Projection //artifact.Group)
  <code>.text)

(the name
  (Projection //artifact.Name)
  <code>.text)

(the version
  (Projection //artifact.Version)
  <code>.text)

(the artifact'
  (Projection //artifact.Artifact)
  (all <>.and ..group ..name ..version))

(the artifact
  (Projection //artifact.Artifact)
  (<code>.tuple ..artifact'))

(the url
  (Projection URL)
  <code>.text)

(the scm
  (Projection /.SCM)
  ..url)

(the description
  (Projection Text)
  <code>.text)

(the license
  (Projection /.License)
  (do [! <>.monad]
    [input (by ! each
               (dictionary.of_list text.hash)
               (<code>.tuple (<>.some (<>.and <code>.text
                                              <code>.any))))]
    (all <>.and
         (..singular input "name" ..name)
         (..singular input "url" ..url)
         (<>.else {/.#Repo}
                  (..singular input "type"
                              (<>.or (<code>.this (' "repo"))
                                     (<code>.this (' "manual"))))))))

(the organization
  (Projection /.Organization)
  (do [! <>.monad]
    [input (by ! each
               (dictionary.of_list text.hash)
               (<code>.tuple (<>.some (<>.and <code>.text
                                              <code>.any))))]
    (all <>.and
         (..singular input "name" ..name)
         (..singular input "url" ..url))))

(the developer
  (Projection /.Developer)
  (do [! <>.monad]
    [input (by ! each
               (dictionary.of_list text.hash)
               (<code>.tuple (<>.some (<>.and <code>.text
                                              <code>.any))))]
    (all <>.and
         (..singular input "name" ..name)
         (..singular input "url" ..url)
         (<>.maybe (..singular input "organization" ..organization))
         )))

(the contributor
  (Projection /.Contributor)
  ..developer)

(the info
  (Projection /.Info)
  (do [! <>.monad]
    [input (by ! each
               (dictionary.of_list text.hash)
               (<code>.tuple (<>.some (<>.and <code>.text
                                              <code>.any))))]
    (all <>.and
         (<>.maybe (..singular input "url" ..url))
         (<>.maybe (..singular input "scm" ..scm))
         (<>.maybe (..singular input "description" ..description))
         (<>.else (list) (..plural input "licenses" ..license))
         (<>.maybe (..singular input "organization" ..organization))
         (<>.else (list) (..plural input "developers" ..developer))
         (<>.else (list) (..plural input "contributors" ..contributor))
         )))

(the repository
  (Projection //repository.Address)
  ..url)

(the type
  (Projection //artifact/type.Type)
  <code>.text)

(the dependency
  (Projection //dependency.Dependency)
  (<code>.tuple
   (all <>.and
        ..artifact'
        (<>.else //artifact/type.lux_library ..type)
        )))

(the compiler
  (Projection Compiler)
  (<code>.tuple
   (all <>.and
        <code>.global
        (<>.some <code>.text)
        )))

(the source
  (Projection /.Source)
  <code>.text)

(the target
  (Projection /.Target)
  <code>.text)

(the module_or_program
  (Projection Name)
  <code>.name)

(the program
  (Projection Name)
  <code>.global)

(the deploy_repository
  (Projection [Text //repository.Address])
  (<>.and <code>.text
          ..repository))

(the configuration/1
  (Projection [Text Text])
  (<>.and <code>.text
          <code>.text))

(the runtime
  (Projection Runtime)
  (let [environment (<>#each (dictionary.of_list text.hash)
                             (<>.some (<code>.tuple (<>.and <code>.text <code>.text))))]
    (<code>.tuple
     (all <>.and
          environment
          <code>.text
          (<>.some <code>.text)))))

(the profile
  (Projection /.Profile)
  (do [! <>.monad]
    [input (by ! each
               (dictionary.of_list text.hash)
               (<code>.tuple (<>.some (<>.and <code>.text
                                              <code>.any))))
     .let [^parents (is (Projection (List /.Name))
                        (<>.else (list)
                                 (..plural input "parents" <code>.text)))
           ^identity (is (Projection (Maybe Artifact))
                         (<>.maybe
                          (..singular input "identity" ..artifact)))
           ^info (is (Projection (Maybe /.Info))
                     (<>.maybe
                      (..singular input "info" ..info)))
           ^repositories (is (Projection (Set //repository.Address))
                             (|> (..plural input "repositories" ..repository)
                                 (by ! each (set.of_list text.hash))
                                 (<>.else (set.empty text.hash))
                                 (by ! each (set.has /.default_repository))))
           ^dependencies (is (Projection (Set //dependency.Dependency))
                             (|> (..plural input "dependencies" ..dependency)
                                 (by ! each (set.of_list //dependency.hash))
                                 (<>.else (set.empty //dependency.hash))))
           ^lux (|> ..dependency
                    (..singular input //injection.lux_compiler_label)
                    (<>.else /.default_lux))
           ^compilers (|> ..compiler
                          (..plural input "compilers")
                          (<>.else (list)))
           ^sources (is (Projection (Set /.Source))
                        (|> (..plural input "sources" ..source)
                            (by ! each (set.of_list text.hash))
                            (<>.else (set.of_list text.hash (list /.default_source)))))
           ^target (is (Projection /.Target)
                       (|> ..target
                           (..singular input "target")
                           (<>.else /.default_target)))
           ^program (is (Projection (Maybe Name))
                        (<>.maybe
                         (..singular input "program" ..module_or_program)))
           ^test (is (Projection (Maybe Name))
                     (<>.maybe
                      (..singular input "test" ..program)))
           ^deploy_repositories (is (Projection (Dictionary Text //repository.Address))
                                    (<| (by ! each (dictionary.of_list text.hash))
                                        (<>.else (list))
                                        (..plural input "deploy_repositories" ..deploy_repository)))
           ^configuration (is (Projection (property.Stack Text))
                              (<| (<>.else (stack))
                                  (<>#each list.as_stack)
                                  (..plural input "configuration" ..configuration/1)))
           ^java (|> ..runtime
                     (..singular input "java")
                     (<>.else //runtime.default_java))
           ^js (|> ..runtime
                   (..singular input "js")
                   (<>.else //runtime.default_js))
           ^python (|> ..runtime
                       (..singular input "python")
                       (<>.else //runtime.default_python))
           ^lua (|> ..runtime
                    (..singular input "lua")
                    (<>.else //runtime.default_lua))
           ^ruby (|> ..runtime
                     (..singular input "ruby")
                     (<>.else //runtime.default_ruby))]]
    (all <>.and
         ^parents
         ^identity
         ^info
         ^repositories
         ^dependencies
         ^lux
         ^compilers
         ^sources
         ^target
         ^program
         ^test
         ^deploy_repositories
         ^configuration
         ^java
         ^js
         ^python
         ^lua
         ^ruby
         )))

(the .public project
  (Projection Project)
  (by <>.monad each
      (dictionary.of_list text.hash)
      (<code>.tuple (<>.some (<>.and <code>.text
                                     ..profile)))))
