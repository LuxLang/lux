... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [control
    ["//" parser]
    ["[0]" try (.only Try)]
    ["[0]" exception]
    [function
     [predicate (.only Predicate)]]]]]
 ["/" \\library])

(every .public (Parser it of)
  (//.Parser (List it) of))

(exception.the .public invalid)

(exception.the .public empty)

(the .public (one it)
  (All (_ of)
    (-> (Predicate of)
        (Parser of of)))
  (function (_ state)
    (when state
      (/.partial head tail)
      (if (it head)
        {try.#Success [tail head]}
        (exception.except ..invalid []))

      (list)
      (exception.except ..empty []))))

(exception.the .public left_over)

(the .public (value it input)
  (All (_ it of)
    (-> (Parser it of) (List it)
        (Try of)))
  (when (it input)
    {try.#Success [remainder output]}
    (when remainder
      (list)
      {try.#Success output}

      _
      (exception.except ..left_over []))
    
    {try.#Failure error}
    {try.#Failure error}))
