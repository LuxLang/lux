... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except left right)
   [abstract
    [monad (.only do)]]
   [control
    ["//" parser]
    ["[0]" try (.only Try)]
    ["[0]" exception]]]]
 [\\library
  [/ (.only Tree)
   ["[0]" zipper (.only Zipper)]]])

(every .public (Parser t a)
  (//.Parser (Zipper t) a))

(def .public (value' parser zipper)
  (All (_ t a) (-> (Parser t a) (Zipper t) (Try a)))
  (do try.monad
    [[zipper output] (//.value parser zipper)]
    (in output)))

(def .public (value parser tree)
  (All (_ t a) (-> (Parser t a) (Tree t) (Try a)))
  (value' parser (zipper.zipper tree)))

(def .public here
  (All (_ t) (Parser t t))
  (function (_ zipper)
    {try.#Success [zipper (zipper.value zipper)]}))

(exception.def .public cannot_move_further)

(with_template [<name> <direction>]
  [(def .public <name>
     (All (_ t) (Parser t Any))
     (function (_ zipper)
       (when (<direction> zipper)
         {.#None}
         (exception.except ..cannot_move_further [])

         {.#Some next}
         {try.#Success [next []]})))]

  [down      zipper.down]
  [up        zipper.up]

  [right     zipper.right]
  [rightmost zipper.rightmost]

  [left      zipper.left]
  [leftmost  zipper.leftmost]
  
  [next      zipper.next]
  [end       zipper.end]
  
  [previous  zipper.previous]
  [start     zipper.start]
  )
