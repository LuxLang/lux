... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except Tag)
   [abstract
    [monad (.only do)]]
   [control
    ["//" parser]
    ["[0]" try (.only Try) (.use "[1]#[0]" functor)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" text
     ["%" \\format (.only format)]]
    [collection
     ["[0]" list]
     ["[0]" dictionary]]]
   [meta
    ["[0]" symbol (.use "[1]#[0]" equivalence absolute)]]]]
 [\\library
  ["[0]" / (.only Attribute Attrs Tag XML)]])

(every .public (Parser of)
  (//.Parser [Attrs (List XML)] of))

(exception.the .public empty_input)
(exception.the .public unexpected_input)

(exception.the .public (wrong_tag [expected actual])
  (Exception [Tag Tag])
  (exception.report
   (list ["Expected" (%.text (/.tag expected))]
         ["Actual" (%.text (/.tag actual))])))

(exception.the .public (unknown_attribute [expected available])
  (Exception [Attribute (List Attribute)])
  (exception.report
   (list ["Expected" (%.text (/.attribute expected))]
         ["Available" (exception.listing (|>> /.attribute %.text) available)])))

(exception.the .public (unconsumed_inputs inputs)
  (Exception (List XML))
  (exception.report
   (list ["Inputs" (exception.listing (of /.codec encoded) inputs)])))

(the (value' parser attrs documents)
  (All (_ of)
    (-> (Parser of) Attrs (List XML)
        (Try of)))
  (when (//.value parser [attrs documents])
    {try.#Success [[attrs' remaining] output]}
    (if (list.empty? remaining)
      {try.#Success output}
      (exception.except ..unconsumed_inputs remaining))
    
    {try.#Failure error}
    {try.#Failure error}))

(the .public (value parser documents)
  (All (_ of)
    (-> (Parser of) (List XML)
        (Try of)))
  (..value' parser /.attributes documents))

(the .public text
  (Parser Text)
  (function (_ [attrs documents])
    (when documents
      {.#End}
      (exception.except ..empty_input [])
      
      {.#Item head tail}
      (when head
        {/.#Text value}
        {try.#Success [[attrs tail] value]}
        
        {/.#Node _}
        (exception.except ..unexpected_input [])))))

(the .public tag
  (Parser Tag)
  (function (_ [attrs documents])
    (when documents
      {.#End}
      (exception.except ..empty_input [])
      
      {.#Item head _}
      (when head
        {/.#Text _}
        (exception.except ..unexpected_input [])
        
        {/.#Node tag _ _}
        {try.#Success [[attrs documents] tag]}))))

(the .public (attribute name)
  (-> Attribute
      (Parser Text))
  (function (_ [attrs documents])
    (when (dictionary.value name attrs)
      {try.#Success value}
      {try.#Success [[attrs documents] value]}

      failure
      (exception.except ..unknown_attribute [name (dictionary.keys attrs)]))))

(the .public (node expected parser)
  (All (_ of)
    (-> Tag (Parser of)
        (Parser of)))
  (function (_ [attrs documents])
    (when documents
      {.#End}
      (exception.except ..empty_input [])
      
      {.#Item head tail}
      (when head
        {/.#Text _}
        (exception.except ..unexpected_input [])
        
        {/.#Node actual attrs' children}
        (if (symbol#= expected actual)
          (|> children
              (..value' parser attrs')
              (try#each (|>> [[attrs tail]])))
          (exception.except ..wrong_tag [expected actual]))))))

(the .public any
  (Parser XML)
  (function (_ [attrs documents])
    (when documents
      {.#End}
      (exception.except ..empty_input [])
      
      {.#Item head tail}
      {try.#Success [[attrs tail] head]})))

(exception.the .public nowhere)

(the .public (somewhere parser)
  (All (_ of)
    (-> (Parser of)
        (Parser of)))
  (function (again [attrs input])
    (when (//.value parser [attrs input])
      {try.#Success [[attrs remaining] output]}
      {try.#Success [[attrs remaining] output]}
      
      {try.#Failure error}
      (when input
        {.#End}
        (exception.except ..nowhere [])
        
        {.#Item head tail}
        (do try.monad
          [[[attrs tail'] output] (again [attrs tail])]
          (in [[attrs {.#Item head tail'}]
               output]))))))
