... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except parameter)
   [abstract
    [monad (.only do)]]
   [control
    ["//" parser]
    ["[0]" try (.only Try)]]
   [data
    ["[0]" text (.use "[1]#[0]" equivalence)
     ["%" \\format (.only format)]]]]])

(every .public (Parser of)
  (//.Parser (List Text)
             of))

(def .public (value parser inputs)
  (All (_ of)
    (-> (Parser of) (List Text)
        (Try of)))
  (when (//.value parser inputs)
    {try.#Success [remaining output]}
    (when remaining 
      {.#End}
      {try.#Success output}

      _
      {try.#Failure (format "Remaining CLI inputs: " (text.interposed " " remaining))})

    {try.#Failure try}
    {try.#Failure try}))

(def .public any
  (Parser Text)
  (function (_ inputs)
    (when inputs
      {.#Item arg inputs'}
      {try.#Success [inputs' arg]}
      
      _
      {try.#Failure "Cannot parse empty arguments."})))

(def .public (parse parser)
  (All (_ of)
    (-> (-> Text (Try of))
        (Parser of)))
  (function (_ inputs)
    (do try.monad
      [[remaining raw] (any inputs)
       output (parser raw)]
      (in [remaining output]))))

(def .public (this reference)
  (-> Text
      (Parser Any))
  (function (_ inputs)
    (do try.monad
      [[remaining raw] (any inputs)]
      (if (text#= reference raw)
        (in [remaining []])
        {try.#Failure (format "Missing token: '" reference "'")}))))

(def .public (somewhere cli)
  (All (_ of)
    (-> (Parser of)
        (Parser of)))
  (function (_ inputs)
    (loop (again [immediate inputs])
      (when (//.value cli immediate)
        {try.#Failure try}
        (when immediate
          {.#End}
          {try.#Failure try}
          
          {.#Item to_omit immediate'}
          (do try.monad
            [[remaining output] (again immediate')]
            (in [{.#Item to_omit remaining}
                 output])))

        success
        success))))

(def .public end
  (Parser Any)
  (function (_ inputs)
    (when inputs
      {.#End} {try.#Success [inputs []]}
      _       {try.#Failure (format "Unknown parameters: " (text.interposed " " inputs))})))

(def .public (named name value)
  (All (_ of)
    (-> Text (Parser of)
        (Parser of)))
  (|> value
      (//.after (..this name))
      ..somewhere))

(def .public (parameter [short long] value)
  (All (_ of)
    (-> [Text Text] (Parser of)
        (Parser of)))
  (|> value
      (//.after (//.either (..this short) (..this long)))
      ..somewhere))
