(.require
 [library
  [lux (.except Synthesis function loop i64 local)
   [abstract
    [monad (.only do)]]
   [control
    ["//" parser]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" bit]
    ["[0]" text (.only)
     ["%" \\format (.only format)]]
    [collection
     ["[0]" list]]]
   [math
    [number
     ["n" nat]
     ["[0]" i64]
     ["[0]" frac]]]
   [meta
    ["[0]" symbol]
    [compiler
     [reference (.only)
      [variable (.only Register)]]
     [arity (.only Arity)]
     [language
      [lux
       [analysis (.only Environment)]]]]]]]
 [\\library
  ["[0]" / (.only Synthesis Abstraction)]])

(with_template [<name>]
  [(exception.def .public (<name> input)
     (Exception (List Synthesis))
     (exception.report
      (list ["Input" (exception.listing /.%synthesis input)])))]

  [cannot_parse]
  [unconsumed_input]
  [expected_empty_input]
  )

(exception.def .public (wrong_arity [expected actual])
  (Exception [Arity Arity])
  (exception.report
   (list ["Expected" (%.nat expected)]
         ["Actual" (%.nat actual)])))

(exception.def .public empty_input)

(type .public Parser
  (//.Parser (List Synthesis)))

(def .public (result parser input)
  (All (_ a) (-> (Parser a) (List Synthesis) (Try a)))
  (when (parser input)
    {try.#Failure error}
    {try.#Failure error}

    {try.#Success [{.#End} value]}
    {try.#Success value}
    
    {try.#Success [unconsumed _]}
    (exception.except ..unconsumed_input unconsumed)))

(def .public any
  (Parser Synthesis)
  (.function (_ input)
    (when input
      {.#End}
      (exception.except ..empty_input [])
      
      {.#Item [head tail]}
      {try.#Success [tail head]})))

(def .public end
  (Parser Any)
  (.function (_ tokens)
    (when tokens
      {.#End} {try.#Success [tokens []]}
      _       (exception.except ..expected_empty_input [tokens]))))

(def .public end?
  (Parser Bit)
  (.function (_ tokens)
    {try.#Success [tokens (when tokens
                            {.#End} true
                            _       false)]}))

(with_template [<query> <assertion> <tag> <type> <eq>]
  [(`` (def .public <query>
         (Parser <type>)
         (.function (_ input)
           (when input
             (list.partial (<tag> @ x) input')
             {try.#Success [input' x]}

             _
             (exception.except ..cannot_parse input)))))

   (`` (def .public (<assertion> expected)
         (-> <type> (Parser Any))
         (.function (_ input)
           (when input
             (list.partial (<tag> @ actual) input')
             (if (at <eq> = expected actual)
               {try.#Success [input' []]}
               (exception.except ..cannot_parse input))

             _
             (exception.except ..cannot_parse input)))))]

  [bit this_bit /.bit Bit bit.equivalence]
  [i64 this_i64 /.i64 I64 i64.equivalence]
  [f64 this_f64 /.f64 Frac frac.equivalence]
  [text this_text /.text Text text.equivalence]
  [local this_local /.variable/local Nat n.equivalence]
  [foreign this_foreign /.variable/foreign Nat n.equivalence]
  [constant this_constant /.constant Symbol symbol.equivalence]
  )

(def .public (tuple parser)
  (All (_ a) (-> (Parser a) (Parser a)))
  (.function (_ input)
    (when input
      (list.partial (/.tuple @ head) tail)
      (do try.monad
        [output (..result parser head)]
        {try.#Success [tail output]})

      _
      (exception.except ..cannot_parse input))))

(def .public (function expected parser)
  (All (_ a) (-> Arity (Parser a) (Parser [(Environment Synthesis) a])))
  (.function (_ input)
    (when input
      (list.partial (/.function/abstraction @ [environment actual body]) tail)
      (if (n.= expected actual)
        (do try.monad
          [output (..result parser (list body))]
          {try.#Success [tail [environment output]]})
        (exception.except ..wrong_arity [expected actual]))

      _
      (exception.except ..cannot_parse input))))

(def .public (loop init_parsers iteration_parser)
  (All (_ a b) (-> (Parser a) (Parser b) (Parser [Register a b])))
  (.function (_ input)
    (when input
      (list.partial (/.loop/scope @ [start inits iteration]) tail)
      (do try.monad
        [inits (..result init_parsers inits)
         iteration (..result iteration_parser (list iteration))]
        {try.#Success [tail [start inits iteration]]})

      _
      (exception.except ..cannot_parse input))))
