(.require
 [library
  [lux (.except Analysis nat int rev local)
   [abstract
    [monad (.only do)]]
   [control
    ["//" parser]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" bit]
    ["[0]" text (.only)
     ["%" \\format (.only format)]]
    [collection
     ["[0]" list (.use "[1]#[0]" functor)]]]
   [math
    [number
     ["[0]" i64]
     ["[0]" nat]
     ["[0]" int]
     ["[0]" rev]
     ["[0]" frac]]]
   [meta
    ["[0]" symbol]
    [macro
     ["[0]" template]]
    [compiler
     [arity (.only Arity)]
     [reference (.only)
      [variable (.only)]]]]]]
 ["/" \\library (.only Environment Analysis)])

(def (remaining_inputs asts)
  (-> (List Analysis) Text)
  (format text.new_line "Remaining input: "
          (|> asts
              (list#each /.format)
              (text.interposed " "))))

(with_template [<name>]
  [(exception.def .public (<name> input)
     (Exception (List Analysis))
     (exception.report
      (list ["Input" (exception.listing /.format input)])))]

  [cannot_parse]
  [unconsumed_input]
  )

(type .public Parser
  (//.Parser (List Analysis)))

(def .public (result parser input)
  (All (_ a) (-> (Parser a) (List Analysis) (Try a)))
  (when (parser input)
    {try.#Success [{.#End} value]}
    {try.#Success value}
    
    {try.#Success [unconsumed _]}
    (exception.except ..unconsumed_input unconsumed)

    {try.#Failure error}
    {try.#Failure error}))

(def .public any
  (Parser Analysis)
  (function (_ input)
    (when input
      {.#End}
      (exception.except ..cannot_parse input)
      
      {.#Item [head tail]}
      {try.#Success [tail head]})))

(def .public end
  (Parser Any)
  (function (_ tokens)
    (when tokens
      {.#End}
      {try.#Success [tokens []]}
      
      _
      {try.#Failure (format "Expected list of tokens to be empty!"
                            (remaining_inputs tokens))})))

(def .public end?
  (Parser Bit)
  (function (_ tokens)
    {try.#Success [tokens (when tokens
                            {.#End} true
                            _       false)]}))

(with_template [<query> <assertion> <tag> <type> <eq>]
  [(`` (these (def .public <query>
                (Parser <type>)
                (function (_ input)
                  (when input
                    (list.partial (<tag> @ x) input')
                    {try.#Success [input' x]}

                    _
                    (exception.except ..cannot_parse input))))

              (def .public (<assertion> expected)
                (-> <type> (Parser Any))
                (function (_ input)
                  (when input
                    (list.partial (<tag> @ actual) input')
                    (if (at <eq> = expected actual)
                      {try.#Success [input' []]}
                      (exception.except ..cannot_parse input))

                    _
                    (exception.except ..cannot_parse input))))))]

  [bit this_bit /.bit Bit bit.equivalence]
  [nat this_nat /.nat Nat nat.equivalence]
  [int this_int /.int Int int.equivalence]
  [rev this_rev /.rev Rev rev.equivalence]
  [frac this_frac /.frac Frac frac.equivalence]
  [text this_text /.text Text text.equivalence]
  [local this_local /.local Nat nat.equivalence]
  [foreign this_foreign /.foreign Nat nat.equivalence]
  [constant this_constant /.constant Symbol symbol.equivalence]
  )

(def .public (tuple parser)
  (All (_ a) (-> (Parser a) (Parser a)))
  (function (_ input)
    (when input
      (list.partial (/.tuple @ head) tail)
      (do try.monad
        [output (..result parser head)]
        {try.#Success [tail output]})

      _
      (exception.except ..cannot_parse input))))
