... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.require
 [library
  [lux (.except)
   [control
    ["//" parser]
    ["[0]" try (.only Try)]
    ["[0]" exception (.only Exception)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     ["%" \\format (.only format)]]
    [collection
     ["[0]" dictionary (.only Dictionary)]]]]])

(type .public Property
  Text)

(type .public Environment
  (Dictionary Property Text))

(exception.def .public (unknown_property property)
  (Exception Property)
  (exception.report
   (list ["Property" (%.text property)])))

(type .public (Parser a)
  (//.Parser Environment a))

(def .public empty
  Environment
  (dictionary.empty text.hash))

(def .public (property name)
  (-> Property (Parser Text))
  (function (_ environment)
    (when (dictionary.value name environment)
      {.#Some value}
      {try.#Success [environment value]}
      
      {.#None}
      (exception.except ..unknown_property [name]))))

(def .public (result parser environment)
  (All (_ a) (-> (Parser a) Environment (Try a)))
  (of try.monad each product.right (parser environment)))
