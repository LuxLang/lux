(.module:
  [library
   [lux (#- and)
    ["$" documentation (#+ documentation:)]
    [control
     ["<>" parser
      ["<.>" code]]]
    [data
     ["." text (#+ \n)
      ["%" format (#+ format)]]]
    [macro
     ["." template]]]]
  [\\library
   ["." /]])

(documentation: /.\\
  (format "Automatic implementation selection (for type-class style polymorphism)."
          \n "This feature layers type-class style polymorphism on top of Lux's signatures and implementations."
          \n "When calling a polymorphic function, or using a polymorphic constant,"
          \n "this macro will check the types of the arguments, and the expected type for the whole expression"
          \n "and it will search in the local scope, the module's scope and the imports' scope"
          \n "in order to find suitable implementations to satisfy those requirements."
          \n "If a single alternative is found, that one will be used automatically."
          \n "If no alternative is found, or if more than one alternative is found (ambiguity)"
          \n "a compile-time error will be raised, to alert the user."
          \n \n "Caveat emptor: You need to make sure to import the module of any implementation you want to use."
          \n "Otherwise, this macro will not find it.")
  ["Nat equivalence"
   (\ number.equivalence = x y)
   (\\ = x y)]
  ["Can optionally add the prefix of the module where the signature was defined."
   (\\ equivalence.= x y)]
  ["(List Nat) equivalence"
   (\\ =
       (list.indices 10)
       (list.indices 10))]
  ["(Functor List) each"
   (\\ each ++ (list.indices 10))])

(documentation: /.with
  "Establish lexical bindings for implementations that will be prioritized over non-lexically-bound implementations."
  [(with [n.addition]
     (n.= (\ n.addition composite left right)
          (\\ composite left right)))])

(documentation: /.implicit:
  "Establish local definitions for implementations that will be prioritized over foreign definitions."
  [(implicit: [n.multiplication])
   
   (n.= (\ n.multiplication composite left right)
        (\\ composite left right))])

(.def: .public documentation
  (.List $.Module)
  ($.module /._
            ""
            [..\\
             ..with
             ..implicit:]
            []))
