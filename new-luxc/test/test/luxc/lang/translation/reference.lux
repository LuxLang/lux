(.module:
  lux
  (lux [io]
       (control [monad #+ do]
                pipe)
       (data ["e" error]
             [text])
       ["r" math/random]
       [macro]
       (macro [code])
       test)
  (luxc [lang]
        (lang ["_." module]
              (host ["$" jvm]
                    (jvm ["$i" inst]))
              ["ls" synthesis]
              (translation [".T" statement]
                           [".T" eval]
                           [".T" expression]
                           [".T" case]
                           [".T" runtime])))
  (test/luxc common))

(def: nilI $.Inst runtimeT.noneI)

(def: cursorI
  $.Inst
  (|>> ($i.int 3)
       ($i.array runtimeT.$Tuple)
       $i.DUP ($i.int 0) ($i.string "") $i.AASTORE
       $i.DUP ($i.int 1) ($i.long 0) ($i.wrap #$.Long) $i.AASTORE
       $i.DUP ($i.int 2) ($i.long 0) ($i.wrap #$.Long) $i.AASTORE))

(def: empty-metaI
  (|>> ($i.int 2)
       ($i.array runtimeT.$Tuple)
       $i.DUP ($i.int 0) cursorI $i.AASTORE
       $i.DUP ($i.int 1) nilI $i.AASTORE))

(context: "Definitions."
  (<| (times +100)
      (do @
        [module-name (|> (r.text +5) (r.filter (|>> (text.contains? "/") not)))
         def-name (r.text +5)
         def-value r.int
         #let [valueI (|>> ($i.long def-value) ($i.wrap #$.Long))]]
        ($_ seq
            (test "Can refer to definitions."
                  (|> (do macro.Monad<Meta>
                        [_ (_module.with-module +0 module-name
                             (statementT.translate-def def-name Int valueI empty-metaI (' {})))
                         sampleI (expressionT.translate (code.symbol [module-name def-name]))]
                        (evalT.eval sampleI))
                      (lang.with-current-module "")
                      (macro.run (init-compiler []))
                      (case> (#e.Success valueT)
                             (i/= def-value (:! Int valueT))

                             (#e.Error error)
                             false)))
            ))))

(context: "Variables."
  (<| (times +100)
      (do @
        [register (|> r.nat (:: @ map (n/% +100)))
         value r.int]
        ($_ seq
            (test "Can refer to local variables/registers."
                  (|> (do macro.Monad<Meta>
                        [sampleI (caseT.translate-let expressionT.translate
                                                      register
                                                      (code.int value)
                                                      (` ((~ (code.int (nat-to-int register))))))]
                        (evalT.eval sampleI))
                      (macro.run (init-compiler []))
                      (case> (#e.Success outputT)
                             (i/= value (:! Int outputT))

                             (#e.Error error)
                             false)))
            ))))
