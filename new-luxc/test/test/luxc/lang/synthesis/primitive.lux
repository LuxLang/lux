(.module:
  lux
  (lux [io]
       (control [monad #+ do]
                pipe)
       (data text/format)
       (macro [code])
       ["r" math/random]
       test)
  (luxc (lang ["la" analysis]
              ["ls" synthesis]
              [".L" extension]
              (synthesis [".S" expression]))))

(context: "Primitives"
  (<| (times +100)
      (do @
        [%bool% r.bool
         %nat% r.nat
         %int% r.int
         %deg% r.deg
         %frac% r.frac
         %text% (r.text +5)]
        (`` ($_ seq
                (test (format "Can synthesize unit.")
                      (|> (expressionS.synthesize extensionL.no-syntheses (' []))
                          (case> (^code [])
                                 true

                                 _
                                 false)))
                (~~ (do-template [<desc> <analysis> <synthesis> <sample>]
                      [(test (format "Can synthesize " <desc> ".")
                             (|> (expressionS.synthesize extensionL.no-syntheses (<analysis> <sample>))
                                 (case> [_ (<synthesis> value)]
                                        (is <sample> value)

                                        _
                                        false)))]

                      ["bool" code.bool #.Bool %bool%]
                      ["nat"  code.nat  #.Nat  %nat%]
                      ["int"  code.int  #.Int  %int%]
                      ["deg"  code.deg  #.Deg  %deg%]
                      ["frac" code.frac #.Frac %frac%]
                      ["text" code.text #.Text %text%])))))))
