(;module:
  lux
  (lux (control pipe)
       ["r" math/random "r/" Monad<Random>]
       (data ["R" result])
       [macro]
       (macro [code]))
  (luxc ["&" base]
        [analyser]))

(def: compiler-version Text "0.6.0")

(def: init-compiler-info
  Compiler-Info
  {#;compiler-version compiler-version
   #;compiler-mode    #;Build})

(def: init-type-context
  Type-Context
  {#;ex-counter +0
   #;var-counter +0
   #;var-bindings (list)})

(def: #export init-compiler
  Compiler
  {#;info            init-compiler-info
   #;source          [dummy-cursor ""]
   #;cursor          dummy-cursor
   #;modules         (list)
   #;scopes          (list)
   #;type-context    init-type-context
   #;expected        #;None
   #;seed            +0
   #;scope-type-vars (list)
   #;host            (:! Void [])})

(def: gen-unit
  (r;Random Code)
  (r/wrap (' [])))

(def: #export gen-primitive
  (r;Random [Type Code])
  (with-expansions
    [<generators> (do-template [<type> <code-wrapper> <value-gen>]
                    [(r;seq (r/wrap <type>) (r/map <code-wrapper> <value-gen>))]

                    [Unit code;tuple (r;list +0 gen-unit)]
                    [Bool code;bool  r;bool]
                    [Nat  code;nat   r;nat]
                    [Int  code;int   r;int]
                    [Deg  code;deg   r;deg]
                    [Real code;real  r;real]
                    [Char code;char  r;char]
                    [Text code;text  (r;text +5)]
                    )]
    ($_ r;either
        <generators>
        )))

(def: #export analyse
  &;Analyser
  (analyser;analyser (:!! [])))

(do-template [<name> <on-success> <on-failure>]
  [(def: #export (<name> analysis)
     (All [a] (-> (Lux a) Bool))
     (|> analysis
         (macro;run init-compiler)
         (case> (#R;Success _)
                <on-success>

                (#R;Error error)
                <on-failure>)))]

  [check-success true false]
  [check-failure false true]
  )
