(;module:
  lux
  (lux [io]
       (control [monad #+ do]
                pipe)
       (data ["R" result])
       ["r" math/random "R/" Monad<Random>]
       [type "Type/" Eq<Type>]
       [macro #+ Monad<Lux>]
       test)
  (luxc ["&;" scope]
        ["&;" module]
        (lang ["~" analysis])
        [analyser]
        (analyser ["@" reference]
                  ["@;" common]))
  (.. common)
  (test/luxc common))

(context: "References"
  [[ref-type _] gen-primitive
   module-name (r;text +5)
   scope-name (r;text +5)
   var-name (r;text +5)]
  ($_ seq
      (test "Can analyse variable."
            (|> (&scope;with-scope scope-name
                  (&scope;with-local [var-name ref-type]
                    (@common;with-unknown-type
                      (@;analyse-reference ["" var-name]))))
                (macro;run (init-compiler []))
                (case> (#R;Success [_type (#~;Variable idx)])
                       (Type/= ref-type _type)

                       _
                       false)))
      (test "Can analyse definition."
            (|> (do Monad<Lux>
                  [_ (&module;create +0 module-name)
                   _ (&module;define [module-name var-name]
                                     [ref-type (list) (:! Void [])])]
                  (@common;with-unknown-type
                    (@;analyse-reference [module-name var-name])))
                (macro;run (init-compiler []))
                (case> (#R;Success [_type (#~;Definition idx)])
                       (Type/= ref-type _type)

                       _
                       false)))
      ))
