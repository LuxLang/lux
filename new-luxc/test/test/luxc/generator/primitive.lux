(;module:
  lux
  (lux [io]
       (control monad
                pipe)
       (data text/format
             ["R" result]
             [bool "B/" Eq<Bool>]
             [text "T/" Eq<Text>])
       ["r" math/random "R/" Monad<Random>]
       [macro]
       test)
  (luxc (lang ["ls" synthesis])
        [analyser]
        [synthesizer]
        (generator ["@" expr]
                   ["@;" runtime]
                   ["@;" eval]
                   ["@;" common]))
  (test/luxc common))

(context: "Primitives."
  [%bool% r;bool
   %nat% r;nat
   %int% r;int
   %deg% r;deg
   %real% r;real
   %text% (r;text +5)]
  (with-expansions
    [<tests> (do-template [<desc> <type> <synthesis> <sample> <test>]
               [(test (format "Can generate " <desc> ".")
                      (|> (@eval;eval (@;generate (<synthesis> <sample>)))
                          (macro;run (init-compiler []))
                          (case> (#R;Success valueG)
                                 (<test> <sample> (:! <type> valueG))

                                 _
                                 false)))]

               ["bool" Bool #ls;Bool %bool% B/=]
               ["nat"  Nat  #ls;Nat  %nat%  n.=]
               ["int"  Int  #ls;Int  %int%  i.=]
               ["deg"  Deg  #ls;Deg  %deg%  d.=]
               ["real" Real #ls;Real %real% r.=]
               ["text" Text #ls;Text %text% T/=])]
    ($_ seq
        (test "Can generate unit."
              (|> (@eval;eval (@;generate #ls;Unit))
                  (macro;run (init-compiler []))
                  (case> (#R;Success valueG)
                         (is @runtime;unit (:! Text valueG))

                         _
                         false)))
        <tests>
        )))
