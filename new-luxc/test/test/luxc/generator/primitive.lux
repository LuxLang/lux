(;module:
  lux
  (lux [io]
       (control [monad #+ do]
                pipe)
       (data text/format
             ["e" error]
             [bool "B/" Eq<Bool>]
             [text "T/" Eq<Text>])
       ["r" math/random]
       [meta]
       test)
  (luxc (lang ["ls" synthesis])
        [analyser]
        [synthesizer]
        (generator ["@" expr]
                   ["@;" runtime]
                   ["@;" eval]
                   ["@;" common]))
  (test/luxc common))

(context: "Primitives."
  [%bool% r;bool
   %nat% r;nat
   %int% r;int
   %deg% r;deg
   %frac% r;frac
   %text% (r;text +5)]
  (with-expansions
    [<tests> (do-template [<desc> <type> <synthesis> <sample> <test>]
               [(test (format "Can generate " <desc> ".")
                      (|> (do meta;Monad<Meta>
                            [sampleI (@;generate (<synthesis> <sample>))]
                            (@eval;eval sampleI))
                          (meta;run (init-compiler []))
                          (case> (#e;Success valueG)
                                 (<test> <sample> (:! <type> valueG))

                                 _
                                 false)))]

               ["bool" Bool #ls;Bool %bool% B/=]
               ["nat"  Nat  #ls;Nat  %nat%  n.=]
               ["int"  Int  #ls;Int  %int%  i.=]
               ["deg"  Deg  #ls;Deg  %deg%  d.=]
               ["frac" Frac #ls;Frac %frac% f.=]
               ["text" Text #ls;Text %text% T/=])]
    ($_ seq
        (test "Can generate unit."
              (|> (do meta;Monad<Meta>
                    [sampleI (@;generate #ls;Unit)]
                    (@eval;eval sampleI))
                  (meta;run (init-compiler []))
                  (case> (#e;Success valueG)
                         (is @runtime;unit (:! Text valueG))

                         _
                         false)))
        <tests>
        )))
