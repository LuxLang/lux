(;module:
  lux
  (lux (data [bool "bool/" Eq<Bool>]
             [text "text/" Eq<Text>])
       ["r" math/random "r/" Monad<Random>])
  (luxc (lang ["la" analysis]
              ["ls" synthesis])))

(def: #export gen-primitive
  (r;Random la;Analysis)
  (r;either (r;either (r;either (r/wrap #la;Unit)
                                (r/map (|>. #la;Bool) r;bool))
                      (r;either (r/map (|>. #la;Nat) r;nat)
                                (r/map (|>. #la;Int) r;int)))
            (r;either (r;either (r/map (|>. #la;Deg) r;deg)
                                (r/map (|>. #la;Frac) r;frac))
                      (r/map (|>. #la;Text) (r;text +5)))))

(def: #export (corresponds? analysis synthesis)
  (-> la;Analysis ls;Synthesis Bool)
  (case [analysis synthesis]
    [#la;Unit [_ (#;Tuple #;Nil)]]
    true
    
    (^template [<analysis> <synthesis> <test>]
      [(<analysis> valueA) [_ (<synthesis> valueS)]]
      (<test> valueA valueS))
    ([#la;Bool #;Bool bool/=]
     [#la;Nat  #;Nat  n.=]
     [#la;Int  #;Int  i.=]
     [#la;Deg  #;Deg  d.=]
     [#la;Frac #;Frac f.=]
     [#la;Text #;Text text/=])

    _
    false))
