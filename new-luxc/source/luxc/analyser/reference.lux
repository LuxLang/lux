(;module:
  lux
  (lux (control monad)
       [macro #+ Monad<Lux>]
       (type ["TC" check]))
  (luxc ["&" base]
        (lang ["la" analysis #+ Analysis])
        ["&;" scope]))

## [Analysers]
(def: (analyse-definition def-name)
  (-> Ident (Lux Analysis))
  (do Monad<Lux>
    [actual (macro;find-def-type def-name)
     expected macro;expected-type
     _ (&;with-type-env
         (TC;check expected actual))]
    (wrap (#la;Definition def-name))))

(def: (analyse-variable var-name)
  (-> Text (Lux (Maybe Analysis)))
  (do Monad<Lux>
    [?var (&scope;find var-name)]
    (case ?var
      (#;Some [actual ref])
      (do @
        [expected macro;expected-type
         _ (&;with-type-env
             (TC;check expected actual))]
        (wrap (#;Some (#la;Variable ref))))

      #;None
      (wrap #;None))))

(def: #export (analyse-reference reference)
  (-> Ident (Lux Analysis))
  (case reference
    ["" simple-name]
    (do Monad<Lux>
      [?var (analyse-variable simple-name)]
      (case ?var
        (#;Some analysis)
        (wrap analysis)

        #;None
        (do @
          [this-module macro;current-module-name]
          (analyse-definition [this-module simple-name]))))

    _
    (analyse-definition reference)))
