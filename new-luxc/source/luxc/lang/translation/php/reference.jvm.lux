(.module:
  lux
  (lux [macro]
       (data [text]
             text/format))
  (luxc ["&" lang]
        (lang [".L" variable #+ Variable Register]
              (host ["_" php #+ VExpression])))
  [//]
  (// [".T" runtime]))

(do-template [<register> <prefix>]
  [(def: #export <register>
     (-> Register VExpression)
     (|>> (:! Int) %i (format <prefix>) _.var))]

  [closure  "c"]
  [variable "v"])

(def: #export (local var)
  (-> Variable VExpression)
  (if (variableL.captured? var)
    (closure (variableL.captured-register var))
    (variable (:! Nat var))))

(def: #export global
  (-> Ident VExpression)
  (|>> //.definition-name _.var))

(do-template [<name> <input> <converter>]
  [(def: #export <name>
     (-> <input> (Meta VExpression))
     (|>> <converter> (:: macro.Monad<Meta> wrap)))]

  [translate-variable   Variable local]
  [translate-definition Ident    global]
  )
