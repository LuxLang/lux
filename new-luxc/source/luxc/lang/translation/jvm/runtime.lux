(.module:
  [lux #*
   [abstract
    [monad (#+ do)]]
   [data
    [collection
     ["." list ("#/." functor)]]]
   ["." math]
   [target
    [jvm
     ["." descriptor (#+ Descriptor)]
     ["$t" type]]]
   [tool
    [compiler
     [arity (#+ Arity)]
     ["." synthesis]
     ["." phase
      ["." generation]]]]]
  [luxc
   [lang
    [host
     ["$" jvm (#+ Label Inst Def Operation)
      ["$d" def]
      ["_" inst]]]]]
  ["." // (#+ ByteCode)])

(def: $Text (descriptor.class "java.lang.String"))
(def: #export $Tag descriptor.int)
(def: #export $Flag (descriptor.class "java.lang.Object"))
(def: #export $Value (descriptor.class "java.lang.Object"))
(def: #export $Index descriptor.int)
(def: #export $Stack (descriptor.array $Value))
(def: $Throwable (descriptor.class "java.lang.Throwable"))
(def: #export $Runtime (descriptor.class "java.lang.Runtime"))

(def: nullary-init-methodT
  (descriptor.method [(list) descriptor.void]))

(def: throw-methodT
  (descriptor.method [(list) descriptor.void]))

(def: #export logI
  Inst
  (let [PrintStream (descriptor.class "java.io.PrintStream")
        outI (_.GETSTATIC (descriptor.class "java.lang.System") "out" PrintStream)
        printI (function (_ method)
                 (_.INVOKEVIRTUAL PrintStream method (descriptor.method [(list $Value) descriptor.void]) #0))]
    (|>> outI (_.string "LOG: ") (printI "print")
         outI _.SWAP (printI "println"))))

(def: variant-method
  (descriptor.method [(list $Tag $Flag $Value) //.$Variant]))

(def: #export variantI
  Inst
  (_.INVOKESTATIC (descriptor.class //.runtime-class) "variant_make" variant-method #0))

(def: #export leftI
  Inst
  (|>> (_.int +0)
       _.NULL
       _.DUP2_X1
       _.POP2
       variantI))

(def: #export rightI
  Inst
  (|>> (_.int +1)
       (_.string "")
       _.DUP2_X1
       _.POP2
       variantI))

(def: #export someI Inst rightI)

(def: #export noneI
  Inst
  (|>> (_.int +0)
       _.NULL
       (_.string synthesis.unit)
       variantI))

(def: (try-methodI unsafeI)
  (-> Inst Inst)
  (<| _.with-label (function (_ @from))
      _.with-label (function (_ @to))
      _.with-label (function (_ @handler))
      (|>> (_.try @from @to @handler (descriptor.class "java.lang.Exception"))
           (_.label @from)
           unsafeI
           someI
           _.ARETURN
           (_.label @to)
           (_.label @handler)
           noneI
           _.ARETURN)))

(def: #export string-concatI
  Inst
  (_.INVOKEVIRTUAL $Text "concat" (descriptor.method [(list $Text) $Text]) #0))

(def: #export partials-field Text "partials")
(def: #export apply-method Text "apply")
(def: #export num-apply-variants Nat 8)

(def: #export (apply-signature arity)
  (-> Arity (Descriptor descriptor.Method))
  (descriptor.method [(list.repeat arity $Value) $Value]))

(def: adt-methods
  Def
  (let [store-tagI (|>> _.DUP (_.int +0) (_.ILOAD 0) (_.wrap descriptor.int) _.AASTORE)
        store-flagI (|>> _.DUP (_.int +1) (_.ALOAD 1) _.AASTORE)
        store-valueI (|>> _.DUP (_.int +2) (_.ALOAD 2) _.AASTORE)]
    (|>> ($d.method #$.Public $.staticM "variant_make"
                    (descriptor.method [(list $Tag $Flag $Value) //.$Variant])
                    (|>> (_.int +3)
                         (_.array //.$Variant)
                         store-tagI
                         store-flagI
                         store-valueI
                         _.ARETURN)))))

(def: frac-shiftI Inst (_.double (math.pow +32.0 +2.0)))

(def: frac-methods
  Def
  (|>> ($d.method #$.Public $.staticM "decode_frac" (descriptor.method [(list $Text) //.$Variant])
                  (try-methodI
                   (|>> (_.ALOAD 0)
                        (_.INVOKESTATIC (descriptor.class "java.lang.Double") "parseDouble" (descriptor.method [(list $Text) descriptor.double]) #0)
                        (_.wrap descriptor.double))))
       ))

(def: #export popI
  (|>> (_.int +1)
       _.AALOAD
       (_.CHECKCAST $Stack)))

(def: #export peekI
  (|>> (_.int +0)
       _.AALOAD))

(def: (illegal-state-exception message)
  (-> Text Inst)
  (let [IllegalStateException (descriptor.class "java.lang.IllegalStateException")]
    (|>> (_.NEW IllegalStateException)
         _.DUP
         (_.string message)
         (_.INVOKESPECIAL IllegalStateException "<init>" (descriptor.method [(list $Text) descriptor.void]) #0))))

(def: pm-methods
  Def
  (let [tuple-sizeI (|>> (_.ALOAD 0) _.ARRAYLENGTH)
        last-rightI (|>> tuple-sizeI (_.int +1) _.ISUB)
        leftsI (_.ILOAD 1)
        left-indexI leftsI
        sub-leftsI (|>> leftsI
                        last-rightI
                        _.ISUB)
        sub-tupleI (|>> (_.ALOAD 0) last-rightI _.AALOAD (_.CHECKCAST //.$Tuple))
        recurI (: (-> Label Inst)
                  (function (_ @loop)
                    (|>> sub-leftsI (_.ISTORE 1)
                         sub-tupleI (_.ASTORE 0)
                         (_.GOTO @loop))))]
    (|>> ($d.method #$.Public $.staticM "pm_fail" throw-methodT
                    (|>> (illegal-state-exception "Invalid expression for pattern-matching.")
                         _.ATHROW))
         ($d.method #$.Public $.staticM "apply_fail" throw-methodT
                    (|>> (illegal-state-exception "Error while applying function.")
                         _.ATHROW))
         ($d.method #$.Public $.staticM "pm_push" (descriptor.method [(list $Stack $Value) $Stack])
                    (|>> (_.int +2)
                         (_.ANEWARRAY $Stack)
                         _.DUP
                         (_.int +1)
                         (_.ALOAD 0)
                         _.AASTORE
                         _.DUP
                         (_.int +0)
                         (_.ALOAD 1)
                         _.AASTORE
                         _.ARETURN))
         ($d.method #$.Public $.staticM "pm_variant" (descriptor.method [(list //.$Variant $Tag $Flag) $Value])
                    (<| _.with-label (function (_ @loop))
                        _.with-label (function (_ @just-return))
                        _.with-label (function (_ @then))
                        _.with-label (function (_ @further))
                        _.with-label (function (_ @shorten))
                        _.with-label (function (_ @wrong))
                        (let [variant-partI (: (-> Nat Inst)
                                               (function (_ idx)
                                                 (|>> (_.int (.int idx)) _.AALOAD)))
                              tagI (: Inst
                                      (|>> (variant-partI 0) (_.unwrap descriptor.int)))
                              flagI (variant-partI 1)
                              datumI (variant-partI 2)
                              shortenI (|>> (_.ALOAD 0) tagI ## Get tag
                                            (_.ILOAD 1) _.ISUB ## Shorten tag
                                            (_.ALOAD 0) flagI ## Get flag
                                            (_.ALOAD 0) datumI ## Get value
                                            variantI ## Build sum
                                            _.ARETURN)
                              update-tagI (|>> _.ISUB (_.ISTORE 1))
                              update-variantI (|>> (_.ALOAD 0) datumI (_.CHECKCAST //.$Variant) (_.ASTORE 0))
                              failureI (|>> _.NULL _.ARETURN)
                              return-datumI (|>> (_.ALOAD 0) datumI _.ARETURN)])
                        (|>> (_.label @loop)
                             (_.ILOAD 1) ## tag
                             (_.ALOAD 0) tagI ## tag, sumT
                             _.DUP2 (_.IF_ICMPEQ @then)
                             _.DUP2 (_.IF_ICMPGT @further)
                             _.DUP2 (_.IF_ICMPLT @shorten)
                             ## _.POP2
                             failureI
                             (_.label @then) ## tag, sumT
                             (_.ALOAD 2) ## tag, sumT, wants-last?
                             (_.ALOAD 0) flagI ## tag, sumT, wants-last?, is-last?
                             (_.IF_ACMPEQ @just-return) ## tag, sumT
                             (_.label @further) ## tag, sumT
                             (_.ALOAD 0) flagI ## tag, sumT, last?
                             (_.IFNULL @wrong) ## tag, sumT
                             update-tagI
                             update-variantI
                             (_.GOTO @loop)
                             (_.label @just-return) ## tag, sumT
                             ## _.POP2
                             return-datumI
                             (_.label @shorten) ## tag, sumT
                             (_.ALOAD 2) (_.IFNULL @wrong)
                             ## _.POP2
                             shortenI
                             (_.label @wrong) ## tag, sumT
                             ## _.POP2
                             failureI)))
         ($d.method #$.Public $.staticM "tuple_left" (descriptor.method [(list //.$Tuple $Index) $Value])
                    (<| _.with-label (function (_ @loop))
                        _.with-label (function (_ @recursive))
                        (let [left-accessI (|>> (_.ALOAD 0) left-indexI _.AALOAD)])
                        (|>> (_.label @loop)
                             leftsI last-rightI (_.IF_ICMPGE @recursive)
                             left-accessI
                             _.ARETURN
                             (_.label @recursive)
                             ## Recursive
                             (recurI @loop))))
         ($d.method #$.Public $.staticM "tuple_right" (descriptor.method [(list //.$Tuple $Index) $Value])
                    (<| _.with-label (function (_ @loop))
                        _.with-label (function (_ @not-tail))
                        _.with-label (function (_ @slice))
                        (let [right-indexI (|>> leftsI
                                                (_.int +1)
                                                _.IADD)
                              right-accessI (|>> (_.ALOAD 0)
                                                 _.SWAP
                                                 _.AALOAD)
                              sub-rightI (|>> (_.ALOAD 0)
                                              right-indexI
                                              tuple-sizeI
                                              (_.INVOKESTATIC (descriptor.class "java.util.Arrays") "copyOfRange"
                                                              (descriptor.method [(list //.$Tuple $Index $Index)
                                                                                  //.$Tuple])
                                                              #0))])
                        (|>> (_.label @loop)
                             last-rightI right-indexI
                             _.DUP2 (_.IF_ICMPNE @not-tail)
                             ## _.POP
                             right-accessI
                             _.ARETURN
                             (_.label @not-tail)
                             (_.IF_ICMPGT @slice)
                             ## Must recurse
                             (recurI @loop)
                             (_.label @slice)
                             sub-rightI
                             _.ARETURN
                             )))
         )))

(def: io-methods
  Def
  (let [StringWriter (descriptor.class "java.io.StringWriter")
        PrintWriter (descriptor.class "java.io.PrintWriter")
        string-writerI (|>> (_.NEW StringWriter)
                            _.DUP
                            (_.INVOKESPECIAL StringWriter "<init>" nullary-init-methodT #0))
        print-writerI (|>> (_.NEW PrintWriter)
                           _.SWAP
                           _.DUP2
                           _.POP
                           _.SWAP
                           (_.boolean true)
                           (_.INVOKESPECIAL PrintWriter "<init>" (descriptor.method [(list (descriptor.class "java.io.Writer") descriptor.boolean) descriptor.void]) #0)
                           )]
    (|>> ($d.method #$.Public $.staticM "try" (descriptor.method [(list //.$Function) //.$Variant])
                    (<| _.with-label (function (_ @from))
                        _.with-label (function (_ @to))
                        _.with-label (function (_ @handler))
                        (|>> (_.try @from @to @handler $Throwable)
                             (_.label @from)
                             (_.ALOAD 0)
                             _.NULL
                             (_.INVOKEVIRTUAL //.$Function apply-method (apply-signature 1) #0)
                             rightI
                             _.ARETURN
                             (_.label @to)
                             (_.label @handler)
                             string-writerI ## TW
                             _.DUP2 ## TWTW
                             print-writerI ## TWTP
                             (_.INVOKEVIRTUAL $Throwable "printStackTrace" (descriptor.method [(list (descriptor.class "java.io.PrintWriter")) descriptor.void]) #0) ## TW
                             (_.INVOKEVIRTUAL StringWriter "toString" (descriptor.method [(list) $Text]) #0) ## TS
                             _.SWAP _.POP leftI
                             _.ARETURN)))
         )))

(def: translate-runtime
  (Operation ByteCode)
  (let [bytecode ($d.class #$.V1_6 #$.Public $.finalC //.runtime-class (list) ["java.lang.Object" (list)] (list)
                           (|>> adt-methods
                                frac-methods
                                pm-methods
                                io-methods))]
    (do phase.monad
      [_ (generation.execute! //.runtime-class [//.runtime-class bytecode])]
      (wrap bytecode))))

(def: translate-function
  (Operation ByteCode)
  (let [applyI (|> (list.n/range 2 num-apply-variants)
                   (list/map (function (_ arity)
                               ($d.method #$.Public $.noneM apply-method (apply-signature arity)
                                          (let [preI (|> (list.n/range 0 (dec arity))
                                                         (list/map _.ALOAD)
                                                         _.fuse)]
                                            (|>> preI
                                                 (_.INVOKEVIRTUAL //.$Function apply-method (apply-signature (dec arity)) #0)
                                                 (_.CHECKCAST //.$Function)
                                                 (_.ALOAD arity)
                                                 (_.INVOKEVIRTUAL //.$Function apply-method (apply-signature 1) #0)
                                                 _.ARETURN)))))
                   (list& ($d.abstract-method #$.Public $.noneM apply-method (apply-signature 1)))
                   $d.fuse)
        bytecode ($d.abstract #$.V1_6 #$.Public $.noneC //.function-class (list) ["java.lang.Object" (list)] (list)
                              (|>> ($d.field #$.Public $.finalF partials-field descriptor.int)
                                   ($d.method #$.Public $.noneM "<init>" (descriptor.method [(list descriptor.int) descriptor.void])
                                              (|>> (_.ALOAD 0)
                                                   (_.INVOKESPECIAL (descriptor.class "java.lang.Object") "<init>" nullary-init-methodT #0)
                                                   (_.ALOAD 0)
                                                   (_.ILOAD 1)
                                                   (_.PUTFIELD //.$Function partials-field descriptor.int)
                                                   _.RETURN))
                                   applyI))]
    (do phase.monad
      [_ (generation.execute! //.function-class [//.function-class bytecode])]
      (wrap bytecode))))

(def: #export translate
  (Operation Any)
  (do phase.monad
    [runtime-bc translate-runtime
     function-bc translate-function]
    (wrap [])))
