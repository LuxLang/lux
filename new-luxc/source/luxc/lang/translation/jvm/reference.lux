(.module:
  [lux #*
   [abstract
    [monad (#+ do)]]
   [data
    [text
     ["%" format (#+ format)]]]
   [target
    [jvm
     ["." type]]]
   [tool
    [compiler
     ["." name]
     ["." reference (#+ Register Variable)]
     ["." generation]
     ["." phase ("operation@." monad)]]]]
  [luxc
   [lang
    [host
     [jvm (#+ Inst Operation)
      ["_" inst]]]]]
  ["." //])

(template [<name> <prefix>]
  [(def: #export <name>
     (-> Nat Text)
     (|>> %.nat (format <prefix>)))]

  [foreign-name "f"]
  [partial-name "p"]
  )

(def: (foreign variable)
  (-> Register (Operation Inst))
  (do phase.monad
    [function-class generation.context]
    (wrap (|>> (_.ALOAD 0)
               (_.GETFIELD (type.class function-class (list))
                           (|> variable .nat foreign-name)
                           //.$Value)))))

(def: local
  (-> Register Inst)
  (|>> _.ALOAD))

(def: #export (variable variable)
  (-> Variable (Operation Inst))
  (case variable
    (#reference.Local variable)
    (operation@wrap (local variable))
    
    (#reference.Foreign variable)
    (foreign variable)))

(def: #export (constant name)
  (-> Name (Operation Inst))
  (do phase.monad
    [bytecode-name (generation.remember name)]
    (wrap (_.GETSTATIC (type.class bytecode-name (list)) //.value-field //.$Value))))
