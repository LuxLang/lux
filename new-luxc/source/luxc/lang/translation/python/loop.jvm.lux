(.module:
  lux
  (lux (control [monad #+ do])
       (data [text]
             text/format
             (coll [list "list/" Functor<List>]))
       [macro])
  (luxc [lang]
        (lang ["ls" synthesis]
              (host [python #+ Expression Statement @@])))
  [//]
  (// [".T" reference]))

(def: #export (translate-loop translate offset initsS+ bodyS)
  (-> (-> ls.Synthesis (Meta Expression)) Nat (List ls.Synthesis) ls.Synthesis
      (Meta Expression))
  (do macro.Monad<Meta>
    [loop-name (|> (macro.gensym "loop")
                   (:: @ map (|>> %code lang.normalize-name)))
     initsO+ (monad.map @ translate initsS+)
     bodyO (//.with-anchor [loop-name offset]
             (translate bodyS))
     #let [$loop-name (python.var loop-name)
           @loop-name (@@ $loop-name)]
     _ (//.save (python.def! $loop-name (|> (list.n/range +0 (n/dec (list.size initsS+)))
                                            (list/map (|>> (n/+ offset) referenceT.variable)))
                             (python.return! bodyO)))]
    (wrap (python.apply initsO+ @loop-name))))

(def: #export (translate-recur translate argsS+)
  (-> (-> ls.Synthesis (Meta Expression)) (List ls.Synthesis)
      (Meta Expression))
  (do macro.Monad<Meta>
    [[loop-name offset] //.anchor
     argsO+ (monad.map @ translate argsS+)]
    (wrap (python.apply argsO+ (python.global loop-name)))))
