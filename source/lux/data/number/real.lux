##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;import lux
         (lux/control (number #as N)
                      (monoid #as m)
                      (eq #as E)
                      (ord #as O)
                      (enum #as EN)
                      (bounded #as B)
                      (show #as S)))

## [Structures]
## Number
(do-template [<name> <type> <+> <-> <*> </> <%> <=> <<> <from> <0> <1> <-1>]
  [(defstruct #export <name> (N;Number <type>)
     (def (+ x y) (<+> x y))
     (def (- x y) (<-> x y))
     (def (* x y) (<*> x y))
     (def (/ x y) (</> x y))
     (def (% x y) (<%> x y))
     (def (from-int x)
       (<from> x))
     (def (negate x)
       (<*> <-1> x))
     (def (abs x)
       (if (<<> x <0>)
         (<*> <-1> x)
         x))
     (def (signum x)
       (cond (<=> x <0>) <0>
             (<<> x <0>) <-1>
             ## else
             <1>))
     )]

  [Real/Number Real _jvm_dadd _jvm_dsub _jvm_dmul _jvm_ddiv _jvm_drem _jvm_deq _jvm_dlt _jvm_l2d 0.0 1.0 -1.0])

## Eq
(defstruct #export Real/Eq (E;Eq Real)
  (def (= x y) (_jvm_deq x y)))

## Ord
(do-template [<name> <type> <eq> <=> <lt> <gt>]
  [(defstruct #export <name> (O;Ord <type>)
     (def _eq <eq>)
     (def (< x y) (<lt> x y))
     (def (<= x y)
       (or (<lt> x y)
           (<=> x y)))
     (def (> x y) (<gt> x y))
     (def (>= x y)
       (or (<gt> x y)
           (<=> x y))))]

  [Real/Ord Real Real/Eq _jvm_deq _jvm_dlt _jvm_dgt])

## Enum
(defstruct Real/Enum (EN;Enum Real)
  (def _ord Real/Ord)
  (def succ (lambda [n] (:: Real/Number (N;+ n 1.0))))
  (def pred (lambda [n] (:: Real/Number (N;- n 1.0)))))

## Bounded
(do-template [<name> <type> <top> <bottom>]
  [(defstruct #export <name> (B;Bounded <type>)
     (def top <top>)
     (def bottom <bottom>))]

  [Real/Bounded Real (_jvm_getstatic "java.lang.Double" "MAX_VALUE") (_jvm_getstatic "java.lang.Double" "MIN_VALUE")])

## Monoid
(do-template [<name> <type> <unit> <++>]
  [(defstruct #export <name> (m;Monoid <type>)
     (def unit <unit>)
     (def (++ x y) (<++> x y)))]

  [RealAdd/Monoid Real 0.0 _jvm_dadd]
  [RealMul/Monoid Real 1.0 _jvm_dmul]
  [RealMax/Monoid Real (:: Real/Bounded B;bottom)  (O;max Real/Ord)]
  [RealMin/Monoid Real (:: Real/Bounded B;top)     (O;min Real/Ord)]
  )

## Show
(do-template [<name> <type> <body>]
  [(defstruct #export <name> (S;Show <type>)
     (def (show x)
       <body>))]

  [Real/Show Real (_jvm_invokevirtual "java.lang.Object" "toString" [] x [])]
  )
