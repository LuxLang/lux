... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    ["<>" projection]
    ["[0]" monad]]
   [error
    ["[0]" try]
    ["[0]" exception]]
   [data
    ["[0]" text]
    [format
     ["[0]" xml (.only)
      ["<[1]>" \\projection]]]]
   [math
    ["[0]" random]]
   [test
    ["_" property (.only Test)]]]]
 [//
  ["@[0]" profile]]
 [\\program
  ["[0]" / (.only)
   ["/[1]" //
    ["[1]" profile]]]])

(the .public test
  Test
  (<| (_.covering /._)
      (all _.and
           (_.coverage [/.file]
             (|> /.file
                 (text.= "")
                 not))
           (monad.let random.monad
             [expected @profile.random]
             (_.coverage [/.write /.of_xml]
               (when [(/.write expected)
                      (its //.:identity expected)]
                 [{try.:Success pom}
                  {.:Some _}]
                 (when (<xml>.value (list pom) /.of_xml)
                   {try.:Success actual}
                   (by //.equivalence =
                       (|> (by //.monoid identity)
                           (has //.:dependencies (its //.:dependencies expected))
                           (has //.:repositories (its //.:repositories expected)))
                       actual)

                   {try.:Failure error}
                   false)

                 [{try.:Failure error}
                  {.:None}]
                 (exception.is? //.no_identity error)

                 _
                 false)))
           )))
