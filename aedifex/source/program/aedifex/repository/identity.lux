... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except)
   [abstract
    [equality (.only Equality)]]
   [data
    ["[0]" product]
    ["[0]" text (.only)
     [encoding
      ["[0]" utf8]]]
    [format
     ["[0]" base64]]]
   [aspect
    ["[0]" case]]]])

(every .public User
  Text)

(every .public Password
  Text)

(every .public Identity
  (Record
   [:user User
    :password Password]))

(the .public equality
  (Equality Identity)
  (all product.equality
       text.equality
       text.equality
       ))

(alias [=]
       ..equality)

(the .public (basic_auth it)
  (-> Identity
      Text)
  (|> (text (its :user it) ":" (its :password it))
      (by utf8.binary as)
      (case.some base64.text)
      (text "Basic ")))
