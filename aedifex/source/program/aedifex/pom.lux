... This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
... If a copy of the MPL was not distributed with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

(.using
 [library
  [lux (.except Tag)
   [abstract
    ["<>" projection]
    ["[0]" monad]]
   [control
    ["[0]" pipe]]
   [error
    ["[0]" try (.only Try)]
    ["[0]" exception]]
   [data
    ["[0]" maybe (.use "[1]:[0]" functor)]
    ["[0]" text]
    [format
     ["[0]" xml (.only Tag XML)
      ["<[1]>" \\projection (.only Projection)]]]
    [collection
     ["[0]" list (.use "[1]:[0]" monoid functor mix)]
     ["[0]" set]
     ["[0]" dictionary]]]
   [macro
    ["[0]" template]]
   [meta
    ["[0]" name]]
   [world
    ["[0]" file]]]]
 ["[0]" //
  ["/" profile]
  ["[1][0]" dependency (.only Dependency)]
  [repository
   [remote (.only Address)]]
  ["[1][0]" artifact (.only Artifact)
   ["[1]/[0]" type]]])

... https://maven.apache.org/pom.html

(the project_tag "project")
(the dependency_tag "dependency")
(the dependencies_tag "dependencies")
(the repositories_tag "repositories")
(the repository_tag "repository")
(the url_tag "url")
(the group_tag "groupId")
(the artifact_tag "artifactId")
(the version_tag "version")

(the .public file
  file.Path
  "pom.xml")

(the version
  XML
  {xml.:Node ["" "modelVersion"] xml.attributes
             (list {xml.:Text "4.0.0"})})

(the (property tag value)
  (-> Text Text XML)
  {xml.:Node ["" tag]
             xml.attributes
             (list {xml.:Text value})})

(the (artifact value)
  (-> Artifact
      (List XML))
  (list (..property ..group_tag (its //artifact.:group value))
        (..property ..artifact_tag (its //artifact.:name value))
        (..property ..version_tag (its //artifact.:version value))))

(the distribution
  (-> /.Distribution XML)
  (|>> (pipe.when
         {/.:Repo} "repo"
         {/.:Manual} "manual")
       (..property "distribution")))

(the (license [name url distribution])
  (-> /.License XML)
  (|> (list (..property "name" name)
            (..property ..url_tag url)
            (..distribution distribution))
      {xml.:Node ["" "license"] xml.attributes}))

(the repository
  (-> Address XML)
  (|>> (..property ..url_tag)
       list
       {xml.:Node ["" ..repository_tag] xml.attributes}))

(the (dependency value)
  (-> Dependency XML)
  {xml.:Node ["" ..dependency_tag]
             xml.attributes
             (list:composite (..artifact (its //dependency.:artifact value))
                             (list (..property "type" (its //dependency.:type value))))})

(the (group tag)
  (-> Text (List XML)
      XML)
  (|>> {xml.:Node ["" tag] xml.attributes}))

(the scm
  (-> /.SCM XML)
  (|>> (..property ..url_tag)
       list
       {xml.:Node ["" "scm"] xml.attributes}))

(the (organization [name url])
  (-> /.Organization XML)
  (|> (list (..property "name" name)
            (..property ..url_tag url))
      {xml.:Node ["" "organization"] xml.attributes}))

(the (developer_organization [name url])
  (-> /.Organization (List XML))
  (list (..property "organization" name)
        (..property "organizationUrl" url)))

(the (developer' [name email organization])
  (-> /.Developer
      (List XML))
  (list:composite (list (..property "name" name)
                        (..property "email" email))
                  (|> organization
                      (maybe:each ..developer_organization)
                      (maybe.else (list)))))

(template.with [<name> <type> <tag>]
  [(the <name>
     (-> <type>
         XML)
     (|>> ..developer'
          {xml.:Node ["" <tag>] xml.attributes}))]

  [[developer /.Developer "developer"]
   [contributor /.Contributor "contributor"]])

(the (info identity value)
  (-> Artifact /.Info
      (List XML))
  (all list:composite
       (|> identity (its //artifact.:name) (..property "name") list)
       (|> value (its /.:url) (maybe:each (..property ..url_tag)) maybe.as_list)
       (|> value (its /.:description) (maybe:each (..property "description")) maybe.as_list)
       (|> value (its /.:licenses) (list:each ..license) (..group "licenses") list)
       (|> value (its /.:scm) (maybe:each ..scm) maybe.as_list)
       (|> value (its /.:organization) (maybe:each ..organization) maybe.as_list)
       (|> value (its /.:developers) (list:each ..developer) (..group "developers") list)
       (|> value (its /.:contributors) (list:each ..contributor) (..group "contributors") list)
       ))

(the .public (write value)
  (-> /.Profile (Try XML))
  (when (its /.:identity value)
    {.:Some identity}
    {try.:Success
     {xml.:Node ["" ..project_tag]
                xml.attributes
                (all list:composite
                     (list ..version)
                     (..artifact identity)
                     (|> value
                         (its /.:info)
                         (maybe:each (..info identity))
                         (maybe.else (list)))
                     (|> value
                         (its /.:repositories)
                         set.as_list
                         (list:each ..repository)
                         (..group "repositories")
                         list)
                     (|> value
                         (its /.:dependencies)
                         set.as_list
                         (list:each ..dependency)
                         (..group ..dependencies_tag)
                         list)
                     )}}

    _
    (exception.except /.no_identity [])))

(the property_of
  (Projection [Tag Text])
  (monad.let [! <>.monad]
    [tag <xml>.tag]
    (<| (<xml>.node tag)
        (by ! each (|>> [tag]))
        <xml>.text)))

(the (dependency_of own_version parent_version)
  (-> Text Text (Projection Dependency))
  (monad.let [! <>.monad]
    [properties (by ! each (dictionary.of_list name.hash)
                    (<| (<xml>.node ["" ..dependency_tag])
                        (<>.some ..property_of)))]
    (<| <>.of_try
        (try.of_maybe "Cannot parse dependency.")
        (monad.let try.monad
          [group (dictionary.value ["" ..group_tag] properties)
           artifact (dictionary.value ["" ..artifact_tag] properties)]
          (pure [//dependency.:artifact [//artifact.:group group
                                         //artifact.:name artifact
                                         //artifact.:version (|> properties
                                                                 (dictionary.value ["" ..version_tag])
                                                                 (try.else "")
                                                                 (text.replaced "${project.version}" own_version)
                                                                 (text.replaced "${project.parent.version}" parent_version))]
                 //dependency.:type (|> properties
                                        (dictionary.value ["" "type"])
                                        (try.else //artifact/type.jvm_library))])))))

(the (dependencies_of own_version parent_version)
  (-> Text Text
      (Projection (List Dependency)))
  (<| (<xml>.node ["" ..dependencies_tag])
      (<>.some (..dependency_of own_version parent_version))))

(the repository_of
  (Projection Address)
  (<| (<xml>.node ["" ..repository_tag])
      (<xml>.node ["" ..url_tag])
      <xml>.text))

(the repositories_of
  (Projection (List Address))
  (<| (<xml>.node ["" ..repositories_tag])
      (<>.some ..repository_of)))

(the own_version
  (Projection Text)
  (<| (<xml>.node ["" ..version_tag])
      <xml>.text))

(the parent_version
  (Projection Text)
  (<| (<xml>.node ["" "parent"])
      ..own_version))

(the .public of_xml
  (Projection /.Profile)
  (monad.let [! <>.monad]
    [own_version (<>.else "" (<xml>.somewhere ..own_version))
     parent_version (<>.else "" (<xml>.somewhere ..parent_version))]
    (<| (<xml>.node ["" ..project_tag])
        (monad.let !
          [dependencies (|> (..dependencies_of own_version parent_version)
                            <xml>.somewhere
                            (<>.else (list)))
           repositories (|> ..repositories_of
                            <xml>.somewhere
                            (<>.else (list)))
           _ (<>.some <xml>.any)]
          (pure (|> (by /.monoid identity)
                    (revised /.:dependencies (function (_ empty)
                                               (list:mix set.has empty dependencies)))
                    (revised /.:repositories (function (_ empty)
                                               (list:mix set.has empty repositories)))))))))
